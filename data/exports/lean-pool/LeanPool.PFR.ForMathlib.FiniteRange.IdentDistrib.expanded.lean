/-
Copyright (c) 2026 PFR contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: PFR contributors
-/

module

public import LeanPool.ZhangYeungInequality.PFR.Mathlib.Probability.IdentDistrib
public import LeanPool.ZhangYeungInequality.PFR.ForMathlib.FiniteRange.Defs
import LeanPool.ZhangYeungInequality.PFR.Mathlib.Probability.Independence.Basic


-- @@ L13-17 verbatim
/-!
# Identically distributed finite-range random variables
-/

-- TODO: Change `ae_snd` to assume `Measurable p`


-- @@ L19-19 verbatim
noncomputable section


-- @@ L21-21 verbatim
open MeasureTheory Measure Filter Set

-- @@ L22-22 verbatim
open scoped Topology MeasureTheory ENNReal NNReal


-- @@ L24-24 verbatim
namespace ProbabilityTheory

-- @@ L25-25 verbatim
section IdentDistrib

-- @@ L26-26 verbatim
universe u u' v

-- @@ L27-29 verbatim
variable {Ω Ω' α ι β β' T : Type*} {mΩ : MeasurableSpace Ω} {mΩ' : MeasurableSpace Ω'}
  {mα : MeasurableSpace α} {mβ : MeasurableSpace β} {μ : Measure Ω} {ν : Measure Ω'} {f g : Ω → β}
  {f' g' : Ω' → β}


-- @@ L31-56 verbatim
/-- If `X` has identical distribution to `X₀`, and `X₀` has finite range, then `X` is almost
everywhere equivalent to a random variable of finite range. -/
public
lemma identDistrib_of_finiteRange {Ω Ω₀ S : Type*}
    [MeasurableSpace Ω] [MeasurableSpace Ω₀] [MeasurableSpace S] [MeasurableSingletonClass S]
    [hS : Nonempty S] {μ : Measure Ω} {μ₀ : Measure Ω₀} {X₀ : Ω₀ → S} [FiniteRange X₀] {X : Ω → S}
    (hX : Measurable X) (hi : IdentDistrib X₀ X μ₀ μ) :
    ∃ X' : Ω → S, Measurable X' ∧ FiniteRange X' ∧ X' =ᵐ[μ] X := by
  set A := FiniteRange.toFinset X₀
  classical
  let X' (ω : Ω) : S := if (X ω ∈ A) then X ω else hS.some
  refine ⟨X', ?_, ?_, ?_⟩
  · exact .ite (.preimage (Finset.measurableSet A) hX) hX measurable_const
  · apply finiteRange_of_finset X' (A ∪ {hS.some})
    intro ω
    simp [X']
    split <;> simp [*]
  apply Filter.eventuallyEq_of_mem (s := X ⁻¹' A)
  · rw [mem_ae_iff, ← Set.preimage_compl, ← IdentDistrib.measure_preimage_eq hi]
    · convert measure_empty (μ := μ₀)
      ext ω
      simp [A]
    measurability
  intro ω
  simp only [mem_preimage, Finset.mem_coe, ite_eq_left_iff, X']
  tauto


-- @@ L58-80 verbatim
/-- A version of `independent_copies` that guarantees that the copies have `FiniteRange` if the
original variables do. -/
public
lemma independent_copies_finiteRange {X : Ω → α} {Y : Ω' → β}
    (hX : Measurable X) (hY : Measurable Y) [FiniteRange X] [FiniteRange Y]
    [MeasurableSingletonClass α] [MeasurableSingletonClass β]
    (μ : Measure Ω) (μ' : Measure Ω') [IsProbabilityMeasure μ] [IsProbabilityMeasure μ'] :
    ∃ ν : Measure (α × β), ∃ X' : α × β → α, ∃
    Y' : α × β → β, IsProbabilityMeasure ν
      ∧ Measurable X' ∧ Measurable Y' ∧ IndepFun X' Y' ν
      ∧ IdentDistrib X' X ν μ ∧ IdentDistrib Y' Y ν μ' ∧ FiniteRange X' ∧ FiniteRange Y' := by
  have : Nonempty α := μ.nonempty_of_neZero.map X
  have : Nonempty β := μ'.nonempty_of_neZero.map Y
  obtain ⟨ν, X', Y', hν, hX', hY', hind, hIdX, hIdY⟩ := independent_copies hX hY μ μ'
  rcases identDistrib_of_finiteRange hX' hIdX.symm with ⟨X'', hX'', hX''_finite, hX''_eq⟩
  rcases identDistrib_of_finiteRange hY' hIdY.symm with ⟨Y'', hY'', hY''_finite, hY''_eq⟩
  use ν, X'', Y''
  refine ⟨hν, hX'', hY'', ?_, ?_, ?_, hX''_finite, hY''_finite⟩
  · exact hind.congr hX''_eq.symm hY''_eq.symm
  · convert IdentDistrib.trans _ hIdX
    exact IdentDistrib.of_ae_eq (Measurable.aemeasurable hX'') hX''_eq
  · convert IdentDistrib.trans _ hIdY
    exact IdentDistrib.of_ae_eq (Measurable.aemeasurable hY'') hY''_eq


-- @@ L82-118 verbatim
/-- A version of `independent_copies3_nondep` that guarantees that the copies have `FiniteRange`
if the original variables do. -/
public
lemma independent_copies3_nondep_finiteRange {α : Type u}
    [mS : MeasurableSpace α] [MeasurableSingletonClass α]
    {Ω₁ : Type u_1} {Ω₂ : Type u_2} {Ω₃ : Type u_3}
    [MeasurableSpace Ω₁] [MeasurableSpace Ω₂] [MeasurableSpace Ω₃]
    {X₁ : Ω₁ → α} {X₂ : Ω₂ → α} {X₃ : Ω₃ → α}
    (hX₁ : Measurable X₁) (hX₂ : Measurable X₂) (hX₃ : Measurable X₃)
    [FiniteRange X₁] [FiniteRange X₂] [FiniteRange X₃]
    (μ₁ : Measure Ω₁) (μ₂ : Measure Ω₂) (μ₃ : Measure Ω₃)
    [hμ₁ : IsProbabilityMeasure μ₁] [hμ₂ : IsProbabilityMeasure μ₂]
    [hμ₃ : IsProbabilityMeasure μ₃] :
    ∃ (A : Type (max u_1 u_2 u_3)) (_ : MeasurableSpace A) (μA : Measure A)
      (X₁' X₂' X₃' : A → α),
    IsProbabilityMeasure μA ∧
    iIndepFun ![X₁', X₂', X₃'] μA ∧
      Measurable X₁' ∧ Measurable X₂' ∧ Measurable X₃' ∧
      IdentDistrib X₁' X₁ μA μ₁ ∧ IdentDistrib X₂' X₂ μA μ₂ ∧ IdentDistrib X₃' X₃ μA μ₃ ∧
      FiniteRange X₁' ∧ FiniteRange X₂' ∧ FiniteRange X₃' := by
    have : Nonempty α := μ₁.nonempty_of_neZero.map X₁
    obtain ⟨A, mA, μA, X₁', X₂', X₃', hμA, hind, hX₁, hX₂, hX₃, hId₁, hId₂, hId₃⟩ :=
      independent_copies3_nondep hX₁ hX₂ hX₃ μ₁ μ₂ μ₃
    rcases identDistrib_of_finiteRange hX₁ hId₁.symm with ⟨X₁'', hX₁'', hX₁''_finite, hX₁''_eq⟩
    rcases identDistrib_of_finiteRange hX₂ hId₂.symm with ⟨X₂'', hX₂'', hX₂''_finite, hX₂''_eq⟩
    rcases identDistrib_of_finiteRange hX₃ hId₃.symm with ⟨X₃'', hX₃'', hX₃''_finite, hX₃''_eq⟩
    use A, mA, μA, X₁'', X₂'', X₃''
    refine ⟨hμA, ?_, hX₁'', hX₂'', hX₃'', ?_, ?_, ?_, hX₁''_finite, hX₂''_finite, hX₃''_finite⟩
    · apply iIndepFun.ae_eq hind
      intro i; fin_cases i
      all_goals simp [hX₁''_eq.symm, hX₂''_eq.symm, hX₃''_eq.symm]
    · convert IdentDistrib.trans _ hId₁
      exact IdentDistrib.of_ae_eq (Measurable.aemeasurable hX₁'') hX₁''_eq
    · convert IdentDistrib.trans _ hId₂
      exact IdentDistrib.of_ae_eq (Measurable.aemeasurable hX₂'') hX₂''_eq
    convert IdentDistrib.trans _ hId₃
    exact IdentDistrib.of_ae_eq (Measurable.aemeasurable hX₃'') hX₃''_eq


-- @@ L120-141 verbatim
/-- A version of `independent_copies'` that guarantees that the copies have `FiniteRange`
if the original variables do. -/
public
lemma independent_copies'_finiteRange {I : Type u} [Finite I] {α : I → Type u'}
    [mS : ∀ i : I, MeasurableSpace (α i)] [mS' : ∀ i, MeasurableSingletonClass (α i)]
    [mnon: ∀ i, Nonempty (α i)] {Ω : I → Type v}
    [mΩ : ∀ i : I, MeasurableSpace (Ω i)] (X : ∀ i : I, Ω i → α i) (hX : ∀ i : I, Measurable (X i))
    (μ : ∀ i : I, Measure (Ω i)) [∀ i, IsProbabilityMeasure (μ i)] [∀ i, FiniteRange (X i)] :
    ∃ (A : Type (max u v)) (_ : MeasurableSpace A) (μA : Measure A) (X' : ∀ i, A → α i),
    IsProbabilityMeasure μA ∧ iIndepFun X' μA ∧
    ∀ i : I, Measurable (X' i) ∧ IdentDistrib (X' i) (X i) μA (μ i) ∧ FiniteRange (X' i) := by
  cases nonempty_fintype I
  obtain ⟨A, mA, μA, X', ⟨hμA, hindep, hident⟩⟩ := independent_copies' X hX μ
  set h := fun i ↦ (identDistrib_of_finiteRange ((hident i).1) (hident i).2.symm)
  choose X'' hX'' using h
  refine ⟨A, mA, μA, X'', hμA, ?_, ?_⟩
  · apply hindep.ae_eq
    intro i
    exact (hX'' i).2.2.symm
  intro i
  refine ⟨(hX'' i).1, ?_, (hX'' i).2.1⟩
  exact .trans (.of_ae_eq (hX'' i).1.aemeasurable (hX'' i).2.2) (hident i).2


-- @@ L143-143 verbatim
end IdentDistrib

-- @@ L144-144 verbatim
end ProbabilityTheory
