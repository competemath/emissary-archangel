/-
Copyright (c) 2026 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

module

public import Mathlib.Analysis.CStarAlgebra.Classes
public import Mathlib.NumberTheory.ModularForms.QExpansion


-- @@ L12-12 verbatim
/-! # QExpansionLems -/



-- @@ L15-15 verbatim
@[expose] public section


-- @@ L17-18 verbatim
open ModularForm UpperHalfPlane TopologicalSpace Set MeasureTheory intervalIntegral
  Metric Filter Function Complex MatrixGroups


-- @@ L20-20 verbatim
open scoped Interval Real NNReal ENNReal Topology BigOperators Nat


-- @@ L22-22 verbatim
open SlashInvariantFormClass ModularFormClass

-- @@ L23-23 verbatim
variable {k : ℤ} {F : Type*} [FunLike F ℍ ℂ] {Γ : Subgroup SL(2, ℤ)} (n : ℕ) (f : F)


-- @@ L25-25 verbatim
open scoped Real MatrixGroups CongruenceSubgroup


-- @@ L27-59 verbatim
theorem modform_tendto_ndhs_zero {k : ℤ} (n : ℕ) [ModularFormClass F Γ(n) k] [inst : NeZero n] :
    Tendsto (fun x ↦ (⇑f ∘ ↑ofComplex) (Periodic.invQParam (↑n) x)) (𝓝[≠] 0)
    (𝓝 (cuspFunction n f 0)) := by
  simp only [comp_apply]
  have hi : IsCusp OnePoint.infty Γ(n) :=
    Γ(n).isCusp_of_mem_strictPeriods (h := n) (by have h := inst.1; positivity) (by simp)
  have h1 := Function.Periodic.boundedAtFilter_cuspFunction (h := n)
    (by simp only [Nat.cast_pos]; exact Nat.pos_of_neZero n)
    ((OnePoint.isBoundedAt_infty_iff.mp (ModularFormClass.bdd_at_cusps f hi)).comp_tendsto
      tendsto_comap_im_ofComplex)
  have h2 : Tendsto (cuspFunction n f) (𝓝[≠] 0) (𝓝 (cuspFunction n f 0)) := by
    apply tendsto_nhdsWithin_of_tendsto_nhds
    apply (Function.Periodic.differentiableAt_cuspFunction_zero (h := n)
      (by simp only [Nat.cast_pos]; exact Nat.pos_of_neZero n) ?_ ?_ ?_).continuousAt.tendsto
    · apply SlashInvariantFormClass.periodic_comp_ofComplex
      simp
    · simp only [eventually_comap, eventually_atTop]
      use 1
      intro b hb a ha
      refine UpperHalfPlane.mdifferentiableAt_iff.mp (ModularFormClass.holo f ⟨a, ?_⟩)
      rw [ha]
      linarith
    exact (OnePoint.isBoundedAt_infty_iff.mp (ModularFormClass.bdd_at_cusps f hi)).comp_tendsto
      tendsto_comap_im_ofComplex
  apply h2.congr'
  rw [@eventuallyEq_nhdsWithin_iff, eventually_iff_exists_mem]
  use ball 0 1
  constructor
  · apply Metric.ball_mem_nhds
    exact Real.zero_lt_one
  intro y hy hy0
  apply Function.Periodic.cuspFunction_eq_of_nonzero
  simpa only [ne_eq, mem_compl_iff, mem_singleton_iff] using hy0


-- @@ L61-67 verbatim
theorem derivWithin_mul2 (f g : ℂ → ℂ) (s : Set ℂ) (hf : DifferentiableOn ℂ f s)
    (hd : DifferentiableOn ℂ g s) :
    s.domRestrict (derivWithin (fun y => f y * g y) s) =
      s.domRestrict (derivWithin f s * g + f * derivWithin g s) := by
  ext y
  simp only [domRestrict_apply, Pi.add_apply, Pi.mul_apply]
  rw [derivWithin_fun_mul (hf y y.2) (hd y y.2)]


-- @@ L69-109 verbatim
lemma iteratedDerivWithin_mul' (f g : ℂ → ℂ) (s : Set ℂ) (hs : IsOpen s)
    (x : ℂ) (hx : x ∈ s) (m : ℕ)
    (hf : ContDiffOn ℂ ⊤ f s) (hg : ContDiffOn ℂ ⊤ g s) :
    iteratedDerivWithin m (f * g) s x =
    ∑ i ∈ Finset.range m.succ, (m.choose i) * (iteratedDerivWithin i f s x) *
    (iteratedDerivWithin (m - i) g s x) := by
  induction m generalizing f g with
  | zero => simp_all
  | succ m hm =>
    have h1 :=
      derivWithin_mul2 f g s (hf.differentiableOn (by simp)) (hg.differentiableOn (by simp))
    have h2 : (fun y => f y * g y) = f * g := by ext y; simp
    rw [iteratedDerivWithin_succ']
    have hset : s.EqOn (derivWithin (f * g) s) (derivWithin f s * g + f * derivWithin g s) := by
      rw [← h2]
      exact Set.domRestrict_eq_domRestrict_iff.mp h1
    rw [iteratedDerivWithin_congr hset hx, iteratedDerivWithin_add hx hs.uniqueDiffOn, hm _ _ hf,
      hm _ _ _ hg]
    · simp_rw [←iteratedDerivWithin_succ']
      have := Finset.sum_choose_succ_mul (fun i => fun j =>
        ((iteratedDerivWithin i f s x) * (iteratedDerivWithin j g s x)) ) m
      simp only [Nat.succ_eq_add_one] at *
      rw [show m + 1 + 1 = m + 2 by ring]
      simp_rw [← mul_assoc] at *
      rw [this, add_comm]
      congr 1
      apply Finset.sum_congr rfl
      intros i hi
      congr
      simp at hi
      omega
    · exact ContDiffOn.derivWithin hf (by exact IsOpen.uniqueDiffOn hs) (m := ⊤) (by simp)
    · exact ContDiffOn.derivWithin hg (by exact IsOpen.uniqueDiffOn hs) (m := ⊤) (by simp)
    · apply ContDiffOn.mul
      · exact ContDiffOn.derivWithin hf (by exact IsOpen.uniqueDiffOn hs) (m := m) (by simp)
      · apply ContDiffOn.of_le hg (by simp)
      exact hx
    · apply ContDiffOn.mul
      · apply ContDiffOn.of_le hf (by simp)
      · apply ContDiffOn.derivWithin hg (by exact IsOpen.uniqueDiffOn hs) (m := m) (by simp)
      exact hx


-- @@ L111-117 verbatim
lemma iteratedDeriv_eq_iteratedDerivWithin (n : ℕ) (f : ℂ → ℂ) (s : Set ℂ) (hs : IsOpen s)
  (z : ℂ) (hz : z ∈ s) : iteratedDeriv n f z = iteratedDerivWithin n f s z := by
  rw [← iteratedDerivWithin_univ]
  simp_rw [iteratedDerivWithin]
  rw [iteratedFDerivWithin_congr_set]
  apply EventuallyEq.symm
  exact eventuallyEqSet_univ.2 (IsOpen.mem_nhds hs hz)


-- @@ L119-124 verbatim
lemma qExpansion_mul_coeff (a b : ℤ) (f : ModularForm Γ(n) a) (g : ModularForm Γ(n) b)
    [hn : NeZero n] : qExpansion n (f.mul g) = qExpansion n f * qExpansion n g := by
  simpa using
    (ModularForm.qExpansion_mul (Γ := Γ(n)) (h := n)
      (hh := by exact Nat.cast_pos.mpr (Nat.pos_of_neZero n))
      (hΓ := by simp) f g)


-- @@ L126-126 verbatim
variable {𝕜 : Type*} [NontriviallyNormedField 𝕜]

-- @@ L127-127 verbatim
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace 𝕜 F]


-- @@ L129-134 verbatim
lemma IteratedDeriv_smul (a : ℂ) (f : ℂ → ℂ) (m : ℕ) :
    iteratedDeriv m (a • f) = a • iteratedDeriv m f := by
  induction m with
  | zero => simp
  | succ m hm =>
    ext x; simp_all



-- @@ L137-140 verbatim
lemma qExpansion_smul2 (a : ℂ) (f : ModularForm Γ(n) k) [NeZero n] :
    (a • qExpansion n f) = (qExpansion n (a • f)) :=
  (ModularForm.qExpansion_smul (Γ := Γ(n)) (h := n)
      (hh := Nat.cast_pos.mpr (Nat.pos_of_neZero n)) (hΓ := by simp) a f).symm


-- @@ L142-143 verbatim
instance instFunLikeUpperHalfPlaneFun :
    FunLike (ℍ → ℂ) ℍ ℂ := { coe := fun ⦃a₁⦄ ↦ a₁, coe_injective := fun ⦃_ _⦄ a ↦ a}


-- @@ L145-146 verbatim
lemma qExpansion_ext (f g : ℍ → ℂ) (h : f = g) : qExpansion 1 f =
    qExpansion 1 g := by rw [h]


-- @@ L148-151 verbatim
lemma cuspFunction_congr_funLike
    {α β : Type*} [FunLike α ℍ ℂ] [FunLike β ℍ ℂ] (n : ℕ) (f : α) (g : β) (h : ⇑f = ⇑g) :
    cuspFunction n f = cuspFunction n g := by
  simp_all


-- @@ L153-157 verbatim
lemma qExpansion_ext2 {α β : Type*} [FunLike α ℍ ℂ] [FunLike β ℍ ℂ] (f : α) (g : β) (h : ⇑f = ⇑g) :
    qExpansion 1 f = qExpansion 1 g := by
  ext m; simp [qExpansion_coeff, congrArg (cuspFunction 1) h]

--generalize this away from ℂ

-- @@ L158-159 verbatim
lemma IteratedDeriv_zero_fun (n : ℕ) (z : ℂ) : iteratedDeriv n (fun _ : ℂ => (0 : ℂ)) z = 0 := by
  induction n <;> simp


-- @@ L161-164 verbatim
lemma iteratedDeriv_const_eq_zero (m : ℕ) (hm : 0 < m) (c : ℂ) :
    iteratedDeriv m (fun _ : ℂ => c) = fun _ : ℂ => 0 := by
  ext z; simpa only [add_zero, IteratedDeriv_zero_fun] using
    iteratedDeriv_const_add hm (f := fun (x : ℂ) => (0 : ℂ)) c (x := z)


-- @@ L166-170 verbatim
lemma qExpansion_pow (f : ModularForm Γ(1) k) (n : ℕ) :
  qExpansion 1 ((((DirectSum.of (ModularForm Γ(1)) k ) f) ^ n) (n * k)) = (qExpansion 1 f) ^ n := by
  exact_mod_cast
    (qExpansion_of_pow (Γ := Γ(1)) (h := (1 : ℕ))
      (hh := by positivity) (hΓ := by simp) (f := f) (n := n))


-- @@ L172-175 verbatim
lemma qExpansion_injective [hn : NeZero n] (f : ModularForm Γ(n) k) :
    qExpansion n f = 0 ↔ f = 0 :=
  ModularForm.qExpansion_eq_zero_iff (Γ := Γ(n)) (h := n)
    (Nat.cast_pos.mpr (Nat.pos_of_neZero n)) (by simp) f
