/-
Copyright (c) 2026 Jiazhen Xia. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiazhen Xia
-/
module

public import LeanPool.WhiteheadTheorem.Auxiliary
public import LeanPool.WhiteheadTheorem.Shapes.Disk


-- @@ L11-15 verbatim
/-!
# LeanPool.WhiteheadTheorem.Shapes.Jar

Imported Lean Pool material for `LeanPool.WhiteheadTheorem.Shapes.Jar`.
-/


-- @@ L17-17 verbatim
@[expose] public section



-- @@ L20-20 verbatim
open TopCat

-- @@ L21-21 verbatim
open scoped Topology unitInterval


-- @@ L23-23 verbatim
universe u v


-- @@ L25-27 verbatim
namespace HEP

-- TODO (?): rewrite using Continuous.piecewise


-- @@ L29-30 expanded
/-- `Jar` -/
abbrev Jar (n : ℕ) :=
  disk n × I


-- @@ L32-32 verbatim
namespace Jar


-- @@ L34-35 verbatim
/-- `mid` -/
def mid (n : ℕ) := {⟨ ⟨⟨x, _⟩⟩, ⟨y, _⟩ ⟩ : Jar n | ‖x‖ ≤ 1 - y / 2}

-- @@ L36-37 verbatim
/-- `rim` -/
def rim (n : ℕ) := {⟨ ⟨⟨x, _⟩⟩, ⟨y, _⟩ ⟩ : Jar n | ‖x‖ ≥ 1 - y / 2}


-- @@ L39-40 verbatim
/-- `closedCover` -/
def closedCover (n : ℕ) : Fin 2 → Set (Jar n) := ![mid n, rim n]


-- @@ L42-43 verbatim
lemma continuous_sub_div_two : Continuous fun (y : ℝ) ↦ 1 - y / 2 :=
  (continuous_sub_left _).comp <| continuous_mul_const _


-- @@ L45-48 verbatim
lemma isClosed_mid (n : ℕ) : IsClosed (mid n) :=
  continuous_iff_isClosed.mp (continuous_uliftDown.subtype_val.norm.prodMap continuous_id)
    {⟨x, y, _⟩ : ℝ × I | x ≤ 1 - y / 2} <| isClosed_le continuous_fst <|
    continuous_sub_div_two.comp <| continuous_subtype_val.comp continuous_snd


-- @@ L50-53 verbatim
lemma isClosed_rim (n : ℕ) : IsClosed (rim n) :=
  continuous_iff_isClosed.mp (continuous_uliftDown.subtype_val.norm.prodMap continuous_id)
    {⟨x, y, _⟩ : ℝ × I | x ≥ 1 - y / 2} <| isClosed_le
    (continuous_sub_div_two.comp <| continuous_subtype_val.comp continuous_snd) continuous_fst


-- @@ L55-70 verbatim
/-- `midProjToFun` -/
noncomputable def midProjToFun (n : ℕ) : mid.{u} n → disk.{u} n := fun p ↦ ⟨{
  -- Note: pattern matching is done inside `toFun` to make `Continuous.subtype_mk` work
  val := match p with
    | ⟨⟨ ⟨⟨x, _⟩⟩, ⟨y, _⟩ ⟩, _⟩ => (2 / (2 - y)) • x,
  property := by
    obtain ⟨⟨ ⟨⟨x, _⟩⟩, ⟨y, _, _⟩ ⟩, hxy⟩ := p
    dsimp only [Int.ofNat_eq_natCast, Set.coe_ofPred, Set.mem_ofPred_eq]
    rw [Metric.mem_closedBall]
    rw [dist_zero_right, norm_smul, norm_div, RCLike.norm_ofNat, Real.norm_eq_abs]
    have : 0 < |2 - y| := lt_of_le_of_ne (abs_nonneg _) (abs_ne_zero.mpr (by linarith)).symm
    rw [← le_div_iff₀' (div_pos (by norm_num) this), one_div, inv_div]
    have : |(2 : ℝ)| = 2  := by apply abs_eq_self.mpr; norm_num
    nth_rw 2 [← this]
    rw [← abs_div, sub_div, div_self (by norm_num), le_abs]
    exact Or.inl hxy }⟩


-- @@ L72-79 verbatim
lemma continuous_midProjToFun (n : ℕ) : Continuous (midProjToFun.{u} n) := by
  refine continuous_uliftUp.comp ?_
  refine Continuous.subtype_mk ?_ _
  have denominator_ne_zero : ∀ p : mid n, (2 : ℝ) - p.val.snd.val ≠ 0 := by
    rintro ⟨⟨_, ⟨y, _, _⟩⟩, _⟩
    dsimp
    linarith
  fun_prop (disch := exact denominator_ne_zero _)


-- @@ L81-83 verbatim
/-- `midProj` -/
noncomputable def midProj (n : ℕ) : C(mid.{u} n, disk.{u} n) :=
  ⟨midProjToFun.{u} n, continuous_midProjToFun n⟩


-- @@ L85-89 verbatim
lemma rim_fst_ne_zero (n : ℕ) : ∀ p : rim.{u} n, ‖p.val.fst.down.val‖ ≠ 0 :=
  fun ⟨⟨ ⟨⟨x, _⟩⟩, ⟨y, _, _⟩ ⟩, hxy⟩ ↦ by
    conv => lhs; arg 1; dsimp
    change ‖x‖ ≥ 1 - y / 2 at hxy
    linarith


-- @@ L91-99 verbatim
/-- `rimProjFstToFun` -/
noncomputable def rimProjFstToFun (n : ℕ) : rim.{u} n → diskBoundary.{u} n := fun p ↦ ⟨{
  val := match p with
    | ⟨⟨ ⟨⟨x, _⟩⟩, _ ⟩, _⟩ => (1 / ‖x‖) • x
  property := by
    obtain ⟨⟨ ⟨⟨x, _⟩⟩, ⟨y, yl, yr⟩ ⟩, hxy⟩ := p
    simp only [one_div, mem_sphere_iff_norm, sub_zero, norm_smul, norm_inv, norm_norm]
    change ‖x‖ ≥ 1 - y / 2 at hxy
    exact inv_mul_cancel₀ (by linarith) }⟩


-- @@ L101-105 verbatim
lemma continuous_rimProjFstToFun (n : ℕ) : Continuous (rimProjFstToFun.{u} n) := by
  refine continuous_uliftUp.comp ?_
  refine Continuous.subtype_mk ?_ _
  have denominator_ne_zero : ∀ p : rim n, ‖p.val.fst.down.val‖ ≠ 0 := rim_fst_ne_zero n
  fun_prop (disch := exact denominator_ne_zero _)


-- @@ L107-109 verbatim
/-- `rimProjFst` -/
noncomputable def rimProjFst (n : ℕ) : C(rim.{u} n, diskBoundary.{u} n) :=
  ⟨rimProjFstToFun.{u} n, continuous_rimProjFstToFun n⟩


-- @@ L111-125 verbatim
/-- `rimProjSndToFun` -/
noncomputable def rimProjSndToFun (n : ℕ) : rim.{u} n → I := fun p ↦ {
  val := match p with
    | ⟨⟨ ⟨⟨x, _⟩⟩, ⟨y, _⟩ ⟩, _⟩ => (y - 2) / ‖x‖ + 2
  property := by
    obtain ⟨⟨ ⟨⟨x, hx⟩⟩, ⟨y, _, _⟩ ⟩, hxy⟩ := p
    simp only [Set.mem_Icc]
    rw [Metric.mem_closedBall, dist_zero_right] at hx
    change ‖x‖ ≥ 1 - y / 2 at hxy
    have : ‖x‖ > 0 := by linarith
    constructor
    all_goals rw [← add_le_add_iff_right (-2)]
    · rw [← neg_le_neg_iff, add_neg_cancel_right, zero_add, neg_neg]
      rw [← neg_div, neg_sub, div_le_iff₀ (by assumption)]; linarith
    · rw [add_assoc, add_neg_cancel, add_zero, div_le_iff₀ (by assumption)]; linarith}


-- @@ L127-133 verbatim
lemma continuous_rimProjSndToFun (n : ℕ) : Continuous (rimProjSndToFun.{u} n) := by
  refine Continuous.subtype_mk ?_ _
  exact (continuous_add_const _).comp <| Continuous.div
    ((continuous_sub_right _).comp <| continuous_subtype_val.comp <|
      continuous_snd.comp <| continuous_subtype_val)
    (continuous_norm.comp <| continuous_subtype_val.comp <| continuous_uliftDown.comp <|
      continuous_fst.comp <| continuous_subtype_val) <| rim_fst_ne_zero n


-- @@ L135-137 verbatim
/-- `rimProjSnd` -/
noncomputable def rimProjSnd (n : ℕ) : C(rim.{u} n, I) :=
  ⟨rimProjSndToFun.{u} n, continuous_rimProjSndToFun n⟩


-- @@ L139-141 verbatim
/-- `rimProj` -/
noncomputable def rimProj (n : ℕ) : C(rim.{u} n, diskBoundary.{u} n × I) :=
  ContinuousMap.prodMk (rimProjFst.{u} n) (rimProjSnd.{u} n)


-- @@ L143-147 verbatim
/-- `proj` -/
noncomputable def proj (n : ℕ) {Y : Type v} [TopologicalSpace Y]
    (f : C(disk.{u} n, Y)) (H : C(diskBoundary.{u} n × I, Y)) :
    ∀ i, C(closedCover.{u} n i, Y) :=
  Fin.cons (f.comp (midProj.{u} n)) <| Fin.cons (H.comp (rimProj.{u} n)) finZeroElim


-- @@ L149-185 verbatim
lemma proj_compatible (n : ℕ) {Y : Type v} [TopologicalSpace Y]
    (f : C(disk.{u} n, Y)) (H : C(diskBoundary.{u} n × I, Y))
    (hf : f ∘ diskBoundaryIncl.{u} n = H ∘ (·, 0)) :
    ∀ (p : Jar.{u} n) (hp0 : p ∈ closedCover.{u} n 0) (hp1 : p ∈ closedCover.{u} n 1),
    proj n f H 0 ⟨p, hp0⟩ = proj n f H 1 ⟨p, hp1⟩ :=
  fun ⟨⟨⟨x, hx⟩⟩, ⟨y, hy0, hy1⟩⟩ hp0 hp1 ↦ by
    change f (midProj n _) = H (rimProj n _)
    change ‖x‖ ≤ 1 - y / 2 at hp0
    change ‖x‖ ≥ 1 - y / 2 at hp1
    have : ‖x‖ = 1 - y / 2 := by linarith only [hp0, hp1]
    let q : diskBoundary.{u} n := ⟨ (2 / (2 - y)) • x, by
      simp only [mem_sphere_iff_norm, sub_zero, norm_smul, norm_div, RCLike.norm_ofNat,
        Real.norm_eq_abs]
      rw [this, abs_of_pos (by linarith), div_mul_eq_mul_div, div_eq_iff (by linarith)]
      rw [mul_sub, mul_one, ← mul_comm_div, div_self (by norm_num), one_mul, one_mul] ⟩
    have hmid :
        midProj n ⟨(⟨⟨x, hx⟩⟩, ⟨y, hy0, hy1⟩), hp0⟩ = diskBoundaryIncl n q := by
      apply ULift.ext
      apply Subtype.ext
      rfl
    have hrim :
        rimProj n ⟨(⟨⟨x, hx⟩⟩, ⟨y, hy0, hy1⟩), hp1⟩ = (q, 0) := by
      apply Prod.ext
      · change rimProjFst n _ = q
        apply ULift.ext
        apply Subtype.ext
        change (1 / ‖x‖) • x = (2 / (2 - y)) • x
        rw [this]
        field_simp
      · change rimProjSnd n _ = (0 : I)
        apply Subtype.ext
        change (y - 2) / ‖x‖ + 2 = 0
        rw [this, ← eq_sub_iff_add_eq, zero_sub, div_eq_iff (by linarith), mul_sub, mul_one]
        rw [mul_div, mul_div_right_comm, neg_div_self (by norm_num), ← neg_eq_neg_one_mul]
        rw [sub_neg_eq_add, add_comm]
        ring
    exact (congrArg f hmid).trans <| (congrFun hf q).trans (congrArg H hrim).symm


-- @@ L187-196 verbatim
lemma proj_compatible' (n : ℕ) {Y : Type v} [TopologicalSpace Y]
    (f : C(disk.{u} n, Y)) (H : C(diskBoundary.{u} n × I, Y))
    (hf : f ∘ diskBoundaryIncl.{u} n = H ∘ (·, 0)) :
    ∀ (i j) (p : Jar.{u} n) (hpi : p ∈ closedCover.{u} n i)
      (hpj : p ∈ closedCover.{u} n j),
    proj n f H i ⟨p, hpi⟩ = proj n f H j ⟨p, hpj⟩ := by
  intro ⟨i, hi⟩ ⟨j, hj⟩ p hpi hpj
  interval_cases i <;> (interval_cases j <;> (try simp only [Fin.zero_eta, Fin.mk_one]))
  · exact proj_compatible n f H hf p hpi hpj
  · exact Eq.symm <| proj_compatible n f H hf p hpj hpi


-- @@ L198-203 verbatim
lemma closedCover_isCover (n : ℕ) :
    ∀ (p : Jar.{u} n), ∃ i, p ∈ closedCover.{u} n i :=
  fun ⟨⟨x, _⟩, ⟨y, _⟩⟩ ↦ by
    by_cases h : ‖x‖ ≤ 1 - y / 2
    · use 0; exact h
    · use 1; change ‖x‖ ≥ 1 - y / 2; linarith


-- @@ L205-208 verbatim
lemma closedCover_isClosed (n : ℕ) :
    ∀ i, IsClosed (closedCover.{u} n i) := fun ⟨i, hi⟩ ↦ by
  interval_cases i
  exacts [isClosed_mid n, isClosed_rim n]


-- @@ L210-217 verbatim
/-- `homotopyExtension` -/
noncomputable def homotopyExtension (n : ℕ) {Y : Type v} [TopologicalSpace Y]
    (f : C(disk.{u} n, Y)) (H : C(diskBoundary.{u} n × I, Y))
    (hf : f ∘ diskBoundaryIncl.{u} n = H ∘ (·, 0)) : C(Jar.{u} n, Y) :=
  ContinuousMap.liftCoverClosed (closedCover n) (proj n f H) (proj_compatible' n f H hf)
    (closedCover_isCover n) (closedCover_isClosed n)

-- The triangle involving the bottom (i.e., `𝔻 (n + 1)`) of the jar commutes.

-- @@ L218-236 verbatim
lemma homotopyExtension_bottom_commutes (n : ℕ) {Y : Type v} [TopologicalSpace Y]
    (f : C(disk.{u} n, Y)) (H : C(diskBoundary.{u} n × I, Y))
    (hf : f ∘ diskBoundaryIncl.{u} n = H ∘ (·, 0)) :
    ⇑f = homotopyExtension n f H hf ∘ (·, 0) := by
  ext p
  change _ = homotopyExtension n f H hf (p, 0)
  have hp : (p, 0) ∈ closedCover n 0 := by
    obtain ⟨x, hx⟩ := p
    change ‖x‖ ≤ 1 - 0 / 2
    simp_all
  conv_rhs => equals (proj n f H 0) ⟨(p, 0), hp⟩ => apply ContinuousMap.liftCoverClosed_coe'
  simp only [proj, Fin.succ_zero_eq_one, Fin.cons_zero]
  change f p = f (midProj n ⟨(p, 0), hp⟩)
  apply congrArg f
  obtain ⟨x, hx⟩ := p
  apply ULift.ext
  simp [midProj, midProjToFun]

-- The triangle involving the wall (i.e., `𝕊 n × I`) of the jar commutes.

-- @@ L237-263 verbatim
lemma homotopyExtension_wall_commutes (n : ℕ) {Y : Type v} [TopologicalSpace Y]
    (f : C(disk.{u} n, Y)) (H : C(diskBoundary.{u} n × I, Y))
    (hf : f ∘ diskBoundaryIncl.{u} n = H ∘ (·, 0)) :
    ⇑H = homotopyExtension n f H hf ∘ Prod.map (diskBoundaryIncl n) id := by
  ext ⟨⟨x, hx⟩, ⟨y, hy⟩⟩
  let q := diskBoundaryIncl n ⟨x, hx⟩
  change _ = homotopyExtension n f H hf ⟨q, ⟨y, hy⟩⟩
  have hq : ⟨q, ⟨y, hy⟩⟩ ∈ closedCover n 1 := by
    change ‖x‖ ≥ 1 - y / 2
    rw [mem_sphere_zero_iff_norm.mp hx]
    obtain ⟨_, _⟩ := hy
    linarith
  conv_rhs => equals (proj n f H 1) ⟨⟨q, ⟨y, hy⟩⟩, hq⟩ => apply ContinuousMap.liftCoverClosed_coe'
  change H _ = (H.comp (rimProj n)) ⟨(q, ⟨y, hy⟩), hq⟩
  rw [ContinuousMap.comp_apply]
  have hrim : rimProj n ⟨(q, ⟨y, hy⟩), hq⟩ = (⟨x, hx⟩, ⟨y, hy⟩) := by
    apply Prod.ext
    · change rimProjFst n _ = { down := ⟨x, hx⟩ }
      apply ULift.ext
      apply Subtype.ext
      change (1 / ‖x‖) • x = x
      rw [mem_sphere_zero_iff_norm.mp hx, div_one, one_smul]
    · change rimProjSnd n _ = (⟨y, hy⟩ : I)
      apply Subtype.ext
      change (y - 2) / ‖x‖ + 2 = y
      rw [mem_sphere_zero_iff_norm.mp hx, div_one, sub_add_cancel]
  rw [hrim]


-- @@ L265-265 verbatim
end Jar


-- @@ L267-267 verbatim
end HEP
