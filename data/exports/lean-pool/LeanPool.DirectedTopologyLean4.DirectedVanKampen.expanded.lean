/-
Copyright (c) 2026 Dominique Lawson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dominique Lawson, Henning Basold, Peter Bruin
-/
module

public import LeanPool.DirectedTopologyLean4.DihomotopyCover
public import LeanPool.DirectedTopologyLean4.DipathSubtype
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.Defs
import LeanPool.DirectedTopologyLean4.DihomotopyToPathDihomotopy
import LeanPool.DirectedTopologyLean4.FractionEqualities
import LeanPool.DirectedTopologyLean4.MorphismAux
import LeanPool.DirectedTopologyLean4.PushoutAlternative
import LeanPool.DirectedTopologyLean4.SplitPath.SplitProperties


-- @@ L17-19 verbatim
/-!
# LeanPool.DirectedTopologyLean4.DirectedVanKampen
-/


-- @@ L21-43 verbatim
@[expose] public section

/-
  This file contains the directed version of the Van Kampen Theorem.
  The statement is as follows:
  Let `X : dTopCat` and `X₁ X₂ : Set X` such that `X₁` and `X₂` are
  both open and `X₁ ∪ X₂ = X`.
  Let `i₁ : X₁ ∩ X₂ → X₁`, `i₂ : X₁ ∩ X₂ → X₂`, `j₁ : X₁ → X`
  and `j₂ : X₂ → X` be the inclusion maps in `dTopCat`.
  Then we have a pushout in `Cat`:
  dπₓ(X₁ ∩ X₂) ------ dπₘ i₁ -----> dπₓ(X₁)
       |                              |
       |                              |
       |                              |
     dπₘ i₂                         dπₘ j₁
       |                              |
       |                              |
       |                              |
    dπₓ(X₂) ------- dπₘ j₂ ------> dπₓ(X)
  The proof we give is constructive and is based on the proof given by
  Marco Grandis, Directed Homotopy Theory I, published in Cahiers de topologie
  et géométrie différentielle catégoriques, 44, no. 4, pages 307-309, 2003.
-/

-- @@ L44-44 verbatim
universe u v

-- @@ L45-45 verbatim
open Set

-- @@ L46-46 verbatim
open scoped unitInterval FundamentalCategory

-- @@ L47-50 verbatim
attribute [local instance] Dipath.Dihomotopic.setoid
-- Lean 4.33 checks transformed tactic goals at `.implicit` transparency. The reducibility
-- exceptions below are attached to individual declarations so they do not expose internals
-- elsewhere.

-- @@ L51-51 verbatim
noncomputable section

-- @@ L52-52 verbatim
namespace DirectedVanKampen

-- @@ L53-53 verbatim
open FundamentalCategory DiSubtype CategoryTheory

-- @@ L54-54 verbatim
attribute [local instance] Classical.propDecidable

-- @@ L55-55 verbatim
variable {X : dTopCat.{u}} {X₁ X₂ : Set X}

-- @@ L56-56 verbatim
variable (hX : X₁ ∪ X₂ = Set.univ)

-- @@ L57-59 verbatim
variable (X₁_open : IsOpen X₁) (X₂_open : IsOpen X₂)
-- We will use a shorthand notation for the 4 morphisms in dTop:
-- i₁ : X₁ ∩ X₂ ⟶ X₁

-- @@ L60-61 verbatim
local notation "i₁" => dTopCat.DirectedSubsetHom <| Set.inter_subset_left (s := X₁) (t := X₂)
-- i₂ : X₁ ∩ X₂ ⟶ X₂

-- @@ L62-63 verbatim
local notation "i₂" => dTopCat.DirectedSubsetHom <| Set.inter_subset_right (s := X₁) (t := X₂)
-- j₁ : X₁ ⟶ X

-- @@ L64-65 verbatim
local notation "j₁" => dTopCat.DirectedSubtypeHom X₁
-- j₂ : X₂ ⟶ X

-- @@ L66-66 verbatim
local notation "j₂" => dTopCat.DirectedSubtypeHom X₂

-- @@ L67-67 verbatim
namespace PushoutFunctor

-- @@ L68-68 verbatim
open Dipath Dipath.covered Dipath.coveredPartwise

-- @@ L69-69 verbatim
variable {x y : X} {C : CategoryTheory.Cat.{u, u}}

-- @@ L70-70 expanded
variable (F₁ : (FundamentalCategory.fundamentalCategoryFunctor.obj (dTopCat.of X₁) ⥤ C))
  (F₂ : (FundamentalCategory.fundamentalCategoryFunctor.obj (dTopCat.of X₂) ⥤ C))


-- @@ L71-74 expanded
variable
  (h_comm :
    (FundamentalCategory.fundamentalCategoryMap
          (dTopCat.DirectedSubsetHom <| Set.inter_subset_left (s := X₁) (t := X₂))) ⋙
        F₁ =
      ((FundamentalCategory.fundamentalCategoryMap
          (dTopCat.DirectedSubsetHom <| Set.inter_subset_right (s := X₁) (t := X₂))) ⋙
        F₂))


-- @@ L75-75 verbatim
open CategoryTheory

-- @@ L76-76 expanded
variable {Y : dTopCat.{u}} {Y₀ : Set Y}
  {F : FundamentalCategory.fundamentalCategoryFunctor.obj (dTopCat.of Y₀) ⥤ C}


-- @@ L77-83 verbatim
lemma subset_functor_trans {x y z : Y} {γ₁ : Dipath x y} {γ₂ : Dipath y z}
    (hγ : range (γ₁.trans γ₂) ⊆ Y₀) :
    (F.map ⟦SubtypeDipath γ₁ (subsets_of_trans_subset hγ).1⟧ ≫
      F.map ⟦SubtypeDipath γ₂ (subsets_of_trans_subset hγ).2⟧) =
      F.map ⟦SubtypeDipath (γ₁.trans γ₂) hγ⟧ := by
  refine (F.map_comp _ _).symm.trans (congrArg (fun p => F.map p) ?_)
  exact congrArg _ (subtype_trans hγ)

-- @@ L84-93 expanded
lemma subset_functor_reparam {x y : Y} {γ : Dipath x y} (hγ : range γ ⊆ Y₀) {f : DirectedMap I I}
    (hf₀ : f 0 = 0) (hf₁ : f 1 = 1) :
    F.map
        ⟦SubtypeDipath (γ.reparam f hf₀ hf₁)
            (show range (γ.reparam f hf₀ hf₁) ⊆ Y₀ by
              exact (Dipath.range_reparam γ f hf₀ hf₁).symm ▸ hγ)⟧ =
      F.map ⟦SubtypeDipath γ hγ⟧ :=
  by
  congr 1
  rw [subtype_reparam hγ hf₀ hf₁]
  symm
  exact Quotient.eq.mpr (Dipath.Dihomotopic.reparam (SubtypeDipath γ hγ) f hf₀ hf₁)


-- @@ L94-101 expanded
lemma functor_cast {X : dTopCat} (F : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C)
    {x y x' y' : X} (γ : Dipath x y) (hx : x' = x) (hy : y' = y) :
    F.map ⟦γ.cast hx hy⟧ =
      (eqToHom (congrArg F.obj (congrArg FundamentalCategory.mk hx))) ≫
        F.map ⟦γ⟧ ≫ (eqToHom (congrArg F.obj (congrArg FundamentalCategory.mk hy)).symm) :=
  by
  subst_vars
  simp
  congr 2


-- @@ L102-116 expanded
@[simp]
lemma functor_cast_heq {X : dTopCat}
    (F : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C) {x y x' y' : X}
    (γ : Dipath x y) (hx : x' = x) (hy : y' = y) : F.map ⟦γ.cast hx hy⟧ ≍ F.map ⟦γ⟧ :=
  by
  cases hx
  cases hy
  rfl
    /-
      Given a category `C` and functors `F₁ : dπₓ X₁ ⥤ C` and
      `F₂ : dπₓ X₂ ⥤ C`, construct a functor `F : dπₓ X ⥤ C`.
    -/
    /- ### Functor on Objects -/
    /-
    - Define the behaviour on objects
    -/


-- @@ L117-123 expanded
/-- Object map for the pushout functor induced by the covering square. -/
def FunctorOnObj (x : FundamentalCategory.fundamentalCategoryFunctor.obj X) : C :=
  Or.by_cases ((Set.mem_union x.as X₁ X₂).mp (Filter.mem_top.mpr hX x.as))
    (fun hx => F₁.obj ⟨x.as, hx⟩)
    (fun hx => F₂.obj ⟨x.as, hx⟩)
      -- We will use the shorhand notation F_obj


-- @@ L124-127 verbatim
local notation "F_obj" => FunctorOnObj hX F₁ F₂
/-
  Under the assumption that the square commutes, we can show how the functor behaves on objects
-/

-- @@ L128-128 verbatim
variable {F₁ F₂}

-- @@ L129-129 verbatim
include h_comm

-- @@ L130-133 verbatim
lemma functorOnObj_apply_one {x : X} (hx : x ∈ X₁) : F₁.obj ⟨x, hx⟩ = F_obj ⟨x⟩ := by
  have := h_comm
  refine Eq.symm ?_
  exact dite_eq_left hx

-- @@ L134-158 expanded
lemma functorOnObj_apply_two {x : X} (hx₂ : x ∈ X₂) : F₂.obj ⟨x, hx₂⟩ = F_obj ⟨x⟩ :=
  by
  by_cases hx₁ : x ∈ X₁
  case pos =>
    have hx₀ : x ∈ X₁ ∩ X₂ := ⟨hx₁, hx₂⟩
    have :
      F₁.obj ((FundamentalCategory.fundamentalCategoryMap i₁).obj ⟨x, hx₀⟩) =
        F₂.obj ((FundamentalCategory.fundamentalCategoryMap i₂).obj ⟨x, hx₀⟩) :=
      show
        ((FundamentalCategory.fundamentalCategoryMap i₁) ⋙ F₁).obj ⟨x, hx₀⟩ =
          ((FundamentalCategory.fundamentalCategoryMap i₂) ⋙ F₂).obj ⟨x, hx₀⟩
        by rw [h_comm]
    have : F₁.obj ⟨x, hx₁⟩ = F₂.obj (⟨x, hx₂⟩) :=
      calc
        F₁.obj ⟨x, hx₁⟩
        _ = F₁.obj ((FundamentalCategory.fundamentalCategoryMap i₁).obj ⟨x, hx₀⟩) := rfl
        _ = F₂.obj ((FundamentalCategory.fundamentalCategoryMap i₂).obj ⟨x, hx₀⟩) := this
        _ = F₂.obj (⟨x, hx₂⟩) := rfl
    rw [this.symm]
    refine Eq.symm ?_
    exact dite_eq_left hx₁
  case neg =>
    refine Eq.symm ?_
    exact dite_eq_right hx₁


-- @@ L159-164 verbatim
/-- Map assigned to a path whose image lies in the first open subset. -/
def FunctorOnHomOfCoveredAux₁ {γ : Dipath x y} (hγ : range γ ⊆ X₁) :
    F_obj ⟨x⟩ ⟶ F_obj ⟨y⟩ :=
  (eqToHom (functorOnObj_apply_one hX h_comm (source_elt_of_image_subset hγ)).symm) ≫
  (F₁.map ⟦SubtypeDipath γ hγ⟧) ≫
  (eqToHom (functorOnObj_apply_one hX h_comm (target_elt_of_image_subset hγ)))

-- @@ L165-173 verbatim
/-- Map assigned to a path whose image lies in the second open subset. -/
def FunctorOnHomOfCoveredAux₂ {γ : Dipath x y} (hγ : range γ ⊆ X₂) :
    F_obj ⟨x⟩ ⟶ F_obj ⟨y⟩ :=
  (eqToHom (functorOnObj_apply_two hX h_comm (source_elt_of_image_subset hγ)).symm) ≫
  (F₂.map ⟦SubtypeDipath γ hγ⟧) ≫
  (eqToHom (functorOnObj_apply_two hX h_comm (target_elt_of_image_subset hγ)))
/-
  Show that these maps respect composition of paths
-/

-- @@ L174-183 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor in
lemma functorOnHomOfCoveredAux₁_trans {x y z : X} {γ₁ : Dipath x y}
    {γ₂ : Dipath y z} (hγ : range (γ₁.trans γ₂) ⊆ X₁) :
    FunctorOnHomOfCoveredAux₁ hX h_comm hγ =
      FunctorOnHomOfCoveredAux₁ hX h_comm (subsets_of_trans_subset hγ).1
      ≫ FunctorOnHomOfCoveredAux₁ hX h_comm (subsets_of_trans_subset hγ).2 := by
  unfold FunctorOnHomOfCoveredAux₁
  rw [(subset_functor_trans hγ).symm]
  simp

-- @@ L184-196 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor in
lemma functorOnHomOfCoveredAux₂_trans {x y z : X} {γ₁ : Dipath x y}
    {γ₂ : Dipath y z} (hγ : range (γ₁.trans γ₂) ⊆ X₂) :
    FunctorOnHomOfCoveredAux₂ hX h_comm hγ =
      FunctorOnHomOfCoveredAux₂ hX h_comm (subsets_of_trans_subset hγ).1
      ≫ FunctorOnHomOfCoveredAux₂ hX h_comm (subsets_of_trans_subset hγ).2 := by
  unfold FunctorOnHomOfCoveredAux₂
  rw [(subset_functor_trans hγ).symm]
  simp
/-
 Show that the maps respect reparametrization of paths
-/

-- @@ L197-202 expanded
lemma functorOnHomOfCoveredAux₁_reparam {x y : X} {γ : Dipath x y} (hγ : range γ ⊆ X₁)
    {f : DirectedMap I I} (hf₀ : f 0 = 0) (hf₁ : f 1 = 1) :
    FunctorOnHomOfCoveredAux₁ hX h_comm hγ =
      FunctorOnHomOfCoveredAux₁ hX h_comm (reparam_subset_of_subset hγ hf₀ hf₁) :=
  by
  unfold FunctorOnHomOfCoveredAux₁
  rw [subset_functor_reparam hγ hf₀ hf₁]


-- @@ L203-211 expanded
lemma functorOnHomOfCoveredAux₂_reparam {x y : X} {γ : Dipath x y} (hγ : range γ ⊆ X₂)
    {f : DirectedMap I I} (hf₀ : f 0 = 0) (hf₁ : f 1 = 1) :
    FunctorOnHomOfCoveredAux₂ hX h_comm hγ =
      FunctorOnHomOfCoveredAux₂ hX h_comm (reparam_subset_of_subset hγ hf₀ hf₁) :=
  by
  unfold FunctorOnHomOfCoveredAux₂
  rw [subset_functor_reparam hγ hf₀ hf₁]
    /-
     Show that the maps respect reparametrization of paths
    -/


-- @@ L212-220 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor in
lemma functorOnHomOfCoveredAux₁_refl {x : X} (hx : x ∈ X₁) :
  FunctorOnHomOfCoveredAux₁ hX h_comm (range_refl_subset_of_mem hx) = 𝟙 (F_obj ⟨x⟩) := by
  unfold FunctorOnHomOfCoveredAux₁
  rw [subtype_refl]
  change eqToHom _ ≫ F₁.map (𝟙 ⟨x, hx⟩) ≫ eqToHom _ = 𝟙 (F_obj ⟨x⟩)
  rw [F₁.map_id]
  simp

-- @@ L221-232 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor in
lemma functorOnHomOfCoveredAux₂_refl {x : X} (hx : x ∈ X₂) :
  FunctorOnHomOfCoveredAux₂ hX h_comm (range_refl_subset_of_mem hx) = 𝟙 (F_obj ⟨x⟩) := by
  unfold FunctorOnHomOfCoveredAux₂
  rw [subtype_refl]
  change eqToHom _ ≫ F₂.map (𝟙 ⟨x, hx⟩) ≫ eqToHom _ = 𝟙 (F_obj ⟨x⟩)
  rw [F₂.map_id]
  simp
/-
  Show that for any path living in `X₁ ∩ X₂`, either map gives the same result.
-/

-- @@ L233-247 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor in
lemma functorOnHomOfCoveredAux_equal {γ : Dipath x y} (hγ₁ : range γ ⊆ X₁)
    (hγ₂ : range γ ⊆ X₂) :
    FunctorOnHomOfCoveredAux₁ hX h_comm hγ₁ =
      FunctorOnHomOfCoveredAux₂ hX h_comm hγ₂ := by
  unfold FunctorOnHomOfCoveredAux₁ FunctorOnHomOfCoveredAux₂
  have hγ₀ : range γ ⊆ X₁ ∩ X₂ := subset_inter hγ₁ hγ₂
  apply (eqToHom_comp_iff _ _ _).mpr
  apply (comp_eqToHom_iff _ _ _).mpr
  simp only [dTopCat.coe_of, eqToHom_trans_assoc, Category.assoc, eqToHom_trans]
  exact map_eq_map_of_eq h_comm ⟦SubtypeDipath γ hγ₀⟧
/-
- ### Define the mapping behaviour on covered paths
-/

-- @@ L248-253 verbatim
/-- Map assigned to a path covered by one of the two open subsets. -/
def FunctorOnHomOfCovered {γ : Dipath x y} (hγ : covered hX γ) :
    F_obj ⟨x⟩ ⟶ F_obj ⟨y⟩ :=
  Or.by_cases hγ
    (fun hγ => FunctorOnHomOfCoveredAux₁ hX h_comm hγ)
    (fun hγ => FunctorOnHomOfCoveredAux₂ hX h_comm hγ)

-- @@ L254-254 verbatim
local notation "F₀" => FunctorOnHomOfCovered hX h_comm

-- @@ L255-256 verbatim
lemma functorOnHomOfCovered_apply_left {γ : Dipath x y} (hγ : range γ ⊆ X₁) :
    F₀ (Or.inl hγ) = FunctorOnHomOfCoveredAux₁ hX h_comm hγ := dite_eq_left hγ

-- @@ L257-259 verbatim
lemma functorOnHomOfCovered_apply_left' {γ : Dipath x y} (hγ : range γ ⊆ X₁) :
    F₀ (covered_partwise_of_covered 0 (Or.inl hγ)) = FunctorOnHomOfCoveredAux₁ hX h_comm hγ :=
  functorOnHomOfCovered_apply_left _ _ _

-- @@ L260-265 verbatim
lemma functorOnHomOfCovered_apply_right {γ : Dipath x y} (hγ : range γ ⊆ X₂) :
    F₀ (Or.inr hγ) = FunctorOnHomOfCoveredAux₂ hX h_comm hγ := by
  by_cases hγ₁ : range γ ⊆ X₁
  · rw [functorOnHomOfCovered_apply_left hX h_comm hγ₁]
    exact functorOnHomOfCoveredAux_equal hX h_comm hγ₁ hγ
  · apply dite_eq_right hγ₁

-- @@ L266-268 verbatim
lemma functorOnHomOfCovered_equal {γ₁ γ₂ : Dipath x y} (h : γ₁ = γ₂)
    (hγ₁ : covered hX γ₁) (hγ₂ : covered hX γ₂) :
    F₀ hγ₁ = F₀ hγ₂ := by subst_vars; rfl

-- @@ L269-280 verbatim
lemma functorOnHomOfCovered_heq_of_ext {x y x' y' : X} {γ : Dipath x y}
    {γ' : Dipath x' y'} (h : ∀ t, γ t = γ' t) (hγ : covered hX γ)
    (hγ' : covered hX γ') :
    F₀ hγ ≍ F₀ hγ' := by
  have hx : x = x' := by simpa using h 0
  have hy : y = y' := by simpa using h 1
  subst hx
  subst hy
  have hγ_eq : γ = γ' := by
    ext t
    exact h t
  simp_all

-- @@ L281-288 verbatim
lemma functorOnHomOfCovered_refl : F₀ (covered_refl x hX) = 𝟙 (F_obj ⟨x⟩) := by
  cases ((Set.mem_union x X₁ X₂).mp (Filter.mem_top.mpr hX x))
  case inl hx₁ =>
    rw [←functorOnHomOfCoveredAux₁_refl hX h_comm hx₁]
    exact functorOnHomOfCovered_apply_left hX h_comm (DiSubtype.range_refl_subset_of_mem hx₁)
  case inr hx₂ =>
    rw [←functorOnHomOfCoveredAux₂_refl hX h_comm hx₂]
    exact functorOnHomOfCovered_apply_right hX h_comm (DiSubtype.range_refl_subset_of_mem hx₂)

-- @@ L289-291 verbatim
lemma functorOnHomOfCovered_apply_right' {γ : Dipath x y} (hγ : range γ ⊆ X₂) :
    F₀ (covered_partwise_of_covered 0 (Or.inr hγ)) = FunctorOnHomOfCoveredAux₂ hX h_comm hγ :=
  functorOnHomOfCovered_apply_right _ _ _

-- @@ L292-306 verbatim
lemma functorOnHomOfCovered_trans {x y z : X} {γ₁ : Dipath x y} {γ₂ : Dipath y z}
    (hγ : covered hX (γ₁.trans γ₂)) :
    F₀ hγ =
      (F₀ (covered_of_covered_trans hγ).1) ≫ (F₀ (covered_of_covered_trans hγ).2) := by
  cases hγ
  case inl hγ => -- γ is covered by X₁
    rw [functorOnHomOfCovered_apply_left _ _ hγ, functorOnHomOfCoveredAux₁_trans]
    congr
    · exact (functorOnHomOfCovered_apply_left _ _ _).symm
    · exact (functorOnHomOfCovered_apply_left _ _ _).symm
  case inr hγ => -- γ is covered by X₂
    rw [functorOnHomOfCovered_apply_right _ _ hγ, functorOnHomOfCoveredAux₂_trans]
    congr
    · exact (functorOnHomOfCovered_apply_right _ _ (subsets_of_trans_subset hγ).1).symm
    · exact (functorOnHomOfCovered_apply_right _ _ (subsets_of_trans_subset hγ).2).symm

-- @@ L307-326 expanded
lemma functorOnHomOfCovered_reparam {x y : X} {γ : Dipath x y} (hγ : covered hX γ)
    {f : DirectedMap I I} (hf₀ : f 0 = 0) (hf₁ : f 1 = 1) :
    F₀ hγ = F₀ ((covered_reparam_iff γ hX f hf₀ hf₁).mp hγ) :=
  by
  cases hγ
  case inl
    hγ =>
    have : range (γ.reparam f hf₀ hf₁) ⊆ X₁ :=
      by
      rw [Dipath.range_reparam γ f hf₀ hf₁]
      exact hγ
    rw [functorOnHomOfCovered_apply_left]
    · rw [functorOnHomOfCoveredAux₁_reparam hX h_comm hγ hf₀ hf₁]
      rw [← functorOnHomOfCovered_apply_left hX h_comm this]
    · exact hγ
  case inr
    hγ =>
    have : range (γ.reparam f hf₀ hf₁) ⊆ X₂ :=
      by
      rw [Dipath.range_reparam γ f hf₀ hf₁]
      exact hγ
    rw [functorOnHomOfCovered_apply_right]
    · rw [functorOnHomOfCoveredAux₂_reparam hX h_comm hγ hf₀ hf₁]
      rw [← functorOnHomOfCovered_apply_right hX h_comm this]
    · exact hγ


-- @@ L327-334 verbatim
attribute [local implicit_reducible] Dipath.cast in
lemma functorOnHomOfCovered_cast {x y x' y' : X} {γ : Dipath x y}
    (hγ : covered hX γ) (hx : x' = x) (hy : y' = y) :
    F₀ ((covered_cast_iff γ hX hx hy).mp hγ) =
      (eqToHom (show F_obj ⟨x'⟩ = F_obj ⟨x⟩ by rw [hx])) ≫
      (F₀ hγ) ≫ (eqToHom (show F_obj ⟨y⟩ = F_obj ⟨y'⟩ by rw [hy])) := by
  subst_vars
  rw [eqToHom_refl, eqToHom_refl, Category.comp_id, Category.id_comp]

-- @@ L335-341 verbatim
attribute [local implicit_reducible] Dipath.cast in
lemma functorOnHomOfCovered_cast_left {x y x' : X} {γ : Dipath x y}
    (hγ : covered hX γ) (hx : x' = x) :
    F₀ ((covered_cast_iff γ hX hx rfl).mp hγ) =
      (eqToHom (show F_obj ⟨x'⟩ = F_obj ⟨x⟩ by rw [hx])) ≫ (F₀ hγ) := by
  subst_vars
  rw [eqToHom_refl, Category.id_comp]

-- @@ L342-348 verbatim
attribute [local implicit_reducible] Dipath.cast in
lemma functorOnHomOfCovered_cast_right {x y y' : X} {γ : Dipath x y}
    (hγ : covered hX γ) (hy : y' = y) :
  F₀ ((covered_cast_iff γ hX rfl hy).mp hγ) =
    (F₀ hγ) ≫ (eqToHom (show F_obj ⟨y⟩ = F_obj ⟨y'⟩ by rw [hy])) := by
  subst_vars
  rw [eqToHom_refl, Category.comp_id]

-- @@ L349-362 verbatim
lemma functorOnHomOfCovered_split_comp {x y : X} {γ : Dipath x y}
    (hγ : covered hX γ) {T : I} (hT₀ : 0 < T) (hT₁ : T < 1) :
    F₀ hγ =
      (F₀ (covered_split_path hT₀ hT₁ hγ).1) ≫
        (F₀ (covered_split_path hT₀ hT₁ hγ).2) := by
  have : covered hX ((SplitDipath.FirstPart γ T).trans (SplitDipath.SecondPart γ T)) := by
    rw [SplitDipath.first_trans_second_reparam_eq_self γ hT₀ hT₁] at hγ
    exact (covered_reparam_iff _ hX _ _ _).mpr hγ
  rw [←functorOnHomOfCovered_trans hX h_comm this]
  rw [functorOnHomOfCovered_reparam hX h_comm this
      (SplitDipath.trans_reparam_map_zero hT₀ hT₁)
      (SplitDipath.trans_reparam_map_one hT₀ hT₁)]
  congr
  apply SplitDipath.first_trans_second_reparam_eq_self

-- @@ L363-390 verbatim
lemma functorOnHomOfCovered_dihomotopic {x y : X} {γ γ' : Dipath x y} {F : Dihomotopy γ γ'}
  (hF : Dipath.Dihomotopy.covered hX F) :
    F₀ (Dipath.Dihomotopy.covered_left_of_covered hF) =
      F₀ (Dipath.Dihomotopy.covered_right_of_covered hF) := by
  cases hF
  case inl hF =>
    have hγ := subset_trans (Dipath.Dihomotopy.range_left_subset F) hF
    have hγ' := subset_trans (Dipath.Dihomotopy.range_right_subset F) hF
    rw [functorOnHomOfCovered_equal hX h_comm rfl _ (Or.inl hγ),
      functorOnHomOfCovered_equal hX h_comm rfl _ (Or.inl hγ'),
      functorOnHomOfCovered_apply_left hX h_comm hγ,
      functorOnHomOfCovered_apply_left hX h_comm hγ']
    unfold FunctorOnHomOfCoveredAux₁
    rw [show F₁.map ⟦SubtypeDipath γ hγ⟧ = F₁.map ⟦SubtypeDipath γ' hγ'⟧ from
      congrArg F₁.map (dihomSubtype_of_dihom_range_subset hγ hγ' hF)]
  case inr hF =>
    have hγ := subset_trans (Dipath.Dihomotopy.range_left_subset F) hF
    have hγ' := subset_trans (Dipath.Dihomotopy.range_right_subset F) hF
    rw [functorOnHomOfCovered_equal hX h_comm rfl _ (Or.inr hγ),
      functorOnHomOfCovered_equal hX h_comm rfl _ (Or.inr hγ'),
      functorOnHomOfCovered_apply_right hX h_comm hγ,
      functorOnHomOfCovered_apply_right hX h_comm hγ']
    unfold FunctorOnHomOfCoveredAux₂
    rw [show F₂.map ⟦SubtypeDipath γ hγ⟧ = F₂.map ⟦SubtypeDipath γ' hγ'⟧ from
      congrArg F₂.map (dihomSubtype_of_dihom_range_subset hγ hγ' hF)]
/-
-  ### Define the behaviour on partwise covered paths
-/

-- @@ L391-397 verbatim
/-- Recursive map on paths split into finitely many covered pieces. -/
def FunctorOnHomOfCoveredPartwiseAux {n : ℕ} :
    ∀ (x y : X) (γ : Dipath x y) (_ : coveredPartwise hX γ n),
      F_obj ⟨x⟩ ⟶ F_obj ⟨y⟩ :=
  Nat.recOn n
    (fun _ _ _ hγ => F₀ hγ)
    (fun _ ih _ _ _ hγ => (F₀ hγ.1) ≫ (ih _ _ _ hγ.2))

-- @@ L398-401 verbatim
/-- Map assigned to a path equipped with a proof that it is piecewise covered. -/
abbrev FunctorOnHomOfCoveredPartwise {n : ℕ} {x y : X} {γ : Dipath x y}
    (hγ : coveredPartwise hX γ n) :=
  FunctorOnHomOfCoveredPartwiseAux hX h_comm x y γ hγ

-- @@ L402-402 verbatim
local notation "Fₙ" => FunctorOnHomOfCoveredPartwise hX h_comm

-- @@ L403-405 verbatim
lemma functorOnHomOfCoveredPartwise_apply_0 {x y : X} {γ : Dipath x y}
    (hγ : coveredPartwise hX γ 0) :
    Fₙ hγ = F₀ hγ := rfl

-- @@ L406-408 verbatim
lemma functorOnHomOfCoveredPartwise_apply_succ {n : ℕ} {x y : X}
    {γ : Dipath x y} (hγ : coveredPartwise hX γ n.succ) :
    Fₙ hγ = (F₀ hγ.left) ≫ (Fₙ hγ.right) := rfl

-- @@ L409-411 verbatim
lemma functorOnHomOfCoveredPartwise_equal {n : ℕ} {γ₁ γ₂ : Dipath x y} (h : γ₁ = γ₂)
  (hγ₁ : coveredPartwise hX γ₁ n) (hγ₂ : coveredPartwise hX γ₂ n) :
    Fₙ hγ₁ = Fₙ hγ₂ := by subst_vars; rfl

-- @@ L412-415 verbatim
lemma functorOnHomOfCoveredPartwise_equal' {n m : ℕ} {γ₁ γ₂ : Dipath x y}
    (h₁ : γ₁ = γ₂)
  (h₂ : n = m) (hγ₁ : coveredPartwise hX γ₁ n) (hγ₂ : coveredPartwise hX γ₂ m) :
    Fₙ hγ₁ = Fₙ hγ₂ := by subst_vars; rfl

-- @@ L416-427 verbatim
lemma functorOnHomOfCoveredPartwise_heq_of_ext {n m : ℕ} {x y x' y' : X}
    {γ : Dipath x y} {γ' : Dipath x' y'} (h : ∀ t, γ t = γ' t) (hn : n = m)
    (hγ : coveredPartwise hX γ n) (hγ' : coveredPartwise hX γ' m) :
    Fₙ hγ ≍ Fₙ hγ' := by
  have hx : x = x' := by simpa using h 0
  have hy : y = y' := by simpa using h 1
  subst hx
  subst hy
  have hγ_eq : γ = γ' := by
    ext t
    exact h t
  simp_all

-- @@ L428-431 verbatim
lemma functorOnHomOfCoveredPartwise_cast_params {n m : ℕ} {γ₁ γ₂ : Dipath x y}
    (h₁ : γ₁ = γ₂)
  (h₂ : n = m) (hγ₁ : coveredPartwise hX γ₁ n) :
    Fₙ hγ₁ = Fₙ (covered_partwise_of_equal hX h₁ h₂ hγ₁) := by subst_vars; rfl

-- @@ L432-439 verbatim
lemma functorOnHomOfCoveredPartwise_cast {x y x' y' : X} {n : ℕ} {γ : Dipath x y}
  (hγ : coveredPartwise hX γ n) (hx : x' = x) (hy : y' = y) :
    Fₙ ((covered_partwise_cast_iff hX γ hx hy).mp hγ) =
      (eqToHom (by rw [hx])) ≫ (Fₙ hγ) ≫ (eqToHom (by rw [hy])) := by
  subst_vars
  rw [eqToHom_refl, eqToHom_refl, Category.comp_id, Category.id_comp]
  apply functorOnHomOfCoveredPartwise_equal
  rfl

-- @@ L440-447 verbatim
lemma functorOnHomOfCoveredPartwise_cast_left {x y x' : X} {n : ℕ} {γ : Dipath x y}
  (hγ : coveredPartwise hX γ n) (hx : x' = x) :
    Fₙ ((covered_partwise_cast_iff hX γ hx rfl).mp hγ) =
      (eqToHom (by rw [hx])) ≫ (Fₙ hγ) := by
  subst_vars
  rw [eqToHom_refl, Category.id_comp]
  apply functorOnHomOfCoveredPartwise_equal
  rfl

-- @@ L448-455 verbatim
lemma functorOnHomOfCoveredPartwise_cast_right {x y y' : X} {n : ℕ} {γ : Dipath x y}
    (hγ : coveredPartwise hX γ n) (hy : y' = y) :
    Fₙ ((covered_partwise_cast_iff hX γ rfl hy).mp hγ) =
      (Fₙ hγ) ≫ (eqToHom (by rw [hy])) := by
  subst_vars
  rw [eqToHom_refl, Category.comp_id]
  apply functorOnHomOfCoveredPartwise_equal
  rfl

-- @@ L456-473 verbatim
lemma functorOnHomOfCoveredPartwise_refine_of_covered (k : ℕ) :
  Π {x y : X} {γ : Dipath x y} (hγ : covered hX γ),
    Fₙ (covered_partwise_of_covered 0 hγ) = Fₙ (covered_partwise_of_covered k hγ) := by
  induction k
  case zero =>
    simp_all
  case succ k ih =>
    intro x y γ hγ
    rw [functorOnHomOfCoveredPartwise_apply_succ hX h_comm
      (covered_partwise_of_covered k.succ hγ)]
    change (FunctorOnHomOfCovered hX h_comm hγ) = _
    have : 1 < k + 2 := by linarith
    rw [functorOnHomOfCovered_split_comp hX h_comm hγ
      (Fraction.ofPos_pos (lt_trans zero_lt_one this)) (Fraction.ofPos_lt_one this)]
    congr
    apply ih
    exact (covered_split_path (Fraction.ofPos_pos (lt_trans zero_lt_one this))
      (Fraction.ofPos_lt_one this) hγ).2

-- @@ L474-521 verbatim
attribute [local implicit_reducible] Dipath.cast SplitDipath.FirstPart SplitDipath.SecondPart
  SplitPath.FirstPart SplitPath.SecondPart in
/--
When a path is partwise covered by `n + 1` paths, applying `Fₙ` to the
two restrictions and composing gives `Fₙ γ`.
-/
lemma functorOnHomOfCoveredPartwise_split {n : ℕ} :
    Π {d : ℕ} (hdn : n > d) {x y : X} {γ : Dipath x y}
      (hγ : coveredPartwise hX γ n),
    Fₙ hγ = Fₙ (covered_partwise_first_part_d hX (Nat.succ_lt_succ hdn) hγ) ≫
          Fₙ (covered_partwise_second_part_d hX (Nat.succ_lt_succ hdn) hγ) := by
  induction n
  case zero =>
    simp_all
  case succ n ih_n =>
    intro d hdn
    induction d
    case zero =>
        intro x y γ hγ
        rfl
    case succ d _ =>
      intro x y γ hγ
      rw [functorOnHomOfCoveredPartwise_apply_succ hX h_comm hγ]
      have : n > d := Nat.succ_lt_succ_iff.mp hdn
      rw [ih_n this _]
      rw [functorOnHomOfCoveredPartwise_apply_succ hX h_comm _]
      rw [Category.assoc]
      change F₀ _ ≫ (Fₙ _ ≫ Fₙ _) = F₀ _ ≫ (Fₙ _ ≫ Fₙ _)
      apply eq_of_morphism
      · apply (comp_eqToHom_iff _ _ _).mp
        rw [←functorOnHomOfCovered_cast_right]
        focus
          apply functorOnHomOfCovered_equal
          rw [SplitProperties.firstPart_of_firstPart γ (Nat.succ_lt_succ hdn)
            (Nat.succ_pos d.succ)]
      · rw [←Category.assoc]
        apply eq_of_morphism
        · apply (comp_eqToHom_iff _ _ _).mp
          focus
            apply (eqToHom_comp_iff _ _ _).mp
            rw [←functorOnHomOfCoveredPartwise_cast]
            focus
              apply functorOnHomOfCoveredPartwise_equal
              rw [SplitProperties.first_part_of_second_part γ hdn (Nat.succ_pos d)]
        · rw [←functorOnHomOfCoveredPartwise_cast_left]
          exact functorOnHomOfCoveredPartwise_equal' hX h_comm
            (SplitProperties.second_part_of_second_part γ (Nat.lt_of_succ_lt_succ hdn))
            (by omega) _ _

-- @@ L522-552 verbatim
/--
If a path can be covered partwise by `(n + 1) ≥ 2` parts, its refinement
to `k * (n + 1)` parts is the composition of covering the first part in
`k` parts and the second part in `k * n` parts.
-/
lemma functorOnHomOfCoveredPartwise_refine_apply (n k : ℕ) {x y : X}
    {γ : Dipath x y} (hγ : coveredPartwise hX γ n.succ) :
    Fₙ (covered_partwise_refine hX n.succ k hγ) =
      (Fₙ <| covered_partwise_of_covered k hγ.left) ≫
        (Fₙ <| covered_partwise_refine hX n k hγ.right) := by
  have h₀ : k + 1 < (n+1+1) * (k + 1) := by
    simp_all
  have h₁ : (n+1+1)*(k+1) - 1 > (k + 1) - 1 :=
    Nat.pred_lt_pred (ne_of_gt (Nat.succ_pos k)) h₀
  have h₂ := FractionEqualities.cancel_common_factor (Nat.succ_pos k)
    (le_of_lt (Nat.succ_lt_succ h₁))
  rw [functorOnHomOfCoveredPartwise_split hX h_comm h₁
    (covered_partwise_refine hX n.succ k hγ)]
  apply eq_of_morphism
  · rw [←functorOnHomOfCoveredPartwise_cast_right hX h_comm _ (congr_arg γ h₂.symm)]
    apply functorOnHomOfCoveredPartwise_equal hX h_comm
    ext t
    rw [Dipath.cast_apply]
    exact SplitProperties.firstPart_eq_of_point_eq _ h₂.symm _
  · rw [←functorOnHomOfCoveredPartwise_cast_left hX h_comm _ (congr_arg γ h₂.symm)]
    apply functorOnHomOfCoveredPartwise_equal' hX h_comm
    · ext t
      rw [Dipath.cast_apply]
      exact SplitProperties.secondPart_eq_of_point_eq _ h₂.symm _
    · simp only [add_tsub_cancel_right, Nat.succ_eq_add_one]
      rw [Nat.succ_mul, Nat.sub_right_comm, Nat.add_sub_cancel]

-- @@ L553-563 verbatim
lemma functorOnHomOfCoveredPartwise_refine {n : ℕ} (k : ℕ) :
    Π {x y : X} {γ : Dipath x y} (hγ_n : coveredPartwise hX γ n),
      Fₙ hγ_n = Fₙ (covered_partwise_refine hX n k hγ_n) := by
  induction n
  case zero => apply functorOnHomOfCoveredPartwise_refine_of_covered
  case succ n ih =>
    intros x y γ hγ
    rw [functorOnHomOfCoveredPartwise_refine_apply hX h_comm n k hγ,
      ← functorOnHomOfCoveredPartwise_refine_of_covered hX h_comm _ hγ.left,
      functorOnHomOfCoveredPartwise_apply_succ hX h_comm hγ, ih hγ.right]
    rfl

-- @@ L564-571 verbatim
attribute [local implicit_reducible] Dipath.coveredPartwise in
lemma functorOnHomOfCoveredPartwise_apply_right_side {x y : X} {γ : Dipath x y}
    {n : ℕ} (hγ : coveredPartwise hX γ n.succ) :
    Fₙ hγ = Fₙ (covered_partwise_first_part_end_split hX hγ) ≫
            F₀ (covered_second_part_end_split hX hγ) := by
  rw [functorOnHomOfCoveredPartwise_split hX h_comm (Nat.lt_succ_self n)]
  rw [functorOnHomOfCoveredPartwise_equal' hX h_comm rfl (Nat.sub_self n.succ)]
  rw [functorOnHomOfCoveredPartwise_apply_0]

-- @@ L572-586 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor Dipath.coveredPartwise in
lemma functorOnHomOfCoveredPartwise_trans_case_0 {x y z : X}
    {γ₁ : Dipath x y} {γ₂ : Dipath y z}
  (hγ₁ : coveredPartwise hX γ₁ 0) (hγ₂ : coveredPartwise hX γ₂ 0) :
    Fₙ (covered_partwise_trans hγ₁ hγ₂) = (Fₙ hγ₁) ≫ (Fₙ hγ₂) := by
  rw [functorOnHomOfCoveredPartwise_apply_0, functorOnHomOfCoveredPartwise_apply_0,
    functorOnHomOfCoveredPartwise_apply_succ, functorOnHomOfCoveredPartwise_apply_0,
    functorOnHomOfCovered_equal hX h_comm (SplitProperties.first_part_trans γ₁ γ₂)
      _ ((covered_cast_iff γ₁ hX _ _).mp hγ₁),
    functorOnHomOfCovered_equal hX h_comm (SplitProperties.second_part_trans γ₁ γ₂)
      _ ((covered_cast_iff γ₂ hX _ _).mp hγ₂),
    functorOnHomOfCovered_cast_right hX h_comm hγ₁,
    functorOnHomOfCovered_cast_left hX h_comm hγ₂]
  simp

-- @@ L587-639 verbatim
lemma functorOnHomOfCoveredPartwise_trans {n : ℕ} :
    Π {x y z : X} {γ₁ : Dipath x y} {γ₂ : Dipath y z}
      (hγ₁ : coveredPartwise hX γ₁ n) (hγ₂ : coveredPartwise hX γ₂ n),
      Fₙ (covered_partwise_trans hγ₁ hγ₂) = (Fₙ hγ₁) ≫ (Fₙ hγ₂) := by
  induction n
  case zero =>
    intro x y z γ₁ γ₂ hγ₁ hγ₂
    exact functorOnHomOfCoveredPartwise_trans_case_0 hX h_comm hγ₁ hγ₂
  case succ n ih =>
    intros x y z γ₁ γ₂ hγ₁ hγ₂
    rw [functorOnHomOfCoveredPartwise_apply_succ hX h_comm]
    rw [functorOnHomOfCoveredPartwise_apply_succ hX h_comm hγ₁]
    rw [Category.assoc]
    apply eq_of_morphism
    · rw [←functorOnHomOfCovered_cast_right]
      focus
        apply functorOnHomOfCovered_equal
        ext t
        rw [Dipath.cast_apply]
        exact SplitProperties.trans_first_part γ₁ γ₂ n.succ t
      exact SplitProperties.trans_image_inv_eq_first γ₁ γ₂ n.succ
    · rw [functorOnHomOfCoveredPartwise_apply_right_side hX h_comm hγ₂]
      apply (eqToHom_comp_iff _ _ _).mp
      rw [←functorOnHomOfCoveredPartwise_cast_left hX h_comm _
          (SplitProperties.trans_image_inv_eq_first γ₁ γ₂ n.succ).symm]
      rw [←Category.assoc (Fₙ _) _ _]
      change Fₙ _ =
        (Fₙ hγ₁.right ≫ Fₙ (covered_partwise_first_part_end_split hX hγ₂)) ≫ F₀ _
      rw [←ih hγ₁.right (covered_partwise_first_part_end_split hX hγ₂)]
      have : (n.succ + n.succ).succ - 1 = (n + n).succ.succ := by
        rw [Nat.sub_one]
        rw [Nat.pred_succ (n.succ + n.succ)]
        rw [Nat.succ_add]
        rw [Nat.add_succ]
      erw [functorOnHomOfCoveredPartwise_cast_params hX h_comm rfl this]
      rw [functorOnHomOfCoveredPartwise_apply_right_side hX h_comm _]
      congr 1
      · apply congrArg F_obj
        congr 1
        rw [Dipath.cast_apply]
        exact SplitProperties.second_part_trans_eval_at_end γ₁ γ₂ n
      · apply functorOnHomOfCoveredPartwise_heq_of_ext hX h_comm
        focus
          intro t
          rw [SplitProperties.firstPart_cast]
          simp only [Dipath.cast_apply]
          exact SplitProperties.trans_first_part_of_second_part γ₁ γ₂ n t
        simp
      · apply functorOnHomOfCovered_heq_of_ext hX h_comm
        intro t
        rw [SplitProperties.secondPart_cast]
        simp only [Dipath.cast_apply]
        exact SplitProperties.trans_second_part_second_part γ₁ γ₂ n t

-- @@ L640-650 verbatim
lemma functorOnHomOfCoveredPartwise_unique {n m : ℕ} {γ : Dipath x y}
  (hγ_n : coveredPartwise hX γ n) (hγ_m : coveredPartwise hX γ m) :
    Fₙ hγ_n = Fₙ hγ_m := by
  rw [functorOnHomOfCoveredPartwise_refine hX h_comm m hγ_n]
  rw [functorOnHomOfCoveredPartwise_refine hX h_comm n hγ_m]
  congr 2
  exact mul_comm _ _

/-
-  ### Define the behaviour on all paths
-/

-- @@ L651-653 verbatim
/-- Map on arbitrary directed paths, choosing a covered subdivision. -/
def FunctorOnHomAux (γ : Dipath x y) : F_obj ⟨x⟩ ⟶ F_obj ⟨y⟩ :=
  Fₙ (Classical.choose_spec (has_subpaths hX X₁_open X₂_open γ))

-- @@ L654-654 verbatim
local notation "Fh_aux" => FunctorOnHomAux hX X₁_open X₂_open h_comm


-- @@ L656-657 verbatim
lemma functorOnHomAux_apply {n : ℕ} {γ : Dipath x y} (hγ : coveredPartwise hX γ n) :
    Fh_aux γ = Fₙ hγ := functorOnHomOfCoveredPartwise_unique hX h_comm _ _

-- @@ L658-661 verbatim
lemma functorOnHomAux_refl {x : X} : Fh_aux (Dipath.refl x) = 𝟙 (F_obj ⟨x⟩) := by
  have : coveredPartwise hX (Dipath.refl x) 0 := covered_refl x hX
  rw [functorOnHomAux_apply _ _ _ _ this, functorOnHomOfCoveredPartwise_apply_0]
  apply functorOnHomOfCovered_refl

-- @@ L662-668 verbatim
lemma functorOnHomAux_cast {x y x' y' : X} (γ : Dipath x y) (hx : x' = x) (hy : y' = y) :
    Fh_aux (γ.cast hx hy) = (eqToHom (by rw [hx])) ≫ Fh_aux γ ≫ (eqToHom (by rw [hy])) := by
  subst_vars
  rw [eqToHom_refl, eqToHom_refl, Category.comp_id, Category.id_comp]
  apply congr_arg
  ext t
  rfl

-- @@ L669-675 verbatim
@[simp]
lemma functorOnHomAux_cast_heq {x y x' y' : X} (γ : Dipath x y) (hx : x' = x)
    (hy : y' = y) :
    Fh_aux (γ.cast hx hy) ≍ Fh_aux γ := by
  cases hx
  cases hy
  rfl

-- @@ L676-686 verbatim
lemma functorOnHomAux_heq_of_ext {x y x' y' : X} {γ : Dipath x y} {γ' : Dipath x' y'}
    (h : ∀ t, γ t = γ' t) :
    Fh_aux γ ≍ Fh_aux γ' := by
  have hx : x = x' := by simpa using h 0
  have hy : y = y' := by simpa using h 1
  subst hx
  subst hy
  have hγ : γ = γ' := by
    ext t
    exact h t
  simp_all

-- @@ L687-698 verbatim
lemma functorOnHomAux_trans {x y z : X} (γ₁ : Dipath x y) (γ₂ : Dipath y z) :
    Fh_aux (γ₁.trans γ₂) = Fh_aux γ₁ ≫ Fh_aux γ₂ := by
  cases has_subpaths hX X₁_open X₂_open γ₁
  cases has_subpaths hX X₁_open X₂_open γ₂
  rename_i n hn m hm
  have hn' : coveredPartwise hX γ₁ ((n + 1) * (m + 1) - 1) := covered_partwise_refine hX n m hn
  have hm' : coveredPartwise hX γ₂ ((n + 1) * (m + 1) - 1) :=
    (mul_comm (m + 1) _) ▸ covered_partwise_refine hX m n hm
  rw [functorOnHomAux_apply hX X₁_open X₂_open h_comm hn',
    functorOnHomAux_apply hX X₁_open X₂_open h_comm hm',
    functorOnHomAux_apply hX X₁_open X₂_open h_comm (covered_partwise_trans hn' hm'),
    functorOnHomOfCoveredPartwise_trans]

-- @@ L699-712 verbatim
lemma functorOnHomAux_split_of_covered_partwise {x y : X} {γ : Dipath x y}
    {n : ℕ} (hγ : coveredPartwise hX γ n.succ) :
    Fh_aux γ =
      Fh_aux (SplitDipath.FirstPart γ
        (Fraction (Nat.succ_pos _) (Nat.succ_le_succ (Nat.zero_le n.succ)))) ≫
      Fh_aux (SplitDipath.SecondPart γ
        (Fraction (Nat.succ_pos _) (Nat.succ_le_succ (Nat.zero_le n.succ)))) := by
  -- Rewrite L.H.S.
  rw [functorOnHomAux_apply hX _ _ h_comm hγ,
    functorOnHomOfCoveredPartwise_apply_succ hX h_comm hγ]
  --Rewrite R.H.S.
  have : coveredPartwise hX (SplitDipath.FirstPart γ _) 0 := hγ.left
  rw [functorOnHomAux_apply hX _ _ h_comm this, functorOnHomOfCoveredPartwise_apply_0,
    functorOnHomAux_apply hX _ _ h_comm hγ.right]

-- @@ L713-720 verbatim
lemma functorOnHomAux_of_covered_dihomotopic {x y : X} {γ γ' : Dipath x y} {F : Dihomotopy γ γ'}
  (hF : Dipath.Dihomotopy.covered hX F) :
    Fh_aux γ = Fh_aux γ' := by
  have : coveredPartwise hX γ 0 := Dipath.Dihomotopy.covered_left_of_covered hF
  rw [functorOnHomAux_apply _ _ _ _ this, functorOnHomOfCoveredPartwise_apply_0]
  have : coveredPartwise hX γ' 0 := Dipath.Dihomotopy.covered_right_of_covered hF
  rw [functorOnHomAux_apply _ _ _ _ this, functorOnHomOfCoveredPartwise_apply_0]
  exact functorOnHomOfCovered_dihomotopic hX h_comm hF

-- @@ L721-753 expanded
lemma functorOnHomAux_of_homotopic_dimaps_0 {f g : DirectedMap I X} {H : DirectedMap.Dihomotopy f g}
    (hcov : DirectedMap.Dihomotopy.coveredPartwise hX H 0 0) :
    Fh_aux (Dipath.ofDirectedMap f) ≫ Fh_aux (H.evalAtRight 1) =
      Fh_aux (H.evalAtRight 0) ≫ Fh_aux (Dipath.ofDirectedMap g) :=
  by
  let Γ := DihomToPathDihom.dihomToPathDihom H
  have Γ_cov : Dipath.Dihomotopy.covered hX Γ :=
    by
    unfold Dipath.Dihomotopy.covered
    cases DirectedMap.Dihomotopy.covered_of_coveredPartwise hcov
    case inl h =>
      left
      exact subset_trans (DihomToPathDihom.dihom_to_path_dihom_range _) h
    case inr h =>
      right
      exact subset_trans (DihomToPathDihom.dihom_to_path_dihom_range _) h
  calc
    Fh_aux (Dipath.ofDirectedMap f) ≫ Fh_aux (H.evalAtRight 1)
    _ = (𝟙 (F_obj ⟨f 0⟩) ≫ Fh_aux (ofDirectedMap f)) ≫ Fh_aux (H.evalAtRight 1) := by
      rw [Category.id_comp]
    _ = (Fh_aux (Dipath.refl (f 0)) ≫ Fh_aux (ofDirectedMap f)) ≫ Fh_aux (H.evalAtRight 1) := by
      rw [functorOnHomAux_refl]
    _ = Fh_aux ((Dipath.refl (f 0)).trans (ofDirectedMap f)) ≫ Fh_aux (H.evalAtRight 1) := by
      rw [functorOnHomAux_trans]
    _ = Fh_aux (((Dipath.refl (f 0)).trans (ofDirectedMap f)).trans (H.evalAtRight 1)) := by
      rw [← functorOnHomAux_trans]
    _ = Fh_aux (((H.evalAtRight 0).trans (ofDirectedMap g)).trans (refl (g 1))) :=
      (functorOnHomAux_of_covered_dihomotopic hX X₁_open X₂_open h_comm Γ_cov)
    _ = Fh_aux ((H.evalAtRight 0).trans (ofDirectedMap g)) ≫ Fh_aux (refl (g 1)) := by
      rw [functorOnHomAux_trans]
    _ = Fh_aux ((H.evalAtRight 0).trans (ofDirectedMap g)) ≫ 𝟙 (F_obj ⟨g 1⟩) := by
      rw [functorOnHomAux_refl]
    _ = Fh_aux ((H.evalAtRight 0).trans (ofDirectedMap g)) := by rw [Category.comp_id]
    _ = Fh_aux (H.evalAtRight 0) ≫ Fh_aux (Dipath.ofDirectedMap g) := by rw [functorOnHomAux_trans]


-- @@ L754-881 expanded
attribute [local implicit_reducible] Dipath.cast SplitDipath.FirstPart SplitDipath.SecondPart
    SplitPath.FirstPart SplitPath.SecondPart Dipath.ofDirectedMap Dipath.toDirectedMap in
lemma functorOnHomAux_of_homotopic_dimaps {m : ℕ} :
    Π {f g : DirectedMap I X} {H : DirectedMap.Dihomotopy f g}
      (_ : DirectedMap.Dihomotopy.coveredPartwise hX H 0 m),
      Fh_aux (Dipath.ofDirectedMap f) ≫ Fh_aux (H.evalAtRight 1) =
        Fh_aux (H.evalAtRight 0) ≫ Fh_aux (Dipath.ofDirectedMap g) :=
  by
  induction m
  case zero => exact fun hcov => functorOnHomAux_of_homotopic_dimaps_0 _ _ _ _ hcov
  case succ m ih =>
    intro f g H hcov
    have f_cov : coveredPartwise hX (Dipath.ofDirectedMap f) m.succ :=
      DirectedMap.Dihomotopy.path_covered_partiwse_of_dihomotopy_coveredPartwise_left hcov
    have g_cov : coveredPartwise hX (Dipath.ofDirectedMap g) m.succ :=
      DirectedMap.Dihomotopy.path_covered_partiwse_of_dihomotopy_coveredPartwise_right hcov
    let T := Fraction.ofPos (Nat.succ_pos m.succ)
    let f₁ := (SplitDipath.FirstPart (Dipath.ofDirectedMap f) T)
    let f₂ := (SplitDipath.SecondPart (Dipath.ofDirectedMap f) T)
    let g₁ := (SplitDipath.FirstPart (Dipath.ofDirectedMap g) T)
    let g₂ := (SplitDipath.SecondPart (Dipath.ofDirectedMap g) T)
    have h₁ : Fh_aux f₂ ≫ Fh_aux (H.evalAtRight 1) = Fh_aux (H.evalAtRight T) ≫ Fh_aux g₂ :=
      by
      have := ih (DirectedMap.Dihomotopy.coveredPartwise_second_hpart hcov)
      rw [SplitDihomotopy.sph_eval_0, SplitDihomotopy.sph_eval_1,
        Dipath.dipath_of_directed_map_of_to_dimap,
        Dipath.dipath_of_directed_map_of_to_dimap] at this
      convert this using 1
      · simp [T]
      · apply heq_comp
        · apply congrArg F_obj
          change
            (⟨(ofDirectedMap f) T⟩ : FundamentalCategory X) =
              ⟨(SplitDipath.SecondPart (ofDirectedMap f)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).toDirectedMap
                  0⟩
          ext
          simp [T, Dipath.ofDirectedMap]
        · simp_all
        · simp_all
        · exact (functorOnHomAux_cast_heq hX X₁_open X₂_open h_comm f₂ _ _).symm
        ·
          simpa using
            (functorOnHomAux_cast_heq hX X₁_open X₂_open h_comm (H.evalAtRight 1) _ _).symm
      · apply heq_comp
        · apply congrArg F_obj
          change
            (⟨f T⟩ : FundamentalCategory X) =
              ⟨(SplitDipath.SecondPart (ofDirectedMap f)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).toDirectedMap
                  0⟩
          ext
          simp [T, Dipath.ofDirectedMap]
        · apply congrArg F_obj
          change
            (⟨g T⟩ : FundamentalCategory X) =
              ⟨(SplitDipath.SecondPart (ofDirectedMap g)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).toDirectedMap
                  0⟩
          ext
          simp [T, Dipath.ofDirectedMap]
        · simp_all
        ·
          simpa [T] using
            (functorOnHomAux_cast_heq hX X₁_open X₂_open h_comm
                (H.evalAtRight (Fraction.ofPos (Nat.succ_pos m.succ))) _ _).symm
        · exact (functorOnHomAux_cast_heq hX X₁_open X₂_open h_comm g₂ _ _).symm
    have h₂ : Fh_aux f₁ ≫ Fh_aux (H.evalAtRight T) = Fh_aux (H.evalAtRight 0) ≫ Fh_aux g₁ :=
      by
      have :=
        functorOnHomAux_of_homotopic_dimaps_0 hX X₁_open X₂_open h_comm
          (DirectedMap.Dihomotopy.coveredPartwise_first_hpart hcov)
      rw [SplitDihomotopy.fph_eval_0, SplitDihomotopy.fph_eval_1,
        Dipath.dipath_of_directed_map_of_to_dimap,
        Dipath.dipath_of_directed_map_of_to_dimap] at this
      convert this using 1
      · simp [T, Dipath.ofDirectedMap]
      · apply heq_comp
        · simp_all
        · apply congrArg F_obj
          change
            (⟨f T⟩ : FundamentalCategory X) =
              ⟨(SplitDipath.FirstPart (ofDirectedMap f)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).toDirectedMap
                  1⟩
          ext
          simp [T, Dipath.ofDirectedMap]
        · apply congrArg F_obj
          change
            (⟨g T⟩ : FundamentalCategory X) =
              ⟨(SplitDipath.FirstPart (ofDirectedMap g)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).toDirectedMap
                  1⟩
          ext
          simp [T, Dipath.ofDirectedMap]
        · apply functorOnHomAux_heq_of_ext hX X₁_open X₂_open h_comm
          intro t
          change
            (SplitDipath.FirstPart (Dipath.ofDirectedMap f) (Fraction.ofPos (Nat.succ_pos m.succ)))
                t =
              ((SplitDipath.FirstPart (Dipath.ofDirectedMap f)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).cast
                  _ _)
                t
          rw [Dipath.cast_apply]
        · apply functorOnHomAux_heq_of_ext hX X₁_open X₂_open h_comm
          intro t
          change
            (H.evalAtRight (Fraction.ofPos (Nat.succ_pos m.succ))) t =
              ((H.evalAtRight (Fraction.ofPos (Nat.succ_pos m.succ))).cast _ _) t
          rw [Dipath.cast_apply]
      · apply heq_comp
        · simp_all
        · simp_all
        · apply congrArg F_obj
          change
            (⟨g T⟩ : FundamentalCategory X) =
              ⟨(SplitDipath.FirstPart (ofDirectedMap g)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).toDirectedMap
                  1⟩
          ext
          simp [T, Dipath.ofDirectedMap]
        · apply functorOnHomAux_heq_of_ext hX X₁_open X₂_open h_comm
          intro t
          change (H.evalAtRight 0) t = ((H.evalAtRight 0).cast _ _) t
          rw [Dipath.cast_apply]
        · apply functorOnHomAux_heq_of_ext hX X₁_open X₂_open h_comm
          intro t
          change
            (SplitDipath.FirstPart (Dipath.ofDirectedMap g) (Fraction.ofPos (Nat.succ_pos m.succ)))
                t =
              ((SplitDipath.FirstPart (Dipath.ofDirectedMap g)
                      (Fraction.ofPos (Nat.succ_pos m.succ))).cast
                  _ _)
                t
          rw [Dipath.cast_apply]
    calc
      Fh_aux (Dipath.ofDirectedMap f) ≫ Fh_aux (H.evalAtRight 1)
      _ = (Fh_aux f₁ ≫ Fh_aux f₂) ≫ Fh_aux (H.evalAtRight 1) := by
        rw [functorOnHomAux_split_of_covered_partwise _ _ _ _ f_cov]
      _ = Fh_aux f₁ ≫ (Fh_aux f₂ ≫ Fh_aux (H.evalAtRight 1)) := by rw [Category.assoc]
      _ = Fh_aux f₁ ≫ (Fh_aux (H.evalAtRight T) ≫ Fh_aux g₂) := by
        exact congrArg (fun q => Fh_aux f₁ ≫ q) h₁
      _ = (Fh_aux f₁ ≫ Fh_aux (H.evalAtRight T)) ≫ Fh_aux g₂ := by rw [Category.assoc]
      _ = (Fh_aux (H.evalAtRight 0) ≫ Fh_aux g₁) ≫ Fh_aux g₂ := by
        exact congrArg (fun q => q ≫ Fh_aux g₂) h₂
      _ = Fh_aux (H.evalAtRight 0) ≫ (Fh_aux g₁ ≫ Fh_aux g₂) := by rw [Category.assoc]
      _ = Fh_aux (H.evalAtRight 0) ≫ Fh_aux (Dipath.ofDirectedMap g) := by
        rw [functorOnHomAux_split_of_covered_partwise _ _ _ _ g_cov]


-- @@ L882-918 verbatim
attribute [local implicit_reducible] dTopCat.of
  FundamentalCategory.fundamentalCategoryFunctor Quiver.Hom Quotient
  Dipath.Dihomotopic.Quotient Dipath.coveredPartwise Dipath.cast
  SplitDipath.FirstPart SplitDipath.SecondPart SplitPath.FirstPart SplitPath.SecondPart
  Dipath.ofDirectedMap Dipath.toDirectedMap in
lemma functorOnHomAux_of_covered_dihomotopic_zero_m {m : ℕ} {x y : X} {γ γ' : Dipath x y}
  (h : Dipath.Dihomotopy.dihomotopicCovered hX γ γ' 0 m) :
    Fh_aux γ = Fh_aux γ' := by
  cases h
  rename_i G HG
  have h₁ : Fh_aux ((G.evalAtRight 0)) = (eqToHom (by simp)) ≫
            (𝟙 (F_obj ⟨x⟩)) ≫ (eqToHom (by simp)) := by
      have : G.evalAtRight 0 = (Dipath.refl x).cast γ.source γ'.source := by
        ext t
        change G (t, 0) = x
        simp
      rw [this]
      erw [functorOnHomAux_cast hX X₁_open X₂_open h_comm]
      rw [functorOnHomAux_refl]
  have h₂ : Fh_aux ((G.evalAtRight 1)) = (eqToHom (by simp)) ≫
            (𝟙 (F_obj ⟨y⟩)) ≫ (eqToHom (by simp)) := by
      have : G.evalAtRight 1 = (Dipath.refl y).cast γ.target γ'.target := by
        ext t
        change G (t, 1) = y
        simp
      rw [this]
      erw [functorOnHomAux_cast hX X₁_open X₂_open h_comm]
      rw [functorOnHomAux_refl]
  have := functorOnHomAux_of_homotopic_dimaps hX X₁_open X₂_open h_comm HG
  rw [h₁, h₂, Dipath.dipath_of_directed_map_of_to_dimap,
    Dipath.dipath_of_directed_map_of_to_dimap] at this
  erw [functorOnHomAux_cast hX X₁_open X₂_open h_comm γ] at this
  erw [functorOnHomAux_cast hX X₁_open X₂_open h_comm γ'] at this
  simp only [coe_toDirectedMap, Dipath.target, eqToHom_naturality, Category.comp_id,
    eqToHom_trans, Category.assoc, Dipath.source, eqToHom_trans_assoc] at this
  have := (comp_eqToHom_iff _ _ _).mp ((eqToHom_comp_iff _ _ _).mp this)
  simpa [Category.assoc] using this

-- @@ L919-932 verbatim
lemma functorOnHomAux_of_partwise_covered_dihomotopic :
    Π {n m : ℕ} {x y : X} {γ γ' : Dipath x y}
      (_ : Dipath.Dihomotopy.dihomotopicCovered hX γ γ' n m),
    Fh_aux γ = Fh_aux γ' := by
  intro n m
  induction n
  case zero =>
    intro x y γ γ' h
    exact functorOnHomAux_of_covered_dihomotopic_zero_m hX X₁_open X₂_open h_comm h
  case succ n ih =>
    rintro x y γ γ' ⟨F, hF⟩
    have ⟨h₁, h₂⟩ := Dipath.Dihomotopy.dihomotopicCovered_split hX hF
    rw [functorOnHomAux_of_covered_dihomotopic_zero_m hX X₁_open X₂_open h_comm h₁]
    exact ih h₂

-- @@ L933-938 verbatim
lemma functorOnHomAux_of_pre_dihomotopic {γ γ' : Dipath x y} (h : γ.PreDihomotopic γ') :
    Fh_aux γ = Fh_aux γ' := by
  rcases Dipath.Dihomotopy.dihomotopicCovered_exists_of_preDihomotopic
      hX h X₁_open X₂_open with
    ⟨n, m, h⟩
  exact functorOnHomAux_of_partwise_covered_dihomotopic hX X₁_open X₂_open h_comm h

-- @@ L939-950 verbatim
lemma functorOnHomAux_of_dihomotopic (γ γ' : Dipath x y) (h : γ.Dihomotopic γ') :
    Fh_aux γ = Fh_aux γ' :=
  Relation.EqvGen.rec
    (fun _ _ h => functorOnHomAux_of_pre_dihomotopic _ _ _ _ h)
    (fun _ => rfl)
    (fun _ _ _ h => h.symm)
    (fun _ _ _ _ _ h₁ h₂ => Eq.trans h₁ h₂)
  h

/-
-  ### Define the behaviour on quotient of paths
-/

-- @@ L951-953 expanded
/-- Map on morphisms of the directed fundamental category. -/
def FunctorOnHom {x y : FundamentalCategory.fundamentalCategoryFunctor.obj X} (γ : x ⟶ y) :
    F_obj x ⟶ F_obj y :=
  Quotient.liftOn γ Fh_aux (functorOnHomAux_of_dihomotopic hX X₁_open X₂_open h_comm)


-- @@ L954-954 verbatim
local notation "F_hom" => FunctorOnHom hX X₁_open X₂_open h_comm

-- @@ L955-956 verbatim
lemma functorOnHom_apply (γ : Dipath x y) :
  F_hom ⟦γ⟧ = Fh_aux γ := rfl

-- @@ L957-960 verbatim
lemma functorOnHom_trans {x y z : X} (γ₁ : Dipath x y) (γ₂ : Dipath y z) :
    F_hom ⟦γ₁.trans γ₂⟧ = F_hom ⟦γ₁⟧ ≫ F_hom ⟦γ₂⟧ := by
  change Fh_aux (γ₁.trans γ₂) = Fh_aux γ₁ ≫ Fh_aux γ₂
  exact functorOnHomAux_trans hX X₁_open X₂_open h_comm γ₁ γ₂

-- @@ L961-963 expanded
lemma functorOnHom_id (x : FundamentalCategory.fundamentalCategoryFunctor.obj X) :
    F_hom (𝟙 x) = 𝟙 (F_obj x) :=
  by
  change Fh_aux (Dipath.refl x.as) = 𝟙 (F_obj x)
  apply functorOnHomAux_refl


-- @@ L964-967 verbatim
lemma functorOnHom_comp_path {x y z : X} (γ₁ : Dipath x y) (γ₂ : Dipath y z) :
    F_hom (⟦γ₁⟧ ≫ ⟦γ₂⟧) = F_hom ⟦γ₁⟧ ≫ F_hom ⟦γ₂⟧ := by
  change Fh_aux (γ₁.trans γ₂) = Fh_aux γ₁ ≫ Fh_aux γ₂
  exact functorOnHom_trans hX X₁_open X₂_open h_comm γ₁ γ₂

-- @@ L968-978 expanded
attribute [local implicit_reducible] dTopCat.of FundamentalCategory.fundamentalCategoryFunctor
    Quiver.Hom Quotient Dipath.Dihomotopic.Quotient in
lemma functorOnHom_comp {x y z : FundamentalCategory.fundamentalCategoryFunctor.obj X} (γ₁ : x ⟶ y)
    (γ₂ : y ⟶ z) : F_hom (γ₁ ≫ γ₂) = F_hom γ₁ ≫ F_hom γ₂ :=
  by
  have := functorOnHom_comp_path hX X₁_open X₂_open h_comm (γ₁.out) (γ₂.out)
  rw [Quotient.out_eq, Quotient.out_eq] at this
  exact this


-- @@ L979-984 expanded
/-- The pushout functor from the directed fundamental category of `X` to `C`. -/
def Functor : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C
    where
  obj := F_obj
  map γ := F_hom γ
  map_id x := functorOnHom_id hX X₁_open X₂_open h_comm x
  map_comp γ₁ γ₂ := functorOnHom_comp hX X₁_open X₂_open h_comm γ₁ γ₂


-- @@ L985-985 verbatim
local notation "F" => Functor hX X₁_open X₂_open h_comm

-- @@ L986-986 expanded
lemma functorObj_def {x : FundamentalCategory.fundamentalCategoryFunctor.obj X} :
    (F).obj x = F_obj x :=
  rfl


-- @@ L987-987 expanded
lemma functorHom_def {x y : FundamentalCategory.fundamentalCategoryFunctor.obj X} (f : x ⟶ y) :
    (F).map f = F_hom f :=
  rfl


-- @@ L988-993 expanded
lemma functor_comp_left_object (x : X₁) :
    (F).obj ((FundamentalCategory.fundamentalCategoryMap j₁).obj ⟨x⟩) = F₁.obj ⟨x⟩ :=
  by
  change F_obj ⟨j₁ _⟩ = _
  rw [← functorOnObj_apply_one hX h_comm]
  · congr 1
  · exact x.property


-- @@ L994-1007 expanded
lemma functor_comp_left_dipath {x y : X₁} (γ : Dipath x y) :
    F_hom ((FundamentalCategory.fundamentalCategoryMap j₁).map ⟦γ⟧) =
      (eqToHom (functor_comp_left_object hX X₁_open X₂_open h_comm x)) ≫
        (F₁.map ⟦γ⟧) ≫ (eqToHom (functor_comp_left_object hX X₁_open X₂_open h_comm y).symm) :=
  by
  rw [subtype_path_class_eq_map]
  change Fh_aux (γ.map (DirectedSubtypeInclusion X₁)) = _
  have h₁ : range (γ.map (DirectedSubtypeInclusion X₁)) ⊆ X₁ := range_dipath_map_inclusion γ
  have h₂ : coveredPartwise hX (γ.map (DirectedSubtypeInclusion X₁)) 0 := Or.inl h₁
  rw [functorOnHomAux_apply hX X₁_open X₂_open h_comm h₂, functorOnHomOfCoveredPartwise_apply_0,
    functorOnHomOfCovered_apply_left' hX h_comm h₁, FunctorOnHomOfCoveredAux₁,
    subtypeDipath_of_included_dipath_eq]
  erw [functor_cast F₁ γ];
  rfl
    /- Shpw that the two obtained triangles commute -/


-- @@ L1008-1014 expanded
lemma functor_comp_left : (FundamentalCategory.fundamentalCategoryMap j₁) ⋙ F = F₁ :=
  by
  refine CategoryTheory.Functor.ext ?_ ?_
  · intro x
    exact functor_comp_left_object hX X₁_open X₂_open h_comm x.as
  · intros x y f
    rw [← Quotient.out_eq f]
    exact functor_comp_left_dipath hX X₁_open X₂_open h_comm f.out


-- @@ L1015-1020 expanded
lemma functor_comp_right_object (x : X₂) :
    (F).obj ((FundamentalCategory.fundamentalCategoryMap j₂).obj ⟨x⟩) = F₂.obj ⟨x⟩ :=
  by
  change F_obj ⟨j₂ _⟩ = _
  rw [← functorOnObj_apply_two hX h_comm]
  · congr 1
  · exact x.property


-- @@ L1021-1035 expanded
lemma functor_comp_right_dipath {x y : X₂} (γ : Dipath x y) :
    F_hom ((FundamentalCategory.fundamentalCategoryMap j₂).map ⟦γ⟧) =
      (eqToHom (functor_comp_right_object hX X₁_open X₂_open h_comm x)) ≫
        (F₂.map ⟦γ⟧) ≫ (eqToHom (functor_comp_right_object hX X₁_open X₂_open h_comm y).symm) :=
  by
  rw [subtype_path_class_eq_map]
  change Fh_aux (γ.map (DirectedSubtypeInclusion X₂)) = _
  have h₁ : range (γ.map (DirectedSubtypeInclusion X₂)) ⊆ X₂ := range_dipath_map_inclusion γ
  have h₂ : coveredPartwise hX (γ.map (DirectedSubtypeInclusion X₂)) 0 := Or.inr h₁
  rw [functorOnHomAux_apply hX X₁_open X₂_open h_comm h₂, functorOnHomOfCoveredPartwise_apply_0,
    functorOnHomOfCovered_apply_right' hX h_comm h₁, FunctorOnHomOfCoveredAux₂,
    subtypeDipath_of_included_dipath_eq]
  erw [functor_cast F₂ γ]; rfl


-- @@ L1036-1042 expanded
lemma functor_comp_right : (FundamentalCategory.fundamentalCategoryMap j₂) ⋙ F = F₂ :=
  by
  refine CategoryTheory.Functor.ext ?_ ?_
  · intro x
    exact functor_comp_right_object hX X₁_open X₂_open h_comm x.as
  · intros x y f
    rw [← Quotient.out_eq f]
    exact functor_comp_right_dipath hX X₁_open X₂_open h_comm f.out


-- @@ L1043-1059 expanded
attribute [local implicit_reducible] dTopCat.of FundamentalCategory.fundamentalCategoryFunctor
    Functor in
lemma functor_uniq_aux_obj (F' : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C)
    (h₁ : (FundamentalCategory.fundamentalCategoryMap j₁) ⋙ F' = F₁)
    (h₂ : (FundamentalCategory.fundamentalCategoryMap j₂) ⋙ F' = F₂) (x : X) :
    F'.obj ⟨x⟩ = (F).obj ⟨x⟩ := by
  rw [functorObj_def]
  cases ((Set.mem_union x X₁ X₂).mp (Filter.mem_top.mpr hX x))
  case inl hx₁ =>
    rw [← functorOnObj_apply_one hX h_comm hx₁, obj_eq_obj_of_eq h₁.symm]
    change F'.obj _ = F'.obj _
    apply congrArg
    rfl
  case inr hx₂ =>
    rw [← functorOnObj_apply_two hX h_comm hx₂, obj_eq_obj_of_eq h₂.symm]
    change F'.obj _ = F'.obj _
    apply congrArg
    rfl


-- @@ L1060-1087 expanded
attribute [local implicit_reducible] dTopCat.of FundamentalCategory.fundamentalCategoryFunctor
    Quiver.Hom Quotient Dipath.Dihomotopic.Quotient Functor in
lemma functor_uniq_of_covered (F' : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C)
    (h₁ : (FundamentalCategory.fundamentalCategoryMap j₁) ⋙ F' = F₁)
    (h₂ : (FundamentalCategory.fundamentalCategoryMap j₂) ⋙ F' = F₂) {x y : X} {γ : Dipath x y}
    (hγ : covered hX γ) :
    F'.map ⟦γ⟧ =
      (eqToHom (functor_uniq_aux_obj hX X₁_open X₂_open h_comm F' h₁ h₂ x)) ≫
        (F).map ⟦γ⟧ ≫ (eqToHom (functor_uniq_aux_obj hX X₁_open X₂_open h_comm F' h₁ h₂ y).symm) :=
  by
  rw [functorHom_def, functorOnHom_apply]
  have : coveredPartwise hX γ 0 := hγ
  rw [functorOnHomAux_apply _ _ _ _ this, functorOnHomOfCoveredPartwise_apply_0 _ _ this]
  cases hγ
  case inl hγ =>
    rw [functorOnHomOfCovered_apply_left' _ _ hγ]
    unfold FunctorOnHomOfCoveredAux₁
    refine Eq.trans (congrArg F'.map (map_subtypeDipath_eq γ hγ)).symm ?_
    change ((FundamentalCategory.fundamentalCategoryMap j₁) ⋙ F').map ⟦SubtypeDipath γ hγ⟧ = _
    rw [map_eq_map_of_eq h₁]
    simp [functorObj_def]; rfl
  case inr hγ =>
    rw [functorOnHomOfCovered_apply_right' _ _ hγ]
    unfold FunctorOnHomOfCoveredAux₂
    refine Eq.trans (congrArg F'.map (map_subtypeDipath_eq γ hγ)).symm ?_
    change ((FundamentalCategory.fundamentalCategoryMap j₂) ⋙ F').map ⟦SubtypeDipath γ hγ⟧ = _
    rw [map_eq_map_of_eq h₂]
    simp [functorObj_def]; rfl


-- @@ L1088-1117 expanded
attribute [local implicit_reducible] dTopCat.of FundamentalCategory.fundamentalCategoryFunctor
    Quiver.Hom Quotient Dipath.Dihomotopic.Quotient Functor in
lemma functor_uniq_aux_map (F' : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C)
    (h₁ : (FundamentalCategory.fundamentalCategoryMap j₁) ⋙ F' = F₁)
    (h₂ : (FundamentalCategory.fundamentalCategoryMap j₂) ⋙ F' = F₂) {n : ℕ} :
    Π {x y : X} {γ : Dipath x y} (_ : coveredPartwise hX γ n),
      F'.map ⟦γ⟧ =
        (eqToHom (functor_uniq_aux_obj hX X₁_open X₂_open h_comm F' h₁ h₂ x)) ≫
          (F).map ⟦γ⟧ ≫
            (eqToHom (functor_uniq_aux_obj hX X₁_open X₂_open h_comm F' h₁ h₂ y).symm) :=
  by
  induction n
  case zero =>
    intros x y γ hγ
    exact functor_uniq_of_covered _ _ _ _ F' h₁ h₂ hγ
  case succ n ih =>
    intros x y γ hγ
    let T :=
      Fraction.ofPos
        (Nat.succ_pos n.succ) -- T = 1/(n+1+1)
          
    have hT₀ : 0 < T := Fraction.ofPos_pos (Nat.succ_pos n.succ)
    have hT₁ : T < 1 := Fraction.ofPos_lt_one (Nat.succ_lt_succ (Nat.succ_pos n))
    rw [SplitDipath.first_trans_second_reparam_eq_self γ hT₀ hT₁]
    rw [Dipath.Dihomotopic.quot_reparam]
    rw [Dipath.Dihomotopic.comp_lift]
    change
      F'.map
          ((⟦SplitDipath.FirstPart γ T⟧ :
              (⟨x⟩ : FundamentalCategory.fundamentalCategoryFunctor.obj X) ⟶
                (⟨γ T⟩ : FundamentalCategory.fundamentalCategoryFunctor.obj X)) ≫
            ⟦SplitDipath.SecondPart γ T⟧) =
        eqToHom _ ≫
          (F).map
              ((⟦SplitDipath.FirstPart γ T⟧ :
                  (⟨x⟩ : FundamentalCategory.fundamentalCategoryFunctor.obj X) ⟶
                    (⟨γ T⟩ : FundamentalCategory.fundamentalCategoryFunctor.obj X)) ≫
                ⟦SplitDipath.SecondPart γ T⟧) ≫
            eqToHom _
    rw [CategoryTheory.Functor.map_comp, CategoryTheory.Functor.map_comp,
      functor_uniq_of_covered hX X₁_open X₂_open h_comm F' h₁ h₂ hγ.left, ih hγ.right]
    simp [T]


-- @@ L1118-1127 expanded
lemma functor_uniq (F' : (FundamentalCategory.fundamentalCategoryFunctor.obj X) ⥤ C)
    (h₁ : (FundamentalCategory.fundamentalCategoryMap j₁) ⋙ F' = F₁)
    (h₂ : (FundamentalCategory.fundamentalCategoryMap j₂) ⋙ F' = F₂) : F' = F :=
  by
  refine CategoryTheory.Functor.ext ?_ ?_
  · intro x
    exact functor_uniq_aux_obj hX X₁_open X₂_open h_comm F' h₁ h₂ x.as
  · intros x y f
    rw [← Quotient.out_eq f]
    cases has_subpaths hX X₁_open X₂_open (Quotient.out f)
    rename_i n hn
    exact functor_uniq_aux_map hX X₁_open X₂_open h_comm F' h₁ h₂ hn


-- @@ L1128-1128 verbatim
end PushoutFunctor

-- @@ L1129-1159 expanded
/-- The Van Kampen Theorem: the fundamental category functor `dπ` induces a
pushout in the category of categories.
-/
theorem directed_van_kampen (X₁_open : IsOpen X₁) (X₂_open : IsOpen X₂) (hX : X₁ ∪ X₂ = Set.univ) :
    IsPushout (FundamentalCategory.fundamentalCategoryFunctor.map i₁)
      (FundamentalCategory.fundamentalCategoryFunctor.map i₂)
      (FundamentalCategory.fundamentalCategoryFunctor.map j₁)
      (FundamentalCategory.fundamentalCategoryFunctor.map j₂) :=
  by
  apply PushoutAlternative.isPushout_alternative
  · rw [← Functor.map_comp, ← Functor.map_comp]
    apply congrArg FundamentalCategory.fundamentalCategoryFunctor.map
    ext x
    rfl
  intros C F₁ F₂ h_comm
  have h_comm' :
    (FundamentalCategory.fundamentalCategoryMap i₁) ⋙ F₁.toFunctor =
      (FundamentalCategory.fundamentalCategoryMap i₂) ⋙ F₂.toFunctor :=
    congrArg CategoryTheory.Cat.Hom.toFunctor h_comm
  use (PushoutFunctor.Functor hX X₁_open X₂_open h_comm').toCatHom
  constructor
  constructor
  · apply CategoryTheory.Cat.Hom.ext
    exact PushoutFunctor.functor_comp_left hX X₁_open X₂_open h_comm'
  · apply CategoryTheory.Cat.Hom.ext
    exact PushoutFunctor.functor_comp_right hX X₁_open X₂_open h_comm'
  · rintro F' ⟨h₁, h₂⟩
    apply CategoryTheory.Cat.Hom.ext
    exact
      PushoutFunctor.functor_uniq hX X₁_open X₂_open h_comm' F'.toFunctor
        (congrArg CategoryTheory.Cat.Hom.toFunctor h₁)
        (congrArg CategoryTheory.Cat.Hom.toFunctor h₂)


-- @@ L1160-1160 verbatim
end DirectedVanKampen
