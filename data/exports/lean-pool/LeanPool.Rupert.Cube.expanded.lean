/-
Copyright (c) 2026 David Renshaw. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Renshaw
-/
module

public import LeanPool.Rupert.Equivalences.Util
import LeanPool.Rupert.Attr
import LeanPool.Rupert.Equivalences.RupertEquivRupertPrime
import LeanPool.Rupert.MatrixSimps


-- @@ L13-17 verbatim
/-!
# LeanPool.Rupert.Cube

Imported Lean Pool material for `LeanPool.Rupert.Cube`.
-/


-- @@ L19-19 verbatim
@[expose] public section


-- @@ L21-21 verbatim
namespace Cube

-- @@ L22-22 verbatim
open Matrix


-- @@ L24-33 expanded
/-- Vertices of the cube centered at the origin with side length 2. -/
def cube : Fin 8 → EuclideanSpace ℝ (Fin 3) :=
  ![!₂[1, 1, 1], !₂[1, -1, 1], !₂[-1, -1, 1], !₂[-1, 1, 1], !₂[1, 1, -1], !₂[1, -1, -1],
    !₂[-1, -1, -1], !₂[-1, 1, -1]]


-- @@ L35-40 verbatim
/-- Denormalized rotation matrix used for the outer cube shadow. -/
noncomputable
abbrev outerRotDenorm : Matrix (Fin 3) (Fin 3) ℝ :=
   !![ 0, -√3, √3;
       2,  -1, -1;
      √2,  √2, √2]


-- @@ L42-45 verbatim
/-- Normalized rotation matrix used for the outer cube shadow. -/
noncomputable
abbrev outerRot : Matrix (Fin 3) (Fin 3) ℝ :=
   (1/√6) • outerRotDenorm


-- @@ L47-50 verbatim
private lemma outerRot_o3_lemma1 : (star outerRotDenorm) * outerRotDenorm = 6 • 1 := by
  (ext i j; fin_cases i, j) <;>
  · simp only [mul_apply, of_apply, cons_val', star_apply, Fin.sum_univ_three, cons_val]
    norm_num


-- @@ L52-56 verbatim
private lemma outerRot_o3_lemma2 : (outerRotDenorm) * (star outerRotDenorm) = 6 • 1 := by
  (ext i j; fin_cases i, j) <;>
  · simp only [mul_apply, of_apply, cons_val', star_apply, Fin.sum_univ_three, cons_val]
    try norm_num
    try ring_nf


-- @@ L58-60 verbatim
lemma two_three_six : √3 * √2 = √6 := by calc √3 * √2
     _ = √(3 * 2) := (Real.sqrt_mul' 3 zero_le_two).symm
     _ = √6 := by norm_num


-- @@ L62-93 verbatim
lemma outerRot_so3 : outerRot ∈ SO3 := by
 have h : (6 : ℝ) • 1 = (6 : Matrix (Fin 3) (Fin 3) ℝ) :=
   Matrix.smul_one_eq_diagonal 6
 have h2: (√6)⁻¹ * (√6)⁻¹ * 6 = 1 := by
   rw [←mul_inv]; simp
 let r := (√6)⁻¹
 have hr : (√6)⁻¹ = r := rfl
 dsimp only [outerRot]
 rw [Matrix.mem_specialOrthogonalGroup_iff]
 constructor
 · constructor
   · rw [star_smul, Matrix.smul_mul, Matrix.mul_smul, outerRot_o3_lemma1, smul_smul]
     simp only [one_div, star_trivial, nsmul_eq_mul, Nat.cast_ofNat, mul_one]
     rw [← h, smul_smul, h2, one_smul]
   · rw [star_smul, Matrix.smul_mul, Matrix.mul_smul, outerRot_o3_lemma2, smul_smul]
     simp only [one_div, star_trivial, nsmul_eq_mul, Nat.cast_ofNat, mul_one]
     rw [← h, smul_smul, h2, one_smul]
 · have : (Fin.succAbove 2 1 : Fin 3) = 1 := by rfl
   simp_all only [one_div, Matrix.smul_of, mul_zero,
     Matrix.det_succ_row_zero,
     Matrix.of_apply,
     Matrix.submatrix_apply, Fin.succ_zero_eq_one,
     Matrix.det_unique, Fin.default_eq_zero, Fin.succ_one_eq_two,
     Fin.sum_univ_two, Fin.val_zero, Fin.zero_succAbove,
     Fin.val_one, ne_eq, one_ne_zero, not_false_eq_true,
     Fin.succAbove_ne_zero_zero, Fin.sum_univ_three, Fin.one_succAbove_one,
     Fin.val_two, Fin.reduceEq, matrix_simps]
   ring_nf
   suffices h : (r * r * 6) * (√3 * √2) * r = 1 by (ring_nf at h; exact h)
   simp only [h2]
   rw [two_three_six]
   grind


-- @@ L95-96 expanded
/-- Projection of the rotated cube vertices for the outer shadow. -/
def outerShadow : Set (EuclideanSpace ℝ (Fin 2)) :=
  {x | ∃ i, projXy (outerRot.toEuclideanLin (cube i)) = x}


-- @@ L98-100 expanded
/-- Projection of the denormalized rotated cube vertices for the outer shadow. -/
def outerShadowDenorm : Set (EuclideanSpace ℝ (Fin 2)) :=
  {x | ∃ i, projXy (outerRotDenorm.toEuclideanLin (cube i)) = x}


-- @@ L102-112 expanded
/-- Denormalized outer-shadow vertices of the cube. -/
noncomputable def outerShadowPointsDenorm : Fin 8 → EuclideanSpace ℝ (Fin 2) :=
  ![!₂[0, 0], !₂[√3 * 2, 2], !₂[√3 * 2, -2], !₂[0, -4], !₂[-√3 * 2, 2], !₂[0, 4], !₂[0, 0],
    !₂[-√3 * 2, -2]]


-- @@ L114-136 expanded
lemma outerShadow_points_in_shadow :
    ∀ (i : Fin 8), (1 / √6) • (outerShadowPointsDenorm i) ∈ outerShadow :=
  by
  intro i
  have xfer (x : EuclideanSpace ℝ (Fin 2)) : x ∈ outerShadowDenorm → (1 / √6) • x ∈ outerShadow :=
    by
    simp only [outerShadow, outerRot, outerShadowDenorm]
    intro ⟨w, hw⟩
    use w
    rw [← hw]
    change _ = _ • projXyLinear _
    simp only [← projXyLinear.map_smul]
    fin_cases w <;>
      simp [matrix_simps, projXy, one_div, smul_of, smul_cons, smul_eq_mul, mul_zero, mul_neg,
        smul_empty, mul_one, cube, Fin.isValue, toLin'_apply, vecHead, vecTail, Nat.succ_eq_add_one,
        Nat.reduceAdd, Function.comp_apply, Fin.succ_zero_eq_one, Fin.succ_one_eq_two, add_zero,
        zero_add, cons_val_zero, cons_val_one, projXyLinear, LinearMap.coe_mk, AddHom.coe_mk]
  apply xfer
  unfold outerShadowDenorm
  rw [Set.mem_ofPred_eq]
  use i
  fin_cases i <;> simp [matrix_simps, cube, projXy, outerShadowPointsDenorm, mul_two] <;> norm_num


-- @@ L138-154 verbatim
private lemma weighted_pair_in_shadow (i j : Fin 8) :
    ((3:ℝ)/4) • (1/√6) • outerShadowPointsDenorm i +
      ((1:ℝ)/4) • (1/√6) • outerShadowPointsDenorm j ∈ convexHull ℝ outerShadow := by
  rw [mem_convexHull_iff_exists_fintype]
  use Fin 2, inferInstance
  use ![3/4, 1/4],
    ![(1/√6) • outerShadowPointsDenorm i, (1/√6) • outerShadowPointsDenorm j]
  refine ⟨?_, ?_, ?_, ?_⟩
  · intro k; fin_cases k <;>
    · simp only [Fin.mk_one, Fin.zero_eta, cons_val_one, cons_val_zero]
      positivity
  · simp only [Fin.sum_univ_two, cons_val_zero, cons_val_one]; norm_num
  · intro k; fin_cases k <;> apply outerShadow_points_in_shadow
  · simp_all

---------------------------------------------------------------------------------
-- ++


-- @@ L156-159 verbatim
/-- Upper-right point of the rectangle inside the outer shadow convex hull. -/
noncomputable
def rpp := ((3:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 1 +
           ((1:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 5


-- @@ L161-162 verbatim
theorem rpp_in_shadow : rpp ∈ convexHull ℝ outerShadow :=
  weighted_pair_in_shadow 1 5


-- @@ L164-173 verbatim
theorem rpp_contains_cube : 1 < rpp 0 ∧ 1 < rpp 1 := by
  dsimp only [rpp, outerShadowPointsDenorm, Matrix.cons_val,
     PiLp.add_apply, PiLp.smul_apply, smul_eq_mul]
  constructor
  · apply lt_of_mul_self_lt_mul_self₀ (by positivity)
    ring_nf
    norm_num
  · apply lt_of_mul_self_lt_mul_self₀ (by positivity)
    ring_nf
    norm_num


-- @@ L175-186 verbatim
theorem rpp_contains_cube2 :-1 < rpp 0 ∧ -1 < rpp 1 := by
 dsimp only [rpp, outerShadowPointsDenorm, Matrix.cons_val,
     PiLp.add_apply, PiLp.smul_apply, smul_eq_mul]
 constructor
 · have : 0 < 3 / 4 * (1 / √6 * (√3 * 2)) + 1 / 4 * (1 / √6 * 0) + 1 := by positivity
   grind
 · have : 0 < 3 / 4 * (1 / √6 * 2) + 1 / 4 * (1 / √6 * 4) + 1 := by positivity
   grind


---------------------------------------------------------------------------------
-- +-


-- @@ L188-191 verbatim
/-- Lower-right point of the rectangle inside the outer shadow convex hull. -/
noncomputable
def rpn := ((3:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 2 +
           ((1:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 3


-- @@ L193-197 verbatim
theorem rpn_in_shadow : rpn ∈ convexHull ℝ outerShadow :=
  weighted_pair_in_shadow 2 3

---------------------------------------------------------------------------------
-- -+


-- @@ L199-202 verbatim
/-- Upper-left point of the rectangle inside the outer shadow convex hull. -/
noncomputable
def rnp := ((3:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 4 +
           ((1:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 5


-- @@ L204-208 verbatim
theorem rnp_in_shadow : rnp ∈ convexHull ℝ outerShadow :=
  weighted_pair_in_shadow 4 5

---------------------------------------------------------------------------------
-- --


-- @@ L210-213 verbatim
/-- Lower-left point of the rectangle inside the outer shadow convex hull. -/
noncomputable
def rnn := ((3:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 7 +
           ((1:ℝ)/4) • (1/√6) • outerShadowPointsDenorm 3


-- @@ L215-216 verbatim
theorem rnn_in_shadow : rnn ∈ convexHull ℝ outerShadow :=
  weighted_pair_in_shadow 7 3


-- @@ L218-231 verbatim
theorem rnn_contains_cube : -1 > rnn 0 ∧ -1 > rnn 1 := by
  dsimp only [rnn, outerShadowPointsDenorm, Matrix.cons_val,
     PiLp.add_apply, PiLp.smul_apply, smul_eq_mul]
  constructor
  · apply neg_lt_neg_iff.mp
    simp only [neg_neg, neg_mul, mul_neg, mul_zero, add_zero]
    apply lt_of_mul_self_lt_mul_self₀ (by positivity)
    ring_nf
    norm_num
  · apply neg_lt_neg_iff.mp
    simp only [neg_neg, mul_neg, neg_add_rev]
    apply lt_of_mul_self_lt_mul_self₀ (by positivity)
    ring_nf
    norm_num


-- @@ L233-242 verbatim
theorem rnn_contains_cube2 : rnn 0 < 1 ∧ rnn 1 < 1 := by
 dsimp only [rnn, outerShadowPointsDenorm, Matrix.cons_val,
     PiLp.add_apply, PiLp.smul_apply, smul_eq_mul]
 constructor
 · have : 0 < 3 / 4 * (1 / √6 * (√3 * 2)) + 1 / 4 * (1 / √6 * 0) + 1 := by positivity
   linarith
 · have : 0 < 3 / 4 * (1 / √6 * 2) + 1 / 4 * (1 / √6 * 4) + 1 := by positivity
   linarith

---------------------------------------------------------------------------------


-- @@ L244-246 expanded
/-- Converts a vector in `ℝ²` to its coordinate pair. -/
@[simp]
def extract (v : EuclideanSpace ℝ (Fin 2)) : ℝ × ℝ :=
  ⟨v 0, v 1⟩


-- @@ L247-249 expanded
/-- Converts a coordinate pair into a vector in `ℝ²`. -/
@[simp]
def inject (v : ℝ × ℝ) : EuclideanSpace ℝ (Fin 2) :=
  !₂[v.1, v.2]


-- @@ L251-253 expanded
/-- Open axis-aligned rectangle in `ℝ²`. -/
def openRectangle (xmin xmax ymin ymax : ℝ) : Set (EuclideanSpace ℝ (Fin 2)) :=
  inject '' (Set.Ioo xmin xmax ×ˢ Set.Ioo ymin ymax)


-- @@ L255-257 expanded
/-- Closed axis-aligned rectangle in `ℝ²`. -/
def closedRectangle (xmin xmax ymin ymax : ℝ) : Set (EuclideanSpace ℝ (Fin 2)) :=
  inject '' (Set.Icc xmin xmax ×ˢ Set.Icc ymin ymax)


-- @@ L259-261 expanded
/-- Vertices of an axis-aligned rectangle in `ℝ²`. -/
def rectVertices (xmin xmax ymin ymax : ℝ) : Fin 4 → EuclideanSpace ℝ (Fin 2) :=
  ![!₂[xmin, ymin], !₂[xmax, ymin], !₂[xmin, ymax], !₂[xmax, ymax]]


-- @@ L263-309 verbatim
/-- A closed rectangle is the convex hull of its four vertices. -/
lemma closedRectangle_is_convex_hull
    (xmin xmax ymin ymax : ℝ) (xlt : xmin < xmax) (ylt : ymin < ymax) :
    closedRectangle xmin xmax ymin ymax
    = convexHull ℝ (Set.range (rectVertices xmin xmax ymin ymax)) := by
  let prodset : Set (ℝ × ℝ) := {xmin, xmax} ×ˢ {ymin, ymax}
  have lemma1 (S : Set (ℝ × ℝ)) :
      inject '' (convexHull ℝ S) = convexHull ℝ (inject '' S) := by
    apply IsLinearMap.image_convexHull
    let m₂ := (PiLp.continuousLinearEquiv 2 ℝ (ι := Fin 2) (β := fun _ ↦ ℝ)).toLinearEquiv
    exact ((LinearEquiv.finTwoArrow ℝ ℝ).symm.trans m₂.symm).isLinear
  have lemma2 : Set.range (rectVertices xmin xmax ymin ymax) = inject '' prodset := by
    ext p; constructor
    · intro ⟨w, e⟩
      rw [← e]
      fin_cases w
      all_goals simp only [Set.mem_image, Prod.exists]
      · use xmin, ymin
        refine ⟨?_, rfl⟩
        exact Set.mk_mem_prod (Set.mem_insert xmin {xmax}) (Set.mem_insert ymin {ymax})
      · use xmax, ymin
        refine ⟨?_, rfl⟩
        exact Set.mk_mem_prod (Set.mem_insert_of_mem xmin rfl) (Set.mem_insert ymin {ymax})
      · use xmin, ymax
        refine ⟨?_, rfl⟩
        exact Set.mk_mem_prod (Set.mem_insert xmin {xmax}) (Set.mem_insert_of_mem ymin rfl)
      · use xmax, ymax
        refine ⟨?_, rfl⟩
        exact Set.mk_mem_prod (Set.mem_insert_of_mem xmin rfl) (Set.mem_insert_of_mem ymin rfl)
    · intro ⟨⟨x, y⟩, ⟨⟨hx, hy⟩, hp⟩⟩
      rw [← hp]
      simp only [Set.mem_insert_iff, Set.mem_singleton_iff] at hx hy
      match hx, hy with
      | Or.inl hx0, Or.inl hy0 => rw [← hx0, ← hy0]; use 0; rfl
      | Or.inr hx0, Or.inl hy0 => rw [← hx0, ← hy0]; use 1; rfl
      | Or.inl hx0, Or.inr hy0 => rw [← hx0, ← hy0]; use 2; rfl
      | Or.inr hx0, Or.inr hy0 => rw [← hx0, ← hy0]; use 3; rfl
  have xmin_fact : (xmin ⊓ xmax) = xmin := min_eq_left_of_lt xlt
  have xmax_fact : (xmin ⊔ xmax) = xmax := max_eq_right_of_lt xlt
  have ymin_fact : (ymin ⊓ ymax) = ymin := min_eq_left_of_lt ylt
  have ymax_fact : (ymin ⊔ ymax) = ymax := max_eq_right_of_lt ylt
  have intervalx : convexHull ℝ {xmin, xmax} = Set.Icc xmin xmax := by
    rw [convexHull_pair, segment_eq_Icc', xmin_fact, xmax_fact]
  have intervaly : convexHull ℝ {ymin, ymax} = Set.Icc ymin ymax := by
    rw [convexHull_pair, segment_eq_Icc', ymin_fact, ymax_fact]
  rw [lemma2, ← lemma1, convexHull_prod, intervalx, intervaly]
  ext; exact ⟨id, id⟩


-- @@ L311-311 expanded
lemma vector_ext (v : EuclideanSpace ℝ (Fin 2)) : !₂[v 0, v 1] = v := by ext i; fin_cases i <;> rfl


-- @@ L313-361 verbatim
/-- The interior of a closed rectangle is the corresponding open rectangle. -/
lemma openRectangle_is_interior (xmin xmax ymin ymax : ℝ) :
    interior (closedRectangle xmin xmax ymin ymax) = openRectangle xmin xmax ymin ymax := by
  simp only [closedRectangle, openRectangle]
  have transfer_int (X : Set (ℝ × ℝ)) : interior (inject '' X) = inject '' (interior X) := by
   ext p; constructor
   · intro h
     simp only [inject, Set.mem_image, Prod.exists, mem_interior] at h ⊢
     let ⟨U,⟨U_fits,⟨U_open,p_in_U⟩⟩⟩ := h
     use p 0, p 1
     refine ⟨⟨ extract '' U,  ⟨ ?_, ⟨ ?_, ?_⟩ ⟩⟩ , ?_⟩
     · intro x ⟨hx1, hx2, hx3⟩
       rw [← hx3]
       subst hx3
       have hx1_in_X := U_fits hx2
       simp_all only [Set.exists_subset_image_iff, Set.mem_image, Prod.exists, extract, Fin.isValue]
       obtain ⟨w, ⟨left, ⟨left_1, ⟨w_3, ⟨w_4, ⟨left_3, right⟩⟩⟩⟩⟩⟩ := h
       obtain ⟨w_1, ⟨w_2, ⟨left_2, right_1⟩⟩⟩ := hx1_in_X
       subst right right_1
       exact left_2
     · let hplp := PiLp.homeomorph (ι := Fin 2) (β := fun _ ↦ ℝ) 2
       let hplp' : (PiLp (ι := Fin 2) 2 fun x ↦ ℝ) ≃ₜ ℝ × ℝ :=
         hplp.trans Homeomorph.finTwoArrow
       rw [←hplp'.isOpen_image] at U_open
       exact U_open
     · rw [Set.mem_image]
       use !₂[p 0, p 1]
       simp_all only [Set.exists_subset_image_iff, Set.mem_image, Prod.exists, Fin.isValue,
         extract, cons_val_zero, cons_val_one, cons_val_fin_one, and_true]
       let ⟨_, ⟨_, ⟨_, ⟨_, ⟨_, ⟨_, right⟩⟩⟩⟩⟩⟩ := h
       subst right
       exact p_in_U
     · apply vector_ext
   · intro h
     simp only [inject, Set.mem_image, Prod.exists, mem_interior] at h ⊢
     let ⟨p0,⟨p1 ,⟨ U, ⟨U_fits,U_open,p_in_U⟩ ⟩ , pext ⟩⟩ := h
     refine ⟨?_ , ⟨?_, ⟨?_, ?_⟩⟩ ⟩
     · exact inject '' U
     · intro x hx
       exact Set.image_mono U_fits hx
     · let hplp := PiLp.homeomorph (ι := Fin 2) (β := fun _ ↦ ℝ) 2
       let hplp' : (PiLp (ι := Fin 2) 2 fun x ↦ ℝ) ≃ₜ ℝ × ℝ :=
         hplp.trans Homeomorph.finTwoArrow
       rw [←hplp'.symm.isOpen_image] at U_open
       exact U_open
     · rw [Set.mem_image]
       use (p0, p1)
       simp_all
  rw [transfer_int, interior_prod_eq, interior_Icc, interior_Icc]


-- @@ L363-366 verbatim
lemma nontrivial_rectangle0 : rnn 0 < rpp 0 := by
  simp only [rnn, rpp, outerShadowPointsDenorm, neg_mul, cons_val, PiLp.add_apply,
    PiLp.smul_apply, smul_eq_mul, mul_neg, mul_zero, add_zero, neg_lt_self_iff]
  positivity


-- @@ L368-373 verbatim
lemma nontrivial_rectangle1 : rnn 1 < rpp 1 := by
  simp only [rnn, rpp, outerShadowPointsDenorm]
  simp only [one_div, neg_mul, Fin.isValue, cons_val, PiLp.add_apply, PiLp.smul_apply, cons_val_one,
    smul_eq_mul, cons_val_zero]
  field_simp
  norm_num


-- @@ L375-376 expanded
/-- Closed rectangle used as the intermediate shadow contained in the outer hull. -/
def closedMediant : Set (EuclideanSpace ℝ (Fin 2)) :=
  closedRectangle (rnn 0) (rpp 0) (rnn 1) (rpp 1)


-- @@ L378-379 expanded
/-- Interior rectangle used as the intermediate shadow contained in the outer hull. -/
def openMediant : Set (EuclideanSpace ℝ (Fin 2)) :=
  openRectangle (rnn 0) (rpp 0) (rnn 1) (rpp 1)


-- @@ L381-389 verbatim
lemma rect_fact1 : ![rpp 0, rnn 1] = rpn := by
  have coord0 : rpp 0 = rpn 0 := by
    simp only [Fin.isValue, rpp, one_div, outerShadowPointsDenorm, neg_mul, cons_val_one,
      cons_val_zero, cons_val, PiLp.add_apply, PiLp.smul_apply, smul_eq_mul, mul_zero, add_zero,
      rpn]
  have coord1 : rnn 1 = rpn 1 := by
    simp only [Fin.isValue, rnn, one_div, outerShadowPointsDenorm, neg_mul, cons_val,
      PiLp.add_apply, PiLp.smul_apply, cons_val_one, cons_val_fin_one, smul_eq_mul, mul_neg, rpn]
  rw [← vector_ext rpn, coord0, coord1]


-- @@ L391-399 verbatim
lemma rect_fact2 : ![rnn 0, rpp 1] = rnp := by
  have coord0 : rnn 0 = rnp 0 := by
    simp only [Fin.isValue, rnn, one_div, outerShadowPointsDenorm, neg_mul, cons_val,
      PiLp.add_apply, PiLp.smul_apply, cons_val_zero, smul_eq_mul, mul_neg, mul_zero, add_zero,
      rnp]
  have coord1 : rpp 1 = rnp 1 := by
    simp only [Fin.isValue, rpp, one_div, outerShadowPointsDenorm, neg_mul, cons_val,
      PiLp.add_apply, PiLp.smul_apply, cons_val_one, cons_val_fin_one, smul_eq_mul, rnp]
  rw [← vector_ext rnp, coord0, coord1]


-- @@ L401-417 verbatim
lemma mediant_sub_hull_outer :
    Set.range (rectVertices (rnn 0) (rpp 0) (rnn 1) (rpp 1)) ⊆
      convexHull ℝ outerShadow := by
  intro x ⟨i, hx⟩
  fin_cases i
  · rw [← hx]
    simpa [matrix_simps, rectVertices, vector_ext, rect_fact1, rect_fact2,
      Fin.isValue] using rnn_in_shadow
  · rw [← hx]
    simpa [matrix_simps, rectVertices, vector_ext, rect_fact1, rect_fact2,
      Fin.isValue] using rpn_in_shadow
  · rw [← hx]
    simpa [matrix_simps, rectVertices, vector_ext, rect_fact1, rect_fact2,
      Fin.isValue] using rnp_in_shadow
  · rw [← hx]
    simpa [matrix_simps, rectVertices, vector_ext, rect_fact1, rect_fact2,
      Fin.isValue] using rpp_in_shadow


-- @@ L419-424 verbatim
lemma mediant_sub_outer : closedMediant ⊆ convexHull ℝ outerShadow := by
  have : Convex ℝ (convexHull ℝ outerShadow) := by apply convex_convexHull
  rw [closedMediant,
    closedRectangle_is_convex_hull _ _ _ _ nontrivial_rectangle0 nontrivial_rectangle1]
  apply (Convex.convexHull_subset_iff this).mpr
  exact mediant_sub_hull_outer


-- @@ L426-428 verbatim
private lemma interior_mediant_sub_outer :
    interior closedMediant ⊆ interior (convexHull ℝ outerShadow) :=
  interior_mono mediant_sub_outer


-- @@ L430-520 verbatim
theorem rupert : IsRupert cube := by
  rw [rupert_iff_rupert']
  use 1, Submonoid.one_mem SO3, 0, outerRot, outerRot_so3
  intro inner_shadow outshad x hx
  let ⟨w, p⟩ := hx
  simp only [zero_add] at p
  fin_cases w
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, Fin.zero_eta, cons_val_zero, cons_val_one,
      cons_val_fin_one, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_,⟩, ⟨⟩⟩
    · simp [rnn_contains_cube2.1]
    · simp [rpp_contains_cube.1]
    · simp [rnn_contains_cube2.2]
    · simp [rpp_contains_cube.2]
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, Fin.mk_one, cons_val_one, cons_val_zero,
      cons_val_fin_one, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, ⟨⟩⟩
    · simp [rnn_contains_cube2.1]
    · simp [rpp_contains_cube.1]
    · simp [rnn_contains_cube.2]
    · simp [rpp_contains_cube2.2]
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, Fin.reduceFinMk, cons_val, cons_val_zero, cons_val_one,
      cons_val_fin_one, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, ⟨⟩⟩
    · simp [rnn_contains_cube.1]
    · simp [rpp_contains_cube2.1]
    · simp [rnn_contains_cube.2]
    · simp [rpp_contains_cube2.2]
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, Fin.reduceFinMk, cons_val, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, ⟨⟩⟩
    · simp [rnn_contains_cube.1]
    · simp [rpp_contains_cube2.1]
    · simp [rnn_contains_cube2.2]
    · simp [rpp_contains_cube.2]
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, rfl⟩
    · simp [rnn_contains_cube2.1]
    · simp [rpp_contains_cube.1]
    · simp [rnn_contains_cube2.2]
    · simp [rpp_contains_cube.2]
-- second half of points, same as the first:
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, rfl⟩
    · simp [rnn_contains_cube2.1]
    · simp [rpp_contains_cube.1]
    · simp [rnn_contains_cube.2]
    · simp [rpp_contains_cube2.2]
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, Fin.reduceFinMk, cons_val, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, ⟨⟩⟩
    · simp [rnn_contains_cube.1]
    · simp [rpp_contains_cube2.1]
    · simp [rnn_contains_cube.2]
    · simp [rpp_contains_cube2.2]
  · apply interior_mediant_sub_outer
    rw [closedMediant, openRectangle_is_interior]
    use extract x
    simp only [projXy, cube, ← p]
    simp only [Fin.isValue, extract, Fin.reduceFinMk, cons_val, inject]
    refine ⟨⟨⟨?_, ?_⟩, ?_, ?_⟩, ⟨⟩⟩
    · simp [rnn_contains_cube.1]
    · simp [rpp_contains_cube2.1]
    · simp [rnn_contains_cube2.2]
    · simp [rpp_contains_cube.2]


-- @@ L522-522 verbatim
end Cube
