/-
Copyright (c) 2026 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
module

public import LeanPool.RiemannMappingTheorem.Cindex
import LeanPool.RiemannMappingTheorem.Uniform
import Mathlib.Analysis.Complex.LocallyUniformLimit


-- @@ L12-14 verbatim
/-!
# LeanPool.RiemannMappingTheorem.Hurwitz
-/


-- @@ L16-16 verbatim
@[expose] public section


-- @@ L18-18 verbatim
open Filter Topology Set Metric Uniformity


-- @@ L20-20 verbatim
section filter


-- @@ L22-22 verbatim
variable {α 𝕜 : Type*} {s : Set α} {z₀ : α} {P : α → Prop} {p : Filter α} {φ : ℝ → Set α}


-- @@ L24-29 verbatim
lemma mem_iff_eventually_subset (hp : p.HasBasis (fun t : ℝ => 0 < t) φ) (hφ : Monotone φ) :
    s ∈ p ↔ (∀ᶠ t in 𝓝[>] 0, φ t ⊆ s) := by
  rw [(nhdsWithin_hasBasis nhds_basis_closedBall (Ioi (0 : ℝ))).eventually_iff]
  simp_rw [hp.mem_iff, ← exists_prop, mem_inter_iff, mem_closedBall_zero_iff]
  refine exists₂_congr (fun ε hε => ⟨fun h r h' => (hφ (le_of_abs_le h'.1)).trans h,
    fun h => h ⟨Eq.le (abs_eq_self.mpr hε.le), hε⟩⟩)


-- @@ L31-33 verbatim
lemma eventually_nhds_iff_eventually_ball [PseudoMetricSpace α] :
    (∀ᶠ z in 𝓝 z₀, P z) ↔ (∀ᶠ r in 𝓝[>] 0, ∀ z ∈ ball z₀ r, P z) :=
  mem_iff_eventually_subset nhds_basis_ball (fun _ _ => ball_subset_ball)


-- @@ L35-37 verbatim
lemma eventually_nhds_iff_eventually_closed_ball [PseudoMetricSpace α] :
    (∀ᶠ z in 𝓝 z₀, P z) ↔ (∀ᶠ r in 𝓝[>] 0, ∀ z ∈ closedBall z₀ r, P z) :=
  mem_iff_eventually_subset nhds_basis_closedBall (fun _ _ => closedBall_subset_closedBall)


-- @@ L39-39 verbatim
end filter


-- @@ L41-41 verbatim
section unifops


-- @@ L43-44 verbatim
variable {𝕜 ι α : Type*} {s K : Set α} [NormedField 𝕜] {F G : ι → α → 𝕜} {f g : α → 𝕜} {x y : 𝕜}
  {η η' : ℝ} {p : Filter ι} {mf mg : ℝ}


-- @@ L46-50 verbatim
lemma dist_inv_le_dist_div (hη : 0 < η) (hη' : 0 < η') (hx : x ∉ ball 0 η) (hy : y ∉ ball 0 η') :
    dist x⁻¹ y⁻¹ ≤ dist x y / (η * η') := by
  simp only [mem_ball, dist_eq_norm, sub_zero, not_lt] at hx hy
  rw [dist_inv_inv₀ (norm_pos_iff.mp (hη.trans_le hx)) (norm_pos_iff.mp (hη'.trans_le hy))]
  gcongr


-- @@ L52-70 verbatim
lemma titi {p q : Filter 𝕜} (hp : p ⊓ 𝓝 0 = ⊥) (hq : q ⊓ 𝓝 0 = ⊥) :
    map (fun x : 𝕜 × 𝕜 => (x.1⁻¹, x.2⁻¹)) (𝓤 𝕜 ⊓ (p ×ˢ q)) ≤ 𝓤 𝕜 := by
  obtain ⟨U, hU, V, hV, hUV⟩ := inf_eq_bot_iff.mp hp
  obtain ⟨U', hU', V', hV', hUV'⟩ := inf_eq_bot_iff.mp hq
  obtain ⟨η, hη, hV⟩ := Metric.mem_nhds_iff.mp hV
  obtain ⟨η', hη', hV'⟩ := Metric.mem_nhds_iff.mp hV'
  have hηη' : 0 < η * η' := mul_pos hη hη'
  intro u hu
  obtain ⟨ε, hε, hu⟩ := mem_uniformity_dist.mp hu
  rw [mem_map_iff_exists_image]
  refine ⟨_, inter_mem_inf (dist_mem_uniformity (mul_pos hε hηη')) (prod_mem_prod hU hU'), ?_⟩
  rintro z ⟨x, ⟨hx1, hx2⟩, rfl⟩
  have hx'1 : x.1 ∉ ball (0 : 𝕜) η :=
    fun h => (Set.nonempty_of_mem (mem_inter hx2.1 (hV h))).ne_empty hUV
  have hx'2 : x.2 ∉ ball (0 : 𝕜) η' :=
    fun h => (Set.nonempty_of_mem (mem_inter hx2.2 (hV' h))).ne_empty hUV'
  refine hu ((dist_inv_le_dist_div hη hη' hx'1 hx'2).trans_lt ?_)
  convert (div_lt_div_iff_of_pos_right hηη').mpr hx1
  field_simp [hη.lt.ne.symm, hη'.lt.ne.symm]


-- @@ L72-74 verbatim
lemma uniform_ContinuousOn_inv {s : Set 𝕜} (hs : 𝓟 s ⊓ 𝓝 0 = ⊥) :
    UniformContinuousOn Inv.inv s := by
  simpa only [UniformContinuousOn, Tendsto, ← prod_principal_principal] using titi hs hs


-- @@ L76-84 expanded
lemma TendstoUniformlyOn.inv_of_isolated_zero (hF : TendstoUniformlyOn F f p s)
    (hf : 𝓟 (f '' s) ⊓ 𝓝 0 = ⊥) : TendstoUniformlyOn F⁻¹ f⁻¹ p s :=
  by
  have : uniformNhdsSet (f '' s) ⊓ 𝓝 0 = ⊥ :=
    by
    rw [inf_comm] at hf ⊢
    exact UniformSpace.nhds_inf_uniform_nhds_eq_bot hf
  have h1 := lemma1 hF
  rw [tendstoUniformlyOn_iff_tendsto] at hF ⊢
  refine (Filter.map_mono (le_inf hF h1)).trans (titi hf this)


-- @@ L86-86 verbatim
lemma lxyab {x y a b : 𝕜} : x * a - y * b = (x - y) * a + y * (a - b) := by ring


-- @@ L88-128 verbatim
lemma TendstoUniformlyOn.mul_of_le
    (hF : TendstoUniformlyOn F f p s) (hG : TendstoUniformlyOn G g p s)
    (hf : ∀ᶠ i in p, ∀ x ∈ s, ‖F i x‖ ≤ mf) (hg : ∀ᶠ i in p, ∀ x ∈ s, ‖G i x‖ ≤ mg) :
    TendstoUniformlyOn (F * G) (f * g) p s := by
  by_cases h : NeBot p
  case neg => simp at h; simp [h, TendstoUniformlyOn]
  case pos =>
    set Mf := |mf| + 1
    set Mg := |mg| + 1
    have hMf : 0 < Mf := by positivity
    have hMg : 0 < Mg := by positivity
    replace hf : ∀ᶠ i in p, ∀ x ∈ s, ‖F i x‖ ≤ Mf := by
      filter_upwards [hf] with i hF x hx using
        (hF x hx).trans ((le_abs_self mf).trans (lt_add_one _).le)
    replace hg : ∀ᶠ i in p, ∀ x ∈ s, ‖G i x‖ ≤ Mg := by
      filter_upwards [hg] with i hG x hx using
        (hG x hx).trans ((le_abs_self mg).trans (lt_add_one _).le)
    have h1 : ∀ x ∈ s, ‖g x‖ ≤ Mg := fun x hx =>
      le_of_tendsto ((continuous_norm.tendsto (g x)).comp (hG.tendsto_at hx))
        (by filter_upwards [hg] with i hg using hg x hx)
    simp_rw [Metric.tendstoUniformlyOn_iff, dist_eq_norm] at hF hG ⊢
    intro ε hε
    filter_upwards [hf, hF (ε / (2 * Mg)) (by positivity),
      hG (ε / (2 * Mf)) (by positivity)] with i hf hF hG x hx
    have h2 : ‖(f x - F i x) * g x‖ < ε / 2 := by
      rw [norm_mul]
      by_cases h : g x = 0
      case pos => simp [h, half_pos hε]
      case neg =>
        refine (mul_lt_mul (hF x hx) (h1 x hx) (norm_pos_iff.mpr h)
          (by positivity)).trans_eq ?_
        field_simp
    have h3 : ‖F i x * (g x - G i x)‖ < ε / 2 := by
      rw [norm_mul]
      by_cases h : F i x = 0
      case pos => simp [h, half_pos hε]
      case neg =>
        refine (mul_lt_mul' (hf x hx) (hG x hx) (norm_nonneg _) hMf).trans_eq ?_
        field_simp
    simp_rw [Pi.mul_apply, lxyab]
    exact (norm_add_le _ _).trans_lt (add_halves ε ▸ add_lt_add h2 h3)


-- @@ L130-136 verbatim
private lemma bound_of_tendstoUniformlyOn (hF : TendstoUniformlyOn F f p s)
    (hf : ∀ x ∈ s, ‖f x‖ ≤ mf) : ∀ᶠ i in p, ∀ x ∈ s, ‖F i x‖ ≤ mf + 1 := by
  simp_rw [Metric.tendstoUniformlyOn_iff, dist_eq_norm] at hF
  filter_upwards [hF 1 zero_lt_one] with i hF x hx
  linarith [hF x hx, hf x hx,
    (by simpa [← norm_neg (F i x - f x)] using norm_add_le (F i x - f x) (f x) :
      ‖F i x‖ ≤ ‖f x - F i x‖ + ‖f x‖)]


-- @@ L138-142 verbatim
lemma TendstoUniformlyOn.mul_of_bound
    (hF : TendstoUniformlyOn F f p s) (hG : TendstoUniformlyOn G g p s)
    (hf : ∀ x ∈ s, ‖f x‖ ≤ mf) (hg : ∀ x ∈ s, ‖g x‖ ≤ mg) :
    TendstoUniformlyOn (F * G) (f * g) p s :=
  hF.mul_of_le hG (bound_of_tendstoUniformlyOn hF hf) (bound_of_tendstoUniformlyOn hG hg)


-- @@ L144-144 verbatim
variable [TopologicalSpace α]


-- @@ L146-151 verbatim
lemma TendstoUniformlyOn.inv_of_compact (hF : TendstoUniformlyOn F f p K)
    (hf : ContinuousOn f K) (hK : IsCompact K) (hfz : ∀ x ∈ K, f x ≠ 0) :
    TendstoUniformlyOn F⁻¹ f⁻¹ p K := by
  apply hF.inv_of_isolated_zero
  rw [inf_comm, inf_principal_eq_bot]
  exact (hK.image_of_continuousOn hf).isClosed.compl_mem_nhds (fun ⟨z, h1, h2⟩ => hfz z h1 h2)


-- @@ L153-165 verbatim
lemma TendstoUniformlyOn.mul_of_compact
    (hF : TendstoUniformlyOn F f p K) (hG : TendstoUniformlyOn G g p K)
    (hf : ContinuousOn f K) (hg : ContinuousOn g K) (hK : IsCompact K) :
    TendstoUniformlyOn (F * G) (f * g) p K := by
  by_cases h : K = ∅
  case pos => simpa only [h] using tendstoUniformlyOn_empty
  case neg =>
    replace h : K.Nonempty := Set.nonempty_iff_ne_empty.2 h
    have h2 : ContinuousOn (norm ∘ f) K := continuous_norm.comp_continuousOn hf
    have h3 : ContinuousOn (norm ∘ g) K := continuous_norm.comp_continuousOn hg
    obtain ⟨xf, _, h4⟩ : ∃ x ∈ K, ∀ y ∈ K, ‖f y‖ ≤ ‖f x‖ := hK.exists_isMaxOn h h2
    obtain ⟨xg, _, h5⟩ : ∃ x ∈ K, ∀ y ∈ K, ‖g y‖ ≤ ‖g x‖ := hK.exists_isMaxOn h h3
    exact hF.mul_of_bound hG h4 h5


-- @@ L167-172 verbatim
lemma TendstoUniformlyOn.div_of_compact
    (hF : TendstoUniformlyOn F f p K) (hG : TendstoUniformlyOn G g p K)
    (hf : ContinuousOn f K) (hg : ContinuousOn g K) (hgK : ∀ z ∈ K, g z ≠ 0) (hK : IsCompact K) :
    TendstoUniformlyOn (F / G) (f / g) p K := by
  simpa [div_eq_mul_inv] using
    hF.mul_of_compact (hG.inv_of_compact hg hK hgK) hf (hg.inv₀ hgK) hK


-- @@ L174-174 verbatim
end unifops


-- @@ L176-176 verbatim
variable {ι : Type*} {F : ι → ℂ → ℂ} {f : ℂ → ℂ} {z₀ : ℂ} {p : Filter ι} {r : ℝ} {U : Set ℂ}


-- @@ L178-180 verbatim
lemma Filter.Eventually.exists' {P : ℝ → Prop} {t₀} (h : ∀ᶠ t in 𝓝[>] t₀, P t) :
    ∃ t > t₀, P t := by
  simpa [and_comm, exists_prop] using (frequently_nhdsWithin_iff.mp h.frequently).exists


-- @@ L182-191 verbatim
lemma order_eq_zero_iff {p : FormalMultilinearSeries ℂ ℂ ℂ}
    (hp : HasFPowerSeriesAt f p z₀) (hz₀ : f z₀ = 0) :
    p.order = 0 ↔ ∀ᶠ z in 𝓝 z₀, f z = 0 := by
  rw [hp.locally_zero_iff]
  by_cases h : p = 0
  case pos => simp [h]
  case neg =>
    simp only [FormalMultilinearSeries.order_eq_zero_iff h, ne_eq, h, iff_false, not_not]
    ext1
    rw [hp.coeff_zero, hz₀]; rfl


-- @@ L193-196 verbatim
lemma order_pos_iff {p : FormalMultilinearSeries ℂ ℂ ℂ}
    (hp : HasFPowerSeriesAt f p z₀) (hz₀ : f z₀ = 0) :
    0 < p.order ↔ ∃ᶠ z in 𝓝 z₀, f z ≠ 0 := by
  simp [pos_iff_ne_zero, (order_eq_zero_iff hp hz₀).not]


-- @@ L198-204 verbatim
lemma cindex_pos (h1 : AnalyticAt ℂ f z₀) (h2 : f z₀ = 0) (h3 : ∀ᶠ z in 𝓝[≠] z₀, f z ≠ 0) :
    ∀ᶠ r in 𝓝[>] 0, cindex z₀ r f ≠ 0 := by
  obtain ⟨p, hp⟩ := h1
  filter_upwards [cindex_eventually_eq_order hp] with r h4
  simpa [h4, order_eq_zero_iff hp h2] using h3.frequently.filter_mono nhdsWithin_le_nhds

-- TODO: this can be generalized a lot

-- @@ L205-218 verbatim
lemma hurwitz2_1 {K : Set ℂ} (hK : IsCompact K) (F_conv : TendstoUniformlyOn F f p K)
    (hf1 : ContinuousOn f K) (hf2 : ∀ z ∈ K, f z ≠ 0) :
    ∀ᶠ n in p, ∀ z ∈ K, F n z ≠ 0 := by
  by_cases h : K = ∅
  case pos => simp [h]
  case neg =>
    obtain ⟨z₀, h1, h2⟩ : ∃ z₀ ∈ K, ∀ z ∈ K, ‖f z₀‖ ≤ ‖f z‖ :=
      hK.exists_isMinOn (nonempty_iff_ne_empty.2 h) (continuous_norm.comp_continuousOn hf1)
    filter_upwards [tendstoUniformlyOn_iff.1 F_conv (‖f z₀‖) (norm_pos_iff.2 (hf2 _ h1))]
      with n hn z hz h
    have hnz := hn z hz
    have h2z := h2 z hz
    simp [h] at hnz
    linarith


-- @@ L220-241 verbatim
lemma TendstoUniformlyOn.tendsto_circle_integral (hr : 0 < r)
    (F_cont : ∀ᶠ n in p, ContinuousOn (F n) (sphere z₀ r))
    (F_conv : TendstoUniformlyOn F f p (sphere z₀ r)) :
    Filter.Tendsto (fun i => ∮ z in C(z₀, r), F i z) p (𝓝 (∮ z in C(z₀, r), f z))
    := by
  by_cases h : NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have f_cont : ContinuousOn f (sphere z₀ r) := F_conv.continuousOn F_cont.frequently
    rw [Metric.tendsto_nhds]
    intro ε hε
    have hpos : (2 * Real.pi * r)⁻¹ * ε > 0 :=
      mul_pos (inv_pos.mpr (mul_pos (mul_pos two_pos Real.pi_pos) hr)) hε.lt
    filter_upwards [tendstoUniformlyOn_iff.mp F_conv ((2 * Real.pi * r)⁻¹ * ε) hpos,
      F_cont] with n h h'
    simp_rw [dist_comm (f _) _, Complex.dist_eq] at h
    rw [Complex.dist_eq,
      ← circleIntegral.integral_sub (h'.circleIntegrable hr.le) (f_cont.circleIntegrable hr.le)]
    have hpt : z₀ + r ∈ sphere z₀ r := by simp [hr.le, Real.norm_eq_abs]
    convert circleIntegral.norm_integral_lt_of_norm_le_const_of_lt hr (h'.sub f_cont)
      (fun z hz => (h z hz).le) ⟨z₀ + r, hpt, h _ hpt⟩
    all_goals first | rfl | field_simp [hr.ne, Real.pi_ne_zero, two_ne_zero]


-- @@ L243-263 verbatim
lemma hurwitz2_2 (hU : IsOpen U) (hF : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U) (hr1 : 0 < r) (hr2 : sphere z₀ r ⊆ U)
    (hf1 : ∀ (z : ℂ), z ∈ sphere z₀ r → f z ≠ 0) :
    Tendsto (cindex z₀ r ∘ F) p (𝓝 (cindex z₀ r f)) := by
  by_cases h : NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have H1 : IsCompact (sphere z₀ r) := isCompact_sphere z₀ r
    have H2 : TendstoUniformlyOn F f p (sphere z₀ r) :=
      (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).1 hf _ hr2 H1
    have H3 : DifferentiableOn ℂ f U := hf.differentiableOn hF hU
    have H4 : ContinuousOn f (sphere z₀ r) := H3.continuousOn.mono hr2
    have H7 : TendstoUniformlyOn (deriv ∘ F) (deriv f) p (sphere z₀ r) :=
      (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).1 (hf.deriv hF hU) _ hr2 H1
    refine Tendsto.const_mul _ (TendstoUniformlyOn.tendsto_circle_integral hr1 ?_ ?_)
    · filter_upwards [hurwitz2_1 H1 H2 H4 hf1,
        hF.mono (fun n h => (h.deriv hU).continuousOn.mono hr2),
        hF.mono (fun n h => h.continuousOn.mono hr2)] with n hn H6 H5 using
        ContinuousOn.div H6 H5 hn
    · exact TendstoUniformlyOn.div_of_compact H7 H2
        ((H3.deriv hU).continuousOn.mono hr2) H4 hf1 H1


-- @@ L265-287 verbatim
lemma hurwitz2
    (hU : IsOpen U)
    (hF : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U)
    (hr1 : 0 < r)
    (hr2 : closedBall z₀ r ⊆ U)
    (hf1 : ∀ z ∈ sphere z₀ r, f z ≠ 0)
    (hf2 : cindex z₀ r f ≠ 0) :
    ∀ᶠ n in p, ∃ z ∈ ball z₀ r, F n z = 0 := by
  by_cases h : NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have H1 : IsCompact (sphere z₀ r) := isCompact_sphere z₀ r
    have H2 : sphere z₀ r ⊆ U := sphere_subset_closedBall.trans hr2
    have H3 : TendstoUniformlyOn F f p (sphere z₀ r) :=
      (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).1 hf _ H2 H1
    have H4 : ContinuousOn f (sphere z₀ r) := (hf.differentiableOn hF hU).continuousOn.mono H2
    filter_upwards [(hurwitz2_2 hU hF hf hr1 H2 hf1).eventually_ne hf2,
      hurwitz2_1 H1 H3 H4 hf1, hF] with n h h' hF
    contrapose! h
    refine cindex_eq_zero hU hr1 hr2 hF ?_
    rw [← ball_union_sphere]
    exact fun z hz => hz.casesOn (h z) (h' z)


-- @@ L289-315 verbatim
lemma hurwitz3 {s : Set ℂ}
    (hU : IsOpen U)
    (hF : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U)
    (hz₀ : z₀ ∈ U)
    (h1 : f z₀ = 0)
    (h2 : ∀ᶠ z in 𝓝[≠] z₀, f z ≠ 0)
    (hs : s ∈ 𝓝 z₀) :
    ∀ᶠ n in p, ∃ z ∈ s, F n z = 0 := by
  by_cases h : NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have H1 := (hf.differentiableOn hF hU).analyticAt (hU.mem_nhds hz₀)
    have H5 := cindex_pos H1 h1 h2
    rw [eventually_nhdsWithin_iff] at h2
    have h4 : ∀ᶠ r in 𝓝[>] 0, closedBall z₀ r ⊆ U :=
      (eventually_closedBall_subset (hU.mem_nhds hz₀)).filter_mono nhdsWithin_le_nhds
    have h4' : ∀ᶠ r in 𝓝[>] 0, closedBall z₀ r ⊆ s :=
      (eventually_closedBall_subset hs).filter_mono nhdsWithin_le_nhds
    obtain ⟨r, hr, h5, h6, h7, h9⟩ :=
      (eventually_nhds_iff_eventually_closed_ball.1 h2 |>.and (h4.and (H5.and h4'))).exists'
    refine (hurwitz2 hU hF hf hr h6 (fun z hz => h5 z (sphere_subset_closedBall hz)
      (ne_of_mem_sphere hz hr.lt.ne.symm)) h7).mono ?_
    rintro n ⟨z, hz, hFnz⟩
    exact ⟨z, h9 (ball_subset_closedBall hz), hFnz⟩

----------------


-- @@ L317-342 verbatim
theorem local_hurwitz [NeBot p]
    (hU : IsOpen U)
    (F_holo : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (F_noz : ∀ n, ∀ z ∈ U, F n z ≠ 0)
    (F_conv : TendstoLocallyUniformlyOn F f p U)
    (hz₀ : z₀ ∈ U)
    (hfz₀ : f z₀ = 0) :
    ∀ᶠ z in 𝓝 z₀, f z = 0 := by
  have H1 := (F_conv.differentiableOn F_holo hU).analyticAt (hU.mem_nhds hz₀)
  cases H1.eventually_eq_zero_or_eventually_ne_zero
  case inl => assumption
  case inr h =>
    obtain ⟨pf, hp⟩ := H1
    by_contra hh
    rw [Filter.not_eventually] at hh
    have h1 := (order_pos_iff hp hfz₀).2 hh
    obtain ⟨r, h1, h2, h3, h4⟩ :
        ∃ r > 0, (closedBall z₀ r ⊆ U) ∧ (∀ z ∈ sphere z₀ r, f z ≠ 0) ∧ (cindex z₀ r f ≠ 0) := by
      rw [eventually_nhdsWithin_iff, eventually_nhds_iff_eventually_closed_ball] at h
      have h5 : ∀ᶠ r in 𝓝[>] 0, closedBall z₀ r ⊆ U :=
        (eventually_closedBall_subset (hU.mem_nhds hz₀)).filter_mono nhdsWithin_le_nhds
      obtain ⟨r, h6, h7, h8, h9⟩ := (h.and (cindex_eventually_eq_order hp |>.and h5)).exists'
      refine ⟨r, h6, h9, fun z hz => ?_, by simp [h8, h1.ne.symm]⟩
      exact h7 z (sphere_subset_closedBall hz) (ne_of_mem_sphere hz h6.lt.ne.symm)
    obtain ⟨n, z, h5, h6⟩ := (hurwitz2 hU F_holo F_conv h1 h2 h3 h4).exists
    cases F_noz n z (h2 (ball_subset_closedBall (mem_ball.mpr h5))) h6


-- @@ L344-355 verbatim
theorem hurwitz [NeBot p]
    (hU : IsOpen U)
    (hU' : IsPreconnected U)
    (F_holo : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (F_noz : ∀ n, ∀ z ∈ U, F n z ≠ 0)
    (F_conv : TendstoLocallyUniformlyOn F f p U)
    (hz₀ : z₀ ∈ U)
    (hfz₀ : f z₀ = 0) :
    ∀ z ∈ U, f z = 0 := by
  have h := (F_conv.differentiableOn F_holo hU).analyticOnNhd hU
  exact h.eqOn_zero_of_preconnected_of_eventuallyEq_zero hU' hz₀
    (local_hurwitz hU F_holo F_noz F_conv hz₀ hfz₀)


-- @@ L357-367 verbatim
theorem hurwitz' [NeBot p]
    (hU : IsOpen U)
    (hU' : IsPreconnected U)
    (F_holo : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (F_noz : ∀ n, ∀ z ∈ U, F n z ≠ 0)
    (F_conv : TendstoLocallyUniformlyOn F f p U) :
    (∀ z ∈ U, f z ≠ 0) ∨ (∀ z ∈ U, f z = 0) := by
  refine or_iff_not_imp_left.mpr (fun h => ?_)
  simp only [not_forall, not_ne_iff] at h
  obtain ⟨z₀, h1, h2⟩ := h
  exact hurwitz hU hU' F_holo F_noz F_conv h1 h2


-- @@ L369-374 verbatim
lemma hurwitz_1 (hU : IsOpen U) (hU' : IsPreconnected U) (hf : DifferentiableOn ℂ f U) :
    (EqOn f 0 U) ∨ (∀ z₀ ∈ U, ∀ᶠ z in 𝓝[≠] z₀, f z ≠ 0) := by
  refine or_iff_not_imp_right.2 (fun h => ?_)
  simp only [not_forall, not_eventually, exists_prop, not_ne_iff] at h
  obtain ⟨z₀, h1, h2⟩ := h
  exact (hf.analyticOnNhd hU).eqOn_zero_of_preconnected_of_frequently_eq_zero hU' h1 h2


-- @@ L376-380 verbatim
lemma hurwitz4 {α β γ : Type*} {U : Set α} [TopologicalSpace α] [UniformSpace β] [UniformSpace γ]
    {F : ι → α → β} {f : α → β} {φ : β → γ}
    (hf : TendstoLocallyUniformlyOn F f p U) (hφ : UniformContinuous φ) :
    TendstoLocallyUniformlyOn (fun n => φ ∘ F n) (φ ∘ f) p U :=
  fun _ hu z hz => hf _ (mem_map.1 (hφ hu)) z hz


-- @@ L382-412 verbatim
theorem hurwitz_inj [NeBot p]
    (hU : IsOpen U)
    (hU' : IsPreconnected U)
    (hF : ∀ᶠ n in p, DifferentiableOn ℂ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U)
    (hi : ∃ᶠ n in p, InjOn (F n) U) :
    (∃ w, ∀ z ∈ U, f z = w) ∨ (InjOn f U) := by
  refine or_iff_not_imp_right.2 (fun h => ?_)
  simp only [InjOn, not_forall] at h
  obtain ⟨x, hx, y, hy, hfxy, hxy⟩ := h
  --
  set g : ℂ → ℂ := fun z => f z - f x
  set G : ι → ℂ → ℂ := fun n z => F n z - f x
  have hG : ∀ᶠ n in p, DifferentiableOn ℂ (G n) U := by
    filter_upwards [hF] with n hF using hF.sub (differentiableOn_const _)
  have hg : TendstoLocallyUniformlyOn G g p U :=
    hurwitz4 hf (uniformContinuous_id.sub uniformContinuous_const)
  have hgx : g x = 0 := sub_self _
  have hgy : g y = 0 := by simp [g, hfxy]
  suffices this : ∀ z ∈ U, g z = 0 by
    exact ⟨f x, by simpa [sub_eq_zero, g] using this⟩
  --
  contrapose hi; simp only [not_frequently, InjOn, not_forall]
  have h1 : DifferentiableOn ℂ g U := hg.differentiableOn hG hU
  have h2 : ∀ z₀ ∈ U, ∀ᶠ z in 𝓝[≠] z₀, g z ≠ 0 := (hurwitz_1 hU hU' h1).resolve_left hi
  obtain ⟨u, v, hu, hv, huv⟩ := t2_separation_nhds hxy
  have h3 := hurwitz3 hU hG hg hx hgx (h2 x hx) (inter_mem hu (hU.mem_nhds hx))
  have h4 := hurwitz3 hU hG hg hy hgy (h2 y hy) (inter_mem hv (hU.mem_nhds hy))
  filter_upwards [h3.and h4] with n ⟨⟨xn, hxn, hGxn⟩, ⟨yn, hyn, hGyn⟩⟩
  rw [sub_eq_zero] at hGxn hGyn
  exact ⟨xn, hxn.2, yn, hyn.2, by rw [hGxn, hGyn], huv.ne_of_mem hxn.1 hyn.1⟩
