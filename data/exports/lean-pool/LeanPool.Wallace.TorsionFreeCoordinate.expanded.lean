/-
Copyright (c) 2026 Juliane Trianon Fraga and Vinicius de Oliveira Rodrigues. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Juliane Trianon Fraga, Vinicius de Oliveira Rodrigues
-/
module

public import Mathlib.LinearAlgebra.Basis.VectorSpace

public import Mathlib.LinearAlgebra.Dimension.StrongRankCondition

public import LeanPool.Wallace.RationalTriangularPreprocess
public import Mathlib.GroupTheory.DivisibleHull
public import Mathlib.LinearAlgebra.Basis.SMul
public import Mathlib.LinearAlgebra.Dimension.Basic
public import Mathlib.LinearAlgebra.FreeModule.Basic
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Combinatorics.Matroid.Init
import Mathlib.Data.EReal.Operations
import Mathlib.Data.Nat.Totient
import Mathlib.Data.Rat.Encodable
import Mathlib.Data.Sym.Sym2.Init
import Mathlib.GroupTheory.OreLocalization.Cardinality
import Mathlib.LinearAlgebra.Dimension.Free
import Mathlib.LinearAlgebra.FreeModule.StrongRankCondition
import Mathlib.Tactic.ContinuousFunctionalCalculus
import Mathlib.Tactic.NormNum.GCD
import Mathlib.Tactic.Positivity.Finset
import Mathlib.Topology.Algebra.InfiniteSum.Order
import Mathlib.Topology.MetricSpace.Bounded


-- @@ L32-38 verbatim
/-!
# Coordinatizing continuum-sized torsion-free Abelian groups

This file formalizes the coordinatization lemma used in Section 2 of the paper.  A torsion-free
Abelian group of cardinality continuum embeds in the rational direct sum of continuum rank in a
way whose image contains every standard basis vector.
-/


-- @@ L40-40 verbatim
@[expose] public section


-- @@ L42-42 verbatim
open Cardinal Module


-- @@ L44-44 verbatim
namespace Wallace


-- @@ L46-46 verbatim
noncomputable section


-- @@ L48-56 verbatim
/-- The witness form of the paper's coordinatization lemma. -/
structure RationalCoordinatization (G : Type) [AddCommGroup G] where
  /-- An additive embedding of `G` into the continuum-indexed rational direct sum. -/
  embedding : G →+ RationalTriangularPreprocess.ContinuumRationalGroup
  embedding_injective : Function.Injective embedding
  /-- A chosen preimage in `G` of every standard basis vector. -/
  basisPreimage : TriangularPreprocess.ContinuumIndex → G
  embedding_basisPreimage : ∀ i,
    embedding (basisPreimage i) = Finsupp.single i 1


-- @@ L58-58 verbatim
namespace RationalCoordinatization


-- @@ L60-60 verbatim
variable {G : Type} [AddCommGroup G]


-- @@ L62-62 verbatim
variable [IsAddTorsionFree G]


-- @@ L64-68 verbatim
omit [IsAddTorsionFree G] in
private theorem exists_divisibleHull_mk (x : DivisibleHull G) :
    ∃ g : G, ∃ d : ℕ+, DivisibleHull.mk g d = x := by
  induction x using DivisibleHull.ind with
  | mk g d => exact ⟨g, d, rfl⟩


-- @@ L70-82 verbatim
/-- Localizing a continuum-sized torsion-free group by the positive integers does not change its
cardinality. -/
theorem mk_divisibleHull (hcard : #G = 𝔠) : #(DivisibleHull G) = 𝔠 := by
  apply le_antisymm
  · refine (OreLocalization.cardinalMk_le_max (nonZeroDivisors ℕ) G).trans ?_
    rw [hcard]
    refine max_le ?_ (by simp)
    have hsub : #(↥(nonZeroDivisors ℕ)) ≤ 𝔠 :=
      (Cardinal.mk_subtype_le (nonZeroDivisors ℕ : Set ℕ)).trans
        (by simpa using Cardinal.aleph0_le_continuum)
    simpa using hsub
  · rw [← hcard]
    exact Cardinal.mk_le_of_injective DivisibleHull.coe_injective


-- @@ L84-90 verbatim
/-- The divisible hull has rational dimension continuum. -/
theorem rank_divisibleHull (hcard : #G = 𝔠) :
    Module.rank ℚ (DivisibleHull G) = 𝔠 := by
  rw [Module.Free.rank_eq_mk_of_infinite_lt ℚ (DivisibleHull G)]
  · exact mk_divisibleHull hcard
  · rw [mk_divisibleHull hcard]
    simpa using Cardinal.aleph0_lt_continuum


-- @@ L92-100 verbatim
/-- A rational basis of the divisible hull indexed by the canonical continuum type. -/
def continuumBasis (hcard : #G = 𝔠) :
    Basis TriangularPreprocess.ContinuumIndex ℚ (DivisibleHull G) := by
  let b := Module.Free.chooseBasis ℚ (DivisibleHull G)
  have hindex : #(Module.Free.ChooseBasisIndex ℚ (DivisibleHull G)) =
      #TriangularPreprocess.ContinuumIndex := by
    rw [b.mk_eq_rank'', rank_divisibleHull hcard,
      TriangularPreprocess.mk_continuumIndex]
  exact b.reindex (Classical.choice (Cardinal.eq.mp hindex))


-- @@ L102-104 verbatim
/-- A numerator in `G` representing a vector of the chosen basis of the divisible hull. -/
def basisNumerator (hcard : #G = 𝔠) (i : TriangularPreprocess.ContinuumIndex) : G :=
  Classical.choose (private_decl% (exists_divisibleHull_mk (G := G) (continuumBasis hcard i)))


-- @@ L106-109 verbatim
/-- The positive denominator attached to `basisNumerator`. -/
def basisDenominator (hcard : #G = 𝔠) (i : TriangularPreprocess.ContinuumIndex) : ℕ+ :=
  Classical.choose (Classical.choose_spec
    (private_decl% (exists_divisibleHull_mk (G := G) (continuumBasis hcard i))))


-- @@ L111-115 verbatim
theorem basisFraction (hcard : #G = 𝔠) (i : TriangularPreprocess.ContinuumIndex) :
    DivisibleHull.mk (basisNumerator hcard i) (basisDenominator hcard i) =
      continuumBasis hcard i :=
  Classical.choose_spec (Classical.choose_spec
    (private_decl% (exists_divisibleHull_mk (G := G) (continuumBasis hcard i))))


-- @@ L117-123 verbatim
theorem denominator_smul_basis (hcard : #G = 𝔠)
    (i : TriangularPreprocess.ContinuumIndex) :
    ((basisDenominator (G := G) hcard i : ℕ) : ℚ) • continuumBasis hcard i =
      (basisNumerator (G := G) hcard i : DivisibleHull G) := by
  rw [← basisFraction hcard i, Nat.cast_smul_eq_nsmul,
    DivisibleHull.nsmul_mk, DivisibleHull.mk_eq_mk_iff_smul_eq_smul]
  simp


-- @@ L125-131 verbatim
/-- Rescale the chosen rational basis so that every basis vector is literally the image of an
element of the original group. -/
def integralBasis (hcard : #G = 𝔠) :
    Basis TriangularPreprocess.ContinuumIndex ℚ (DivisibleHull G) :=
  (continuumBasis hcard).isUnitSMul
    (fun i ↦ (isUnit_iff_ne_zero.mpr (by
      positivity : ((basisDenominator (G := G) hcard i : ℕ) : ℚ) ≠ 0)))


-- @@ L133-137 verbatim
theorem integralBasis_apply (hcard : #G = 𝔠)
    (i : TriangularPreprocess.ContinuumIndex) :
    integralBasis hcard i = (basisNumerator (G := G) hcard i : DivisibleHull G) := by
  simp only [integralBasis, Basis.isUnitSMul_apply]
  exact denominator_smul_basis hcard i


-- @@ L139-143 verbatim
/-- The additive embedding furnished by the rescaled basis. -/
def canonicalEmbedding (hcard : #G = 𝔠) :
    G →+ RationalTriangularPreprocess.ContinuumRationalGroup :=
  (integralBasis hcard).repr.toLinearMap.toAddMonoidHom.comp
    (DivisibleHull.coeAddMonoidHom G)


-- @@ L145-147 verbatim
theorem canonicalEmbedding_injective (hcard : #G = 𝔠) :
    Function.Injective (canonicalEmbedding hcard) :=
  (integralBasis hcard).repr.injective.comp DivisibleHull.coe_injective


-- @@ L149-156 verbatim
theorem canonicalEmbedding_basisNumerator (hcard : #G = 𝔠)
    (i : TriangularPreprocess.ContinuumIndex) :
    canonicalEmbedding hcard (basisNumerator (G := G) hcard i) =
      Finsupp.single i 1 := by
  change (integralBasis hcard).repr
    (basisNumerator (G := G) hcard i : DivisibleHull G) = _
  rw [← integralBasis_apply hcard i]
  simp


-- @@ L158-165 verbatim
/-- **Torsion-free coordinatization lemma (paper, Section 2).**  Every torsion-free Abelian
group of cardinality continuum embeds in `ℚ^(𝔠)` and its image contains `ℤ^(𝔠)`, expressed by
the preimage of every standard basis vector. -/
def ofCardinalityContinuum (hcard : #G = 𝔠) : RationalCoordinatization G where
  embedding := canonicalEmbedding hcard
  embedding_injective := canonicalEmbedding_injective hcard
  basisPreimage := basisNumerator (G := G) hcard
  embedding_basisPreimage := canonicalEmbedding_basisNumerator hcard


-- @@ L167-167 verbatim
end RationalCoordinatization


-- @@ L169-169 verbatim
end


-- @@ L171-171 verbatim
end Wallace
