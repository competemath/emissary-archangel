/-
Copyright (c) 2026 Cameron Freer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Cameron Freer
-/
module

public import LeanPool.InfinitaryLogic.Lomega1omega.Semantics

-- @@ L9-17 verbatim
/-!
# Indiscernible Sequences for Lω₁ω

A sequence `(aᵢ)_{i ∈ I}` in a model M is Lω₁ω-indiscernible if for every
n-variable formula and every two strictly increasing n-tuples from I, the
formula holds on one iff it holds on the other.

Standalone API — does not advance the Hanf boundary.
-/


-- @@ L19-19 verbatim
@[expose] public section


-- @@ L21-21 verbatim
universe u v w


-- @@ L23-23 verbatim
namespace FirstOrder.Language


-- @@ L25-25 verbatim
variable {L : Language.{u, v}}


-- @@ L27-27 verbatim
section Indiscernible


-- @@ L29-29 verbatim
variable {I : Type w} [LinearOrder I] {M : Type*} [L.Structure M]


-- @@ L31-40 verbatim
/-- A sequence `a : I → M` is Lω₁ω-indiscernible if for every n-variable
formula φ (no free variables) and every two strictly increasing maps
`s t : Fin n → I`, the formula holds on `a ∘ s` iff on `a ∘ t`. -/
def IsLomega1omegaIndiscernible (a : I → M) : Prop :=
  ∀ (n : ℕ) (φ : L.BoundedFormulaω Empty n)
    (s : Fin n → I) (_ : StrictMono s)
    (t : Fin n → I) (_ : StrictMono t),
    letI := ‹L.Structure M›
    φ.Realize (Empty.elim : Empty → M) (a ∘ s) ↔
    φ.Realize (Empty.elim : Empty → M) (a ∘ t)


-- @@ L42-57 verbatim
/-- `IsLomega1omegaIndiscernibleOn a Γ` is the `Γ`-restricted form: the
indiscernibility equivalence is required only for formulas whose sigma-pair
lies in `Γ ⊆ Σ n, L.BoundedFormulaω Empty n`. Strictly weaker than
`IsLomega1omegaIndiscernible a` (which is the `Γ = Set.univ` case).

Motivation: the EM pipeline only uses indiscernibility on the countable
family `Set.range s` for a chosen formula enumeration `s`, not on all
Lω₁ω formulas. Weakening to the restricted form lets callers supply the
genuinely-needed hypothesis rather than the stronger full indiscernibility. -/
def IsLomega1omegaIndiscernibleOn (a : I → M)
    (Γ : Set (Σ n, L.BoundedFormulaω Empty n)) : Prop :=
  ∀ {n : ℕ} {φ : L.BoundedFormulaω Empty n}, ⟨n, φ⟩ ∈ Γ →
    ∀ (s t : Fin n → I), StrictMono s → StrictMono t →
      letI := ‹L.Structure M›
      φ.Realize (Empty.elim : Empty → M) (a ∘ s) ↔
      φ.Realize (Empty.elim : Empty → M) (a ∘ t)


-- @@ L59-67 verbatim
/-- Restricting an `On`-indiscernible sequence to a sub-order preserves
restricted indiscernibility on the same family. -/
theorem IsLomega1omegaIndiscernibleOn.restrict {a : I → M}
    {Γ : Set (Σ n, L.BoundedFormulaω Empty n)}
    (h : IsLomega1omegaIndiscernibleOn (L := L) a Γ)
    {J : Type*} [LinearOrder J] (e : J ↪o I) :
    IsLomega1omegaIndiscernibleOn (L := L) (a ∘ e) Γ := by
  intro n φ hφ s t hs ht
  exact h hφ (e ∘ s) (e ∘ t) (e.strictMono.comp hs) (e.strictMono.comp ht)


-- @@ L69-76 verbatim
/-- Monotonicity: shrinking the formula family preserves restricted
indiscernibility. -/
theorem IsLomega1omegaIndiscernibleOn.mono {a : I → M}
    {Γ Γ' : Set (Σ n, L.BoundedFormulaω Empty n)} (hΓ : Γ ⊆ Γ')
    (h : IsLomega1omegaIndiscernibleOn (L := L) a Γ') :
    IsLomega1omegaIndiscernibleOn (L := L) a Γ := by
  intro n φ hφ s t hs ht
  exact h (hΓ hφ) s t hs ht


-- @@ L78-83 verbatim
/-- Restricting an indiscernible sequence to a sub-order. -/
theorem IsLomega1omegaIndiscernible.restrict {a : I → M}
    (h : IsLomega1omegaIndiscernible (L := L) a)
    {J : Type*} [LinearOrder J] (e : J ↪o I) :
    IsLomega1omegaIndiscernible (L := L) (a ∘ e) :=
  fun n φ s hs t ht => h n φ (e ∘ s) (e.strictMono.comp hs) (e ∘ t) (e.strictMono.comp ht)


-- @@ L85-85 verbatim
end Indiscernible


-- @@ L87-87 verbatim
end FirstOrder.Language
