/-
Copyright (c) 2026 Cameron Freer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Cameron Freer
-/
module

public import Mathlib.ModelTheory.Syntax


-- @@ L10-39 verbatim
/-!
# Infinitary first-order formulas

This file defines the syntax of `L_{∞ω}`: first-order formulas with conjunctions and
disjunctions indexed by a *fixed branching carrier* `ι`, one per formula. `L_{ω₁ω}` is the
definitional specialization `ι := ℕ`.

## Design

The infinitary constructors `iSup`/`iInf` branch over the single type parameter `ι` rather than
quantifying over a fresh index type at every node. Consequences:

- `BoundedFormulaInf L ι α n : Type (max u v u' uι)` — the syntax lives in the `max` of its
  parameters' universes, with no `+ 1` bump. In particular
  `BoundedFormulaω L α n := BoundedFormulaInf L ℕ α n` has exactly the universe
  `Type (max u v u')` of the finitary `BoundedFormula`.
- An `ι`-indexed conjunction at a larger carrier `κ`, and transport of whole formulas
  between carriers, are expressed through codings — see `Infinitary/Reindex.lean`. In
  particular, Karp's theorem, the consumer that forces arbitrary index types, needs only the
  single carrier `M ⊕ N`.

## Main definitions

- `FirstOrder.Language.BoundedFormulaInf`: infinitary formulas with carrier `ι`, free variables
  in `α`, and `n` free *bound-variable* slots.
- `FirstOrder.Language.BoundedFormulaω`: the `ι := ℕ` specialization (an `abbrev`, so all
  `BoundedFormulaInf` API applies definitionally).
- Derived connectives and quantifier closures (`not`, `⊤`/`⊥`, `ex`, `alls`, `exs`), and the
  carrier-generic finitary embedding `BoundedFormula.toInf`.
-/


-- @@ L41-41 verbatim
@[expose] public section


-- @@ L43-43 verbatim
universe u v u' uι w


-- @@ L45-45 verbatim
namespace FirstOrder


-- @@ L47-47 verbatim
namespace Language


-- @@ L49-49 verbatim
variable (L : Language.{u, v})


-- @@ L51-69 verbatim
/-- An infinitary bounded formula of `L_{∞ω}`, with infinitary conjunctions and disjunctions
branching over the fixed carrier `ι`, free variables indexed by `α`, and `n` additional bound
variables available. -/
inductive BoundedFormulaInf (ι : Type uι) (α : Type u') : ℕ → Type (max u v u' uι) where
  /-- The false formula. -/
  | falsum {n} : BoundedFormulaInf ι α n
  /-- Equality of two terms. -/
  | equal {n} (t₁ t₂ : L.Term (α ⊕ Fin n)) : BoundedFormulaInf ι α n
  /-- A relation symbol applied to terms. -/
  | rel {n l : ℕ} (R : L.Relations l) (ts : Fin l → L.Term (α ⊕ Fin n)) :
      BoundedFormulaInf ι α n
  /-- Implication. -/
  | imp {n} (φ ψ : BoundedFormulaInf ι α n) : BoundedFormulaInf ι α n
  /-- Universal quantification over the last bound variable. -/
  | all {n} (φ : BoundedFormulaInf ι α (n + 1)) : BoundedFormulaInf ι α n
  /-- Infinitary disjunction over the carrier. -/
  | iSup {n} (φs : ι → BoundedFormulaInf ι α n) : BoundedFormulaInf ι α n
  /-- Infinitary conjunction over the carrier. -/
  | iInf {n} (φs : ι → BoundedFormulaInf ι α n) : BoundedFormulaInf ι α n


-- @@ L71-73 verbatim
/-- A bounded formula of `L_{ω₁ω}`: the definitional `ι := ℕ` specialization of
`BoundedFormulaInf`. Its universe is exactly that of the finitary `BoundedFormula`. -/
abbrev BoundedFormulaω (α : Type u') (n : ℕ) := L.BoundedFormulaInf ℕ α n


-- @@ L75-76 verbatim
/-- An `L_{∞ω}` formula: a bounded formula with no free bound variables. -/
abbrev FormulaInf (ι : Type uι) (α : Type u') := L.BoundedFormulaInf ι α 0


-- @@ L78-79 verbatim
/-- An `L_{∞ω}` sentence: a formula with no free variables at all. -/
abbrev SentenceInf (ι : Type uι) := L.FormulaInf ι Empty


-- @@ L81-88 verbatim
/-- An `L_{ω₁ω}` formula.

Routed through `BoundedFormulaω` rather than stated as `FormulaInf ℕ α`, though the two are the
same type. Dot-notation resolution walks an abbreviation chain one unfolding at a time, trying
each head constant's namespace in turn, so this routing keeps declarations in a downstream
`BoundedFormulaω` namespace reachable as `φ.op` on an `L_{ω₁ω}` formula while the generic
`BoundedFormulaInf` namespace stays reachable at the end of the chain. -/
abbrev Formulaω (α : Type u') := L.BoundedFormulaω α 0


-- @@ L90-91 verbatim
/-- An `L_{ω₁ω}` sentence. Routed through `Formulaω` for the reason given there. -/
abbrev Sentenceω := L.Formulaω Empty


-- @@ L93-93 verbatim
variable {L} {ι : Type uι} {α : Type u'} {n : ℕ}


-- @@ L95-95 verbatim
namespace BoundedFormulaInf


-- @@ L97-100 verbatim
/-- The negation of an infinitary formula. -/
@[match_pattern]
protected def not (φ : L.BoundedFormulaInf ι α n) : L.BoundedFormulaInf ι α n :=
  φ.imp .falsum


-- @@ L102-104 verbatim
/-- The true formula. -/
protected def verum : L.BoundedFormulaInf ι α n :=
  BoundedFormulaInf.not .falsum


-- @@ L106-107 verbatim
instance : Bot (L.BoundedFormulaInf ι α n) :=
  ⟨.falsum⟩


-- @@ L109-110 verbatim
instance : Top (L.BoundedFormulaInf ι α n) :=
  ⟨BoundedFormulaInf.verum⟩


-- @@ L112-115 verbatim
/-- Existential quantification over the last bound variable. -/
@[match_pattern]
protected def ex (φ : L.BoundedFormulaInf ι α (n + 1)) : L.BoundedFormulaInf ι α n :=
  φ.not.all.not


-- @@ L117-117 verbatim
end BoundedFormulaInf


-- @@ L119-119 verbatim
end Language


-- @@ L121-121 verbatim
end FirstOrder
