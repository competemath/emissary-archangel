/-
Copyright (c) 2026 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
module

public import LeanPool.LeanModularForms.GeneralizedResidueTheory.Basic
import Mathlib.CategoryTheory.Category.Init
import Mathlib.Combinatorics.Matroid.Init
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.Tactic.Positivity.Finset


-- @@ L14-19 verbatim
/-!
# Cauchy Principal Value Theory

Theory of Cauchy principal value integrals for piecewise C¹ contour integration.
The principal value approach allows contours to pass through singularities.
-/


-- @@ L21-21 verbatim
@[expose] public section


-- @@ L23-23 verbatim
open Complex MeasureTheory Set Filter Topology

-- @@ L24-24 verbatim
open scoped Real Interval


-- @@ L26-26 verbatim
noncomputable section


-- @@ L28-66 verbatim
theorem cauchyPrincipalValueIntegrand_bounded
    (f : ℂ → ℂ) (γ : ℝ → ℂ) (a b : ℝ) (z₀ : ℂ) (ε : ℝ)
    (_hε : 0 < ε)
    (hf_cont : ContinuousOn f (γ '' Icc a b \ Metric.ball z₀ ε))
    (hγ_cont : ContinuousOn γ (Icc a b))
    (hγ'_cont : ContinuousOn (deriv γ) (Icc a b)) :
    ∃ M : ℝ, ∀ t ∈ Icc a b,
      ‖cauchyPrincipalValueIntegrand' f γ z₀ ε t‖ ≤ M := by
  by_cases h_empty :
      (γ '' Icc a b \ Metric.ball z₀ ε).Nonempty
  · have hcompact_domain : IsCompact (γ '' Icc a b \ Metric.ball z₀ ε) :=
      (isCompact_Icc.image_of_continuousOn hγ_cont).inter_right
        Metric.isOpen_ball.isClosed_compl
    obtain ⟨Mf, hMf⟩ := hcompact_domain.exists_bound_of_continuousOn hf_cont.norm
    obtain ⟨Mγ, hMγ⟩ := isCompact_Icc.exists_bound_of_continuousOn hγ'_cont.norm
    have hMf' : ∀ x ∈ γ '' Icc a b \ Metric.ball z₀ ε, ‖f x‖ ≤ Mf := fun x hx => by
      simpa [Real.norm_eq_abs, abs_norm] using hMf x hx
    have hMγ' : ∀ t ∈ Icc a b, ‖deriv γ t‖ ≤ Mγ := fun t ht => by
      simpa [Real.norm_eq_abs, abs_norm] using hMγ t ht
    refine ⟨Mf * Mγ + 1, fun t ht => ?_⟩
    unfold cauchyPrincipalValueIntegrand'; split_ifs with h
    · have hγt_in : γ t ∈ γ '' Icc a b \ Metric.ball z₀ ε :=
        ⟨⟨t, ht, rfl⟩, by simp only [Metric.mem_ball, not_lt, dist_eq_norm]; exact h.le⟩
      calc ‖f (γ t) * deriv γ t‖ = ‖f (γ t)‖ * ‖deriv γ t‖ := norm_mul _ _
        _ ≤ Mf * Mγ := mul_le_mul (hMf' _ hγt_in) (hMγ' t ht) (norm_nonneg _)
              (le_trans (norm_nonneg _) (hMf' _ hγt_in))
        _ ≤ Mf * Mγ + 1 := le_add_of_nonneg_right one_pos.le
    · simp only [norm_zero]
      exact add_nonneg (mul_nonneg
        (le_trans (norm_nonneg _) (hMf' _ h_empty.some_mem))
        (by obtain ⟨_, ⟨t', ht', _⟩, _⟩ := h_empty
            exact le_trans (norm_nonneg _) (hMγ' _ ht'))) (by norm_num)
  · exact ⟨0, fun t ht => by
      unfold cauchyPrincipalValueIntegrand'
      split_ifs with h
      · exact absurd ⟨γ t, ⟨t, ht, rfl⟩, by
          simp only [Metric.mem_ball, not_lt, dist_eq_norm]
          exact le_of_lt h⟩ h_empty
      · simp only [norm_zero, le_refl]⟩


-- @@ L68-92 verbatim
lemma measurableSet_pv_support (γ : ℝ → ℂ) (a b : ℝ) (z₀ : ℂ)
    (ε : ℝ) (hγ_cont : ContinuousOn γ (Icc a b)) :
    MeasurableSet ({t | ε < ‖γ t - z₀‖} ∩ Icc a b) := by
  have h_norm_cont : ContinuousOn (fun t => ‖γ t - z₀‖) (Icc a b) :=
    (hγ_cont.sub continuousOn_const).norm
  have h_open_sub :
      IsOpen ((Icc a b).domRestrict (fun t => ‖γ t - z₀‖) ⁻¹' Ioi ε) :=
    isOpen_Ioi.preimage h_norm_cont.domRestrict
  rw [isOpen_induced_iff] at h_open_sub
  obtain ⟨U, hU_open, hU_eq⟩ := h_open_sub
  have h_eq : {t | ε < ‖γ t - z₀‖} ∩ Icc a b = U ∩ Icc a b := by
    ext x; constructor
    · intro ⟨hx_far, hx_Icc⟩
      exact ⟨by
        have : (⟨x, hx_Icc⟩ : ↑(Icc a b)) ∈
            (Icc a b).domRestrict (fun t => ‖γ t - z₀‖) ⁻¹' Ioi ε := by
          simp only [mem_preimage, mem_Ioi]; exact hx_far
        rwa [← hU_eq] at this, hx_Icc⟩
    · intro ⟨hx_U, hx_Icc⟩
      exact ⟨by
        have : (⟨x, hx_Icc⟩ : ↑(Icc a b)) ∈ Subtype.val ⁻¹' U := hx_U
        rw [hU_eq] at this
        exact this, hx_Icc⟩
  rw [h_eq]
  exact hU_open.measurableSet.inter isClosed_Icc.measurableSet


-- @@ L94-114 verbatim
lemma continuousOn_pv_base (f : ℂ → ℂ) (γ : ℝ → ℂ)
    (a b : ℝ) (z₀ : ℂ) (ε : ℝ)
    (hf_cont : ContinuousOn f
      (γ '' Icc a b \ Metric.ball z₀ ε))
    (hγ_cont : ContinuousOn γ (Icc a b))
    (hγ'_cont : ContinuousOn (deriv γ) (Icc a b)) :
    ContinuousOn (fun t => f (γ t) * deriv γ t)
      ({t | ε < ‖γ t - z₀‖} ∩ Icc a b) := by
  intro t ⟨ht_far, ht_Icc⟩
  have hγt_in : γ t ∈ γ '' Icc a b \ Metric.ball z₀ ε :=
    ⟨mem_image_of_mem γ ht_Icc, by
      simp only [Metric.mem_ball, not_lt, dist_eq_norm]; exact le_of_lt ht_far⟩
  have h_maps :
      MapsTo γ ({t | ε < ‖γ t - z₀‖} ∩ Icc a b)
        (γ '' Icc a b \ Metric.ball z₀ ε) := by
    intro s ⟨hs_far, hs_Icc⟩
    exact ⟨mem_image_of_mem γ hs_Icc, by
      simp only [Metric.mem_ball, not_lt, dist_eq_norm]; exact le_of_lt hs_far⟩
  exact (ContinuousWithinAt.comp (hf_cont _ hγt_in)
      ((hγ_cont t ht_Icc).mono inter_subset_right) h_maps).mul
    ((hγ'_cont t ht_Icc).mono inter_subset_right)


-- @@ L116-119 verbatim
/-- If `f =ᶠ g` along a filter, their `limUnder` values agree. -/
theorem limUnder_eventually_eq {α : Type*} [TopologicalSpace α] [Nonempty α]
    {f g : ℝ → α} {l : Filter ℝ} (h : ∀ᶠ x in l, f x = g x) :
    limUnder l f = limUnder l g := by simp only [limUnder, Filter.map_congr h]


-- @@ L121-145 verbatim
private theorem aEStronglyMeasurable_pv_integrand
    {f : ℂ → ℂ} {γ : ℝ → ℂ} {a b : ℝ} {z₀ : ℂ} {ε : ℝ}
    (hf : ContinuousOn f (γ '' Icc a b \ Metric.ball z₀ ε))
    (hγ : ContinuousOn γ (Icc a b))
    (hγ' : ContinuousOn (deriv γ) (Icc a b)) :
    AEStronglyMeasurable
      (fun t => if ε < ‖γ t - z₀‖ then f (γ t) * deriv γ t
        else 0) (volume.restrict (Icc a b)) := by
  let S := {t | ε < ‖γ t - z₀‖}
  have hS_meas : MeasurableSet (S ∩ Icc a b) :=
    measurableSet_pv_support γ a b z₀ ε hγ
  refine ((AEStronglyMeasurable.piecewise hS_meas
    ((continuousOn_pv_base f γ a b z₀ ε hf hγ hγ').aestronglyMeasurable hS_meas)
    (aestronglyMeasurable_const :
      AEStronglyMeasurable (fun _ : ℝ => (0 : ℂ))
        (volume.restrict (S ∩ Icc a b)ᶜ))).mono_measure
    Measure.restrict_le_self).congr ?_
  filter_upwards [ae_restrict_mem isClosed_Icc.measurableSet] with t ht
  simp only [piecewise]
  by_cases ht_S : t ∈ S
  · simp only [show t ∈ S ∩ Icc a b from ⟨ht_S, ht⟩,
      ↓reduceIte, show ε < ‖γ t - z₀‖ from ht_S, ↓reduceIte]
  · simp only [show t ∉ S ∩ Icc a b from fun h => ht_S h.1,
      ↓reduceIte, show ¬(ε < ‖γ t - z₀‖) from ht_S,
      ↓reduceIte]


-- @@ L147-172 verbatim
theorem cauchyPrincipalValueIntegrand_integrable
    (f : ℂ → ℂ) (γ : ℝ → ℂ) (a b : ℝ) (z₀ : ℂ)
    (ε : ℝ) (hε : 0 < ε) (hab : a < b)
    (hf_cont : ContinuousOn f
      (γ '' Icc a b \ Metric.ball z₀ ε))
    (hγ_cont : ContinuousOn γ (Icc a b))
    (hγ'_cont : ContinuousOn (deriv γ) (Icc a b)) :
    IntervalIntegrable
      (cauchyPrincipalValueIntegrand' f γ z₀ ε) volume a b := by
  obtain ⟨M, hM⟩ := cauchyPrincipalValueIntegrand_bounded
    f γ a b z₀ ε hε hf_cont hγ_cont hγ'_cont
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le (le_of_lt hab)]
  apply IntegrableOn.mono_set
  · apply IntegrableOn.of_bound measure_Icc_lt_top
      (aEStronglyMeasurable_pv_integrand hf_cont hγ_cont
        hγ'_cont)
      (max M 0)
    filter_upwards [ae_restrict_mem
      isClosed_Icc.measurableSet] with x hx
    calc ‖if ε < ‖γ x - z₀‖ then f (γ x) * deriv γ x
        else 0‖
        ≤ M := by
          simp only [cauchyPrincipalValueIntegrand'] at hM
          exact hM x hx
      _ ≤ max M 0 := le_max_left M 0
  · exact Ioc_subset_Icc_self


-- @@ L174-207 verbatim
/-- Dominated convergence for principal value integrals. -/
theorem cauchyPrincipalValue_of_dominated
    (f : ℂ → ℂ) (γ : ℝ → ℂ) (a b : ℝ) (z₀ : ℂ)
    (hab : a < b) (M : ℝ) (_hM : 0 < M)
    (h_bound : ∀ ε > 0, ∀ t ∈ Icc a b,
      ‖cauchyPrincipalValueIntegrand' f γ z₀ ε t‖ ≤ M)
    (h_ae_limit : ∀ᵐ t ∂volume.restrict (Icc a b),
      ∃ L, Tendsto
        (fun ε => cauchyPrincipalValueIntegrand' f γ z₀ ε t)
        (𝓝[>] 0) (𝓝 L))
    (hF_meas : ∀ᶠ ε in 𝓝[>] (0 : ℝ),
      AEStronglyMeasurable
        (cauchyPrincipalValueIntegrand' f γ z₀ ε)
        (volume.restrict (uIoc a b))) :
    CauchyPrincipalValueExists' f γ a b z₀ := by
  have hab' := le_of_lt hab
  let g : ℝ → ℂ := fun t => Filter.limUnder (𝓝[>] (0 : ℝ))
    (fun ε => cauchyPrincipalValueIntegrand' f γ z₀ ε t)
  have h_limit_ae : ∀ᵐ t ∂volume, t ∈ uIoc a b →
      ∃ L, Tendsto
        (fun ε => cauchyPrincipalValueIntegrand' f γ z₀ ε t)
        (𝓝[>] 0) (𝓝 L) := by
    filter_upwards [(ae_restrict_iff' isClosed_Icc.measurableSet).mp h_ae_limit]
      with t ht ht_mem
    exact ht (Ioc_subset_Icc_self (uIoc_of_le hab' ▸ ht_mem))
  exact ⟨∫ t in a..b, g t,
    intervalIntegral.tendsto_integral_filter_of_dominated_convergence (fun _ => M) hF_meas
      (by filter_upwards [self_mem_nhdsWithin] with ε hε
          exact Eventually.of_forall fun t ht =>
            h_bound ε hε t (Ioc_subset_Icc_self (uIoc_of_le hab' ▸ ht)))
      intervalIntegrable_const
      (by filter_upwards [h_limit_ae] with t ht ht_mem
          obtain ⟨L, hL⟩ := ht ht_mem
          rwa [show g t = L from hL.limUnder_eq])⟩


-- @@ L209-236 verbatim
private theorem pv_uniform_bound_of_continuous_aux
    (g : ℂ → ℂ) (γ : ℝ → ℂ) (a b : ℝ) (z₀ : ℂ) (hab : a < b)
    (hg : ContinuousOn g (γ '' Icc a b))
    (hγ : ContinuousOn γ (Icc a b))
    (hγ' : ContinuousOn (deriv γ) (Icc a b)) :
    ∃ M > 0, ∀ ε > 0, ∀ t ∈ Icc a b,
      ‖cauchyPrincipalValueIntegrand' g γ z₀ ε t‖ ≤ M := by
  obtain ⟨Mg, hMg⟩ :=
    (isCompact_Icc.image_of_continuousOn hγ).exists_bound_of_continuousOn hg.norm
  obtain ⟨Mγ', hMγ'⟩ :=
    isCompact_Icc.exists_bound_of_continuousOn hγ'.norm
  have hMg' : ∀ z ∈ γ '' Icc a b, ‖g z‖ ≤ Mg := fun z hz => by
    simpa only [Real.norm_eq_abs, abs_norm] using hMg z hz
  have hMγ'' : ∀ t ∈ Icc a b, ‖deriv γ t‖ ≤ Mγ' := fun t ht => by
    simpa only [Real.norm_eq_abs, abs_norm] using hMγ' t ht
  have hMg_nn : (0 : ℝ) ≤ Mg :=
    le_trans (norm_nonneg _) (hMg' _ ⟨a, left_mem_Icc.mpr hab.le, rfl⟩)
  have hMγ_nn : (0 : ℝ) ≤ Mγ' :=
    le_trans (norm_nonneg _) (hMγ'' a (left_mem_Icc.mpr hab.le))
  refine ⟨Mg * Mγ' + 1, by linarith [mul_nonneg hMg_nn hMγ_nn],
    fun ε _ t ht => ?_⟩
  unfold cauchyPrincipalValueIntegrand'; split_ifs with h
  · calc ‖g (γ t) * deriv γ t‖
        = ‖g (γ t)‖ * ‖deriv γ t‖ := norm_mul _ _
      _ ≤ Mg * Mγ' := mul_le_mul (hMg' _ ⟨t, ht, rfl⟩) (hMγ'' t ht)
          (norm_nonneg _) hMg_nn
      _ ≤ Mg * Mγ' + 1 := by linarith
  · simp only [norm_zero]; linarith [mul_nonneg hMg_nn hMγ_nn]


-- @@ L238-268 verbatim
/-- PV exists for continuous integrands on C¹ curves. -/
theorem cauchyPrincipalValueExists_of_continuous
    (g : ℂ → ℂ) (γ : ℝ → ℂ) (a b : ℝ) (z₀ : ℂ) (hab : a < b)
    (hg : ContinuousOn g (γ '' Icc a b))
    (hγ : ContinuousOn γ (Icc a b))
    (hγ' : ContinuousOn (deriv γ) (Icc a b)) :
    CauchyPrincipalValueExists' g γ a b z₀ := by
  obtain ⟨M, hM_pos, h_bound⟩ :=
    pv_uniform_bound_of_continuous_aux g γ a b z₀ hab hg hγ hγ'
  refine cauchyPrincipalValue_of_dominated g γ a b z₀ hab M hM_pos
    h_bound ?_ ?_
  · apply Eventually.of_forall; intro t
    by_cases h : γ t = z₀
    · exact ⟨0, Tendsto.congr' (by
        rw [EventuallyEq, eventually_iff_exists_mem]
        exact ⟨Ioi 0, self_mem_nhdsWithin, fun ε hε => by
          simp only [cauchyPrincipalValueIntegrand', h, sub_self,
            norm_zero, not_lt.mpr (le_of_lt (mem_Ioi.mp hε)),
            ite_false]⟩) tendsto_const_nhds⟩
    · exact ⟨g (γ t) * deriv γ t, Tendsto.congr' (by
        rw [EventuallyEq, eventually_iff_exists_mem]
        exact ⟨Ioo 0 ‖γ t - z₀‖,
          Ioo_mem_nhdsGT (norm_pos_iff.mpr (sub_ne_zero.mpr h)),
          fun ε hε => by
            simp_all⟩) tendsto_const_nhds⟩
  · filter_upwards [self_mem_nhdsWithin] with ε _
    exact (aEStronglyMeasurable_pv_integrand
      (hg.mono sdiff_subset) hγ hγ').mono_measure
      (Measure.restrict_mono
        (by rw [uIoc_of_le hab.le]; exact Ioc_subset_Icc_self)
        le_rfl)


-- @@ L270-305 verbatim
/-- PV exists for singular 1/(z-z₀) integrands on C¹ immersions. -/
theorem cauchyPrincipalValueExists_of_singular_inv
    (γ : PiecewiseC1Immersion) (z₀ : ℂ)
    (h_crossing_cauchy :
      (∃ t ∈ Icc γ.a γ.b, γ.toFun t = z₀) →
        Cauchy (Filter.map (fun ε =>
          ∫ t in γ.a..γ.b,
            if ε < ‖γ.toFun t - z₀‖
            then (γ.toFun t - z₀)⁻¹ * deriv γ.toFun t
            else 0) (𝓝[>] 0))) :
    CauchyPrincipalValueExists' (fun z => (z - z₀)⁻¹)
      γ.toFun γ.a γ.b z₀ := by
  by_cases h_cross : ∃ t ∈ Icc γ.a γ.b, γ.toFun t = z₀
  · exact CompleteSpace.complete (h_crossing_cauchy h_cross)
  · push Not at h_cross
    have h_cont : ContinuousOn
        (fun t => ‖γ.toFun t - z₀‖) (Icc γ.a γ.b) :=
      (γ.continuous_toFun.sub continuousOn_const).norm
    obtain ⟨t₀, ht₀, ht₀_min⟩ :=
      IsCompact.exists_isMinOn isCompact_Icc
        ⟨γ.a, left_mem_Icc.mpr γ.hab.le⟩ h_cont
    have hδ : 0 < ‖γ.toFun t₀ - z₀‖ :=
      norm_pos_iff.mpr (sub_ne_zero.mpr (h_cross t₀ ht₀))
    have hδ_le : ∀ t ∈ Icc γ.a γ.b,
        ‖γ.toFun t₀ - z₀‖ ≤ ‖γ.toFun t - z₀‖ :=
      Filter.eventually_principal.mp ht₀_min
    refine ⟨∫ t in γ.a..γ.b,
        (γ.toFun t - z₀)⁻¹ * deriv γ.toFun t, ?_⟩
    exact tendsto_const_nhds.congr' (by
      filter_upwards [Ioo_mem_nhdsGT hδ] with ε hε
      symm
      apply intervalIntegral.integral_congr
      intro t ht
      rw [uIcc_of_le γ.hab.le] at ht
      simp only [gt_iff_lt, show ε < ‖γ.toFun t - z₀‖ from
        lt_of_lt_of_le hε.2 (hδ_le t ht), ite_true])


-- @@ L307-318 verbatim
/-- Uniform avoidance on compact sets. -/
theorem uniform_avoidance_on_compact
    (γ : ℝ → ℂ) (K : Set ℝ) (z₀ : ℂ)
    (hK_compact : IsCompact K) (hK_nonempty : K.Nonempty)
    (hγ_cont : ContinuousOn γ K)
    (h_avoid : ∀ t ∈ K, γ t ≠ z₀) :
    ∃ δ > 0, ∀ t ∈ K, δ ≤ ‖γ t - z₀‖ := by
  obtain ⟨t₀, ht₀, h_min⟩ := hK_compact.exists_isMinOn
    hK_nonempty (hγ_cont.sub continuousOn_const).norm
  exact ⟨‖γ t₀ - z₀‖,
    norm_pos_iff.mpr (sub_ne_zero.mpr (h_avoid t₀ ht₀)),
    Filter.eventually_principal.mp h_min⟩


-- @@ L320-320 verbatim
end
