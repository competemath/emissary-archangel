/-
Copyright (c) 2026 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

module

public import LeanPool.LeanModularForms.Modularforms.MDifferentiableFunProp
public import LeanPool.LeanModularForms.Modularforms.SlashActionAuxil
import LeanPool.LeanModularForms.ForMathlib.AtImInfty
import LeanPool.LeanModularForms.ForMathlib.FunctionsBoundedAtInfty
import LeanPool.LeanModularForms.ForMathlib.SlashActions
import LeanPool.LeanModularForms.ForMathlib.UpperHalfPlane
import LeanPool.LeanModularForms.Modularforms.DimensionFormulas
import LeanPool.LeanModularForms.Modularforms.ForMathlibCusps
import LeanPool.LeanModularForms.Modularforms.ForMathlibFunctionsBoundedAtInfty
import Mathlib.Algebra.Order.Star.Real
import Mathlib.Analysis.Normed.Group.Tannery
import Mathlib.Data.Int.Star
import Mathlib.Order.CompletePartialOrder


-- @@ L23-23 verbatim
/-! # JacobiTheta -/



-- @@ L26-26 verbatim
@[expose] public section


-- @@ L28-34 verbatim
/-!
# Jacobi theta functions

Define Jacobi theta functions Θ₂, Θ₃, Θ₄ and their fourth powers H₂, H₃, H₄.
Prove that H₂, H₃, H₄ are modualar forms of weight 2 and level Γ(2).
Also Jacobi identity: Θ₂^4 + Θ₄^4 = Θ₃^4.
-/


-- @@ L36-36 verbatim
open scoped Real MatrixGroups ModularForm

-- @@ L37-37 verbatim
open UpperHalfPlane hiding I

-- @@ L38-39 verbatim
open Complex Real Asymptotics Filter Topology Manifold SlashInvariantForm Matrix ModularGroup
  ModularForm SlashAction MatrixGroups


-- @@ L41-41 verbatim
local notation "GL(" n ", " R ")" "⁺" => Matrix.GLPos (Fin n) R

-- @@ L42-42 verbatim
local notation "Γ " n:100 => CongruenceSubgroup.Gamma n


-- @@ L44-45 verbatim
/-- The `n`-th term of the Jacobi theta series `Θ₂`. -/
noncomputable def Θ₂Term (n : ℤ) (τ : ℍ) : ℂ := cexp (π * I * (n + 1 / 2 : ℂ) ^ 2 * τ)

-- @@ L46-47 verbatim
/-- The `n`-th term of the Jacobi theta series `Θ₃`. -/
noncomputable def Θ₃Term (n : ℤ) (τ : ℍ) : ℂ := cexp (π * I * (n : ℂ) ^ 2 * τ)

-- @@ L48-49 verbatim
/-- The `n`-th term of the Jacobi theta series `Θ₄`. -/
noncomputable def Θ₄Term (n : ℤ) (τ : ℍ) : ℂ := (-1) ^ n * cexp (π * I * (n : ℂ) ^ 2 * τ)

-- @@ L50-51 verbatim
/-- The Jacobi theta function `Θ₂`. -/
noncomputable def Θ₂ (τ : ℍ) : ℂ := ∑' n : ℤ, Θ₂Term n τ

-- @@ L52-53 verbatim
/-- The Jacobi theta function `Θ₃`. -/
noncomputable def Θ₃ (τ : ℍ) : ℂ := ∑' n : ℤ, Θ₃Term n τ

-- @@ L54-55 verbatim
/-- The Jacobi theta function `Θ₄`. -/
noncomputable def Θ₄ (τ : ℍ) : ℂ := ∑' n : ℤ, Θ₄Term n τ

-- @@ L56-57 verbatim
/-- The fourth power `Θ₂ ^ 4` of the theta function `Θ₂`. -/
noncomputable def H₂ (τ : ℍ) : ℂ := (Θ₂ τ) ^ 4

-- @@ L58-59 verbatim
/-- The fourth power `Θ₃ ^ 4` of the theta function `Θ₃`. -/
noncomputable def H₃ (τ : ℍ) : ℂ := (Θ₃ τ) ^ 4

-- @@ L60-61 verbatim
/-- The fourth power `Θ₄ ^ 4` of the theta function `Θ₄`. -/
noncomputable def H₄ (τ : ℍ) : ℂ := (Θ₄ τ) ^ 4


-- @@ L63-67 verbatim
/-- Theta functions as specializations of jacobiTheta₂ -/
theorem Θ₂_term_as_jacobiTheta₂_term (τ : ℍ) (n : ℤ) :
    Θ₂Term n τ = cexp (π * I * τ / 4) * jacobiTheta₂_term n (τ / 2) τ := by
  rw [Θ₂Term, jacobiTheta₂_term, ← Complex.exp_add]
  ring_nf


-- @@ L69-70 verbatim
theorem Θ₂_as_jacobiTheta₂ (τ : ℍ) : Θ₂ τ = cexp (π * I * τ / 4) * jacobiTheta₂ (τ / 2) τ := by
  simp_rw [Θ₂, Θ₂_term_as_jacobiTheta₂_term, tsum_mul_left, jacobiTheta₂]


-- @@ L72-73 verbatim
theorem Θ₃_term_as_jacobiTheta₂_term (τ : ℍ) (n : ℤ) :
    Θ₃Term n τ = jacobiTheta₂_term n 0 τ := by simp [Θ₃Term, jacobiTheta₂_term]


-- @@ L75-76 verbatim
theorem Θ₃_as_jacobiTheta₂ (τ : ℍ) : Θ₃ τ = jacobiTheta₂ (0 : ℂ) τ := by
  simp_rw [Θ₃, Θ₃_term_as_jacobiTheta₂_term, jacobiTheta₂]


-- @@ L78-81 verbatim
theorem Θ₄_term_as_jacobiTheta₂_term (τ : ℍ) (n : ℤ) :
    Θ₄Term n τ = jacobiTheta₂_term n (1 / 2 : ℂ) τ := by
  rw [Θ₄Term, jacobiTheta₂_term, ← exp_pi_mul_I, ← exp_int_mul, ← Complex.exp_add]
  ring_nf


-- @@ L83-84 verbatim
theorem Θ₄_as_jacobiTheta₂ (τ : ℍ) : Θ₄ τ = jacobiTheta₂ (1 / 2 : ℂ) τ := by
  simp_rw [Θ₄, Θ₄_term_as_jacobiTheta₂_term, jacobiTheta₂]


-- @@ L86-86 verbatim
section H_SlashInvariant


-- @@ L88-89 verbatim
/-- Slash action of various elements on H₂, H₃, H₄ -/
lemma H₂_negI_action : (H₂ ∣[(2 : ℤ)] negI.1) = H₂ := modular_slash_negI_of_even H₂ (2: ℤ) even_two

-- @@ L90-90 verbatim
lemma H₃_negI_action : (H₃ ∣[(2 : ℤ)] negI.1) = H₃ := modular_slash_negI_of_even H₃ (2: ℤ) even_two

-- @@ L91-91 verbatim
lemma H₄_negI_action : (H₄ ∣[(2 : ℤ)] negI.1) = H₄ := modular_slash_negI_of_even H₄ (2: ℤ) even_two


-- @@ L93-119 verbatim
/-- These three transformation laws follow directly from tsum definition. -/
lemma H₂_T_action : (H₂ ∣[(2 : ℤ)] T) = -H₂ := by
  ext x
  suffices hΘ₂ : Θ₂ ((1 : ℝ) +ᵥ x) = cexp (π * I / 4) * Θ₂ x by
    simp_rw [modular_slash_T_apply, Pi.neg_apply, H₂, hΘ₂, mul_pow, ← Complex.exp_nat_mul,
      mul_comm ((4 : ℕ) : ℂ), Nat.cast_ofNat, div_mul_cancel₀ (b := (4 : ℂ)) _ (by simp),
      Complex.exp_pi_mul_I, neg_one_mul]
  calc
  _ = ∑' (n : ℤ), cexp (π * I * (n + 1 / 2) ^ 2 * ((1 : ℝ) +ᵥ x)) := by simp_rw [Θ₂, Θ₂Term]
  _ = ∑' (n : ℤ), cexp (π * I / 4) * cexp (π * I * (n ^ 2 + n) + π * I * (n + 1 / 2) ^ 2 * x) := by
    apply tsum_congr fun b ↦ ?_
    rw [coe_vadd, ofReal_one]
    repeat rw [← Complex.exp_add]
    congr
    ring_nf
  _ = cexp (π * I / 4) * ∑' (n : ℤ), cexp (π * I * (n ^ 2 + n) + π * I * (n + 1 / 2) ^ 2 * x) := by
    rw [tsum_mul_left]
  _ = _ := by
    simp_rw [Θ₂, Θ₂Term]
    congr 1
    apply tsum_congr fun b ↦ ?_
    have : Even (b ^ 2 + b) := by
      convert Int.even_mul_succ_self b using 1
      ring_nf
    norm_cast
    rw [Complex.exp_add, mul_comm (π * I), Complex.exp_int_mul, Complex.exp_pi_mul_I,
      this.neg_one_zpow, one_mul]


-- @@ L121-133 verbatim
lemma H₃_T_action : (H₃ ∣[(2 : ℤ)] T) = H₄ := by
  ext x
  simp_rw [modular_slash_T_apply, H₃, H₄, Θ₃, Θ₄, Θ₃Term, Θ₄Term]
  congr 1
  apply tsum_congr fun b ↦ ?_
  rw [coe_vadd, ofReal_one, mul_add, Complex.exp_add, mul_one, mul_comm (π * I), ← Int.cast_pow,
    Complex.exp_int_mul, Complex.exp_pi_mul_I]
  congr 1
  rcases Int.even_or_odd b with (hb | hb)
  · rw [hb.neg_one_zpow, Even.neg_one_zpow]
    simp [sq, hb]
  · rw [hb.neg_one_zpow, Odd.neg_one_zpow]
    simp [sq, hb]


-- @@ L135-140 verbatim
lemma H₄_T_action : (H₄ ∣[(2 : ℤ)] T) = H₃ := by
  -- H₄|T = H₃|T^2 = Θ₂(0, z + 2) = Θ₂(0, z) = H₃
  ext x
  simp_rw [← H₃_T_action, modular_slash_T_apply, H₃, Θ₃_as_jacobiTheta₂, coe_vadd, ← add_assoc]
  norm_num
  rw [add_comm, jacobiTheta₂_add_right]


-- @@ L142-143 verbatim
lemma H₂_T_inv_action : (H₂ ∣[(2 : ℤ)] T⁻¹) = -H₂ := by
  nth_rw 1 [← neg_eq_iff_eq_neg.mpr H₂_T_action, neg_slash, ← slash_mul, mul_inv_cancel, slash_one]


-- @@ L145-146 verbatim
lemma H₃_T_inv_action : (H₃ ∣[(2 : ℤ)] T⁻¹) = H₄ := by
  nth_rw 1 [← H₄_T_action, ← slash_mul, mul_inv_cancel, slash_one]


-- @@ L148-149 verbatim
lemma H₄_T_inv_action : (H₄ ∣[(2 : ℤ)] T⁻¹) = H₃ := by
  nth_rw 1 [← H₃_T_action, ← slash_mul, mul_inv_cancel, slash_one]


-- @@ L151-152 verbatim
/-- Use α = T * T -/
lemma H₂_α_action : (H₂ ∣[(2 : ℤ)] α.1) = H₂ := by simp [α_eq_T_sq, sq, slash_mul, H₂_T_action]


-- @@ L154-155 verbatim
lemma H₃_α_action : (H₃ ∣[(2 : ℤ)] α.1) = H₃ := by
  simp [α_eq_T_sq, sq, slash_mul, H₃_T_action, H₄_T_action]


-- @@ L157-158 verbatim
lemma H₄_α_action : (H₄ ∣[(2 : ℤ)] α.1) = H₄ := by
  simp [α_eq_T_sq, sq, slash_mul, H₃_T_action, H₄_T_action]


-- @@ L160-208 verbatim
/-- Use jacobiTheta₂_functional_equation -/
lemma H₂_S_action : (H₂ ∣[(2 : ℤ)] S) = -H₄ := by
  ext ⟨x, hx⟩
  have hx' : x ≠ 0 := by simp [Complex.ext_iff, hx.ne.symm]
  calc
  _ = cexp (-π * I / x) * jacobiTheta₂ (-1 / (2 * x)) (-1 / x) ^ 4 * x ^ (-2 : ℤ) := by
    rw [modular_slash_S_apply, H₂, Θ₂_as_jacobiTheta₂]
    simp only [inv_neg, mul_neg, mul_pow, ← Complex.exp_nat_mul, Nat.cast_ofNat, Int.reduceNeg,
      _root_.zpow_neg, neg_mul, mul_eq_mul_right_iff, inv_eq_zero]
    rw [mul_comm 4, div_mul_cancel₀ _ (by norm_num)]
    left
    congr 3
    · rw [← div_eq_mul_inv, neg_div]
    · rw [← one_div, neg_div, div_div, mul_comm, neg_div]
    · rw [← one_div, neg_div]
  _ = cexp (-π * I / x) * x ^ (-2 : ℤ)
        * (1 / (I / x) ^ ((1 : ℂ) / 2) * cexp (π * I / (4 * x)) * jacobiTheta₂ (1 / 2) x) ^ 4 := by
    rw [mul_right_comm, jacobiTheta₂_functional_equation]
    congr 4
    · ring_nf
    · congr 1
      rw [neg_mul, neg_div, one_div, neg_div, div_neg, neg_mul, neg_div, neg_neg]
      ring_nf
      simp [sq, ← mul_assoc, inv_mul_cancel_right₀ hx']
    · ring_nf; simp [hx']
    · ring_nf; simp [inv_inv]
  _ = cexp (-π * I / x) * x ^ (-2 : ℤ)
        * ((1 / (I / x) ^ ((1 : ℂ) / 2)) ^ 4 * cexp (π * I / (4 * x)) ^ 4
          * jacobiTheta₂ (1 / 2) x ^ 4) := by simp [mul_pow]
  _ = cexp (-π * I / x) * x ^ (-2 : ℤ)
        * ((1 / (I / x) ^ (2 : ℂ)) * cexp (π * I / (4 * x)) ^ 4 * jacobiTheta₂ (1 / 2) x ^ 4) := by
    congr 3
    simp only [div_pow, one_pow, ← cpow_mul_nat]
    ring_nf
  _ = cexp (-π * I / x) * (x ^ (-2 : ℤ) * (-x ^ (2 : ℤ)))
        * cexp (π * I / (4 * x)) ^ 4 * jacobiTheta₂ (1 / 2) x ^ 4 := by
    repeat rw [← mul_assoc]
    congr 4
    rw [cpow_ofNat, div_pow, one_div_div, I_sq, div_neg, div_one]
    rfl
  _ = -cexp (-π * I / x) * cexp (π * I / x) * jacobiTheta₂ (1 / 2) x ^ 4 := by
    rw [mul_neg, ← zpow_add₀ hx', neg_add_cancel, mul_neg, zpow_zero, mul_one]
    congr 2
    rw [← Complex.exp_nat_mul]
    ring_nf
  _ = -jacobiTheta₂ (1 / 2) x ^ 4 := by
    rw [neg_mul, ← Complex.exp_add, neg_mul (π : ℂ), neg_div, neg_add_cancel, Complex.exp_zero,
      neg_one_mul]
  _ = -H₄ ⟨x, hx⟩ := by simp [H₄, Θ₄_as_jacobiTheta₂]


-- @@ L210-225 verbatim
lemma H₃_S_action : (H₃ ∣[(2 : ℤ)] S) = -H₃ := by
  ext x
  have hx' : (x : ℂ) ≠ 0 := by obtain ⟨x, hx⟩ := x; change x ≠ 0; simp [Complex.ext_iff, hx.ne.symm]
  have := jacobiTheta₂_functional_equation 0
  simp only [neg_mul, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, mul_zero, zero_div,
    Complex.exp_zero, mul_one] at this
  simp only [modular_slash_S_apply, H₃, inv_neg, Θ₃_as_jacobiTheta₂, Int.reduceNeg, _root_.zpow_neg,
    Pi.neg_apply]
  rw [this, mul_pow, neg_div, div_neg, neg_neg, one_div (x : ℂ)⁻¹, inv_inv,
    mul_right_comm, ← neg_one_mul (_ ^ 4)]
  congr
  rw [div_pow, ← cpow_mul_nat, mul_neg, neg_neg]
  ring_nf!
  rw [← mul_inv, cpow_ofNat, sq, ← mul_assoc, zpow_two]
  ring_nf!
  simp_all


-- @@ L227-229 verbatim
lemma H₄_S_action : (H₄ ∣[(2 : ℤ)] S) = - H₂ := by
  rw [← neg_eq_iff_eq_neg.mpr H₂_S_action, neg_slash, ← slash_mul, modular_S_sq,
    ModularForm.slash_neg' _ _ (by decide), slash_one]


-- @@ L231-235 verbatim
lemma H₂_S_action' (z : ℍ) : H₂ (S • z) = - z ^ 2 * H₄ z := by
    have h := congrFun H₂_S_action z
    simp only [SL_slash_apply, denom_S, _root_.zpow_neg, zpow_two, Pi.neg_apply] at h
    field_simp [ne_zero] at h ⊢
    exact h


-- @@ L237-241 verbatim
lemma H₄_S_action' (z : ℍ) : H₄ (S • z) = - z ^ 2 * H₂ z := by
    have h := congrFun H₄_S_action z
    simp only [SL_slash_apply, denom_S, _root_.zpow_neg, zpow_two, Pi.neg_apply] at h
    field_simp [ne_zero z] at h ⊢
    exact h


-- @@ L243-244 verbatim
lemma H₂_S_inv_action : (H₂ ∣[(2 : ℤ)] S⁻¹) = -H₄ := by
  rw [← neg_eq_iff_eq_neg.mpr H₄_S_action, neg_slash, ← slash_mul, mul_inv_cancel, slash_one]


-- @@ L246-247 verbatim
lemma H₃_S_inv_action : (H₃ ∣[(2 : ℤ)] S⁻¹) = -H₃ := by
  nth_rw 1 [← neg_eq_iff_eq_neg.mpr H₃_S_action, neg_slash, ← slash_mul, mul_inv_cancel, slash_one]


-- @@ L249-250 verbatim
lemma H₄_S_inv_action : (H₄ ∣[(2 : ℤ)] S⁻¹) = -H₂ := by
  rw [← neg_eq_iff_eq_neg.mpr H₂_S_action, neg_slash, ← slash_mul, mul_inv_cancel, slash_one]


-- @@ L252-258 verbatim
/-- Use β = -S * α^(-1) * S -/
lemma H₂_β_action : (H₂ ∣[(2 : ℤ)] β.1) = H₂ := calc
  _ = (((H₂ ∣[(2 : ℤ)] negI.1) ∣[(2 : ℤ)] S) ∣[(2 : ℤ)] α.1⁻¹) ∣[(2 : ℤ)] S := by
    simp [β_eq_negI_mul_S_mul_α_inv_mul_S, slash_mul]
  _ = _ := by
    rw [H₂_negI_action, H₂_S_action, neg_slash, neg_slash, α_eq_T_sq]
    simp [sq, slash_mul, H₄_T_inv_action, H₃_T_inv_action, H₄_S_action]


-- @@ L260-265 verbatim
lemma H₃_β_action : (H₃ ∣[(2 : ℤ)] β.1) = H₃ := calc
  _ = (((H₃ ∣[(2 : ℤ)] negI.1) ∣[(2 : ℤ)] S) ∣[(2 : ℤ)] α.1⁻¹) ∣[(2 : ℤ)] S := by
    simp [β_eq_negI_mul_S_mul_α_inv_mul_S, slash_mul]
  _ = _ := by
    rw [H₃_negI_action, H₃_S_action, neg_slash, neg_slash, α_eq_T_sq]
    simp [sq, slash_mul, H₄_T_inv_action, H₃_T_inv_action, H₃_S_action]


-- @@ L267-272 verbatim
lemma H₄_β_action : (H₄ ∣[(2 : ℤ)] β.1) = H₄ := calc
  _ = (((H₄ ∣[(2 : ℤ)] negI.1) ∣[(2 : ℤ)] S) ∣[(2 : ℤ)] α.1⁻¹) ∣[(2 : ℤ)] S := by
    simp [β_eq_negI_mul_S_mul_α_inv_mul_S, slash_mul]
  _ = _ := by
    rw [H₄_negI_action, H₄_S_action, neg_slash, neg_slash, α_eq_T_sq]
    simp [sq, slash_mul, H₂_T_inv_action, H₂_S_action]


-- @@ L274-277 expanded
/-- H₂, H₃, H₄ are modular forms of weight 2 and level Γ(2) -/
noncomputable def H₂SIF : SlashInvariantForm (CongruenceSubgroup.Gamma 2) 2
    where
  toFun := H₂
  slash_action_eq' := slashaction_generators_Γ2 H₂ (2 : ℤ) H₂_α_action H₂_β_action H₂_negI_action


-- @@ L279-282 expanded
/-- `H₃` as a slash-invariant form of weight 2 and level `Γ(2)`. -/
noncomputable def H₃SIF : SlashInvariantForm (CongruenceSubgroup.Gamma 2) 2
    where
  toFun := H₃
  slash_action_eq' := slashaction_generators_Γ2 H₃ (2 : ℤ) H₃_α_action H₃_β_action H₃_negI_action


-- @@ L284-287 expanded
/-- `H₄` as a slash-invariant form of weight 2 and level `Γ(2)`. -/
noncomputable def H₄SIF : SlashInvariantForm (CongruenceSubgroup.Gamma 2) 2
    where
  toFun := H₄
  slash_action_eq' := slashaction_generators_Γ2 H₄ (2 : ℤ) H₄_α_action H₄_β_action H₄_negI_action


-- @@ L289-289 verbatim
@[simp] lemma H₂_SIF_coe : (H₂SIF : ℍ → ℂ) = H₂ := rfl


-- @@ L291-291 verbatim
@[simp] lemma H₃_SIF_coe : (H₃SIF : ℍ → ℂ) = H₃ := rfl


-- @@ L293-293 verbatim
@[simp] lemma H₄_SIF_coe : (H₄SIF : ℍ → ℂ) = H₄ := rfl


-- @@ L295-295 verbatim
end H_SlashInvariant




-- @@ L299-299 verbatim
section H_MDifferentiable


-- @@ L301-331 verbatim
lemma H₂_SIF_MDifferentiable : MDiff H₂SIF := by
  intro τ
  suffices h_diff : DifferentiableAt ℂ (↑ₕH₂) (τ : ℂ) by
    have : (H₂ ∘ ↑ofComplex) ∘ UpperHalfPlane.coe = H₂SIF := by
      ext x
      simp [H₂SIF, ofComplex_apply]
    rw [← this]
    exact h_diff.mdifferentiableAt.comp τ τ.mdifferentiable_coe
  have hU : {z : ℂ | 0 < z.im} ∈ 𝓝 (τ : ℂ) := isOpen_upperHalfPlaneSet.mem_nhds τ.2
  let F : ℂ → ℂ := fun t => (cexp (((π : ℂ) * I / 4) * t) * jacobiTheta₂ (t / 2) t) ^ 4
  have hF : DifferentiableAt ℂ F (τ : ℂ) := by
    have h_exp : DifferentiableAt ℂ (fun t : ℂ => cexp ((π * I / 4) * t)) (τ : ℂ) :=
      (differentiableAt_id.const_mul ((π : ℂ) * I / 4)).cexp
    have h_theta : DifferentiableAt ℂ (fun t : ℂ => jacobiTheta₂ (t / 2) t) (τ : ℂ) := by
      let f : ℂ → ℂ × ℂ := fun t : ℂ => (t / 2, t)
      let g : ℂ × ℂ → ℂ := fun p => jacobiTheta₂ p.1 p.2
      have hg : DifferentiableAt ℂ g (f (τ : ℂ)) :=
        by simpa [f] using (hasFDerivAt_jacobiTheta₂ ((τ : ℂ) / 2) τ.2).differentiableAt
      have hf : DifferentiableAt ℂ f (τ : ℂ) :=
        (differentiableAt_id.mul_const ((2 : ℂ)⁻¹)).prodMk differentiableAt_id
      simpa [f, g] using (DifferentiableAt.fun_comp' (τ : ℂ) hg hf)
    simp only [F]; exact (h_exp.mul h_theta).pow 4
  have h_ev : F =ᶠ[𝓝 (τ : ℂ)] (↑ₕH₂) := by
    refine Filter.eventually_of_mem hU ?_
    intro z hz
    have h_arg : cexp (((π : ℂ) * I / 4) * z) = cexp (π * I * z / 4) := by
      have : ((π : ℂ) * I / 4) * z = (π * I * z) / 4 := by
        simp [div_eq_mul_inv, mul_comm, mul_assoc]
      simp [this]
    simp [F, H₂, Θ₂_as_jacobiTheta₂, ofComplex_apply_of_im_pos hz, h_arg]
  exact (DifferentiableAt.congr_of_eventuallyEq hF h_ev.symm)


-- @@ L333-340 verbatim
lemma H₃_SIF_MDifferentiable : MDiff H₃SIF := by
  rw [mdifferentiable_iff]
  simp only [H₃SIF, SlashInvariantForm.coe_mk]
  have hθ : DifferentiableOn ℂ (fun z => jacobiTheta₂ (0 : ℂ) z) {z | 0 < z.im} :=
    fun x hx => (differentiableAt_jacobiTheta₂_snd 0 (by simpa using hx)).differentiableWithinAt
  apply (hθ.pow 4).congr
  intro _ hz
  simp [Function.comp, H₃, Θ₃_as_jacobiTheta₂, ofComplex_apply_of_im_pos hz]


-- @@ L342-350 verbatim
lemma H₄_SIF_MDifferentiable : MDiff H₄SIF := by
  rw [mdifferentiable_iff]
  simp only [H₄SIF, SlashInvariantForm.coe_mk]
  have hθ : DifferentiableOn ℂ (fun z => jacobiTheta₂ (1 / 2 : ℂ) z) {z | 0 < z.im} :=
    fun x hx =>
      (differentiableAt_jacobiTheta₂_snd (1 / 2 : ℂ) (by simpa using hx)).differentiableWithinAt
  apply (hθ.pow 4).congr
  intro _ hz
  simp [Function.comp, H₄, Θ₄_as_jacobiTheta₂, ofComplex_apply_of_im_pos hz]


-- @@ L352-354 verbatim
@[fun_prop]
lemma H₂_MDifferentiable : MDiff H₂ := by
  simpa [H₂SIF, SlashInvariantForm.coe_mk] using H₂_SIF_MDifferentiable


-- @@ L356-358 verbatim
@[fun_prop]
lemma H₃_MDifferentiable : MDiff H₃ := by
  simpa [H₃SIF, SlashInvariantForm.coe_mk] using H₃_SIF_MDifferentiable


-- @@ L360-362 verbatim
@[fun_prop]
lemma H₄_MDifferentiable : MDiff H₄ := by
  simpa [H₄SIF, SlashInvariantForm.coe_mk] using H₄_SIF_MDifferentiable


-- @@ L364-372 verbatim
/-- Differentiability of `t ↦ jacobiTheta₂(t/2, t)` at points in the upper half-plane. -/
lemma differentiableAt_jacobiTheta₂_half (τ : ℍ) :
    DifferentiableAt ℂ (fun t : ℂ => jacobiTheta₂ (t / 2) t) ↑τ := by
  let f : ℂ → ℂ × ℂ := fun t => (t / 2, t)
  have hf : DifferentiableAt ℂ f ↑τ :=
    (differentiableAt_id.mul_const ((2 : ℂ)⁻¹)).prodMk differentiableAt_id
  have hg : DifferentiableAt ℂ (fun p : ℂ × ℂ => jacobiTheta₂ p.1 p.2) (f ↑τ) :=
    by simpa [f] using (hasFDerivAt_jacobiTheta₂ ((τ : ℂ) / 2) τ.2).differentiableAt
  simpa [f, Function.comp_def] using DifferentiableAt.comp (x := (τ : ℂ)) hg hf


-- @@ L374-384 verbatim
lemma Θ₂_MDifferentiable : MDiff Θ₂ := by
  intro τ
  have hΘ₂_diff : DifferentiableAt ℂ
      (fun t : ℂ => cexp ((π * I / 4) * t) * jacobiTheta₂ (t / 2) t) (τ : ℂ) :=
    ((differentiableAt_id.const_mul ((π : ℂ) * I / 4)).cexp).mul
      (differentiableAt_jacobiTheta₂_half τ)
  have hMD := hΘ₂_diff.mdifferentiableAt.comp τ τ.mdifferentiable_coe
  have : (fun t : ℂ => cexp ((π * I / 4) * t) * jacobiTheta₂ (t / 2) t) ∘
      UpperHalfPlane.coe = Θ₂ := by
    ext x; simp only [Function.comp_apply, Θ₂_as_jacobiTheta₂]; ring_nf
  rwa [this] at hMD


-- @@ L386-386 verbatim
end H_MDifferentiable




-- @@ L390-390 verbatim
section H_isBoundedAtImInfty


-- @@ L392-394 verbatim
variable (γ : SL(2, ℤ))

-- TODO: Isolate this somewhere

-- @@ L395-398 verbatim
lemma jacobiTheta₂_term_half_apply (n : ℤ) (z : ℂ) :
    jacobiTheta₂_term n (z / 2) z = cexp (π * I * (n ^ 2 + n) * z) := by
  rw [jacobiTheta₂_term]
  ring_nf


-- @@ L400-407 verbatim
lemma jacobiTheta₂_rel_aux (n : ℤ) (t : ℝ) :
    rexp (-π * (n + 1 / 2) ^ 2 * t)
      = rexp (-π * t / 4) * jacobiTheta₂_term n (I * t / 2) (I * t) := by
  rw [jacobiTheta₂_term_half_apply, ofReal_exp, ofReal_exp, ← Complex.exp_add, ofReal_mul]
  congr
  ring_nf
  simp
  ring_nf!


-- @@ L409-409 verbatim
lemma Complex.norm_exp_mul_I (z : ℂ) : ‖cexp (z * I)‖ = rexp (-z.im) := by simp [norm_exp]


-- @@ L411-465 verbatim
theorem isBoundedAtImInfty_H₂ : IsBoundedAtImInfty H₂ := by
  simp_rw [UpperHalfPlane.isBoundedAtImInfty_iff, H₂, Θ₂]
  use (∑' n : ℤ, rexp (-π * ((n : ℝ) + 1 / 2) ^ 2)) ^ 4, 1
  intro z hz
  rw [norm_pow]
  gcongr
  calc
    _ = ‖∑' (n : ℤ), cexp (π * I * (n + 1 / 2) ^ 2 * z)‖ := rfl
    _ ≤ ∑' (n : ℤ), ‖cexp (π * I * (n + 1 / 2) ^ 2 * z)‖ := norm_tsum_le_tsum_norm ?_
    _ = ∑' (n : ℤ), ‖cexp (π * I * ((n + 1 / 2) ^ 2 * z : ℂ))‖ := by simp only [← mul_assoc]
    _ = ∑' (n : ℤ), ‖rexp (-π * (((n + 1 / 2) ^ 2 : ℝ) * z : ℂ).im)‖ := by
      apply tsum_congr fun b ↦ ?_
      have (z : ℂ) : ‖cexp z‖ = ‖cexp z.re‖ := by
        nth_rw 1 [← Complex.re_add_im z, Complex.exp_add, norm_mul, norm_exp_ofReal_mul_I, mul_one]
      simp_all
    _ = ∑' (n : ℤ), ‖rexp (-π * ((n + 1 / 2) ^ 2 : ℝ) * z.im)‖ := by
      simp_rw [im_ofReal_mul, UpperHalfPlane.im, ← mul_assoc]
    _ ≤ _ := Summable.tsum_le_tsum (fun b ↦ ?_) ?_ ?_
  · -- TODO: simplify and refactor this proof with subproof 3 & 4
    have (n : ℤ) : cexp (π * I * (n + 1 / 2) ^ 2 * z)
        = cexp (π * I * z / 4) * jacobiTheta₂_term n (z / 2) z := by
      rw [jacobiTheta₂_term_half_apply, ← Complex.exp_add]
      ring_nf
    simp_rw [this, ← smul_eq_mul (a := cexp _)]
    apply Summable.norm
    apply Summable.const_smul
    rw [summable_jacobiTheta₂_term_iff, coe_im]
    linarith
  · rw [Real.norm_eq_abs, Real.abs_exp]
    apply Real.exp_monotone
    repeat rw [neg_mul]
    apply neg_le_neg
    have : (b : ℝ) + 1 / 2 ≠ 0 := by
      intro hb
      rw [add_eq_zero_iff_eq_neg] at hb
      have : (2 * b : ℝ) = -1 := by simp [hb]
      norm_cast at this
      exact Int.not_odd_iff_even.mpr (even_two_mul b) (by rw [this]; simp)
    convert (mul_le_mul_iff_right₀ (mul_pos pi_pos (sq_pos_of_ne_zero this))).mpr hz using 1
    rw [mul_one]
  · apply Summable.norm
    apply summable_ofReal.mp
    simp_rw [jacobiTheta₂_rel_aux, ofReal_exp, ← smul_eq_mul (a := cexp _)]
    apply Summable.const_smul
    rw [summable_jacobiTheta₂_term_iff, I_mul_im, ofReal_re]
    linarith
  · apply summable_ofReal.mp
    have (n : ℤ) := jacobiTheta₂_rel_aux n 1
    simp_rw [mul_one] at this
    simp_rw [this, ← smul_eq_mul]
    apply Summable.const_smul
    rw [summable_jacobiTheta₂_term_iff]
    simp

-- We isolate this lemma out as it's also used in the proof for Θ₄

-- @@ L466-486 verbatim
lemma isBoundedAtImInfty_H₃_aux (z : ℍ) (hz : 1 ≤ z.im) :
    ∑' (n : ℤ), ‖Θ₃Term n z‖ ≤ ∑' (n : ℤ), rexp (-π * n ^ 2) := by
  have h_rw (z : ℍ) (n : ℤ) : -(π * n ^ 2 * z : ℂ).im = -π * n ^ 2 * z.im := by
    rw [mul_assoc, im_ofReal_mul, ← Int.cast_pow, ← ofReal_intCast, im_ofReal_mul]
    simp [← mul_assoc]
  have h_sum (z : ℍ) : Summable fun n : ℤ ↦ rexp (-π * n ^ 2 * z.im) := by
    have := (summable_jacobiTheta₂_term_iff 0 z).mpr z.2
    rw [← summable_norm_iff, ← summable_ofReal] at this
    simp_rw [jacobiTheta₂_term, mul_zero, zero_add, mul_right_comm _ I, norm_exp_mul_I, h_rw]
      at this
    simpa using summable_ofReal.mp this
  calc
    _ = ∑' (n : ℤ), ‖cexp (π * (n : ℂ) ^ 2 * z * I)‖ := by simp_rw [Θ₃Term, mul_right_comm _ I]
    _ = ∑' (n : ℤ), rexp (-π * (n : ℂ) ^ 2 * z).im := by simp_rw [Complex.norm_exp_mul_I]; simp
    _ = ∑' (n : ℤ), rexp (-π * (n : ℝ) ^ 2 * z.im) := by
      simp_all
    _ ≤ _ := Summable.tsum_le_tsum (fun b ↦ ?_) ?_ ?_
  · apply exp_monotone
    simpa only [neg_mul, neg_le_neg_iff] using le_mul_of_one_le_right (by positivity) hz
  · exact h_sum z
  · simpa using h_sum UpperHalfPlane.I


-- @@ L488-498 verbatim
theorem isBoundedAtImInfty_H₃ : IsBoundedAtImInfty H₃ := by
  simp_rw [UpperHalfPlane.isBoundedAtImInfty_iff, H₃, Θ₃]
  use (∑' n : ℤ, rexp (-π * n ^ 2)) ^ 4, 1
  intro z hz
  rw [norm_pow]
  gcongr
  apply (norm_tsum_le_tsum_norm ?_).trans (isBoundedAtImInfty_H₃_aux z hz)
  simp_rw [Θ₃_term_as_jacobiTheta₂_term]
  apply Summable.norm
  rw [summable_jacobiTheta₂_term_iff]
  exact z.2


-- @@ L500-513 verbatim
theorem isBoundedAtImInfty_H₄ : IsBoundedAtImInfty H₄ := by
  simp_rw [UpperHalfPlane.isBoundedAtImInfty_iff, H₄, Θ₄]
  use (∑' n : ℤ, rexp (-π * n ^ 2)) ^ 4, 1
  intro z hz
  rw [norm_pow]
  gcongr
  calc
    _ ≤ ∑' (n : ℤ), ‖Θ₄Term n z‖ := norm_tsum_le_tsum_norm ?_
    _ = ∑' (n : ℤ), ‖Θ₃Term n z‖ := by congr with n; simp [Θ₄Term, Θ₃Term]
    _ ≤ _ := isBoundedAtImInfty_H₃_aux z hz
  simp_rw [Θ₄_term_as_jacobiTheta₂_term]
  apply Summable.norm
  rw [summable_jacobiTheta₂_term_iff]
  exact z.2


-- @@ L515-553 verbatim
theorem isBoundedAtImInfty_H_slash : IsBoundedAtImInfty (H₂ ∣[(2 : ℤ)] γ)
      ∧ IsBoundedAtImInfty (H₃ ∣[(2 : ℤ)] γ) ∧ IsBoundedAtImInfty (H₄ ∣[(2 : ℤ)] γ) := by
  apply Subgroup.closure_induction_left (s := {S, T, ↑negI})
      (p := fun γ _ ↦ IsBoundedAtImInfty (H₂ ∣[(2 : ℤ)] γ) ∧ IsBoundedAtImInfty (H₃ ∣[(2 : ℤ)] γ)
        ∧ IsBoundedAtImInfty (H₄ ∣[(2 : ℤ)] γ))
  · simp [isBoundedAtImInfty_H₂, isBoundedAtImInfty_H₃, isBoundedAtImInfty_H₄]
  · intro x hx y _ h
    simp_rw [slash_mul]
    rcases hx with (rfl | rfl | rfl | _)
    · simp_rw [H₂_S_action, H₃_S_action, H₄_S_action, neg_slash, isBoundedAtImInfty_neg_iff]
      use h.right.right, h.right.left, h.left
    · simp_rw [H₂_T_action, H₃_T_action, H₄_T_action, neg_slash, isBoundedAtImInfty_neg_iff]
      use h.left, h.right.right, h.right.left
    · rw [SL_slash, H₂_negI_action, H₃_negI_action, H₄_negI_action]
      exact h
  · intro x hx y _ h
    simp_rw [slash_mul]
    rcases hx with (rfl | rfl | rfl | _)
    · simp_rw [H₂_S_inv_action, H₃_S_inv_action, H₄_S_inv_action, neg_slash,
        isBoundedAtImInfty_neg_iff]
      use h.right.right, h.right.left, h.left
    · simp_rw [H₂_T_inv_action, H₃_T_inv_action, H₄_T_inv_action, neg_slash,
        isBoundedAtImInfty_neg_iff]
      use h.left, h.right.right, h.right.left
    · rw [← Subgroup.coe_inv, modular_negI_inv, SL_slash,
        modular_slash_negI_of_even _ 2 (by decide)]
      rw [H₃_negI_action, H₄_negI_action]
      exact h
  · intro s hs
    simp_rw [Set.mem_ofPred_eq, Set.mem_range] at hs
    obtain ⟨s, rfl⟩ := hs
    rw [Set.mem_iInter, SetLike.mem_coe]
    intro hs
    have hs2 : {S, T} ⊆ (s : Set (SL(2, ℤ))) := by
      apply subset_trans _ hs
      simp only [Set.singleton_subset_iff, Set.mem_insert_iff, Set.mem_singleton_iff, true_or,
        Set.insert_subset_insert]
    simp only [top_le_iff.mp <| SL2Z_generate.symm ▸ (Subgroup.closure_le s).mpr hs2,
      Subgroup.mem_top]


-- @@ L555-558 verbatim
theorem isBoundedAtImInfty_H₂_slash :
    ∀ A ∈ 𝒮ℒ, IsBoundedAtImInfty (H₂ ∣[(2 : ℤ)] (A : GL (Fin 2) ℝ)) := by
  intro A ⟨A', hA⟩
  exact hA.symm ▸ (isBoundedAtImInfty_H_slash A').left


-- @@ L560-563 verbatim
theorem isBoundedAtImInfty_H₃_slash :
    ∀ A ∈ 𝒮ℒ, IsBoundedAtImInfty (H₃ ∣[(2 : ℤ)] (A : GL (Fin 2) ℝ)) := by
  intro A ⟨A', hA⟩
  exact hA.symm ▸ (isBoundedAtImInfty_H_slash A').right.left


-- @@ L565-568 verbatim
theorem isBoundedAtImInfty_H₄_slash :
    ∀ A ∈ 𝒮ℒ, IsBoundedAtImInfty (H₄ ∣[(2 : ℤ)] (A : GL (Fin 2) ℝ)) := by
  intro A ⟨A', hA⟩
  exact hA.symm ▸ (isBoundedAtImInfty_H_slash A').right.right


-- @@ L570-570 verbatim
end H_isBoundedAtImInfty


-- @@ L572-577 expanded
/-- `H₂` as a modular form of weight 2 and level `Γ(2)`. -/
noncomputable def H₂MF : ModularForm (CongruenceSubgroup.Gamma 2) 2 :=
  { H₂SIF with
    holo' := H₂_SIF_MDifferentiable
    bdd_at_cusps' hc := bounded_at_cusps_of_bounded_at_infty hc isBoundedAtImInfty_H₂_slash }


-- @@ L579-584 expanded
/-- `H₃` as a modular form of weight 2 and level `Γ(2)`. -/
noncomputable def H₃MF : ModularForm (CongruenceSubgroup.Gamma 2) 2 :=
  { H₃SIF with
    holo' := H₃_SIF_MDifferentiable
    bdd_at_cusps' hc := bounded_at_cusps_of_bounded_at_infty hc isBoundedAtImInfty_H₃_slash }


-- @@ L586-591 expanded
/-- `H₄` as a modular form of weight 2 and level `Γ(2)`. -/
noncomputable def H₄MF : ModularForm (CongruenceSubgroup.Gamma 2) 2 :=
  { H₄SIF with
    holo' := H₄_SIF_MDifferentiable
    bdd_at_cusps' hc := bounded_at_cusps_of_bounded_at_infty hc isBoundedAtImInfty_H₄_slash }


-- @@ L593-593 verbatim
@[simp] lemma H₂_MF_coe : (H₂MF : ℍ → ℂ) = H₂ := rfl


-- @@ L595-595 verbatim
@[simp] lemma H₃_MF_coe : (H₃MF : ℍ → ℂ) = H₃ := rfl


-- @@ L597-597 verbatim
@[simp] lemma H₄_MF_coe : (H₄MF : ℍ → ℂ) = H₄ := rfl


-- @@ L599-614 verbatim
/-!
## Jacobi identity

The Jacobi identity states H₂ + H₄ = H₃ (equivalently Θ₂⁴ + Θ₄⁴ = Θ₃⁴).
This is blueprint Lemma 6.41, proved via dimension vanishing for weight 4 cusp forms.

The proof strategy:
1. Define g := H₂ + H₄ - H₃ and f := g²
2. Show f is SL₂(ℤ)-invariant (weight 4, level 1) via S/T invariance
3. Show f vanishes at i∞ (is a cusp form)
4. Apply cusp form vanishing: dim S₄(Γ₁) = 0
5. From g² = 0 conclude g = 0

The S/T slash action lemmas are proved here. The full proof requiring
asymptotics (atImInfty) is in AtImInfty.lean to avoid circular imports.
-/


-- @@ L616-616 verbatim
section JacobiIdentity


-- @@ L618-619 verbatim
/-- The difference g := H₂ + H₄ - H₃ -/
noncomputable def jacobiG : ℍ → ℂ := H₂ + H₄ - H₃


-- @@ L621-622 verbatim
/-- The squared difference f := g² -/
noncomputable def jacobiF : ℍ → ℂ := jacobiG ^ 2


-- @@ L624-631 verbatim
/-- S-action on g: g|[2]S = -g -/
lemma jacobi_g_S_action : (jacobiG ∣[(2 : ℤ)] S) = -jacobiG := by
  change ((H₂ + H₄ - H₃) ∣[(2 : ℤ)] S) = -(H₂ + H₄ - H₃)
  simp only [sub_eq_add_neg, SlashAction.add_slash, SlashAction.neg_slash,
    H₂_S_action, H₃_S_action, H₄_S_action]
  ext z
  simp only [Pi.add_apply, Pi.neg_apply]
  ring


-- @@ L633-640 verbatim
/-- T-action on g: g|[2]T = -g -/
lemma jacobi_g_T_action : (jacobiG ∣[(2 : ℤ)] T) = -jacobiG := by
  change ((H₂ + H₄ - H₃) ∣[(2 : ℤ)] T) = -(H₂ + H₄ - H₃)
  simp only [sub_eq_add_neg, SlashAction.add_slash, SlashAction.neg_slash,
    H₂_T_action, H₃_T_action, H₄_T_action]
  ext z
  simp only [Pi.add_apply, Pi.neg_apply]
  ring


-- @@ L642-645 verbatim
/-- Rewrite jacobiF as a pointwise product -/
lemma jacobi_f_eq_mul : jacobiF = jacobiG * jacobiG := by
  ext
  simp [jacobiF, sq]


-- @@ L647-651 verbatim
/-- S-invariance of f: f|[4]S = f, because g|[2]S = -g. -/
lemma jacobi_f_S_action : (jacobiF ∣[(4 : ℤ)] S) = jacobiF := by
  -- simp only needed: lemmas must be applied in order (not a terminal simp)
  simp only [jacobi_f_eq_mul, show (4 : ℤ) = 2 + 2 by norm_num,
    mul_slash_SL2 2 2 S _ _, jacobi_g_S_action, neg_mul_neg]


-- @@ L653-657 verbatim
/-- T-invariance of f: f|[4]T = f, because g|[2]T = -g. -/
lemma jacobi_f_T_action : (jacobiF ∣[(4 : ℤ)] T) = jacobiF := by
  -- simp only needed: lemmas must be applied in order (not a terminal simp)
  simp only [jacobi_f_eq_mul, show (4 : ℤ) = 2 + 2 by norm_num,
    mul_slash_SL2 2 2 T _ _, jacobi_g_T_action, neg_mul_neg]


-- @@ L659-661 verbatim
/-- Full SL₂(ℤ) invariance of f with weight 4 -/
lemma jacobi_f_SL2Z_invariant : ∀ γ : SL(2, ℤ), jacobiF ∣[(4 : ℤ)] γ = jacobiF :=
  slashaction_generators_SL2Z jacobiF 4 jacobi_f_S_action jacobi_f_T_action


-- @@ L663-666 verbatim
/-- jacobiF as a SlashInvariantForm of weight 4 and level Γ(1) -/
noncomputable def jacobiFSIF : SlashInvariantForm (CongruenceSubgroup.Gamma 1) 4 where
  toFun := jacobiF
  slash_action_eq' := slashaction_generators_GL2R jacobiF 4 jacobi_f_S_action jacobi_f_T_action


-- @@ L668-669 verbatim
/-- jacobiG is holomorphic (MDifferentiable) since H₂, H₃, H₄ are -/
lemma jacobi_g_MDifferentiable : MDiff jacobiG := by unfold jacobiG; fun_prop


-- @@ L671-673 verbatim
/-- jacobiF is holomorphic (MDifferentiable) since jacobiG is -/
lemma jacobi_f_MDifferentiable : MDiff jacobiF := by
  unfold jacobiF; have _ := jacobi_g_MDifferentiable; fun_prop


-- @@ L675-676 verbatim
/-- jacobiFSIF is holomorphic -/
lemma jacobi_f_SIF_MDifferentiable : MDiff jacobiFSIF := jacobi_f_MDifferentiable


-- @@ L678-678 verbatim
end JacobiIdentity


-- @@ L680-684 verbatim
/-!
## Limits at infinity

We prove the limit of Θᵢ(z) and Hᵢ(z) as z tends to i∞. This is used to prove the Jacobi identity.
-/


-- @@ L686-749 verbatim
theorem jacobiTheta₂_half_mul_apply_tendsto_atImInfty :
    Tendsto (fun x : ℍ ↦ jacobiTheta₂ (x / 2) x) atImInfty (𝓝 2) := by
  simp_rw [jacobiTheta₂, jacobiTheta₂_term]
  convert tendsto_tsum_of_dominated_convergence
    (f := fun z (n : ℤ) ↦ cexp (2 * π * I * n * (z / 2) + π * I * n ^ 2 * z))
    (𝓕 := atImInfty)
    (g := Set.indicator {-1, 0} 1)
    (bound := fun n : ℤ ↦ rexp (π / 4) * rexp (-π * ((n : ℝ) + 1 / 2) ^ 2)) ?_ ?_ ?_
  · simp [← tsum_subtype]
  · -- TODO: merge this with proof of isBoundedAtImInfty_H₂
    apply summable_ofReal.mp
    have (n : ℤ) := jacobiTheta₂_rel_aux n 1
    simp_rw [mul_one] at this
    simp_rw [ofReal_mul, this, ← smul_eq_mul]
    apply Summable.const_smul
    apply Summable.const_smul
    rw [summable_jacobiTheta₂_term_iff]
    simp
  · intro n
    have : n = -1 ∨ n = 0 ∨ n ∉ ({-1, 0} : Set ℤ) := by
      rw [Set.mem_insert_iff, Set.mem_singleton_iff]
      tauto
    rcases this with (rfl | rfl | hn) <;> ring_nf
    · simp
    · simp
    · simp only [hn, not_false_eq_true, Set.indicator_of_notMem]
      apply tendsto_zero_iff_norm_tendsto_zero.mpr
      have h₁ (n : ℤ) (z : ℂ) : (π * I * n * z + π * I * n ^ 2 * z) = π * (n + n ^ 2) * z * I := by
        ring_nf
      have h_base' : rexp (-π) ^ ((n : ℝ) + n ^ 2) < 1 := by
        apply Real.rpow_lt_one
        · positivity
        · apply Real.exp_lt_one_iff.mpr (by simp only [Left.neg_neg_iff]; positivity)
        convert_to 0 < ((n * (n + 1) : ℤ) : ℝ)
        · push_cast
          ring_nf
        · apply Int.cast_pos.mpr
          by_cases hn' : 0 < n
          · apply mul_pos hn' (by omega)
          · rw [Set.mem_insert_iff, Set.mem_singleton_iff] at hn
            exact mul_pos_of_neg_of_neg (by omega) (by omega)
      simp_rw [h₁, norm_exp_mul_I, mul_assoc, im_ofReal_mul, ← Int.cast_pow, ← Int.cast_add,
        ← ofReal_intCast, im_ofReal_mul, ← mul_assoc, Int.cast_add, Int.cast_pow, ← neg_mul,
        Real.exp_mul, coe_im]
      refine (tendsto_rpow_atTop_of_base_lt_one _ ?_ h_base').comp tendsto_im_atImInfty
      exact neg_one_lt_zero.trans (by positivity)
  · rw [eventually_atImInfty]
    use 1
    intro z hz k
    simp_rw [← Real.exp_add]
    ring_nf
    trans ‖cexp (((π * k + π * k ^ 2 : ℝ) * z) * I)‖
    · apply le_of_eq
      simpa [add_mul] using by ring_nf
    · rw [norm_exp_mul_I, im_ofReal_mul]
      have (n : ℤ) : 0 ≤ (n : ℝ) ^ 2 + n := by
        nth_rw 2 [← mul_one n]
        rw [sq, Int.cast_mul, Int.cast_one, ← mul_add]
        rcases lt_trichotomy (-1) n with (hn | rfl | hn)
        · apply mul_nonneg <;> norm_cast; omega
        · norm_num
        · apply mul_nonneg_of_nonpos_of_nonpos <;> norm_cast <;> omega
      simpa using le_mul_of_one_le_right
        (by rw [← mul_add, add_comm]; exact mul_nonneg Real.pi_nonneg (this k)) hz


-- @@ L751-758 verbatim
private theorem summable_ofReal_exp_neg_pi_sq :
    Summable (fun n : ℤ ↦ ((rexp (-π * n ^ 2) : ℝ) : ℂ)) := by
  have := (summable_jacobiTheta₂_term_iff 0 I).mpr (by simp)
  rw [← summable_norm_iff, ← summable_ofReal] at this
  simp_rw [jacobiTheta₂_term, mul_zero, zero_add, mul_right_comm _ I, mul_assoc, ← sq, I_sq,
    mul_neg_one, norm_exp, re_ofReal_mul, neg_re, mul_neg, ← neg_mul, ← ofReal_intCast,
    ← ofReal_pow, ofReal_re] at this
  exact this


-- @@ L760-780 verbatim
theorem jacobiTheta₂_zero_apply_tendsto_atImInfty :
    Tendsto (fun x : ℍ ↦ jacobiTheta₂ 0 x) atImInfty (𝓝 1) := by
  simp_rw [jacobiTheta₂, jacobiTheta₂_term, mul_zero, zero_add]
  convert tendsto_tsum_of_dominated_convergence
    (f := fun (z : ℍ) (n : ℤ) ↦ cexp (π * I * n ^ 2 * z))
    (𝓕 := atImInfty)
    (g := fun k ↦ if k = 0 then 1 else 0)
    (bound := fun n : ℤ ↦ rexp (-π * n ^ 2)) ?_ ?_ ?_
  · simp
  · exact summable_ofReal.mp summable_ofReal_exp_neg_pi_sq
  · intro k
    split_ifs with hk
    · simp_all
    · rw [tendsto_zero_iff_norm_tendsto_zero]
      simp_rw [mul_right_comm _ I, norm_exp_mul_I, mul_assoc, im_ofReal_mul, ← ofReal_intCast,
        ← ofReal_pow, im_ofReal_mul, ← mul_assoc]
      simpa using tendsto_im_atImInfty.const_mul_atTop (by positivity)
  · rw [eventually_atImInfty]
    use 1, fun z hz k ↦ ?_
    simp_rw [mul_right_comm _ I, norm_exp_mul_I]
    simpa [← ofReal_intCast, ← ofReal_pow] using le_mul_of_one_le_right (by positivity) hz


-- @@ L782-810 verbatim
theorem jacobiTheta₂_half_apply_tendsto_atImInfty :
    Tendsto (fun x : ℍ ↦ jacobiTheta₂ (1 / 2 : ℂ) x) atImInfty (𝓝 1) := by
  have hnorm (z : ℍ) (k : ℤ) :
      ‖cexp (2 * π * I * k * (1 / 2 : ℂ) + π * I * k ^ 2 * z)‖ = rexp (-π * k ^ 2 * z.im) := by
    simpa [jacobiTheta₂_term, coe_im] using
      (norm_jacobiTheta₂_term k (1 / 2 : ℂ) (z : ℂ))
  simp_rw [jacobiTheta₂, jacobiTheta₂_term]
  convert tendsto_tsum_of_dominated_convergence
    (f := fun (z : ℍ) (n : ℤ) ↦ cexp (2 * π * I * n * (1 / 2 : ℂ) + π * I * n ^ 2 * z))
    (𝓕 := atImInfty)
    (g := fun k ↦ if k = 0 then 1 else 0)
    (bound := fun n : ℤ ↦ rexp (-π * n ^ 2)) ?_ ?_ ?_
  · simp
  · exact summable_ofReal.mp summable_ofReal_exp_neg_pi_sq
  · intro k
    split_ifs with hk
    · simp_all
    · rw [tendsto_zero_iff_norm_tendsto_zero]
      simp_rw [hnorm]
      have hk2_pos : 0 < (k : ℝ) ^ 2 := sq_pos_of_ne_zero (Int.cast_ne_zero.mpr hk)
      exact (Real.tendsto_exp_atBot).comp
        (tendsto_im_atImInfty.const_mul_atTop_of_neg (by nlinarith [Real.pi_pos, hk2_pos]))
  · rw [eventually_atImInfty]
    use 1, fun z hz k ↦ ?_
    rw [hnorm]
    have hcoef_nonpos : (-π * (k : ℝ) ^ 2) ≤ 0 := by nlinarith [Real.pi_pos, sq_nonneg (k : ℝ)]
    have hmul : (-π * (k : ℝ) ^ 2) * z.im ≤ (-π * (k : ℝ) ^ 2) * 1 := by
      exact mul_le_mul_of_nonpos_left hz hcoef_nonpos
    simpa using Real.exp_le_exp.mpr hmul


-- @@ L812-824 verbatim
theorem Θ₂_tendsto_atImInfty : Tendsto Θ₂ atImInfty (𝓝 0) := by
  rw [funext Θ₂_as_jacobiTheta₂, ← zero_mul (2 : ℂ)]
  refine Tendsto.mul ?_ jacobiTheta₂_half_mul_apply_tendsto_atImInfty
  apply tendsto_zero_iff_norm_tendsto_zero.mpr
  -- simp_rw directly below fails
  have (z : ℍ) : ‖cexp (π * I * z / 4)‖ = rexp (-π * z.im / 4) := by
    rw [mul_right_comm, mul_div_right_comm, norm_exp_mul_I]
    simp [neg_div]
  simp_rw [this]
  exact (Real.tendsto_exp_atBot).comp <|
    -- TODO: tendsto_div_const_atBot_of_pos and its friends should be aliased under Tendsto.
    (tendsto_div_const_atBot_of_pos zero_lt_four).mpr
      (tendsto_im_atImInfty.const_mul_atTop_of_neg (neg_lt_zero.mpr Real.pi_pos))


-- @@ L826-827 verbatim
theorem Θ₃_tendsto_atImInfty : Tendsto Θ₃ atImInfty (𝓝 1) := by
  simpa [funext Θ₃_as_jacobiTheta₂] using jacobiTheta₂_zero_apply_tendsto_atImInfty


-- @@ L829-830 verbatim
theorem Θ₄_tendsto_atImInfty : Tendsto Θ₄ atImInfty (𝓝 1) := by
  simpa [funext Θ₄_as_jacobiTheta₂] using jacobiTheta₂_half_apply_tendsto_atImInfty


-- @@ L832-834 verbatim
theorem H₂_tendsto_atImInfty : Tendsto H₂ atImInfty (𝓝 0) := by
  convert Θ₂_tendsto_atImInfty.pow 4
  all_goals first | rfl | norm_num


-- @@ L836-838 verbatim
theorem H₃_tendsto_atImInfty : Tendsto H₃ atImInfty (𝓝 1) := by
  convert Θ₃_tendsto_atImInfty.pow 4
  all_goals first | rfl | norm_num


-- @@ L840-842 verbatim
theorem H₄_tendsto_atImInfty : Tendsto H₄ atImInfty (𝓝 1) := by
  convert Θ₄_tendsto_atImInfty.pow 4
  all_goals first | rfl | norm_num


-- @@ L844-849 verbatim
/-!
## Jacobi identity proof

We prove that g := H₂ + H₄ - H₃ → 0 at i∞, hence f := g² → 0.
Combined with the dimension vanishing for weight 4 cusp forms, this proves the Jacobi identity.
-/


-- @@ L851-856 verbatim
/-- The function g := H₂ + H₄ - H₃ tends to 0 at i∞.
    Since H₂ → 0, H₃ → 1, H₄ → 1, we have g → 0 + 1 - 1 = 0. -/
theorem jacobi_g_tendsto_atImInfty : Tendsto jacobiG atImInfty (𝓝 0) := by
  convert (H₂_tendsto_atImInfty.add H₄_tendsto_atImInfty).sub H₃_tendsto_atImInfty using 1
  · ext x; rfl
  · norm_num


-- @@ L858-862 verbatim
/-- The function f := g² tends to 0 at i∞. -/
theorem jacobi_f_tendsto_atImInfty : Tendsto jacobiF atImInfty (𝓝 0) := by
  convert jacobi_g_tendsto_atImInfty.pow 2 using 1
  · ext x; rfl
  · norm_num


-- @@ L864-866 expanded
private noncomputable def jacobi_f_CF : CuspForm (CongruenceSubgroup.Gamma 1) 4 :=
  cuspFormOfSIFTendstoZero jacobiFSIF jacobi_f_SIF_MDifferentiable jacobi_f_tendsto_atImInfty


-- @@ L868-871 verbatim
/-- jacobiF = 0 by dimension argument: weight-4 cusp forms vanish. -/
theorem jacobi_f_eq_zero : jacobiF = 0 :=
  congr_arg (·.toFun)
    (rank_zero_iff_forall_zero.mp (cuspform_weight_lt_12_zero 4 (by norm_num)) jacobi_f_CF)


-- @@ L873-876 verbatim
/-- jacobiG = 0 as a function (from g² = 0) -/
theorem jacobi_g_eq_zero : jacobiG = 0 := by
  ext z
  simpa [jacobiF] using congr_fun jacobi_f_eq_zero z


-- @@ L878-880 verbatim
/-- Jacobi identity: H₂ + H₄ = H₃ (Blueprint Lemma 6.41) -/
theorem jacobi_identity : H₂ + H₄ = H₃ := by
  ext z; simpa [jacobiG, sub_eq_zero] using congr_fun jacobi_g_eq_zero z


-- @@ L882-882 verbatim
private noncomputable def theta_prod : ℍ → ℂ := H₂ * H₃ * H₄


-- @@ L884-888 verbatim
private lemma theta_prod_S_action : (theta_prod ∣[(6 : ℤ)] S) = -theta_prod := by
  simp only [theta_prod, show (6 : ℤ) = (2 + 2) + 2 from by norm_num,
    mul_slash_SL2 (2 + 2) 2 S _ _, mul_slash_SL2 2 2 S _ _,
    H₂_S_action, H₃_S_action, H₄_S_action]
  ext z; simp [Pi.mul_apply, Pi.neg_apply]; ring


-- @@ L890-894 verbatim
private lemma theta_prod_T_action : (theta_prod ∣[(6 : ℤ)] T) = -theta_prod := by
  simp only [theta_prod, show (6 : ℤ) = (2 + 2) + 2 from by norm_num,
    mul_slash_SL2 (2 + 2) 2 T _ _, mul_slash_SL2 2 2 T _ _,
    H₂_T_action, H₃_T_action, H₄_T_action]
  ext z; simp [Pi.mul_apply, Pi.neg_apply]; ring


-- @@ L896-896 verbatim
private noncomputable def theta_prod_sq : ℍ → ℂ := fun z => (H₂ z * H₃ z * H₄ z) ^ 2


-- @@ L898-899 verbatim
private lemma theta_prod_sq_eq_mul : theta_prod_sq = theta_prod * theta_prod := by
  ext z; simp [theta_prod_sq, theta_prod, sq, Pi.mul_apply]


-- @@ L901-903 verbatim
private lemma theta_prod_sq_S_action : (theta_prod_sq ∣[(12 : ℤ)] S) = theta_prod_sq := by
  rw [theta_prod_sq_eq_mul, show (12 : ℤ) = 6 + 6 from by norm_num,
    mul_slash_SL2 6 6 S _ _, theta_prod_S_action, neg_mul_neg]


-- @@ L905-907 verbatim
private lemma theta_prod_sq_T_action : (theta_prod_sq ∣[(12 : ℤ)] T) = theta_prod_sq := by
  rw [theta_prod_sq_eq_mul, show (12 : ℤ) = 6 + 6 from by norm_num,
    mul_slash_SL2 6 6 T _ _, theta_prod_T_action, neg_mul_neg]


-- @@ L909-911 verbatim
private lemma theta_prod_sq_MDifferentiable : MDiff theta_prod_sq := by
  change MDiff (fun z => (H₂ z * H₃ z * H₄ z) ^ 2)
  exact ((H₂_SIF_MDifferentiable.mul H₃_SIF_MDifferentiable).mul H₄_SIF_MDifferentiable).pow 2


-- @@ L913-916 verbatim
private lemma theta_prod_sq_tendsto_atImInfty : Tendsto theta_prod_sq atImInfty (𝓝 0) := by
  change Tendsto (fun z => (H₂ z * H₃ z * H₄ z) ^ 2) atImInfty (𝓝 0)
  have : (0 : ℂ) = (0 * 1 * 1) ^ 2 := by norm_num
  rw [this]; exact ((H₂_tendsto_atImInfty.mul H₃_tendsto_atImInfty).mul H₄_tendsto_atImInfty).pow 2


-- @@ L918-922 verbatim
private noncomputable def theta_prod_sq_SIF :
    SlashInvariantForm (CongruenceSubgroup.Gamma 1) 12 where
  toFun := theta_prod_sq
  slash_action_eq' := slashaction_generators_GL2R theta_prod_sq 12
    theta_prod_sq_S_action theta_prod_sq_T_action


-- @@ L924-926 verbatim
private noncomputable def theta_prod_sq_CF : CuspForm (CongruenceSubgroup.Gamma 1) 12 :=
  cuspFormOfSIFTendstoZero theta_prod_sq_SIF theta_prod_sq_MDifferentiable
    theta_prod_sq_tendsto_atImInfty


-- @@ L928-929 verbatim
private lemma theta_prod_sq_CF_apply (z : ℍ) :
    theta_prod_sq_CF z = theta_prod_sq z := rfl


-- @@ L931-941 verbatim
/-- Transport a level-one modular-form rank fact from `𝒮ℒ` to the (definitionally distinct)
group `↑Γ(1)`; `subst` on a generalized group avoids the `IsArithmetic`/`HasDetOne` motive issue. -/
private lemma rank_modularForm_gammaOne {k : ℤ} {r : Cardinal}
    (h : Module.rank ℂ (ModularForm 𝒮ℒ k) = r) :
    Module.rank ℂ (ModularForm (CongruenceSubgroup.Gamma 1) k) = r := by
  have key : ∀ (G : Subgroup (GL (Fin 2) ℝ)) [G.IsArithmetic] [G.HasDetOne],
      G = (Subgroup.map (Matrix.SpecialLinearGroup.mapGL ℝ) (CongruenceSubgroup.Gamma 1)) →
      Module.rank ℂ (ModularForm G k) = r →
      Module.rank ℂ (ModularForm (CongruenceSubgroup.Gamma 1) k) = r := by
    intro G _ _ hh hrank; subst hh; exact hrank
  exact key 𝒮ℒ CongruenceSubgroup.Gamma_one_coe_eq_SL.symm h


-- @@ L943-948 verbatim
private lemma finrank_cuspform_12 :
    Module.finrank ℂ (CuspForm (CongruenceSubgroup.Gamma 1) 12) = 1 := by
  apply Module.finrank_eq_of_rank_eq
  rw [LinearEquiv.rank_eq (CuspFormsIsoModforms 12),
    show (12 - 12 : ℤ) = 0 from by norm_num]
  exact rank_modularForm_gammaOne ModularForm.levelOne_weight_zero_rank_one


-- @@ L950-952 verbatim
private lemma theta_prod_sq_proportional :
    ∃ c : ℂ, c • Delta = theta_prod_sq_CF :=
  (finrank_eq_one_iff_of_nonzero' Delta Delta_ne_zero).mp finrank_cuspform_12 theta_prod_sq_CF


-- @@ L954-965 verbatim
private lemma H₂_div_exp_tendsto :
    Tendsto (fun z : ℍ ↦ H₂ z / cexp (↑π * I * ↑z)) atImInfty (nhds 16) := by
  have h_eq : ∀ z : ℍ, H₂ z / cexp (↑π * I * ↑z) = (jacobiTheta₂ (↑z / 2) ↑z) ^ 4 := by
    intro z
    rw [H₂, Θ₂_as_jacobiTheta₂, mul_pow]
    have he : cexp (↑π * I * ↑z / 4) ^ 4 = cexp (↑π * I * ↑z) := by
      rw [← Complex.exp_nat_mul]; congr 1; ring
    rw [he, mul_div_cancel_left₀ _ (Complex.exp_ne_zero _)]
  simp_rw [h_eq]
  have h16 : (2 : ℂ) ^ 4 = (16 : ℂ) := by norm_num
  rw [← h16]
  exact jacobiTheta₂_half_mul_apply_tendsto_atImInfty.pow 4


-- @@ L967-1006 verbatim
lemma Delta_eq_H₂_H₃_H₄ (τ : ℍ) :
    Delta τ = ((H₂ τ) * (H₃ τ) * (H₄ τ))^2 / (256 : ℂ) := by
  obtain ⟨c, hc⟩ := theta_prod_sq_proportional
  have hc_pw : ∀ z : ℍ, c * Delta z = theta_prod_sq z := by
    intro z
    have h := DFunLike.congr_fun hc z
    rw [show (c • Delta : CuspForm _ _) z = c * Delta z from rfl] at h
    rwa [theta_prod_sq_CF_apply] at h
  have hc_eq : c = 256 := by
    have hD_asymp : Tendsto (fun z : ℍ ↦ Delta z / cexp (2 * ↑π * I * ↑z))
        atImInfty (nhds 1) := by
      have h_eq : ∀ z : ℍ, Delta z / cexp (2 * ↑π * I * ↑z) =
          ∏' (n : ℕ), (1 - cexp (2 * ↑π * I * (↑n + 1) * ↑z)) ^ 24 := by
        intro z; rw [Delta_apply, Δ]
        rw [mul_div_cancel_left₀ _ (Complex.exp_ne_zero _)]
      simp_rw [h_eq]; exact Delta_boundedfactor
    have hP_asymp : Tendsto (fun z : ℍ ↦ theta_prod_sq z / cexp (2 * ↑π * I * ↑z))
        atImInfty (nhds 256) := by
      have h_rewrite : ∀ z : ℍ, theta_prod_sq z / cexp (2 * ↑π * I * ↑z) =
          (H₂ z / cexp (↑π * I * ↑z)) ^ 2 * (H₃ z) ^ 2 * (H₄ z) ^ 2 := by
        intro z
        have hq : cexp (2 * ↑π * I * ↑z) = cexp (↑π * I * ↑z) ^ 2 := by
          rw [← Complex.exp_nat_mul]; ring_nf
        simp only [theta_prod_sq]
        rw [hq]; field_simp
      simp_rw [h_rewrite]
      have : (256 : ℂ) = 16 ^ 2 * 1 ^ 2 * 1 ^ 2 := by norm_num
      rw [this]
      exact ((H₂_div_exp_tendsto.pow 2).mul (H₃_tendsto_atImInfty.pow 2)).mul
        (H₄_tendsto_atImInfty.pow 2)
    have h_eq_fns : ∀ z : ℍ, c * (Delta z / cexp (2 * ↑π * I * ↑z)) =
        theta_prod_sq z / cexp (2 * ↑π * I * ↑z) := by intro z; rw [← mul_div_assoc, hc_pw]
    have hc_lim : Tendsto (fun z : ℍ ↦ c * (Delta z / cexp (2 * ↑π * I * ↑z)))
        atImInfty (nhds c) := by have := hD_asymp.const_mul c; rwa [mul_one] at this
    exact tendsto_nhds_unique (hc_lim.congr h_eq_fns) hP_asymp
  have h := hc_pw τ
  rw [hc_eq] at h
  simp only [theta_prod_sq] at h
  rw [eq_div_iff (show (256 : ℂ) ≠ 0 by norm_num), mul_comm]
  exact h


-- @@ L1008-1012 verbatim
/-!
## Imaginary Axis Properties

Properties of theta functions when restricted to the positive imaginary axis z = I*t.
-/


-- @@ L1014-1014 verbatim
section ImagAxisProperties


-- @@ L1016-1028 verbatim
/-- Each term Θ₂Term n (I*t) has zero imaginary part for t > 0. -/
lemma Θ₂_term_imag_axis_real (n : ℤ) (t : ℝ) (ht : 0 < t) :
    (Θ₂Term n ⟨I * t, by simp [ht]⟩).im = 0 := by
  unfold Θ₂Term
  change (cexp (Real.pi * I * ((n : ℂ) + 1 / 2) ^ 2 * (I * t))).im = 0
  have hexpr : Real.pi * I * ((n : ℂ) + 1 / 2) ^ 2 * (I * ↑t) =
      (-(Real.pi * ((n : ℝ) + 1/2) ^ 2 * t) : ℝ) := by
    push_cast
    ring_nf
    simp only [I_sq]
    ring
  rw [hexpr]
  exact exp_ofReal_im _


-- @@ L1030-1033 verbatim
/-- `im` distributes over tsum when each term has zero imaginary part. -/
lemma Complex.im_tsum_eq_zero_of_im_eq_zero (f : ℤ → ℂ)
    (hf : Summable f) (him : ∀ n, (f n).im = 0) :
    (∑' n : ℤ, f n).im = 0 := by simp [Complex.im_tsum hf, him]


-- @@ L1035-1045 verbatim
/-- Θ₂(I*t) has zero imaginary part for t > 0. -/
lemma Θ₂_imag_axis_real (t : ℝ) (ht : 0 < t) :
    (Θ₂ ⟨I * t, by simp [ht]⟩).im = 0 := by
  unfold Θ₂
  let z : ℍ := ⟨I * t, by simp [ht]⟩
  have hsum : Summable fun n : ℤ => Θ₂Term n z := by
    simp_rw [Θ₂_term_as_jacobiTheta₂_term]
    apply Summable.mul_left
    rw [summable_jacobiTheta₂_term_iff]
    exact z.im_pos
  exact Complex.im_tsum_eq_zero_of_im_eq_zero _ hsum (fun n => Θ₂_term_imag_axis_real n t ht)


-- @@ L1047-1049 verbatim
/-- `(-1 : ℂ)^n` has zero imaginary part for any integer n. -/
lemma neg_one_zpow_im_eq_zero (n : ℤ) : ((-1 : ℂ) ^ n).im = 0 := by
  rcases Int.even_or_odd n with hn | hn <;> (rw [hn.neg_one_zpow]; simp)


-- @@ L1051-1062 verbatim
/-- Each term Θ₄Term n (I*t) has zero imaginary part for t > 0. -/
lemma Θ₄_term_imag_axis_real (n : ℤ) (t : ℝ) (ht : 0 < t) :
    (Θ₄Term n ⟨I * t, by simp [ht]⟩).im = 0 := by
  unfold Θ₄Term
  change ((-1 : ℂ) ^ n * cexp (Real.pi * I * (n : ℂ) ^ 2 * (I * t))).im = 0
  have hexpr : Real.pi * I * (n : ℂ) ^ 2 * (I * t) =
      (-(Real.pi * (n : ℝ) ^ 2 * t) : ℝ) := by
    push_cast
    ring_nf
    simp_all
  rw [hexpr]
  simp only [Complex.mul_im, neg_one_zpow_im_eq_zero n, exp_ofReal_im, mul_zero, zero_mul, add_zero]


-- @@ L1064-1073 verbatim
/-- Θ₄(I*t) has zero imaginary part for t > 0. -/
lemma Θ₄_imag_axis_real (t : ℝ) (ht : 0 < t) :
    (Θ₄ ⟨I * t, by simp [ht]⟩).im = 0 := by
  unfold Θ₄
  let z : ℍ := ⟨I * t, by simp [ht]⟩
  have hsum : Summable fun n : ℤ => Θ₄Term n z := by
    simp_rw [Θ₄_term_as_jacobiTheta₂_term]
    rw [summable_jacobiTheta₂_term_iff]
    exact z.im_pos
  exact Complex.im_tsum_eq_zero_of_im_eq_zero _ hsum (fun n => Θ₄_term_imag_axis_real n t ht)


-- @@ L1075-1085 verbatim
/--
`H₂(it)` is real for all `t > 0`.
Blueprint: Follows from the q-expansion having real coefficients.
Proof strategy: H₂ = Θ₂^4 where Θ₂(it) = ∑ₙ exp(-π(n+1/2)²t) is a sum of real
exponentials.
-/
@[fun_prop]
theorem H₂_imag_axis_real : ResToImagAxis.Real H₂ := by
  intro t ht
  simp only [Function.resToImagAxis, ResToImagAxis, ht, ↓reduceDIte, H₂]
  exact Complex.im_pow_eq_zero_of_im_eq_zero (Θ₂_imag_axis_real t ht) 4


-- @@ L1087-1100 verbatim
/-- Each term Θ₂Term n (I*t) has positive real part equal to exp(-π(n+1/2)²t) for t > 0. -/
lemma Θ₂_term_imag_axis_re (n : ℤ) (t : ℝ) (ht : 0 < t) :
    (Θ₂Term n ⟨I * t, by simp [ht]⟩).re =
      Real.exp (-Real.pi * ((n : ℝ) + 1/2) ^ 2 * t) := by
  unfold Θ₂Term
  change (cexp (Real.pi * I * ((n : ℂ) + 1 / 2) ^ 2 * (I * t))).re = _
  have hexpr : Real.pi * I * ((n : ℂ) + 1 / 2) ^ 2 * (I * ↑t) =
      (-(Real.pi * ((n : ℝ) + 1/2) ^ 2 * t) : ℝ) := by
    push_cast
    ring_nf
    simp only [I_sq]
    ring
  rw [hexpr, Complex.exp_ofReal_re]
  ring_nf


-- @@ L1102-1106 verbatim
/-- Each term Θ₂Term n (I*t) has positive real part for t > 0. -/
lemma Θ₂_term_imag_axis_re_pos (n : ℤ) (t : ℝ) (ht : 0 < t) :
    0 < (Θ₂Term n ⟨I * t, by simp [ht]⟩).re := by
  rw [Θ₂_term_imag_axis_re n t ht]
  exact Real.exp_pos _


-- @@ L1108-1122 verbatim
/-- Θ₂(I*t) has positive real part for t > 0. -/
lemma Θ₂_imag_axis_re_pos (t : ℝ) (ht : 0 < t) :
    0 < (Θ₂ ⟨I * t, by simp [ht]⟩).re := by
  let z : ℍ := ⟨I * t, by simp [ht]⟩
  have hsum : Summable fun n : ℤ => Θ₂Term n z := by
    simp_rw [Θ₂_term_as_jacobiTheta₂_term]
    apply Summable.mul_left
    rw [summable_jacobiTheta₂_term_iff]
    exact z.im_pos
  unfold Θ₂
  rw [Complex.re_tsum hsum]
  have hsum_re : Summable fun n : ℤ => (Θ₂Term n z).re := by
    obtain ⟨x, hx⟩ := hsum; exact ⟨x.re, Complex.hasSum_re hx⟩
  exact Summable.tsum_pos hsum_re (fun n => le_of_lt (Θ₂_term_imag_axis_re_pos n t ht))
    0 (Θ₂_term_imag_axis_re_pos 0 t ht)


-- @@ L1124-1143 verbatim
/--
`H₂(it) > 0` for all `t > 0`.
Blueprint: Lemma 6.43 - H₂ is positive on the imaginary axis.
Proof strategy: Each term exp(-π(n+1/2)²t) > 0, so Θ₂(it) > 0, hence H₂ = Θ₂^4 > 0.
-/
@[fun_prop]
theorem H₂_imag_axis_pos : ResToImagAxis.Pos H₂ := by
  constructor
  · exact H₂_imag_axis_real
  · intro t ht
    simp only [Function.resToImagAxis, ResToImagAxis, ht, ↓reduceDIte, H₂]
    have hΘ₂_im := Θ₂_imag_axis_real t ht
    have hΘ₂_re_pos := Θ₂_imag_axis_re_pos t ht
    have hpow : (Θ₂ ⟨I * t, by simp [ht]⟩ ^ 4).re =
        (Θ₂ ⟨I * t, by simp [ht]⟩).re ^ 4 := by
      set z := Θ₂ ⟨I * t, by simp [ht]⟩ with hz_def
      have hz_eq : z = (z.re : ℂ) := Complex.ext (by simp) (by simp [hΘ₂_im])
      rw [hz_eq]
      norm_cast
    simp_all


-- @@ L1145-1153 verbatim
/--
`H₄(it)` is real for all `t > 0`.
Blueprint: Corollary 6.43 - follows from Θ₄ being real on the imaginary axis.
-/
@[fun_prop]
theorem H₄_imag_axis_real : ResToImagAxis.Real H₄ := by
  intro t ht
  simp only [Function.resToImagAxis, ResToImagAxis, ht, ↓reduceDIte, H₄]
  exact Complex.im_pow_eq_zero_of_im_eq_zero (Θ₄_imag_axis_real t ht) 4


-- @@ L1155-1193 verbatim
/--
`H₄(it) > 0` for all `t > 0`.
Blueprint: Corollary 6.43 - H₄ is positive on the imaginary axis.

Proof strategy: Use the modular S-transformation relating H₄ and H₂.
From H₄_S_action: (H₄ ∣[2] S) = -H₂
From ResToImagAxis.SlashActionS: relates values at t and 1/t.
This gives H₂(i/t) = t² * H₄(it), so H₄(it) > 0 follows from H₂(i/t) > 0.
-/
@[fun_prop]
theorem H₄_imag_axis_pos : ResToImagAxis.Pos H₄ := by
  constructor
  · exact H₄_imag_axis_real
  · intro t ht
    have h1t_pos : 0 < 1 / t := one_div_pos.mpr ht
    have hSlash := ResToImagAxis.SlashActionS H₄ 2 h1t_pos
    rw [H₄_S_action] at hSlash
    have hI_neg2 : (I : ℂ) ^ (-2 : ℤ) = -1 := by
      change (I ^ 2)⁻¹ = -1
      rw [I_sq]; norm_num
    have h1t_neg2 : ((1 / t : ℝ) : ℂ) ^ (-2 : ℤ) = (t : ℂ) ^ 2 := by
      have ht_ne : (t : ℂ) ≠ 0 := ofReal_ne_zero.mpr (ne_of_gt ht)
      simp only [one_div, ofReal_inv, _root_.zpow_neg]
      field_simp
    have h1_div_1t : 1 / (1 / t) = t := by field_simp
    have hNeg : (-H₂).resToImagAxis (1 / t) = -(H₂.resToImagAxis (1 / t)) := by
      simp only [Function.resToImagAxis_apply, ResToImagAxis, h1t_pos, ↓reduceDIte, Pi.neg_apply]
    rw [hNeg, hI_neg2, h1t_neg2, h1_div_1t] at hSlash
    have hEq : H₂.resToImagAxis (1 / t) = (t : ℂ) ^ 2 * H₄.resToImagAxis t := by
      simp_all
    have hH₂_pos := H₂_imag_axis_pos.2 (1 / t) h1t_pos
    have hH₄_real := H₄_imag_axis_real t ht
    have hProd_re : ((t : ℂ) ^ 2 * H₄.resToImagAxis t).re =
        (t : ℝ) ^ 2 * (H₄.resToImagAxis t).re := by
      simp only [Function.resToImagAxis_apply, ResToImagAxis, ht, ↓reduceDIte] at hH₄_real ⊢
      simp only [sq, Complex.mul_re, ofReal_re, ofReal_im, zero_mul, sub_zero]
      simp_all
    rw [hEq, hProd_re, mul_comm] at hH₂_pos
    exact pos_of_mul_pos_left hH₂_pos (le_of_lt (sq_pos_of_pos ht))


-- @@ L1195-1195 verbatim
end ImagAxisProperties
