/-
Copyright (c) 2026 Matevz Miščič, Maša Žaucer, Job Petrovčič. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matevz Miščič, Maša Žaucer, Job Petrovčič
-/
module

public import LeanPool.ArtinWedderburn.PrimeRing
public import LeanPool.ArtinWedderburn.NonUnitalToUnital
public import LeanPool.ArtinWedderburn.Auxiliary
public import Mathlib.Data.Matrix.Basic
public import Mathlib.RingTheory.Artinian.Defs
public import Mathlib.Tactic.NoncommRing
import Mathlib.Data.Rat.Cast.Order
import Mathlib.Tactic.ContinuousFunctionalCalculus
import Mathlib.Tactic.NormNum.Abs
import Mathlib.Tactic.NormNum.DivMod
import Mathlib.Tactic.NormNum.OfScientific


-- @@ L20-28 verbatim
/-!
# Corner subrings `eRe`

For an idempotent `e : R`, the *corner subring* `eRe` is the non-unital
subring of `R` consisting of elements of the form `e * x * e`. It becomes a
(unital) ring with `1 = e`. This file develops the basic theory: containment,
artinianness, primality, lifts and pushes of ideals, and isomorphisms between
corner subrings of equal idempotents.
-/


-- @@ L30-30 verbatim
@[expose] public section


-- @@ L32-32 verbatim
namespace LeanPool.ArtinWedderburn


-- @@ L34-34 verbatim
variable {R : Type*} [Ring R]

-- @@ L35-35 verbatim
variable {e : R}


-- @@ L37-40 verbatim
/-- The carrier set of the corner ring `eRe`, defined as `{e * x * e | x : R}`. -/
def CornerRingSet (e : R) : Set R := bothMul e e

-- an element x of R is in the corner ring if and only if x = e * x * e

-- @@ L41-50 verbatim
theorem corner_ring_set_mem {x : R} (idem_e : IsIdempotentElem e) :
    x ∈ CornerRingSet e ↔ x = e * x * e := by
  constructor
  · rintro ⟨x, rfl⟩
    rw [mul_assoc, mul_assoc, mul_assoc, mul_assoc, idem_e]
    rw [← mul_assoc e x e, ← mul_assoc e (e * x) e, ← mul_assoc e e x, idem_e]
  · intro hx
    exact ⟨x, hx⟩

-- an element is in the set of corner ring if it can be written as e * y * e for some y

-- @@ L51-52 verbatim
theorem x_in_corner_x_eq_e_y_e {x : R} (h : x ∈ CornerRingSet e) :
    ∃ (y : R), x = e * y * e := h


-- @@ L54-74 verbatim
/-- The nonunital corner subring `eRe` as a `NonUnitalSubring`. -/
@[reducible]
def CornerSubringNonUnital (e : R) : NonUnitalSubring R where
  carrier := bothMul e e
  zero_mem' := ⟨0, by simp⟩
  add_mem' := by
    rintro x y ⟨r, hr⟩ ⟨s, hs⟩
    use r + s
    rw [hr, hs]
    noncomm_ring
  neg_mem' := by
    rintro x ⟨r, hr⟩
    use -r
    simp_all
  mul_mem' := by
    rintro x y ⟨r, hr⟩ ⟨s, hs⟩
    use r * e * e * s
    rw [hr, hs]
    noncomm_ring

-- definition unfolding theorems

-- @@ L75-75 verbatim
theorem corner_ring_carrier : (CornerSubringNonUnital e).carrier = bothMul e e := rfl


-- @@ L77-79 verbatim
theorem el_in_corner_ring (x : R) : x ∈ bothMul e e ↔ x ∈ CornerSubringNonUnital e := Iff.rfl

-- reducing corner subring equality to set equality

-- @@ L80-82 verbatim
theorem eq_carrier_eq_corner (x y : R) (h : bothMul x x = bothMul y y) :
    CornerSubringNonUnital x = CornerSubringNonUnital y := by
  simp_all


-- @@ L84-90 verbatim
/-- The corner subring `eRe` packaged as a `NonUnitalSubring`, tagged with the
proof that `e` is idempotent. The proof argument lets later constructions
attach the unital ring structure on `eRe`. -/
@[reducible]
def CornerSubring (idem_e : IsIdempotentElem e) : NonUnitalSubring R :=
  have : IsIdempotentElem e := idem_e
  CornerSubringNonUnital e


-- @@ L92-93 verbatim
theorem CornerSubringEq (idem_e idem_e' : IsIdempotentElem e) :
    CornerSubring idem_e = CornerSubring idem_e' := rfl


-- @@ L95-95 verbatim
variable (idem_e : IsIdempotentElem e)


-- @@ L97-99 verbatim
variable {x : R}

-- an element is in the corner subring if it stays the same when it is multiplied by e on both sides

-- @@ L100-106 verbatim
theorem subring_mem_idem : x ∈ CornerSubring idem_e ↔ x = e * x * e := by
  rw [← corner_ring_set_mem]
  · rfl
  · exact idem_e

-- if x and y are in the corner ring, then so is x * w * y
-- note that e does not have to be an idempotent element

-- @@ L107-115 verbatim
theorem corner_ring_both_mul_mem (x w y : R) (hx : x ∈ CornerSubringNonUnital e)
    (hy : y ∈ CornerSubringNonUnital e) : (x * w * y) ∈ CornerSubringNonUnital e := by
  have ⟨u, hx⟩ := hx
  have ⟨v, hy⟩ := hy
  use u * e * w * e * v
  rw [hx, hy]
  noncomm_ring

-- e is the left/right unit in the subring

-- @@ L116-119 verbatim
theorem left_unit_mul (idem_e : IsIdempotentElem e) (h : x ∈ CornerSubringNonUnital e) :
    e * x = x := by
  rw [(corner_ring_set_mem idem_e).1 h]
  rw [← mul_assoc, ← mul_assoc, idem_e]


-- @@ L121-124 verbatim
theorem right_unit_mul (idem_e : IsIdempotentElem e) (h : x ∈ CornerSubringNonUnital e) :
    x * e = x := by
  rw [(corner_ring_set_mem idem_e).1 h]
  rw [mul_assoc, mul_assoc, idem_e, mul_assoc]


-- @@ L126-126 verbatim
open NonUnitalSubringClass


-- @@ L128-131 verbatim
/-- The non-unital ring instance on the corner subring `eRe`. -/
abbrev CornerSubringIsNonUNitalRing := toNonUnitalRing (CornerSubringNonUnital e)

-- inclusion homomorphism of the corner subring

-- @@ L132-135 verbatim
theorem corner_ring_hom (a b : CornerSubringNonUnital e) :
    (a * b : CornerSubringNonUnital e) = (a : R) * (b : R) := rfl

-- we can now start on adding the unit to the nonunital subring in the case when e is an idempotent

-- @@ L136-137 verbatim
instance CornerRingOne (idem_e : IsIdempotentElem e) : One (CornerSubring idem_e) :=
  ⟨e, by rw [subring_mem_idem idem_e]; rw [idem_e, idem_e]⟩


-- @@ L139-142 verbatim
theorem corner_ring_one (idem_e : IsIdempotentElem e) :
    (1 : CornerSubring idem_e) = e := rfl

-- e is the left/right unit but e is now written as 1

-- @@ L143-148 verbatim
theorem is_left_unit : ∀ (x : CornerSubring idem_e), 1 * x = x := by
  rw [Subtype.forall]
  intro a hx
  apply Subtype.ext
  simp only [NonUnitalSubring.val_mul]
  rw [corner_ring_one, left_unit_mul idem_e hx]


-- @@ L150-157 verbatim
theorem is_right_unit : ∀ (x : CornerSubring idem_e), x * 1 = x := by
  rw [Subtype.forall]
  intro a hx
  apply Subtype.ext
  simp only [NonUnitalSubring.val_mul]
  rw [corner_ring_one, right_unit_mul idem_e hx]

-- e is in the subring belonging to it

-- @@ L158-162 verbatim
lemma e_in_corner_ring : e ∈ (CornerSubring idem_e) := by
  rw [subring_mem_idem]
  rw [IsIdempotentElem.eq idem_e, IsIdempotentElem.eq idem_e]

-- the underlying set of the corner subring defined by 1 is the whole ring

-- @@ L163-171 verbatim
theorem both_mul_one_one_eq_R : bothMul (1 : R) 1 = ⊤ := by
  ext x
  constructor
  · simp_all
  · intro _
    use x
    noncomm_ring

-- if a nonunital subring's carrier is R it is isomorphic to R

-- @@ L172-183 verbatim
/-- A non-unital subring whose carrier is all of `R` is ring-isomorphic to `R`. -/
def topSubringEquivRing (S : NonUnitalSubring R) (h : S.carrier = ⊤) : S ≃+* R :=
  { toFun := fun a => a,
    invFun := fun a => ⟨a, by
      have ha : a ∈ S.carrier := by rw [h]; exact trivial
      exact ha⟩,
    left_inv := by intro a; simp
    right_inv := by intro a; simp
    map_mul' := by intro a b; simp
    map_add' := by intro a b; simp }

-- 1 R 1 is isomorphic to R

-- @@ L184-192 verbatim
/-- The corner subring of `1` is ring-isomorphic to the ambient ring `R`. -/
def isoCornerOne :
    CornerSubring ((IsIdempotentElem.one : IsIdempotentElem (1 : R))) ≃+* R := by
  apply topSubringEquivRing
  unfold CornerSubring
  rw [corner_ring_carrier]
  exact both_mul_one_one_eq_R

-- a nonzero element in the corner subring is nonzero in R

-- @@ L193-197 verbatim
lemma nonzero (x : CornerSubring idem_e) :
    (x : CornerSubring idem_e) ≠ 0 ↔ x.val ≠ 0 := by
  simp_all

-- nonzero elements produces a non-zero corner subring

-- @@ L198-201 verbatim
lemma e_nonzero_corner_nontrivial (R : Type*) [Ring R] {e : R} (idem_e : IsIdempotentElem e)
    (e_nonzero : e ≠ 0) : Nontrivial (CornerSubring idem_e) := by
  refine ⟨⟨⟨e, e_in_corner_ring idem_e⟩, 0, ?_⟩⟩
  exact (nonzero idem_e ⟨e, e_in_corner_ring idem_e⟩).mpr e_nonzero


-- @@ L203-204 verbatim
lemma eq_iff_val (x y z : CornerSubring idem_e) :
    (x + y).val = z.val ↔ x.val + y.val = z.val := Eq.congr_right rfl


-- @@ L206-212 verbatim
lemma e_x_e_in_corner : ∀ (x : R), e * x * e ∈ CornerSubring idem_e := by
  intro x
  rw [subring_mem_idem, eq_comm]
  calc _ = (e * e) * x * (e * e) := by noncomm_ring
        _ = e * x * e := by rw [idem_e]

-- The corner ring is a ring

-- @@ L213-216 verbatim
instance CornerRingIsRing (idem_e : IsIdempotentElem e) : Ring (CornerSubring idem_e) :=
  nonUnitalWEIsRing 1 (is_left_unit idem_e) (is_right_unit idem_e)

-- an element in the cornersubring of f (where f is in eRe) can be lifted to eRe

-- @@ L217-230 verbatim
/-- Lift an element of the corner subring `fRf` (where `f` lies in `eRe`) to an element of
the outer corner subring `eRe`. -/
def coercionToERe (e f : R) (idem_e : IsIdempotentElem e) (idem_f : IsIdempotentElem f)
    (f_mem : f ∈ CornerSubring idem_e) (x : CornerSubring idem_f) : CornerSubring idem_e := by
  refine ⟨x.val, ?_⟩
  have h : x.val ∈ bothMul e e := by
    let ⟨y, hy⟩ := f_mem
    let ⟨z, hz⟩ := x.property
    rw [hz, hy]
    refine ⟨y * e * z * e * y, ?_⟩
    noncomm_ring
  exact h

-- If eRe is a division ring then e is nonzero

-- @@ L231-243 verbatim
lemma corner_ring_division_e_nonzero
    (idem_e : IsIdempotentElem e) (heRe : IsDivisionRing (CornerSubring idem_e)) : e ≠ 0 := by
  by_contra he
  have ha : ∀ (a : R), e * a * e = 0 := fun a ↦ mul_eq_zero_of_right (e * a) he
  have h_zero : ∀ (x : CornerSubring idem_e), x = 0 := by
    intro ⟨x, hx⟩
    apply x_in_corner_x_eq_e_y_e at hx
    obtain ⟨y, hy⟩ := hx
    specialize ha y
    rw [ha] at hy
    exact (NonUnitalSubring.coe_eq_zero_iff (CornerSubring idem_e)).mp hy
  obtain ⟨⟨x, hx⟩, _⟩ := heRe
  exact hx (h_zero x)


-- @@ L245-258 verbatim
/-- If `e = f`, then the corner subrings `eRe` and `fRf` are ring-isomorphic via the
identity on carriers. -/
def eqElIsoCorner (e f : R) (idem_e : IsIdempotentElem e) (idem_f : IsIdempotentElem f)
    (e_eq_f : e = f) : (CornerSubring idem_e) ≃+* (CornerSubring idem_f) :=
  { toFun := fun x => ⟨x.val, by
      let ⟨y, hy⟩ := x.property
      have h : x = (f : R) * y * f := by rw [← e_eq_f]; exact hy
      exact ⟨y, h⟩⟩,
    invFun := fun x => ⟨x.val, by
      simp_all⟩,
    left_inv := fun _ => rfl,
    right_inv := fun _ => rfl,
    map_mul' := fun _ _ => rfl,
    map_add' := fun _ _ => rfl }


-- @@ L260-268 verbatim
/-- Equal idempotents induce a ring isomorphism between the matrix rings over their
corner subrings. -/
def equalElIsoMatrixRings' (e f : R) (idem_e : IsIdempotentElem e) (idem_f : IsIdempotentElem f)
    (e_eq_f : e = f) (n : ℕ) :
    Matrix (Fin n) (Fin n) (CornerSubring idem_e) ≃+*
      Matrix (Fin n) (Fin n) (CornerSubring idem_f) :=
  RingEquiv.mapMatrix (eqElIsoCorner e f idem_e idem_f e_eq_f)

-- same element produce same Matrix rings over corner subrings

-- @@ L269-281 verbatim
/-- Equal idempotents induce a ring isomorphism between matrix rings over their non-unital
corner subrings. -/
def equalElIsoMatrixRings (e f : R) (idem_e : IsIdempotentElem e) (idem_f : IsIdempotentElem f)
    (e_eq_f : e = f) (n : ℕ) :
    Matrix (Fin n) (Fin n) (CornerSubringNonUnital e) ≃+*
      Matrix (Fin n) (Fin n) (CornerSubringNonUnital f) := by
  let _ψ : (CornerSubringNonUnital e) ≃+* (CornerSubringNonUnital f) := by rw [e_eq_f]
  apply equalElIsoMatrixRings'
  · exact idem_e
  · exact idem_f
  · exact e_eq_f

-- coercions from Sets of CornerSubrings to Set of R

-- @@ L282-285 verbatim
instance : CoeOut (Set (CornerSubring idem_e)) (Set R) :=
  { coe := fun X => Set.image Subtype.val X }

-- I left ideal in eRe -> RI is a left ideal in R

-- @@ L286-290 verbatim
/-- Lift a (left) ideal of the corner subring `eRe` to a (left) ideal of `R` by taking the
`R`-span of its carrier. -/
def idealLift (I : Ideal (CornerSubring idem_e)) : Ideal R := Ideal.span (I.carrier)

-- coercion from Ideals of CornerSubrings to Ideals of R

-- @@ L291-293 verbatim
instance : CoeOut (Ideal (CornerSubring idem_e)) (Ideal R) := { coe := idealLift idem_e }

-- I ⊆ J -> RI ⊆ RJ

-- @@ L294-300 verbatim
theorem lift_monotonicity (I J : Ideal (CornerSubring idem_e)) :
    I ≤ J → (idealLift idem_e I) ≤ (idealLift idem_e J) := by
  intro I_leq_J
  apply Ideal.span_mono
  exact Set.image_mono I_leq_J

-- pushing an element into eRe: x |-> e x e

-- @@ L301-304 verbatim
/-- Push an element `x : R` into the corner subring `eRe` via `x ↦ e * x * e`. -/
def elPush (x : R) : CornerSubring idem_e := ⟨e * x * e, e_x_e_in_corner idem_e x⟩

-- A left ideal I can be pushed down to eRe by eIe

-- @@ L305-330 verbatim
/-- Push a (left) ideal of `R` down to a (left) ideal of `eRe` by taking the elementwise
image under `elPush`. -/
def idealPush (idem_e : IsIdempotentElem e) (J : Ideal R) : Ideal (CornerSubring idem_e) where
  carrier := {elPush idem_e x | x ∈ J}
  zero_mem' := by
    refine ⟨0, Submodule.zero_mem J, ?_⟩
    apply Subtype.ext
    change e * 0 * e = (0 : R)
    noncomm_ring
  add_mem' := by
    rintro x y ⟨r, ⟨hr_mem, hr⟩⟩ ⟨s, ⟨hs_mem, hs⟩⟩
    refine ⟨r + s, (Submodule.add_mem_iff_right J hr_mem).mpr hs_mem, ?_⟩
    apply Subtype.ext
    change e * (r + s) * e = (x + y : CornerSubring idem_e).val
    rw [← hr, ← hs]
    change e * (r + s) * e = e * r * e + e * s * e
    noncomm_ring
  smul_mem' := by
    rintro ⟨c, ⟨a, hc⟩⟩ x ⟨r, ⟨hr_mem, hr⟩⟩
    refine ⟨a * e * e * r, Ideal.mul_mem_left J (a * e * e) hr_mem, ?_⟩
    apply Subtype.ext
    rw [← hr]
    simp only [smul_eq_mul, MulMemClass.mk_mul_mk, hc, elPush]
    noncomm_ring

-- pushing is an additive homomorphism

-- @@ L331-337 verbatim
theorem add_el_push_eq_add (x y : R) :
    elPush idem_e x + elPush idem_e y = elPush idem_e (x + y) := by
  simp only [elPush]
  noncomm_ring
  simp only [AddMemClass.mk_add_mk]

-- multiplication by scalar keeps pushed element in ideal

-- @@ L338-364 verbatim
lemma el_push_smul_in_I (a y : R) (I : Ideal (CornerSubring idem_e)) :
    y ∈ (I.carrier : Set R) → elPush idem_e (a • y) ∈ I := by
  intro hy
  obtain ⟨r, ⟨hr1, hr2⟩⟩ := hy
  obtain ⟨s, hs⟩ := r.2
  rw [← hr2]
  have h : e * (a • r) * e = (e * a * e) * r := by
    calc _ = e * a * r * e := by noncomm_ring
        _ = e * a * (e * s * e) * e := by rw [hs]
        _ = e * a * e * s * (e * e) := by noncomm_ring
        _ = e * a * (e * e) * s * e := by rw [idem_e, ← idem_e]
        _ = e * a * e * (e * s * e) := by noncomm_ring
        _ = (e * a * e) * r := by rw [← hs]
  let w : CornerSubring idem_e := ⟨e * a * e, e_x_e_in_corner idem_e a⟩
  have h' : w * r ∈ I := by
    have hr : r ∈ I := hr1
    exact Ideal.mul_mem_left I w hr
  let v : CornerSubring idem_e := elPush idem_e (a • r)
  have v_val : v.val = e * (a * r) * e := rfl
  have h'' : v = w * r := by
    rw [Subtype.ext_iff]
    rw [NonUnitalSubring.val_mul]
    simpa only [v, w, elPush] using h
  rw [← h''] at h'
  exact h'

-- if x in the lift of I then its push is in I

-- @@ L365-380 verbatim
theorem ideal_push_pull_inclusion (I : Ideal (CornerSubring idem_e)) (x : R) :
    (x ∈ idealLift idem_e I) → (elPush idem_e x) ∈ I := by
  intro hx
  induction hx using Submodule.closure_induction with
  | zero =>
    have h0 : elPush idem_e 0 = (0 : CornerSubring idem_e) := by
      apply Subtype.ext
      change e * 0 * e = (0 : R)
      noncomm_ring
    simp_all
  | add y z _ _ hyp hyz =>
    rw [← add_el_push_eq_add]
    exact (Submodule.add_mem_iff_right I hyp).mpr hyz
  | smul_mem a y hy => exact el_push_smul_in_I idem_e a y I hy

-- pushing and pulling an ideal brings us back to the same ideal

-- @@ L381-401 verbatim
theorem push_pull (idem_e : IsIdempotentElem e) (I : Ideal (CornerSubring idem_e)) :
    idealPush idem_e (idealLift idem_e I) = I := by
  ext x
  constructor
  · rintro ⟨y, ⟨hy_mem, hy⟩⟩
    rw [← hy]
    exact ideal_push_pull_inclusion idem_e I y hy_mem
  · intro hx
    have h : (↑x : R) ∈ idealLift idem_e I := by
      unfold idealLift
      have hx1 : (↑x : R) ∈ (Subtype.val '' I.carrier : Set R) :=
        Set.mem_image_of_mem Subtype.val hx
      exact (Ideal.mem_span (↑x : R)).mpr fun _ a ↦ a hx1
    unfold idealPush
    refine ⟨↑x, h, ?_⟩
    obtain ⟨y, hy⟩ := x.2
    have hx' : (↑x : R) ∈ CornerRingSet e := Subtype.coe_prop x
    apply (corner_ring_set_mem idem_e).1 at hx'
    unfold elPush
    symm
    exact SetLike.coe_eq_coe.mp hx'


-- @@ L403-417 verbatim
theorem lift_strict_monotonicity (I J : Ideal (CornerSubring idem_e)) :
    I < J → (idealLift idem_e I) < (idealLift idem_e J) := by
  intro I_leq_J
  have I_neq_J : I ≠ J := ne_of_lt I_leq_J
  have lift_leq : (idealLift idem_e I) ≤ (idealLift idem_e J) :=
    lift_monotonicity idem_e I J (le_of_lt I_leq_J)
  have lift_neq : (idealLift idem_e I) ≠ (idealLift idem_e J) := by
    by_contra h_eq
    have h_eq : idealPush idem_e (idealLift idem_e I) = idealPush idem_e (idealLift idem_e J) :=
      congrArg (idealPush idem_e) h_eq
    rw [push_pull, push_pull] at h_eq
    exact I_neq_J h_eq
  exact lt_of_le_of_ne lift_leq lift_neq

-- if the lift of an ideal is accesible then so is the ideal

-- @@ L418-435 verbatim
theorem lift_acc_then_ideal_acc (idem_e : IsIdempotentElem e) (J : Ideal R)
    (h_J_is_lift : ∃ I3 : Ideal (CornerSubring idem_e), J = idealLift idem_e I3)
    (h_acc_J : Acc (fun x y => x < y) J) :
    Acc (fun x y => x < y) (idealPush idem_e J) := by
  induction h_acc_J with
  | intro J2 _ hi =>
    obtain ⟨I, hI⟩ := h_J_is_lift
    rw [hI, push_pull idem_e I]
    have c1 : (I2 : Ideal (CornerSubring idem_e)) → I2 < I → Acc (fun x y => x < y) I2 := by
      intro I2 hI2
      rw [← push_pull idem_e I2]
      have subJ2 := (lift_strict_monotonicity idem_e I2 I) hI2
      rw [← hI] at subJ2
      exact hi (idealLift idem_e I2) subJ2 ⟨I2, rfl⟩
    exact Acc.intro I c1

-- Lemma 2.10
-- a) If R is artinian, then the corner ring is artinian

-- @@ L436-448 verbatim
theorem corner_ring_artinian [h_ar : IsArtinian R R] :
    IsArtinian (CornerSubring idem_e) (CornerSubring idem_e) := by
  have Iacc : ∀ I : Ideal R, Acc (fun x y => x < y) I := fun I ↦ WellFounded.apply h_ar I
  have allacc : ∀ I : Ideal (CornerSubring idem_e), Acc (fun x y => x < y) I := by
    intro I
    have h : Acc (fun x y => x < y) (idealPush idem_e (idealLift idem_e I)) :=
      lift_acc_then_ideal_acc idem_e I ⟨I, rfl⟩ (Iacc (idealLift idem_e I))
    rw [push_pull idem_e I] at h
    exact h
  exact WellFounded.intro allacc

-- if we have two elements x y in the corners subring, then any element of the form x w y is
-- also in the corner

-- @@ L449-455 verbatim
theorem corner_ring_both_mul_mem' (x y : CornerSubring idem_e) (w : R) :
    x * w * y ∈ CornerSubring idem_e := by
  apply corner_ring_both_mul_mem
  · exact x.property
  · exact y.property

-- if a and b in eRe, then a (e R e) b = a R b as sets

-- @@ L456-479 verbatim
theorem both_mul_lift (x y : CornerSubring idem_e) :
    (bothMul (x : CornerSubring idem_e) y) = bothMul (x : R) (y : R) := by
  ext a
  constructor
  · rintro ⟨r, ⟨s, hs⟩, rfl⟩
    refine ⟨s, ?_⟩
    simp only [NonUnitalSubring.val_mul, hs]
  · rintro ⟨s, hs⟩
    rw [← is_right_unit idem_e ↑x, ← is_left_unit idem_e ↑y] at hs
    simp only [NonUnitalSubring.val_mul] at hs
    let sc : R := (1 : (CornerSubring idem_e)) * s * (1 : (CornerSubring idem_e))
    rw [← mul_assoc] at hs
    have ha : a = x * sc * y := by
      simp only [sc]
      rw [hs]
      simp only [mul_assoc]
    have hsc : sc ∈ CornerSubring idem_e := by
      simp only [sc]
      apply corner_ring_both_mul_mem'
    refine ⟨x * ⟨sc, hsc⟩ * y, ⟨⟨sc, hsc⟩, ?_⟩, ?_⟩
    · rfl
    · simp only [NonUnitalSubring.val_mul, ha]

-- b) If R is a prime ring, then the corner ring is prime

-- @@ L480-489 verbatim
theorem corner_ring_prime (hRP : IsPrimeRing R) : IsPrimeRing (CornerSubring idem_e) := by
  rw [prime_ring_equiv]
  intro a b h
  have h_lift : ((bothMul a b) : Set R) = {0} := by
    rw [← both_mul_lift, congrArg (Set.image Subtype.val) h]
    exact Set.image_singleton
  have l := prime_ring_equiv.1 hRP _ _ h_lift
  simp_all

-- if a cornersubring is a division subring then it is a division ring on its own

-- @@ L490-504 verbatim
theorem div_subring_to_div_ring (e : R) (idem_e : IsIdempotentElem e)
    (h : IsDivisionSubring (CornerSubringNonUnital e) e) :
    IsDivisionRing (CornerSubring idem_e) := by
  obtain ⟨⟨a, ⟨a_mem, a_nz⟩⟩, h_inv⟩ := h
  have corner_nontrivial : Nontrivial (CornerSubring idem_e) := by
    refine ⟨⟨(⟨a, a_mem⟩ : CornerSubring idem_e),
      ⟨0, NonUnitalSubring.zero_mem (CornerSubring idem_e)⟩, ?_⟩⟩
    simp_all
  apply left_inv_implies_divring
  clear a a_mem a_nz
  intro x x_nz
  let ⟨y, ⟨y_mem, hy⟩⟩ := h_inv x (SetLike.coe_mem x) ((nonzero idem_e x).mp x_nz)
  refine ⟨⟨y, y_mem⟩, ?_⟩
  apply Subtype.ext
  exact hy


-- @@ L506-506 verbatim
end LeanPool.ArtinWedderburn
