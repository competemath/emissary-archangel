/-
Copyright (c) 2026 Juliane Trianon Fraga and Vinicius de Oliveira Rodrigues. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Juliane Trianon Fraga, Vinicius de Oliveira Rodrigues
-/
module

public import LeanPool.Wallace.ConcreteClosure
public import LeanPool.Wallace.CountableDisjointization
import LeanPool.Wallace.BoundedIndependentMap
import Mathlib.Analysis.SpecialFunctions.Bernstein
import Mathlib.CategoryTheory.Category.Init
import Mathlib.Combinatorics.Matroid.Init
import Mathlib.MeasureTheory.Covering.Besicovitch


-- @@ L16-23 verbatim
/-!
# The concrete countable block schedule around one nonzero vector

For one nonzero vector `x`, only codes whose fresh coordinates lie in its dependency closure
matter to the local fusion.  They form a countable type.  This module disjointizes their fixed
almost-disjoint labels, selects the unique active code at each block label, and defines the
finite independent set presented to bounded deletion at that stage.
-/


-- @@ L25-25 verbatim
@[expose] public section


-- @@ L27-27 verbatim
open Set


-- @@ L29-29 verbatim
namespace Wallace

-- @@ L30-30 verbatim
namespace ConcreteLocalSetup


-- @@ L32-32 verbatim
noncomputable section


-- @@ L34-34 verbatim
open TriangularPreprocess

-- @@ L35-35 verbatim
open BlockData

-- @@ L36-36 verbatim
open ConcreteData

-- @@ L37-37 verbatim
open ConcreteClosure

-- @@ L38-38 verbatim
open FiniteCombinatorics


-- @@ L40-40 verbatim
variable (N : ℕ → ℕ) (hN : ∀ l, 0 < N l) (M : ℕ → ℕ)


-- @@ L42-44 verbatim
/-- Codes whose prescribed basis coordinate belongs to the local dependency closure of `x`. -/
abbrev RelevantCode (x : ContinuumFreeGroup) :=
  LocalCodeSchedule.RelevantCode codeIndex (closure N hN M x)


-- @@ L46-50 verbatim
/-- Pairwise disjoint labels obtained by deleting finitely many points from each relevant
almost-disjoint label. -/
def refinedLabel (x : ContinuumFreeGroup) : RelevantCode N hN M x → Set ℕ := by
  exact LocalCodeSchedule.refinedLabel
    codeIndex (closure N hN M x) (closure_countable N hN M x)


-- @@ L52-56 verbatim
theorem label_diff_refinedLabel_finite
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) :
    (label a.1 \ refinedLabel N hN M x a).Finite :=
  LocalCodeSchedule.label_diff_refinedLabel_finite
    codeIndex (closure N hN M x) (closure_countable N hN M x) a


-- @@ L58-62 verbatim
/-- The unique relevant code scheduled at label `l`, if there is one. -/
def activeCode (x : ContinuumFreeGroup) (l : ℕ) :
    Option (RelevantCode N hN M x) :=
  LocalCodeSchedule.activeCode codeIndex
    (closure N hN M x) (closure_countable N hN M x) l


-- @@ L64-68 verbatim
theorem activeCode_eq_some_of_mem (x : ContinuumFreeGroup) (l : ℕ)
    (a : RelevantCode N hN M x) (ha : l ∈ refinedLabel N hN M x a) :
    activeCode N hN M x l = some a :=
  LocalCodeSchedule.activeCode_eq_some_of_mem
    codeIndex (closure N hN M x) (closure_countable N hN M x) l a ha


-- @@ L70-70 verbatim
/-! ## Restriction to the countable local free group -/


-- @@ L72-78 verbatim
/-- Inclusion of the free group on the closure coordinates into the ambient free group. -/
def closureInclusion (x : ContinuumFreeGroup) :
    (closure N hN M x →₀ ℤ) →+ ContinuumFreeGroup :=
  by
    classical
    exact Finsupp.embDomain.addMonoidHom
      (.subtype (closure N hN M x : ContinuumIndex → Prop))


-- @@ L80-86 verbatim
theorem closureInclusion_apply (x : ContinuumFreeGroup)
    (z : closure N hN M x →₀ ℤ) :
    closureInclusion N hN M x z =
      Finsupp.embDomain
        (.subtype (closure N hN M x : ContinuumIndex → Prop)) z := by
  classical
  rfl


-- @@ L88-98 verbatim
theorem closureInclusion_injective (x : ContinuumFreeGroup) :
    Function.Injective (closureInclusion N hN M x) := by
  classical
  intro y z h
  apply Finsupp.embDomain_injective
    (.subtype (closure N hN M x : ContinuumIndex → Prop))
  change Finsupp.embDomain
    (.subtype (closure N hN M x : ContinuumIndex → Prop)) y =
      Finsupp.embDomain
        (.subtype (closure N hN M x : ContinuumIndex → Prop)) z at h
  exact h


-- @@ L100-106 verbatim
/-- The shifted prepared value, restricted to the local closure. -/
def localDifference (x : ContinuumFreeGroup)
    (a : RelevantCode N hN M x) (n : ℕ) : closure N hN M x →₀ ℤ :=
  by
    classical
    exact Finsupp.subtypeDomain (closure N hN M x)
      (prepared N hN M a.1 n - codeBasisVector a.1)


-- @@ L108-126 verbatim
private theorem difference_support_subset_closure
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) (n : ℕ) :
    ∀ i ∈ (prepared N hN M a.1 n - codeBasisVector a.1).support,
      i ∈ closure N hN M x := by
  intro i hi
  by_contra hiD
  have hprep0 : prepared N hN M a.1 n i = 0 := by
    by_contra hne
    apply hiD
    exact prepared_support_mem_closure N hN M x a.1 a.2 n i
      (Finsupp.mem_support_iff.mpr hne)
  have hbasis0 : codeBasisVector a.1 i = 0 := by
    by_cases hai : codeIndex a.1 = i
    · apply (hiD (hai ▸ a.2)).elim
    · simp [codeBasisVector, hai]
  have hne :
      (prepared N hN M a.1 n - codeBasisVector a.1) i ≠ 0 :=
    Finsupp.mem_support_iff.mp hi
  exact hne (by simp [hprep0, hbasis0])


-- @@ L128-147 verbatim
theorem closureInclusion_localDifference
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) (n : ℕ) :
    closureInclusion N hN M x (localDifference N hN M x a n) =
      prepared N hN M a.1 n - codeBasisVector a.1 := by
  classical
  let : DecidablePred (closure N hN M x : ContinuumIndex → Prop) :=
    fun _ ↦ Classical.propDecidable _
  rw [closureInclusion_apply]
  change Finsupp.embDomain
    (.subtype (closure N hN M x : ContinuumIndex → Prop))
      (Finsupp.subtypeDomain (closure N hN M x)
        (prepared N hN M a.1 n - codeBasisVector a.1)) = _
  exact
    (Finsupp.extendDomain_eq_embDomain_subtype
      (P := (closure N hN M x : ContinuumIndex → Prop))
      (Finsupp.subtypeDomain (closure N hN M x)
        (prepared N hN M a.1 n - codeBasisVector a.1))).symm.trans
      (Finsupp.extendDomain_subtypeDomain
        (prepared N hN M a.1 n - codeBasisVector a.1)
        (difference_support_subset_closure N hN M x a n))


-- @@ L149-157 verbatim
theorem localDifference_injective
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) :
    Function.Injective (localDifference N hN M x a) := by
  intro m n hmn
  apply preparedDifference_injective N hN M a.1
  change prepared N hN M a.1 m - codeBasisVector a.1 =
    prepared N hN M a.1 n - codeBasisVector a.1
  rw [← closureInclusion_localDifference N hN M x a m,
    ← closureInclusion_localDifference N hN M x a n, hmn]


-- @@ L159-163 verbatim
/-- The independent shifted set in one block, now inside the countable local group. -/
def localDifferenceBlock (x : ContinuumFreeGroup)
    (a : RelevantCode N hN M x) (l : ℕ) :
    Finset (closure N hN M x →₀ ℤ) :=
  (blockPositions N hN l).image (localDifference N hN M x a)


-- @@ L165-170 verbatim
theorem localDifferenceBlock_card
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) (l : ℕ) :
    (localDifferenceBlock N hN M x a l).card = N l := by
  rw [localDifferenceBlock,
    Finset.card_image_iff.mpr (localDifference_injective N hN M x a).injOn,
    blockPositions_card]


-- @@ L172-179 verbatim
theorem localDifferenceBlock_image_inclusion
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) (l : ℕ) :
    (localDifferenceBlock N hN M x a l).image (closureInclusion N hN M x) =
      differenceBlock N hN M a.1 l := by
  rw [localDifferenceBlock, differenceBlock, Finset.image_image]
  apply Finset.image_congr
  intro n hn
  exact closureInclusion_localDifference N hN M x a n


-- @@ L181-187 verbatim
theorem localDifferenceBlock_boundedIndependent
    (x : ContinuumFreeGroup) (a : RelevantCode N hN M x) (l : ℕ) :
    BoundedIndependent (M l) (localDifferenceBlock N hN M x a l) := by
  apply boundedIndependent_of_image (closureInclusion N hN M x)
    (closureInclusion_injective N hN M x)
  rw [localDifferenceBlock_image_inclusion]
  exact differenceBlock_boundedIndependent N hN M a.1 l


-- @@ L189-194 verbatim
/-- The active finite set inside the local free group. -/
def localActiveBlock (x : ContinuumFreeGroup) (l : ℕ) :
    Finset (closure N hN M x →₀ ℤ) :=
  match activeCode N hN M x l with
  | none => ∅
  | some a => localDifferenceBlock N hN M x a l


-- @@ L196-199 verbatim
theorem localActiveBlock_eq_of_mem (x : ContinuumFreeGroup) (l : ℕ)
    (a : RelevantCode N hN M x) (ha : l ∈ refinedLabel N hN M x a) :
    localActiveBlock N hN M x l = localDifferenceBlock N hN M x a l := by
  simp [localActiveBlock, activeCode_eq_some_of_mem N hN M x l a ha]


-- @@ L201-207 verbatim
theorem localActiveBlock_boundedIndependent (x : ContinuumFreeGroup) (l : ℕ) :
    BoundedIndependent (M l) (localActiveBlock N hN M x l) := by
  unfold localActiveBlock
  split
  · simp [BoundedIndependent]
  · rename_i a hactive
    exact localDifferenceBlock_boundedIndependent N hN M x a l


-- @@ L209-215 verbatim
theorem localActiveBlock_card_le (x : ContinuumFreeGroup) (l : ℕ) :
    (localActiveBlock N hN M x l).card ≤ N l := by
  unfold localActiveBlock
  split
  · simp
  · rename_i a hactive
    rw [localDifferenceBlock_card]


-- @@ L217-217 verbatim
end


-- @@ L219-219 verbatim
end ConcreteLocalSetup

-- @@ L220-220 verbatim
end Wallace
