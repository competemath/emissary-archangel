/-
Copyright (c) 2026 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
module

public import Mathlib.MeasureTheory.Integral.CircleIntegral
import Mathlib.Analysis.Complex.RemovableSingularity


-- @@ L11-13 verbatim
/-!
# LeanPool.RiemannMappingTheorem.Cindex
-/


-- @@ L15-15 verbatim
@[expose] public section


-- @@ L17-17 verbatim
open Real Complex Function TopologicalSpace Filter Topology Metric MeasureTheory Nat


-- @@ L19-23 verbatim
/-- The argument-principle integral
`(2πi)⁻¹ ∮_{C(z₀, r)} f'(z)/f(z) dz`, which counts zeroes of `f` inside
the circle of radius `r` around `z₀` (with multiplicity). -/
noncomputable def cindex (z₀ : ℂ) (r : ℝ) (f : ℂ → ℂ) : ℂ :=
  (2 * π * I)⁻¹ * ∮ z in C(z₀, r), deriv f z / f z


-- @@ L25-25 verbatim
section circle_integral


-- @@ L27-28 verbatim
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ℂ E] {f g : ℂ → E}
  {r : ℝ} {U : Set ℂ} {c : ℂ}


-- @@ L30-36 verbatim
lemma circle_integral_eq_zero (hU : IsOpen U) (hr : 0 < r) (hcr : closedBall c r ⊆ U)
      (f_hol : DifferentiableOn ℂ f U) :
    (∮ z in C(c, r), f z) = 0 :=
  circleIntegral_eq_zero_of_differentiable_on_off_countable hr.le Set.countable_empty
    (f_hol.continuousOn.mono hcr)
    (fun _ hz => f_hol.differentiableAt
      (hU.mem_nhds (hcr (ball_subset_closedBall (Set.sdiff_subset hz)))))


-- @@ L38-40 verbatim
lemma circle_integral_sub_center_inv_smul [CompleteSpace E] {v : E} (hr : 0 < r) :
    (∮ z in C(c, r), (z - c)⁻¹ • v) = (2 * π * I : ℂ) • v := by
  simp [circleIntegral.integral_sub_inv_of_mem_ball (mem_ball_self hr)]


-- @@ L42-42 verbatim
end circle_integral


-- @@ L44-44 verbatim
section dslope


-- @@ L46-47 verbatim
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ℂ E] [CompleteSpace E] {f : ℂ → E}
  {p : FormalMultilinearSeries ℂ ℂ E} {U : Set ℂ} {z₀ c : ℂ} {n : ℕ}


-- @@ L49-53 verbatim
lemma DifferentiableOn.iterate_dslope (hf : DifferentiableOn ℂ f U) (hU : IsOpen U) (hc : c ∈ U) :
    DifferentiableOn ℂ (iterate (swap dslope c) n f) U := by
  induction n generalizing f
  case zero => exact hf
  case succ n_ih => exact n_ih ((differentiableOn_dslope (hU.mem_nhds hc)).mpr hf)


-- @@ L55-62 verbatim
lemma HasFPowerSeriesAt.dslope_order_eventually_ne_zero (hp : HasFPowerSeriesAt f p z₀)
    (h : p ≠ 0) :
    ∀ᶠ z in 𝓝 z₀, iterate (swap dslope z₀) p.order f z ≠ 0 := by
  obtain ⟨r, hf⟩ := id hp
  exact ContinuousAt.eventually_ne
    ((DifferentiableOn.iterate_dslope hf.differentiableOn Metric.isOpen_eball
      (Metric.mem_eball_self hf.r_pos)).continuousOn.continuousAt
      (Metric.eball_mem_nhds _ hf.r_pos)) (hp.iterate_dslope_fslope_ne_zero h)


-- @@ L64-64 verbatim
end dslope


-- @@ L66-66 verbatim
variable {f g : ℂ → ℂ} {p : FormalMultilinearSeries ℂ ℂ ℂ} {z z₀ c : ℂ} {n : ℕ} {U : Set ℂ} {r : ℝ}


-- @@ L68-83 verbatim
lemma deriv_div_self_eq_div_add_deriv_div_self (hg : DifferentiableAt ℂ g z) (hgz : g z ≠ 0)
    (hfg : f =ᶠ[𝓝 z] fun w => (w - z₀) ^ n * g w) (hz : z ≠ z₀) :
    deriv f z / f z = n / (z - z₀) + deriv g z / g z := by
  have h1 : DifferentiableAt ℂ (fun y => (y - z₀) ^ n) z :=
    ((differentiable_fun_id.sub_const z₀).pow n).differentiableAt
  simp only [hfg.deriv_eq, hfg.self_of_nhds]
  cases n
  case zero => simp
  case succ n =>
    have hzz : z - z₀ ≠ 0 := sub_ne_zero.mpr hz
    have h2 : deriv (fun y : ℂ => (y - z₀) ^ (n + 1)) z = (n + 1) * (z - z₀) ^ n := by
      simp_all
    simp only [cast_add, cast_one]
    rw [deriv_fun_mul h1 hg, h2]
    field_simp [_root_.pow_succ, hzz]
    ring


-- @@ L85-96 verbatim
lemma eventually_deriv_div_self_eq (hp : HasFPowerSeriesAt f p z₀) (h : p ≠ 0) :
    let g := (iterate (swap dslope z₀) p.order) f
    ∀ᶠ z in 𝓝 z₀, z ≠ z₀ → deriv f z / f z = p.order / (z - z₀) + deriv g z / g z := by
  intro g
  obtain ⟨r, h2⟩ := hp.has_fpower_series_iterate_dslope_fslope p.order
  have lh1 := h2.differentiableOn.eventually_differentiableAt (Metric.eball_mem_nhds _ h2.r_pos)
  have lh2 := hp.dslope_order_eventually_ne_zero h
  have lh3 : ∀ᶠ z in 𝓝 z₀, f =ᶠ[𝓝 z] fun w => (w - z₀) ^ p.order * g w := by
    exact Eventually.of_forall fun _ =>
      Eventually.of_forall fun w => by
        simpa [g, smul_eq_mul] using hp.eq_pow_order_mul_iterate_dslope w
  filter_upwards [lh1, lh2, lh3] with z using deriv_div_self_eq_div_add_deriv_div_self


-- @@ L98-110 verbatim
lemma cindex_eq_zero (hU : IsOpen U) (hr : 0 < r) (hcr : closedBall c r ⊆ U)
    (f_hol : DifferentiableOn ℂ f U) (hf : ∀ z ∈ closedBall c r, f z ≠ 0) :
    cindex c r f = 0 := by
  set s : Set ℂ := { z ∈ U | f z ≠ 0 }
  have e2 : IsOpen s := f_hol.continuousOn.isOpen_inter_preimage hU isOpen_compl_singleton
  have e3 : closedBall c r ⊆ s := fun z hz => ⟨hcr hz, hf z hz⟩
  obtain ⟨δ, e4, e5⟩ := (isCompact_closedBall c r).exists_thickening_subset_open e2 e3
  simpa [cindex, Real.pi_ne_zero] using
    circle_integral_eq_zero isOpen_thickening hr (self_subset_thickening e4 _)
      (((f_hol.mono (e5.trans <| Set.sep_subset _ _)).deriv isOpen_thickening).div
        (f_hol.mono (e5.trans <| Set.sep_subset _ _)) (fun z hz => (e5 hz).2))

-- TODO: off-center using `integral_sub_inv_of_mem_ball`


-- @@ L112-133 verbatim
lemma cindex_eq_order_aux (hU : IsOpen U) (hr : 0 < r) (h0 : closedBall z₀ r ⊆ U)
    (h1 : DifferentiableOn ℂ g U) (h2 : ∀ z ∈ closedBall z₀ r, g z ≠ 0)
    (h3 : ∀ {z}, z ∈ sphere z₀ r → deriv f z / f z = c / (z - z₀) + deriv g z / g z) :
    cindex z₀ r f = c := by
  have e2 : sphere z₀ r ⊆ U := sphere_subset_closedBall.trans h0
  have e4 : (∮ z in C(z₀,r), deriv f z / f z) =
      ∮ z in C(z₀,r), c / (z - z₀) + deriv g z / g z :=
    circleIntegral.integral_congr hr.le (fun z hz => h3 hz)
  have e5 : (∮ z in C(z₀,r), c / (z - z₀) + deriv g z / g z) =
      (∮ z in C(z₀, r), c / (z - z₀)) + (∮ z in C(z₀, r), deriv g z / g z) :=
    circleIntegral.integral_add
      ((continuousOn_const.div (continuousOn_id.sub continuousOn_const)
        (fun z hz => sub_ne_zero.mpr (ne_of_mem_sphere hz hr.ne.symm))).circleIntegrable hr.le)
      ((ContinuousOn.div (h1.contDiffOn hU |>.continuousOn_deriv_of_isOpen hU le_rfl |>.mono e2)
        (h1.continuousOn.mono e2)
        (fun z hz => h2 _ (sphere_subset_closedBall hz))).circleIntegrable hr.le)
  have e6 : (∮ z in C(z₀, r), deriv g z / g z) = 0 :=
    by simpa [cindex, Real.pi_ne_zero, I_ne_zero] using cindex_eq_zero hU hr h0 h1 h2
  have e7 : (∮ z in C(z₀, r), c / (z - z₀)) = 2 * π * I * c := by
    simpa [div_eq_mul_inv, smul_eq_mul, mul_comm, mul_left_comm, mul_assoc] using
      circle_integral_sub_center_inv_smul (E := ℂ) (c := z₀) (v := c) hr
  simp [field, cindex, e4, e5, e6, e7]


-- @@ L135-147 verbatim
lemma exists_cindex_eq_order' (hp : HasFPowerSeriesAt f p z₀) (h : p ≠ 0) :
    ∃ R > (0 : ℝ), ∀ r ∈ Set.Ioo 0 R, cindex z₀ r f = p.order := by
  set g : ℂ → ℂ := iterate (swap dslope z₀) p.order f
  obtain ⟨R, hR₁, hh⟩ := Metric.mem_nhds_iff.mp
    ((hp.dslope_order_eventually_ne_zero h).and
      ((eventually_deriv_div_self_eq hp h).and
        (hp.has_fpower_series_iterate_dslope_fslope p.order).eventually_differentiableAt))
  refine ⟨R, hR₁, fun r hr => ?_⟩
  refine cindex_eq_order_aux isOpen_ball hr.1 (closedBall_subset_ball hr.2)
    (fun z hz => (hh hz).2.2.differentiableWithinAt)
    (fun z hz => (hh (closedBall_subset_ball hr.2 hz)).1)
    (fun hz => (hh (sphere_subset_closedBall.trans (closedBall_subset_ball hr.2) hz)).2.1
      (ne_of_mem_sphere hz hr.1.ne.symm))


-- @@ L149-167 verbatim
lemma exists_cindex_eq_order (hp : HasFPowerSeriesAt f p z₀) :
    ∃ R > (0 : ℝ), ∀ r ∈ Set.Ioo 0 R, cindex z₀ r f = p.order := by
  by_cases h : p = 0
  case neg => exact exists_cindex_eq_order' hp h
  case pos =>
    subst_vars
    obtain ⟨R, hR, hf⟩ := Metric.eventually_nhds_iff.mp (hp.locally_zero_iff.mpr rfl)
    refine ⟨R, hR, fun r hr => ?_⟩
    have heq : Set.EqOn (fun z => deriv f z / f z) 0 (sphere z₀ r) := by
      intro z hz
      have hd : dist z z₀ < R := by
        rw [mem_sphere_iff_norm, ← Complex.dist_eq] at hz
        simpa [hz] using hr.2
      simp_all
    simp only [cindex, mul_inv_rev, inv_I, neg_mul, FormalMultilinearSeries.order_zero,
      CharP.cast_eq_zero, neg_eq_zero, _root_.mul_eq_zero, I_ne_zero, inv_eq_zero,
      ofReal_eq_zero, pi_ne_zero, OfNat.ofNat_ne_zero, or_self, false_or]
    rw [circleIntegral.integral_congr hr.1.le heq]
    simp [circleIntegral]


-- @@ L169-173 verbatim
lemma cindex_eventually_eq_order (hp : HasFPowerSeriesAt f p z₀) :
    ∀ᶠ r in 𝓝[>] 0, cindex z₀ r f = p.order := by
  obtain ⟨R, hR, hf⟩ := exists_cindex_eq_order hp
  rw [eventually_nhdsWithin_iff, Metric.eventually_nhds_iff]
  exact ⟨R, hR, fun r hr1 hr2 => hf r ⟨hr2, by simpa using lt_of_abs_lt hr1⟩⟩
