/-
Copyright (c) 2026 Juliane Trianon Fraga and Vinicius de Oliveira Rodrigues. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Juliane Trianon Fraga, Vinicius de Oliveira Rodrigues
-/
module

public import LeanPool.Wallace.RationalClosure
public import LeanPool.Wallace.CountableDisjointization
import LeanPool.Wallace.BoundedIndependentMap
import Mathlib.Analysis.SpecialFunctions.Bernstein
import Mathlib.CategoryTheory.Category.Init
import Mathlib.Combinatorics.Matroid.Init
import Mathlib.MeasureTheory.Covering.Besicovitch


-- @@ L16-22 verbatim
/-!
# The countable block schedule around one rational vector

Relevant codes are countable.  Their almost-disjoint labels are disjointized, so every block
has at most one active code, and its shifted prepared terms form the finite independent set
used by the fusion.
-/


-- @@ L24-24 verbatim
@[expose] public section


-- @@ L26-26 verbatim
open Set


-- @@ L28-28 verbatim
namespace Wallace

-- @@ L29-29 verbatim
namespace RationalLocalSetup


-- @@ L31-31 verbatim
noncomputable section


-- @@ L33-33 verbatim
open RationalTriangularPreprocess

-- @@ L34-34 verbatim
open BlockData

-- @@ L35-35 verbatim
open RationalData

-- @@ L36-36 verbatim
open RationalClosure

-- @@ L37-37 verbatim
open FiniteCombinatorics


-- @@ L39-39 verbatim
variable (N : ℕ → ℕ) (hN : ∀ l, 0 < N l) (M : ℕ → ℕ)


-- @@ L41-43 verbatim
/-- Codes whose distinguished coordinate lies in the local closure of `x`. -/
abbrev RelevantCode (x : ContinuumRationalGroup) :=
  LocalCodeSchedule.RelevantCode codeIndex (closure N hN M x)


-- @@ L45-49 verbatim
/-- Pairwise-disjoint refinements of the block labels of all relevant codes. -/
def refinedLabel (x : ContinuumRationalGroup) :
    RelevantCode N hN M x → Set ℕ := by
  exact LocalCodeSchedule.refinedLabel
    codeIndex (closure N hN M x) (closure_countable N hN M x)


-- @@ L51-55 verbatim
theorem label_diff_refinedLabel_finite (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) :
    (label a.1 \ refinedLabel N hN M x a).Finite :=
  LocalCodeSchedule.label_diff_refinedLabel_finite
    codeIndex (closure N hN M x) (closure_countable N hN M x) a


-- @@ L57-61 verbatim
/-- The unique relevant code assigned to stage `l`, when one exists. -/
def activeCode (x : ContinuumRationalGroup) (l : ℕ) :
    Option (RelevantCode N hN M x) :=
  LocalCodeSchedule.activeCode codeIndex
    (closure N hN M x) (closure_countable N hN M x) l


-- @@ L63-67 verbatim
theorem activeCode_eq_some_of_mem (x : ContinuumRationalGroup) (l : ℕ)
    (a : RelevantCode N hN M x) (ha : l ∈ refinedLabel N hN M x a) :
    activeCode N hN M x l = some a :=
  LocalCodeSchedule.activeCode_eq_some_of_mem
    codeIndex (closure N hN M x) (closure_countable N hN M x) l a ha


-- @@ L69-69 verbatim
/-! ## The countable local rational group -/


-- @@ L71-76 verbatim
/-- The additive inclusion of the local rational direct sum into the ambient one. -/
def closureInclusion (x : ContinuumRationalGroup) :
    (closure N hN M x →₀ ℚ) →+ ContinuumRationalGroup := by
  classical
  exact Finsupp.embDomain.addMonoidHom
    (.subtype (closure N hN M x : ContinuumIndex → Prop))


-- @@ L78-84 verbatim
theorem closureInclusion_apply (x : ContinuumRationalGroup)
    (z : closure N hN M x →₀ ℚ) :
    closureInclusion N hN M x z =
      Finsupp.embDomain
        (.subtype (closure N hN M x : ContinuumIndex → Prop)) z := by
  classical
  rfl


-- @@ L86-96 verbatim
theorem closureInclusion_injective (x : ContinuumRationalGroup) :
    Function.Injective (closureInclusion N hN M x) := by
  classical
  intro y z h
  apply Finsupp.embDomain_injective
    (.subtype (closure N hN M x : ContinuumIndex → Prop))
  change Finsupp.embDomain
    (.subtype (closure N hN M x : ContinuumIndex → Prop)) y =
      Finsupp.embDomain
        (.subtype (closure N hN M x : ContinuumIndex → Prop)) z at h
  exact h


-- @@ L98-103 verbatim
/-- The prepared difference restricted to the local coordinate closure. -/
def localDifference (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) (n : ℕ) : closure N hN M x →₀ ℚ := by
  classical
  exact Finsupp.subtypeDomain (closure N hN M x)
    (prepared N hN M a.1 n - codeBasisVector a.1)


-- @@ L105-122 verbatim
private theorem difference_support_subset_closure
    (x : ContinuumRationalGroup) (a : RelevantCode N hN M x) (n : ℕ) :
    ∀ i ∈ (prepared N hN M a.1 n - codeBasisVector a.1).support,
      i ∈ closure N hN M x := by
  intro i hi
  by_contra hiD
  have hprep0 : prepared N hN M a.1 n i = 0 := by
    by_contra hne
    apply hiD
    exact prepared_support_mem_closure N hN M x a.1 a.2 n i
      (Finsupp.mem_support_iff.mpr hne)
  have hbasis0 : codeBasisVector a.1 i = 0 := by
    by_cases hai : codeIndex a.1 = i
    · exact (hiD (hai ▸ a.2)).elim
    · simp [codeBasisVector, hai]
  have hne : (prepared N hN M a.1 n - codeBasisVector a.1) i ≠ 0 :=
    Finsupp.mem_support_iff.mp hi
  exact hne (by simp [hprep0, hbasis0])


-- @@ L124-143 verbatim
theorem closureInclusion_localDifference
    (x : ContinuumRationalGroup) (a : RelevantCode N hN M x) (n : ℕ) :
    closureInclusion N hN M x (localDifference N hN M x a n) =
      prepared N hN M a.1 n - codeBasisVector a.1 := by
  classical
  let : DecidablePred (closure N hN M x : ContinuumIndex → Prop) :=
    fun _ ↦ Classical.propDecidable _
  rw [closureInclusion_apply]
  change Finsupp.embDomain
    (.subtype (closure N hN M x : ContinuumIndex → Prop))
      (Finsupp.subtypeDomain (closure N hN M x)
        (prepared N hN M a.1 n - codeBasisVector a.1)) = _
  exact
    (Finsupp.extendDomain_eq_embDomain_subtype
      (P := (closure N hN M x : ContinuumIndex → Prop))
      (Finsupp.subtypeDomain (closure N hN M x)
        (prepared N hN M a.1 n - codeBasisVector a.1))).symm.trans
      (Finsupp.extendDomain_subtypeDomain
        (prepared N hN M a.1 n - codeBasisVector a.1)
        (difference_support_subset_closure N hN M x a n))


-- @@ L145-153 verbatim
theorem localDifference_injective (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) :
    Function.Injective (localDifference N hN M x a) := by
  intro m n hmn
  apply preparedDifference_injective N hN M a.1
  change prepared N hN M a.1 m - codeBasisVector a.1 =
    prepared N hN M a.1 n - codeBasisVector a.1
  rw [← closureInclusion_localDifference N hN M x a m,
    ← closureInclusion_localDifference N hN M x a n, hmn]


-- @@ L155-159 verbatim
/-- The finite block of local prepared differences for a relevant code. -/
def localDifferenceBlock (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) (l : ℕ) :
    Finset (closure N hN M x →₀ ℚ) :=
  (TriangularPreprocess.blockPositions N hN l).image (localDifference N hN M x a)


-- @@ L161-166 verbatim
theorem localDifferenceBlock_card (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) (l : ℕ) :
    (localDifferenceBlock N hN M x a l).card = N l := by
  rw [localDifferenceBlock,
    Finset.card_image_iff.mpr (localDifference_injective N hN M x a).injOn,
    TriangularPreprocess.blockPositions_card]


-- @@ L168-175 verbatim
theorem localDifferenceBlock_image_inclusion (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) (l : ℕ) :
    (localDifferenceBlock N hN M x a l).image (closureInclusion N hN M x) =
      differenceBlock N hN M a.1 l := by
  rw [localDifferenceBlock, differenceBlock, Finset.image_image]
  apply Finset.image_congr
  intro n hn
  exact closureInclusion_localDifference N hN M x a n


-- @@ L177-183 verbatim
theorem localDifferenceBlock_boundedIndependent (x : ContinuumRationalGroup)
    (a : RelevantCode N hN M x) (l : ℕ) :
    BoundedIndependent (M l) (localDifferenceBlock N hN M x a l) := by
  apply boundedIndependent_of_image (closureInclusion N hN M x)
    (closureInclusion_injective N hN M x)
  rw [localDifferenceBlock_image_inclusion]
  exact differenceBlock_boundedIndependent N hN M a.1 l


-- @@ L185-190 verbatim
/-- The local difference block active at stage `l`, or the empty block. -/
def localActiveBlock (x : ContinuumRationalGroup) (l : ℕ) :
    Finset (closure N hN M x →₀ ℚ) :=
  match activeCode N hN M x l with
  | none => ∅
  | some a => localDifferenceBlock N hN M x a l


-- @@ L192-195 verbatim
theorem localActiveBlock_eq_of_mem (x : ContinuumRationalGroup) (l : ℕ)
    (a : RelevantCode N hN M x) (ha : l ∈ refinedLabel N hN M x a) :
    localActiveBlock N hN M x l = localDifferenceBlock N hN M x a l := by
  simp [localActiveBlock, activeCode_eq_some_of_mem N hN M x l a ha]


-- @@ L197-203 verbatim
theorem localActiveBlock_boundedIndependent (x : ContinuumRationalGroup) (l : ℕ) :
    BoundedIndependent (M l) (localActiveBlock N hN M x l) := by
  unfold localActiveBlock
  split
  · simp [BoundedIndependent]
  · rename_i a hactive
    exact localDifferenceBlock_boundedIndependent N hN M x a l


-- @@ L205-211 verbatim
theorem localActiveBlock_card_le (x : ContinuumRationalGroup) (l : ℕ) :
    (localActiveBlock N hN M x l).card ≤ N l := by
  unfold localActiveBlock
  split
  · simp
  · rename_i a hactive
    rw [localDifferenceBlock_card]


-- @@ L213-213 verbatim
end

-- @@ L214-214 verbatim
end RationalLocalSetup

-- @@ L215-215 verbatim
end Wallace
