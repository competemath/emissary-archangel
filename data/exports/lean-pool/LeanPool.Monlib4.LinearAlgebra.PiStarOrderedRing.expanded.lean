/-
Copyright (c) 2024 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Algebra.Order.Star.Basic
public import Mathlib.Algebra.Star.Pi
import Mathlib.Algebra.Order.AbsoluteValue.Basic
import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Tactic.NormNum.Inv
import Mathlib.Tactic.NormNum.Pow


-- @@ L15-19 verbatim
/-!
  # pi.star_ordered_ring

  This file contains the definition of `pi.star_ordered_ring`.
-/


-- @@ L21-21 verbatim
@[expose] public section


-- @@ L23-25 verbatim
/-- Coordinate projection of a set of dependent functions along `Pi.single`. -/
def Set.ofPi {ι : Type _} {B : ι → Type _} [DecidableEq ι] [∀ i, Zero (B i)] (s : Set (∀ i, B i)) :
    ∀ i, Set (B i) := fun i x => Pi.single i x ∈ s


-- @@ L27-40 verbatim
theorem Set.piOfPi {ι : Type _} {B : ι → Type _} [DecidableEq ι] [∀ i, AddZeroClass (B i)]
    {s : ∀ i, Set (B i)} (h : ∀ i, s i 0) : (Set.univ.pi s).ofPi = s := by
  ext i x
  change Pi.single i x ∈ Set.univ.pi s ↔ x ∈ s i
  constructor
  · intro hi
    specialize hi i
    simp_all
  · intro hx j _
    by_cases hj : j = i
    · rw [hj]
      rwa [Pi.single_eq_same]
    · rw [Pi.single_eq_of_ne hj]
      exact h j


-- @@ L42-52 verbatim
/-- Coordinate projection of an additive submonoid of dependent functions along `Pi.single`. -/
def AddSubmonoid.ofPi {ι : Type _} {B : ι → Type _} [DecidableEq ι] [∀ i, AddZeroClass (B i)] :
    AddSubmonoid (∀ i, B i) → ∀ i, AddSubmonoid (B i) := fun h i =>
  { carrier := fun x => x ∈ Set.ofPi h.carrier i
    zero_mem' := by
      change Pi.single i 0 ∈ h.carrier
      simp_all
    add_mem' := fun x y => by
      have := h.add_mem' x y
      simp only [AddSubmonoid.mem_carrier, ← Pi.single_add] at this ⊢
      exact this }


-- @@ L54-58 verbatim
theorem AddSubmonoid.piOfPi {ι : Type _} {B : ι → Type _} [DecidableEq ι] [∀ i, AddZeroClass (B i)]
    (h : ∀ i, AddSubmonoid (B i)) : (AddSubmonoid.pi Set.univ h).ofPi = h := by
  ext i x
  change Pi.single i x ∈ AddSubmonoid.pi Set.univ h ↔ x ∈ h i
  simp_all


-- @@ L60-71 verbatim
theorem Set.ofPi_mem' {ι : Type _} {B : ι → Type _} [DecidableEq ι] [∀ i, AddZeroClass (B i)]
    {s : ∀ i, Set (B i)} {S : Set (∀ i, B i)} (hs : Set.univ.pi s ⊆ S) (h : ∀ i, s i 0) (i : ι) :
    s i ⊆ S.ofPi i := by
  intro x hx
  change Pi.single i x ∈ S
  apply hs
  intro j _
  by_cases hj : j = i
  · rw [hj]
    rwa [Pi.single_eq_same]
  · rw [Pi.single_eq_of_ne hj]
    exact h j


-- @@ L73-87 verbatim
theorem AddSubmonoid.mem_closure_pi {ι : Type _} {B : ι → Type _}
    [∀ i, AddZeroClass (B i)] {s : ∀ i, Set (B i)} {x : ∀ i, B i} :
    x ∈ AddSubmonoid.closure (Set.univ.pi fun i => s i) → ∀ i, x i ∈ AddSubmonoid.closure (s i) :=
  by
  simp_rw [AddSubmonoid.mem_closure]
  intro h i S hS
  specialize h (AddSubmonoid.pi Set.univ fun i => AddSubmonoid.closure (s i))
  have hx : x ∈ AddSubmonoid.pi Set.univ (fun j => AddSubmonoid.closure (s j)) := by
    apply h
    intro y hy
    change ∀ j ∈ (Set.univ : Set ι), y j ∈ AddSubmonoid.closure (s j)
    intro j _
    exact AddSubmonoid.subset_closure (hy j (Set.mem_univ j))
  rw [AddSubmonoid.mem_pi] at hx
  exact (AddSubmonoid.mem_closure.mp (hx i (Set.mem_univ i))) S hS


-- @@ L89-96 verbatim
theorem Pi.StarOrderedRing.nonneg_def {ι : Type _} {α : ι → Type _} [∀ i, NonUnitalSemiring (α i)]
    [∀ i, PartialOrder (α i)] [∀ i, StarRing (α i)]
    (h : ∀ (i : ι) (x : α i), 0 ≤ x ↔ ∃ y, star y * y = x) (x : ∀ i, α i) :
    0 ≤ x ↔ ∃ y, star y * y = x := by
  simp_rw [Pi.le_def, Pi.zero_apply, funext_iff, Pi.mul_apply, Pi.star_apply, h]
  exact
    ⟨fun hx => ⟨fun i => (hx i).choose, fun i => (hx i).choose_spec⟩,
    fun ⟨y, hy⟩ i => ⟨y i, hy i⟩⟩


-- @@ L98-102 verbatim
instance {ι : Type _} {α : ι → Type _} [∀ i, Ring (α i)]
    [∀ i, PartialOrder (α i)] [∀ i, StarRing (α i)] [∀ i, StarOrderedRing (α i)] :
  CovariantClass ((i : ι) → α i) ((i : ι) → α i)
      (Function.swap fun x x_1 ↦ x + x_1) fun x x_1 ↦ x ≤ x_1 :=
⟨fun x y z h j => by simp_rw [Function.swap, Pi.add_apply, add_le_add_iff_right, h j]⟩


-- @@ L104-108 verbatim
theorem Pi.StarOrderedRing.le_def {ι : Type _} {α : ι → Type _} [∀ i, Ring (α i)]
    [∀ i, PartialOrder (α i)] [∀ i, StarRing (α i)] [∀ i, StarOrderedRing (α i)]
    (h : ∀ (i : ι) (x : α i), 0 ≤ x ↔ ∃ y, star y * y = x) (x y : ∀ i, α i) :
    x ≤ y ↔ ∃ z, star z * z = y - x := by
  rwa [← sub_nonneg, ← Pi.StarOrderedRing.nonneg_def]


-- @@ L110-118 verbatim
/-- Dependent functions over star-ordered rings are star-ordered coordinatewise. -/
theorem Pi.starOrderedRing {ι : Type _} {B : ι → Type _} [∀ i, Ring (B i)] [∀ i, PartialOrder (B i)]
    [∀ i, StarRing (B i)] [∀ i, StarOrderedRing (B i)]
    (h : ∀ (i : ι) (x : B i), 0 ≤ x ↔ ∃ y, star y * y = x) :
    StarOrderedRing (∀ i, B i) :=
StarOrderedRing.of_le_iff
  (fun a b => by
    rw [Pi.StarOrderedRing.le_def h]
    simp_rw [eq_sub_iff_add_eq', eq_comm])
