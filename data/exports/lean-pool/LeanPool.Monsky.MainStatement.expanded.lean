/-
Copyright (c) 2026 Dhyan Aranha and contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dhyan Aranha, contributors
-/
module

public import LeanPool.Monsky.Square
public import Mathlib.Analysis.Normed.Lp.MeasurableSpace
public import Mathlib.MeasureTheory.Measure.Haar.OfBasis
import LeanPool.Monsky.Appendix
import LeanPool.Monsky.MonskyEven
import LeanPool.Monsky.SegmentCounting
import LeanPool.Monsky.TriangleCorollary
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Arctan
import Mathlib.MeasureTheory.Covering.Besicovitch


-- @@ L18-22 verbatim
/-!
# LeanPool.Monsky.MainStatement

Imported Lean Pool material for `LeanPool.Monsky.MainStatement`.
-/


-- @@ L24-24 verbatim
@[expose] public section


-- @@ L26-26 verbatim
namespace LeanPool.Monsky


-- @@ L28-28 verbatim
local notation "Triangle" => Fin 3 → (EuclideanSpace ℝ (Fin 2))


-- @@ L30-30 verbatim
open BigOperators



-- @@ L33-95 verbatim
theorem monsky_theorem (n : ℕ) :
    (∃ (S : Finset Triangle),
      closedHull unitSquare = ⋃ (Δ ∈ S), closedHull Δ ∧
      Set.PairwiseDisjoint (↑S : Set Triangle) openHull ∧
      (∀ Δ₁ ∈ S, ∀ Δ₂ ∈ S,
        MeasureTheory.volume (openHull Δ₁) = MeasureTheory.volume (openHull Δ₂)) ∧
      S.card = n)
    ↔ (n ≠ 0 ∧ Even n) := by
  constructor
  · -- Hard direction
    intro ⟨S, hCover, hDisjoint, hArea, hCard⟩
    refine ⟨?_,?_⟩
    · rw [←hCard]
      exact (Nat.ne_of_lt (no_empty_cover hCover (closed_pol_nonempty (by norm_num) _))).symm
    · by_contra hOdd
      rw [Nat.not_even_iff_odd] at hOdd
      have ⟨_,Γ,v,hv⟩ := valuation_on_reals
      have ⟨T,hTS,hTrainbow⟩ := monsky_rainbow v S ⟨hCover,hDisjoint⟩ ?_
      · apply no_odd_rainbowTriangle v T hTrainbow hv ?_
        · use n, hOdd
          rw [←hCard]
          refine equal_area_cover_implies_triangleArea_n S ?_ T hTS
          -- Refactor isEqualAreaCover to mean actually the hypotheses here
          -- So that the rest of the proof one line.
          refine ⟨⟨hCover,hDisjoint⟩, ?_⟩
          use triangleArea T
          intro T' hT'S
          specialize hArea T hTS T' hT'S
          have this := congrArg (f := fun x ↦ x.toReal) hArea
          rw [volume_open_triangle,volume_open_triangle ] at this
          exact this.symm
      · -- Similarly we should have a seperate lemma that says that for any "equal area cover"
        -- of the triangles all determinants are nonzero.
        intro T hTS hcontra
        have bla := equal_area_cover_implies_triangleArea_n S ?_ T hTS
        · rw [triangleArea, hcontra, hCard] at bla
          simp only [abs_zero, zero_div, one_div, zero_eq_inv] at bla
          apply Nat.not_odd_iff_even.2 (Even.zero (α := ℕ))
          convert hOdd
          rw [←Nat.cast_inj (R := ℝ)]
          simp_all
        · -- This is exactly the same as above so should be abstracted
          -- when refactoring isEqualAreaCover.
          -- We put it in for now.
          refine ⟨⟨hCover,hDisjoint⟩, ?_⟩
          use triangleArea T
          intro T' hT'S
          specialize hArea T hTS T' hT'S
          have this := congrArg (f := fun x ↦ x.toReal) hArea
          rw [volume_open_triangle,volume_open_triangle ] at this
          exact this.symm
  · -- Easy direction
    intro ⟨hnNonzero, hnEven⟩
    have ⟨S, hScover, hScard⟩  := monsky_easy_direction' hnEven hnNonzero
    use S
    -- monsky_easy_direction' should be changed so that the rest of this is one line
    -- To Do: Make area definitions combine better...
    refine ⟨hScover.1.1, hScover.1.2, ?_, hScard⟩
    intro T₁ hT₁ T₂ hT₂
    rw [volume_open_triangle', volume_open_triangle']
    have ⟨A,hA⟩ := hScover.2
    unfold triangleArea at hA
    rw [hA _ hT₁, hA _ hT₂]


-- @@ L97-97 verbatim
end Monsky

-- @@ L98-98 verbatim
end LeanPool
