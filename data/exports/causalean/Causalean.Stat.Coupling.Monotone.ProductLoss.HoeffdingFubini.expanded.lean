/-
Copyright (c) 2026 Jiyuan Tan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiyuan Tan
-/

module
public import Causalean.Stat.Coupling.Monotone.ProductLoss.Survival
public import Causalean.Mathlib.Analysis.SignedTailRepresentation
public import Causalean.Tactic.IntegralLinearity
public import Mathlib.MeasureTheory.Integral.Prod
public import Mathlib.MeasureTheory.Function.L2Space


-- @@ L14-39 verbatim
/-!
# The Fubini step in Hoeffding's covariance identity

Fix a coupling `π ∈ Π(μ, ν)` with `L²` marginals. Writing
`Φ q p = signedTail p.1 q.1 * signedTail p.2 q.2` for the product tail
representation of `p.1 * p.2` (see `TailIntegral.lean`), this file performs the
two Fubini swaps that turn the moments of `π` into Lebesgue integrals of
survival functions:

* `integral_prod_eq_integral_fiber` : `E_π[XY] = ∫∫ (∫ Φ q p ∂π) dq`;
* `mean_fst_tail` / `mean_snd_tail` : `E[X] = ∫ (SX s - 𝟙{s<0}) ds`, likewise `E[Y]`;
* `fiber_integral_pi` : the inner `π`-integral evaluates, by linearity of the
  expectation of a product of tail indicators, to
  `S s t - 𝟙{t<0}·SX s - 𝟙{s<0}·SY t + 𝟙{s<0}·𝟙{t<0}`.

The domination that legitimises both swaps is `∫∫ |Φ q p| dq = |p.1| * |p.2|`
(`integral_norm_signedTail_prod`), whose `π`-integral is finite by Cauchy–Schwarz
on the `L²` marginals. Subtracting `E[X]·E[Y]` (rewritten as a double integral
via `integral_prod_mul`) cancels the three inhomogeneous terms and leaves the
survival gap `S - SX·SY`, which is the Fréchet gap by `surv_gap_eq`.

Orientation convention: all product measures are taken as
`(volume.prod volume).prod π`, i.e. *tail variables first*, so that
`MeasureTheory.Integrable.integral_prod_left` directly yields integrability of
the fibre `q ↦ ∫ Φ q p ∂π` against Lebesgue×Lebesgue.
-/


-- @@ L41-41 verbatim
public section


-- @@ L43-43 verbatim
open Causalean.Mathlib.Analysis.SignedTailRepresentation


-- @@ L45-45 verbatim
namespace Causalean.Stat


-- @@ L47-47 verbatim
open MeasureTheory ProbabilityTheory Set


-- @@ L49-49 verbatim
variable {π : Measure (ℝ × ℝ)} {μ ν : Measure ℝ}


-- @@ L51-51 verbatim
/-! ### Moving the `L²` hypotheses through the marginals -/


-- @@ L53-59 verbatim
/-- The first coordinate is in `L²(π)` when `μ = π.map Prod.fst` has a second
moment. -/
lemma coupling_fst_memLp (h : IsCoupling π μ ν) (hμ : MemLp (fun x : ℝ => x) 2 μ) :
    MemLp (fun p : ℝ × ℝ => p.1) 2 π := by
  rw [← h.map_fst] at hμ
  simpa [Function.comp_def] using
    (hμ.comp_of_map (μ := π) (f := Prod.fst) measurable_fst.aemeasurable)


-- @@ L61-67 verbatim
/-- The second coordinate is in `L²(π)` when `ν = π.map Prod.snd` has a second
moment. -/
lemma coupling_snd_memLp (h : IsCoupling π μ ν) (hν : MemLp (fun y : ℝ => y) 2 ν) :
    MemLp (fun p : ℝ × ℝ => p.2) 2 π := by
  rw [← h.map_snd] at hν
  simpa [Function.comp_def] using
    (hν.comp_of_map (μ := π) (f := Prod.snd) measurable_snd.aemeasurable)


-- @@ L69-76 verbatim
/-- `E_π[X] = E_μ[id]`: the first moment of a coupling is that of its first
marginal. -/
lemma coupling_integral_fst (h : IsCoupling π μ ν) (hμ : MemLp (fun x : ℝ => x) 2 μ) :
    (∫ p : ℝ × ℝ, p.1 ∂π) = ∫ x : ℝ, x ∂μ := by
  have hμ' : AEStronglyMeasurable (fun x : ℝ => x) (π.map Prod.fst) := by
    simpa [h.map_fst] using hμ.aestronglyMeasurable
  rw [← h.map_fst]
  exact (integral_map measurable_fst.aemeasurable hμ').symm


-- @@ L78-85 verbatim
/-- `E_π[Y] = E_ν[id]`: the first moment of a coupling is that of its second
marginal. -/
lemma coupling_integral_snd (h : IsCoupling π μ ν) (hν : MemLp (fun y : ℝ => y) 2 ν) :
    (∫ p : ℝ × ℝ, p.2 ∂π) = ∫ y : ℝ, y ∂ν := by
  have hν' : AEStronglyMeasurable (fun y : ℝ => y) (π.map Prod.snd) := by
    simpa [h.map_snd] using hν.aestronglyMeasurable
  rw [← h.map_snd]
  exact (integral_map measurable_snd.aemeasurable hν').symm


-- @@ L87-92 verbatim
/-- `XY ∈ L¹(π)` by Cauchy–Schwarz from the two `L²` marginals. -/
lemma coupling_integrable_mul (h : IsCoupling π μ ν)
    (hμ : MemLp (fun x : ℝ => x) 2 μ) (hν : MemLp (fun y : ℝ => y) 2 ν) :
    Integrable (fun p : ℝ × ℝ => p.1 * p.2) π := by
  letI : IsProbabilityMeasure π := h.isProbabilityMeasure
  exact (coupling_fst_memLp h hμ).integrable_mul (coupling_snd_memLp h hν)


-- @@ L94-94 verbatim
/-! ### The fibre integrals -/


-- @@ L96-118 verbatim
/-- The `π`-integral of the signed tail indicator of the first coordinate is the
marginal survival function minus the constant `𝟙{s<0}`:
`∫ p, (𝟙{s < p.1} - 𝟙{s < 0}) ∂π = SX s - 𝟙{s < 0}`. Uses
`∫ p, 𝟙{s < p.1} ∂π = π.real (Prod.fst ⁻¹' Ioi s)` and `π univ = 1`. -/
lemma integral_signedTail_fst (h : IsCoupling π μ ν) (s : ℝ) :
    (∫ p : ℝ × ℝ, signedTail p.1 s ∂π) = survFst π s - tailInd 0 s := by
  letI : IsProbabilityMeasure π := h.isProbabilityMeasure
  let A : Set (ℝ × ℝ) := Prod.fst ⁻¹' Ioi s
  have hA : MeasurableSet A := measurableSet_Ioi.preimage measurable_fst
  have hfun :
      (fun p : ℝ × ℝ => signedTail p.1 s)
        = fun p : ℝ × ℝ => A.indicator (fun _ => (1 : ℝ)) p - tailInd 0 s := by
    funext p
    by_cases hsp : s < p.1 <;> simp [A, signedTail, tailInd_apply, hsp]
  have hInd : Integrable (fun p : ℝ × ℝ => A.indicator (fun _ => (1 : ℝ)) p) π :=
    (integrable_const (1 : ℝ)).indicator hA
  have hConst : Integrable (fun _ : ℝ × ℝ => tailInd 0 s) π :=
    integrable_const _
  have hIntA :
      (∫ p : ℝ × ℝ, A.indicator (fun _ => (1 : ℝ)) p ∂π) = π.real A := by
    exact (integral_indicator_one (μ := π) (s := A) hA)
  rw [hfun, integral_sub hInd hConst, hIntA, integral_const]
  simp [survFst, A, Measure.real, smul_eq_mul]


-- @@ L120-139 verbatim
/-- The `π`-integral of the signed tail indicator of the second coordinate. -/
lemma integral_signedTail_snd (h : IsCoupling π μ ν) (t : ℝ) :
    (∫ p : ℝ × ℝ, signedTail p.2 t ∂π) = survSnd π t - tailInd 0 t := by
  letI : IsProbabilityMeasure π := h.isProbabilityMeasure
  let B : Set (ℝ × ℝ) := Prod.snd ⁻¹' Ioi t
  have hB : MeasurableSet B := measurableSet_Ioi.preimage measurable_snd
  have hfun :
      (fun p : ℝ × ℝ => signedTail p.2 t)
        = fun p : ℝ × ℝ => B.indicator (fun _ => (1 : ℝ)) p - tailInd 0 t := by
    funext p
    by_cases htp : t < p.2 <;> simp [B, signedTail, tailInd_apply, htp]
  have hInd : Integrable (fun p : ℝ × ℝ => B.indicator (fun _ => (1 : ℝ)) p) π :=
    (integrable_const (1 : ℝ)).indicator hB
  have hConst : Integrable (fun _ : ℝ × ℝ => tailInd 0 t) π :=
    integrable_const _
  have hIntB :
      (∫ p : ℝ × ℝ, B.indicator (fun _ => (1 : ℝ)) p ∂π) = π.real B := by
    exact (integral_indicator_one (μ := π) (s := B) hB)
  rw [hfun, integral_sub hInd hConst, hIntB, integral_const]
  simp [survSnd, B, Measure.real, smul_eq_mul]


-- @@ L141-220 expanded
/-- **Fibre integral.** [For a coupling `π` of two probability measures](hyp:h), [the
`π`-integral of the product of the two signed tail indicators at thresholds `s` and
`t`](hyp:s,t) [equals the joint survival function of `π` at `(s, t)`, adjusted by cross
terms built from the two marginal survival functions and the sign indicators of `s` and
`t`](goal).

    `∫ p, signedTail p.1 s * signedTail p.2 t ∂π
       = S s t - 𝟙{t<0}·SX s - 𝟙{s<0}·SY t + 𝟙{s<0}·𝟙{t<0}`,

where `S` is the joint survival function of `π` and `SX, SY` its marginal
survival functions. Each of the four terms is the `π`-mass of a measurable set
(the quadrant `Ioi s ×ˢ Ioi t`, the two half-planes, and `univ`). -/
lemma fiber_integral_pi (h : IsCoupling π μ ν) (s t : ℝ) :
    (∫ p : ℝ × ℝ, signedTail p.1 s * signedTail p.2 t ∂π) =
      jointSurv π s t - tailInd 0 t * survFst π s - tailInd 0 s * survSnd π t +
        tailInd 0 s * tailInd 0 t :=
  by
  letI : IsProbabilityMeasure π := h.isProbabilityMeasure
  let A : Set (ℝ × ℝ) := Prod.fst ⁻¹' Ioi s
  let B : Set (ℝ × ℝ) := Prod.snd ⁻¹' Ioi t
  let C : Set (ℝ × ℝ) := Ioi s ×ˢ Ioi t
  let a : ℝ := tailInd 0 s
  let b : ℝ := tailInd 0 t
  have hA : MeasurableSet A := measurableSet_Ioi.preimage measurable_fst
  have hB : MeasurableSet B := measurableSet_Ioi.preimage measurable_snd
  have hC : MeasurableSet C := measurableSet_Ioi.prod measurableSet_Ioi
  have hfun :
    (fun p : ℝ × ℝ => signedTail p.1 s * signedTail p.2 t) = fun p : ℝ × ℝ =>
      C.indicator (fun _ => (1 : ℝ)) p - b * A.indicator (fun _ => (1 : ℝ)) p -
          a * B.indicator (fun _ => (1 : ℝ)) p +
        a * b :=
    by
    funext p
    by_cases hsp : s < p.1
    · by_cases htp : t < p.2
      ·
        by_cases hs0 : s < 0 <;> by_cases ht0 : t < 0 <;>
          simp [A, B, C, a, b, signedTail, tailInd_apply, hsp, htp, hs0, ht0]
      ·
        by_cases hs0 : s < 0 <;> by_cases ht0 : t < 0 <;>
          simp [A, B, C, a, b, signedTail, tailInd_apply, hsp, htp, hs0, ht0]
    · by_cases htp : t < p.2
      ·
        by_cases hs0 : s < 0 <;> by_cases ht0 : t < 0 <;>
          simp [A, B, C, a, b, signedTail, tailInd_apply, hsp, htp, hs0, ht0]
      ·
        by_cases hs0 : s < 0 <;> by_cases ht0 : t < 0 <;>
          simp [A, B, C, a, b, signedTail, tailInd_apply, hsp, htp, hs0, ht0]
  have hIA : Integrable (fun p : ℝ × ℝ => A.indicator (fun _ => (1 : ℝ)) p) π :=
    (integrable_const (1 : ℝ)).indicator hA
  have hIB : Integrable (fun p : ℝ × ℝ => B.indicator (fun _ => (1 : ℝ)) p) π :=
    (integrable_const (1 : ℝ)).indicator hB
  have hIC : Integrable (fun p : ℝ × ℝ => C.indicator (fun _ => (1 : ℝ)) p) π :=
    (integrable_const (1 : ℝ)).indicator hC
  have hBA : Integrable (fun p : ℝ × ℝ => b * A.indicator (fun _ => (1 : ℝ)) p) π := hIA.const_mul b
  have hAB : Integrable (fun p : ℝ × ℝ => a * B.indicator (fun _ => (1 : ℝ)) p) π := hIB.const_mul a
  have hK : Integrable (fun _ : ℝ × ℝ => a * b) π := integrable_const _
  have hIntA : (∫ p : ℝ × ℝ, A.indicator (fun _ => (1 : ℝ)) p ∂π) = π.real A := by
    exact (integral_indicator_one (μ := π) (s := A) hA)
  have hIntB : (∫ p : ℝ × ℝ, B.indicator (fun _ => (1 : ℝ)) p ∂π) = π.real B := by
    exact (integral_indicator_one (μ := π) (s := B) hB)
  have hIntC : (∫ p : ℝ × ℝ, C.indicator (fun _ => (1 : ℝ)) p ∂π) = π.real C := by
    exact (integral_indicator_one (μ := π) (s := C) hC)
  rw [hfun]
  change
    (∫ p : ℝ × ℝ,
        (((fun p : ℝ × ℝ => C.indicator (fun _ => (1 : ℝ)) p) -
                (fun p : ℝ × ℝ => b * A.indicator (fun _ => (1 : ℝ)) p) -
              (fun p : ℝ × ℝ => a * B.indicator (fun _ => (1 : ℝ)) p)) +
            (fun _ : ℝ × ℝ => a * b))
          p ∂π) =
      jointSurv π s t - tailInd 0 t * survFst π s - tailInd 0 s * survSnd π t +
        tailInd 0 s * tailInd 0 t
  first
  |
    simp (disch :=
      first
      | assumption
      | fun_prop) only [MeasureTheory.integral_add,
      MeasureTheory.integral_add', MeasureTheory.integral_sub, MeasureTheory.integral_sub',
      MeasureTheory.integral_neg, MeasureTheory.integral_neg', MeasureTheory.integral_finsetSum,
      MeasureTheory.integral_smul, MeasureTheory.integral_const_mul,
      MeasureTheory.integral_mul_const, MeasureTheory.integral_div]
  |
    fail "integral_linearity: nothing to normalize.\n\
                  The normal form is: `+`, `-`, negation, `•`, a factor independent of the \
                  integration variable, and finite sums all outside the `∫`. Either the goal is \
                  already in that form, or the integrand's linear structure is hidden (unfold or \
                  `integral_congr_ae` first), or an integrability side condition could not be \
                  discharged by `assumption` or `fun_prop` (state it as a `have`)."
  rw [hIntA, hIntB, hIntC, integral_const]
  simp [jointSurv, survFst, survSnd, A, B, C, a, b, Measure.real, smul_eq_mul]


-- @@ L222-222 verbatim
/-! ### Integrability on the triple product, and the two swaps -/


-- @@ L224-224 verbatim
end Causalean.Stat
