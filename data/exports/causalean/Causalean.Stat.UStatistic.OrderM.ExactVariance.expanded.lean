module
public import Causalean.Stat.UStatistic.OrderM.Variance


-- @@ L4-30 verbatim
/-!
# Exact variance for completely degenerate fixed-order U-statistics

This module proves the exact second-moment calculation for a completely
degenerate fixed-order kernel.  The cross-term lemmas
`IIDSample.crossterm_eq_zeta_of_image_eq` and
`IIDSample.crossterm_eq_zero_of_image_ne` classify pairs of injective tuples by
whether their images agree; `card_injectiveTuples_image_eq` counts the
same-image reorderings.

The headline variance identities are
`IIDSample.integral_injectiveTuples_sum_sq_degen` for the raw injective-tuple
sum and `IIDSample.integral_rescaled_order_sq_degen` for the `√n`-rescaled
degenerate U-statistic.  These sharpen the general rate bound to an exact
formula in the completely degenerate case.

The proof classifies pairs of injective tuples by whether their images agree.
Different images give a zero cross moment by complete degeneracy; equal images
differ by one of `m!` coordinate permutations and contribute the common kernel
second moment.
-/

/-
Copyright (c) 2026 Jiyuan Tan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiyuan Tan
-/


-- @@ L32-32 verbatim
@[expose] public section


-- @@ L34-34 verbatim
namespace Causalean.Stat


-- @@ L36-36 verbatim
open MeasureTheory ProbabilityTheory Filter Topology


-- @@ L38-39 verbatim
variable {Ω X : Type*} [MeasurableSpace Ω] [MeasurableSpace X]
  {μ : Measure Ω} {P : Measure X}


-- @@ L41-41 verbatim
namespace IIDSample


-- @@ L43-44 verbatim
variable [IsProbabilityMeasure μ] [IsProbabilityMeasure P]
  {m : ℕ} [NeZero m] {g : (Fin m → X) → ℝ} (S : IIDSample Ω X μ P)


-- @@ L46-46 verbatim
/-! ## Cross expectations by image comparison -/


-- @@ L48-92 verbatim
/-- Given [a nonnegative integer $m$ specifying the size of a source index set](hyp:m), [a
nonnegative integer $n$ specifying the size of a target index set](hyp:n), [two maps from the
source index set into the target index set](hyp:t,q), [the assumption that both maps are
injective](hyp:ht,hq), and [the assumption that their images coincide](hyp:himg), [the selected
permutation of the source index set](goal) reorders the first map into the second. [It first
selects, for each target value of the second map, a source index having the same first-map
value](step:1), and then selects, for each target value of the first map, a source index having
the same second-map value. -/
noncomputable def permOfImageEq {n : ℕ} {t q : Fin m → Fin n}
    (ht : Function.Injective t) (hq : Function.Injective q)
    (himg : Finset.univ.image t = Finset.univ.image q) : Equiv.Perm (Fin m) := by
  classical
  let f : Fin m → Fin m := fun j =>
    Classical.choose ((Finset.mem_image.mp (by
      have hqmem : q j ∈ Finset.univ.image q :=
        Finset.mem_image.mpr ⟨j, Finset.mem_univ j, rfl⟩
      simp [himg])) : ∃ i, i ∈ Finset.univ ∧ t i = q j)
  let r : Fin m → Fin m := fun i =>
    Classical.choose ((Finset.mem_image.mp (by
      have htmem : t i ∈ Finset.univ.image t :=
        Finset.mem_image.mpr ⟨i, Finset.mem_univ i, rfl⟩
      simp [← himg])) : ∃ j, j ∈ Finset.univ ∧ q j = t i)
  have hf : ∀ j, t (f j) = q j := by
    intro j
    exact (Classical.choose_spec ((Finset.mem_image.mp (by
      have hqmem : q j ∈ Finset.univ.image q :=
        Finset.mem_image.mpr ⟨j, Finset.mem_univ j, rfl⟩
      simp [himg])) : ∃ i, i ∈ Finset.univ ∧ t i = q j)).2
  have hr : ∀ i, q (r i) = t i := by
    intro i
    exact (Classical.choose_spec ((Finset.mem_image.mp (by
      have htmem : t i ∈ Finset.univ.image t :=
        Finset.mem_image.mpr ⟨i, Finset.mem_univ i, rfl⟩
      simp [← himg])) : ∃ j, j ∈ Finset.univ ∧ q j = t i)).2
  exact
    { toFun := f
      invFun := r
      left_inv := by
        intro j
        apply hq
        rw [hr (f j), hf j]
      right_inv := by
        intro i
        apply ht
        rw [hf (r i), hr i] }


-- @@ L94-104 verbatim
omit [NeZero m] in
private theorem permOfImageEq_apply {n : ℕ} {t q : Fin m → Fin n}
    (ht : Function.Injective t) (hq : Function.Injective q)
    (himg : Finset.univ.image t = Finset.univ.image q) (j : Fin m) :
    t (permOfImageEq (m := m) ht hq himg j) = q j := by
  classical
  simp only [permOfImageEq]
  exact (Classical.choose_spec ((Finset.mem_image.mp (by
    have hqmem : q j ∈ Finset.univ.image q :=
      Finset.mem_image.mpr ⟨j, Finset.mem_univ j, rfl⟩
    simp [himg])) : ∃ i, i ∈ Finset.univ ∧ t i = q j)).2


-- @@ L106-137 verbatim
omit [IsProbabilityMeasure μ] [IsProbabilityMeasure P] [NeZero m] in
/-- **Equal-image cross term.**  If two ordered injective `m`-tuples have the same
image, then `q` is a reordering of `t`, so by symmetry `g(Z_q) = g(Z_t)` and the
cross expectation is `ζ_m = zetaOrder P g`. -/
theorem crossterm_eq_zeta_of_image_eq (hmeas : Measurable g)
    (hsymm : ∀ σ : Equiv.Perm (Fin m), ∀ z, g (z ∘ σ) = g z)
    {n : ℕ} {t q : Fin m → Fin n}
    (ht : Function.Injective t) (hq : Function.Injective q)
    (himg : Finset.univ.image t = Finset.univ.image q) :
    ∫ ω, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω) ∂μ
      = zetaOrder P g := by
  classical
  let σ : Equiv.Perm (Fin m) := permOfImageEq (m := m) ht hq himg
  have hq_rewrite : ∀ ω,
      (fun j => S.Z (q j : ℕ) ω) = (fun j => S.Z (t j : ℕ) ω) ∘ σ := by
    intro ω
    funext j
    rw [Function.comp_apply]
    have hσ := permOfImageEq_apply (m := m) ht hq himg j
    rw [← hσ]
  have hcongr :
      (fun ω => g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω))
        =
      (fun ω => (g (fun j => S.Z (t j : ℕ) ω)) ^ 2) := by
    funext ω
    rw [hq_rewrite ω, hsymm σ]
    ring
  rw [hcongr]
  rw [zetaOrder, ← S.map_tuple_eq ht]
  rw [integral_map
    (measurable_pi_lambda _ (fun j : Fin m => S.meas (t j : ℕ))).aemeasurable
    (hmeas.pow_const 2).aestronglyMeasurable]


-- @@ L139-309 expanded
omit [IsProbabilityMeasure μ] [NeZero m] in
/-- **Distinct-image cross term.**  If two ordered injective `m`-tuples have
different images, complete degeneracy kills the cross expectation: some index of
`t` is absent from `q`, and integrating that coordinate out gives `0`. -/
theorem crossterm_eq_zero_of_image_ne (hmeas : Measurable g)
    (hsq : Integrable (fun z => (g z) ^ 2) (Measure.pi fun _ : Fin m => P))
    (hdeg :
      ∀ (j : Fin m) (tail : ({ k : Fin m // k ≠ j }) → X), ∫ x, g (insertCoord j x tail) ∂P = 0)
    {n : ℕ} {t q : Fin m → Fin n} (ht : Function.Injective t) (hq : Function.Injective q)
    (himg : Finset.univ.image t ≠ Finset.univ.image q) :
    ∫ ω, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω) ∂μ = 0 :=
  by
  letI : IsProbabilityMeasure μ :=
    ⟨by
      calc
        μ Set.univ = μ (S.Z 0 ⁻¹' Set.univ) := by simp
        _ = (μ.map (S.Z 0)) Set.univ := (Measure.map_apply (S.meas 0) MeasurableSet.univ).symm
        _ = P Set.univ := by rw [S.law]
        _ = 1 := measure_univ⟩
  classical
  let A : Finset (Fin n) := Finset.univ.image t
  let B : Finset (Fin n) := Finset.univ.image q
  have hAcard : A.card = m := by
    simpa [A] using (Finset.card_image_of_injective (s := (Finset.univ : Finset (Fin m))) ht)
  have hBcard : B.card = m := by
    simpa [B] using (Finset.card_image_of_injective (s := (Finset.univ : Finset (Fin m))) hq)
  have hnot_subset : ¬A ⊆ B := by
    intro hsub
    apply himg
    have hcard_le : B.card ≤ A.card := by rw [hAcard, hBcard]
    simpa [A, B] using (Finset.eq_of_subset_of_card_le hsub hcard_le)
  rcases Finset.not_subset.mp hnot_subset with ⟨a, haA, haB⟩
  rcases Finset.mem_image.mp haA with ⟨p, _hp, htp⟩
  subst a
  let R : Finset (Fin n) := (A ∪ B).erase (t p)
  let XR : Ω → (R → X) := fun ω i => S.Z (i.1 : ℕ) ω
  let πR : Measure (R → X) := Measure.pi fun _ : R => P
  let tailOf : (R → X) → ({ k : Fin m // k ≠ p } → X) := fun xr k =>
    xr
      ⟨t k.1,
        by
        have hmemA : t k.1 ∈ A := Finset.mem_image.mpr ⟨k.1, Finset.mem_univ k.1, rfl⟩
        have hne : t k.1 ≠ t p := fun h => k.2 (ht h)
        simp [R, hmemA, hne]⟩
  let Ψ : (R → X) → ℝ := fun xr =>
    g
      (fun j =>
        xr
          ⟨q j, by
            have hmemB : q j ∈ B := Finset.mem_image.mpr ⟨j, Finset.mem_univ j, rfl⟩
            have hne : q j ≠ t p := by
              intro h
              exact haB (by simpa [B, h] using hmemB)
            simp [R, hmemB, hne]⟩)
  let F : X × (R → X) → ℝ := fun z => g (insertCoord p z.1 (tailOf z.2)) * Ψ z.2
  have hXRmeas : Measurable XR := by fun_prop
  have hX0meas : Measurable (fun ω : Ω => S.Z (t p : ℕ) ω) := S.meas (t p : ℕ)
  have hPairMeas : Measurable (fun ω : Ω => (S.Z (t p : ℕ) ω, XR ω)) := hX0meas.prodMk hXRmeas
  have hXRmap : μ.map XR = πR :=
    by
    have hr : Function.Injective (fun i : R => i.1) :=
      by
      intro i j hij
      exact Subtype.ext hij
    simpa [XR, πR, R] using (S.map_fintype_tuple_eq (ι := R) (r := fun i : R => i.1) hr)
  have hdisj : Disjoint ({t p} : Finset (Fin n)) R :=
    by
    rw [Finset.disjoint_iff_inter_eq_empty]
    ext x
    constructor
    · intro hx
      rcases Finset.mem_inter.mp hx with ⟨hx0, hxR⟩
      have hxeq : x = t p := by simpa using hx0
      subst x
      simp [R] at hxR
    · intro hx
      simp at hx
  have hindFin : iIndepFun (fun i : Fin n => S.Z (i : ℕ)) μ :=
    S.indep.precomp (fun _ _ h => Fin.ext h)
  have hindBlocks :
    IndepFun (fun ω : Ω => fun i : ({t p} : Finset (Fin n)) => S.Z (i.1 : ℕ) ω) XR μ := by
    simpa [XR] using
      (ProbabilityTheory.iIndepFun.indepFun_finset ({t p} : Finset (Fin n)) R hdisj hindFin
        (fun i : Fin n => S.meas (i : ℕ)))
  have hEval : Measurable (fun x : ({t p} : Finset (Fin n)) → X => x ⟨t p, by simp⟩) :=
    measurable_pi_apply _
  have hind : IndepFun (fun ω : Ω => S.Z (t p : ℕ) ω) XR μ :=
    by
    have hcomp := hindBlocks.comp hEval measurable_id
    simpa [Function.comp_def] using hcomp
  have hΦ : Measurable (fun z : X × (R → X) => g (insertCoord p z.1 (tailOf z.2))) := by
    exact
      hmeas.comp
        (measurable_pi_lambda _
          (fun j : Fin m => by
            by_cases hj : j = p
            · subst j
              simpa [insertCoord] using measurable_fst
            ·
              simpa [tailOf, insertCoord, hj, Function.comp_def] using
                (measurable_pi_apply
                      (⟨t j,
                          by
                          have hmemA : t j ∈ A := Finset.mem_image.mpr ⟨j, Finset.mem_univ j, rfl⟩
                          have hne : t j ≠ t p := fun h => hj (ht h)
                          simp [R, hmemA, hne]⟩ :
                        R)).comp
                  measurable_snd))
  have hΨ : Measurable Ψ := by
    exact hmeas.comp (measurable_pi_lambda _ (fun j : Fin m => measurable_pi_apply _))
  have hFmeas : Measurable F := hΦ.mul (hΨ.comp measurable_snd)
  have ht_rewrite :
    ∀ ω, g (fun j => S.Z (t j : ℕ) ω) = g (insertCoord p (S.Z (t p : ℕ) ω) (tailOf (XR ω))) :=
    by
    intro ω
    congr 1
    funext j
    by_cases hj : j = p
    · subst j
      simp [insertCoord]
    · simp [tailOf, XR, insertCoord, hj]
  have hq_rewrite : ∀ ω, g (fun j => S.Z (q j : ℕ) ω) = Ψ (XR ω) :=
    by
    intro ω
    rfl
  have hcomp_int : Integrable (fun ω => F (S.Z (t p : ℕ) ω, XR ω)) μ :=
    by
    have hterm_mem :
      ∀ {r : Fin m → Fin n},
        Function.Injective r → MemLp (fun ω => g (fun j => S.Z (r j : ℕ) ω)) 2 μ :=
      by
      intro r hr
      have hm : AEStronglyMeasurable (fun ω => g (fun j => S.Z (r j : ℕ) ω)) μ :=
        (hmeas.comp
            (measurable_pi_lambda _ (fun j : Fin m => S.meas (r j : ℕ)))).aestronglyMeasurable
      apply (memLp_two_iff_integrable_sq hm).mpr
      have hmap :
        Integrable (fun z => (g z) ^ 2) (μ.map (fun ω : Ω => fun j : Fin m => S.Z (r j : ℕ) ω)) :=
        by
        rw [S.map_tuple_eq hr]
        exact hsq
      exact
        (integrable_map_measure (hmeas.pow_const 2).aestronglyMeasurable
              (measurable_pi_lambda _ (fun j : Fin m => S.meas (r j : ℕ))).aemeasurable).mp
          hmap
    have horig := (hterm_mem ht).integrable_mul (hterm_mem hq)
    refine horig.congr ?_
    filter_upwards with ω
    simp [F, ht_rewrite ω, hq_rewrite ω]
  have hmap_pair : μ.map (fun ω : Ω => (S.Z (t p : ℕ) ω, XR ω)) = P.prod (μ.map XR) :=
    by
    have h :=
      (indepFun_iff_map_prod_eq_prod_map_map hX0meas.aemeasurable hXRmeas.aemeasurable).mp hind
    simpa [S.map_eq (t p : ℕ)] using h
  have hFint_map : Integrable F (P.prod (μ.map XR)) :=
    by
    have hmap_int : Integrable F (μ.map fun ω : Ω => (S.Z (t p : ℕ) ω, XR ω)) :=
      (integrable_map_measure hFmeas.aestronglyMeasurable hPairMeas.aemeasurable).mpr hcomp_int
    simpa [hmap_pair] using hmap_int
  have hinner : ∀ xr : R → X, (∫ x : X, F (x, xr) ∂P) = 0 :=
    by
    intro xr
    change (∫ x : X, g (insertCoord p x (tailOf xr)) * Ψ xr ∂P) = 0
    first
    |
      simp (disch :=
        first
        | assumption
        | fun_prop) only [MeasureTheory.integral_add,
        MeasureTheory.integral_add', MeasureTheory.integral_sub, MeasureTheory.integral_sub',
        MeasureTheory.integral_neg, MeasureTheory.integral_neg', MeasureTheory.integral_finsetSum,
        MeasureTheory.integral_smul, MeasureTheory.integral_const_mul,
        MeasureTheory.integral_mul_const, MeasureTheory.integral_div]
    |
      fail "integral_linearity: nothing to normalize.\n\
                    The normal form is: `+`, `-`, negation, `•`, a factor independent of the \
                    integration variable, and finite sums all outside the `∫`. Either the goal is \
                    already in that form, or the integrand's linear structure is hidden (unfold or \
                    `integral_congr_ae` first), or an integrability side condition could not be \
                    discharged by `assumption` or `fun_prop` (state it as a `have`)."
    rw [hdeg p (tailOf xr), zero_mul]
  calc
    ∫ ω, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω) ∂μ =
        ∫ ω, F (S.Z (t p : ℕ) ω, XR ω) ∂μ :=
      by
      apply integral_congr_ae
      filter_upwards with ω
      simp [F, ht_rewrite ω, hq_rewrite ω]
    _ = ∫ z, F z ∂(μ.map fun ω : Ω => (S.Z (t p : ℕ) ω, XR ω)) := by
      rw [integral_map hPairMeas.aemeasurable hFmeas.aestronglyMeasurable]
    _ = ∫ z, F z ∂(P.prod (μ.map XR)) := by rw [hmap_pair]
    _ = ∫ xr, ∫ x, F (x, xr) ∂P ∂(μ.map XR) := by rw [integral_prod_symm F hFint_map]
    _ = 0 :=
      by
      rw [show (fun xr : R → X => ∫ x, F (x, xr) ∂P) = fun _ => 0
          by
          funext xr
          exact hinner xr]
      simp


-- @@ L311-311 verbatim
/-! ## Counting reorderings -/


-- @@ L313-349 verbatim
omit [NeZero m] in
/-- For an ordered injective `m`-tuple `t`, the ordered injective tuples with the
same image are exactly its `m!` reorderings. -/
theorem card_injectiveTuples_image_eq {n : ℕ} {t : Fin m → Fin n}
    (ht : Function.Injective t) :
    ((injectiveTuples m n).filter
        (fun q => Finset.univ.image q = Finset.univ.image t)).card = m.factorial := by
  classical
  have hcard_perm : (Finset.univ : Finset (Equiv.Perm (Fin m))).card = m.factorial := by
    rw [Finset.card_univ, Fintype.card_perm, Fintype.card_fin]
  rw [← hcard_perm]
  symm
  refine Finset.card_bij (fun σ _ => t ∘ σ) ?hmem ?hinj ?hsurj
  · intro σ _hσ
    rw [Finset.mem_filter]
    refine ⟨?_, ?_⟩
    · simp [injectiveTuples, ht.comp σ.injective]
    · ext x
      constructor
      · intro hx
        rcases Finset.mem_image.mp hx with ⟨j, _hj, hjx⟩
        exact Finset.mem_image.mpr ⟨σ j, Finset.mem_univ _, hjx⟩
      · intro hx
        rcases Finset.mem_image.mp hx with ⟨j, _hj, hjx⟩
        exact Finset.mem_image.mpr ⟨σ.symm j, Finset.mem_univ _, by simp [Function.comp, hjx]⟩
  · intro σ₁ _ σ₂ _ hσ
    ext j
    exact congrArg Fin.val (ht (congrFun hσ j))
  · intro q hq
    rw [Finset.mem_filter] at hq
    have hqinj : Function.Injective q := by
      simpa [injectiveTuples] using hq.1
    let σ : Equiv.Perm (Fin m) :=
      permOfImageEq (m := m) ht hqinj hq.2.symm
    refine ⟨σ, Finset.mem_univ σ, ?_⟩
    funext j
    exact permOfImageEq_apply (m := m) ht hqinj hq.2.symm j


-- @@ L351-351 verbatim
/-! ## Exact second moment and variance -/


-- @@ L353-441 expanded
omit [IsProbabilityMeasure μ] in
/-- **Exact second moment of the injective-tuple sum.** For an i.i.d. sample `S` and
sample size `n`, if the order-`m` kernel `g` is [completely degenerate: symmetric,
square-integrable, and with zero conditional mean after integrating out any single
coordinate](hyp:hg), then [the second moment of the sum of `g` over all ordered
injective `m`-tuples drawn from the first `n` sample indices equals `m! · n^{(m)} · ζ_m`,
where `n^{(m)}` is the number of such tuples and `ζ_m` is the kernel's second moment
under the `m`-fold product law](goal). -/
theorem integral_injectiveTuples_sum_sq_degen (hg : OrderDegenKernel P g) (n : ℕ) :
    ∫ ω, (∑ t ∈ injectiveTuples m n, g (fun j => S.Z (t j : ℕ) ω)) ^ 2 ∂μ =
      (m.factorial : ℝ) * injectiveTupleCount m n * zetaOrder P g :=
  by
  letI : IsProbabilityMeasure μ :=
    ⟨by
      calc
        μ Set.univ = μ (S.Z 0 ⁻¹' Set.univ) := by simp
        _ = (μ.map (S.Z 0)) Set.univ := (Measure.map_apply (S.meas 0) MeasurableSet.univ).symm
        _ = P Set.univ := by rw [S.law]
        _ = 1 := measure_univ⟩
  letI : IsProbabilityMeasure P := by
    rw [← S.law]
    exact Measure.isProbabilityMeasure_map (S.meas 0).aemeasurable
  classical
  let T : Finset (Fin m → Fin n) := injectiveTuples m n
  let F : (Fin m → Fin n) → (Fin m → Fin n) → ℝ := fun t q =>
    ∫ ω, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω) ∂μ
  have hinj_of_mem_T : ∀ t ∈ T, Function.Injective t :=
    by
    intro t ht
    have ht' : t ∈ injectiveTuples m n := by simpa [T] using ht
    exact (Finset.mem_filter.mp ht').2
  have hexpand :
    (fun ω => (∑ t ∈ T, g (fun j => S.Z (t j : ℕ) ω)) ^ 2) =
      (fun ω => ∑ t ∈ T, ∑ q ∈ T, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω)) :=
    by
    funext ω
    rw [sq, Finset.sum_mul_sum]
  rw [show
      (∫ ω, (∑ t ∈ injectiveTuples m n, g (fun j => S.Z (t j : ℕ) ω)) ^ 2 ∂μ) =
        ∫ ω, (∑ t ∈ T, g (fun j => S.Z (t j : ℕ) ω)) ^ 2 ∂μ
      by rfl]
  rw [hexpand]
  have hterm_int :
    ∀ t ∈ T,
      Integrable (fun ω => ∑ q ∈ T, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω))
        μ :=
    fun t ht => by
    apply integrable_finset_sum
    intro q hq
    exact S.integrable_orderTerm_mul hg.meas hg.sq (hinj_of_mem_T t ht) (hinj_of_mem_T q hq)
  first
  |
    simp (disch :=
      first
      | assumption
      | fun_prop) only [MeasureTheory.integral_add,
      MeasureTheory.integral_add', MeasureTheory.integral_sub, MeasureTheory.integral_sub',
      MeasureTheory.integral_neg, MeasureTheory.integral_neg', MeasureTheory.integral_finsetSum,
      MeasureTheory.integral_smul, MeasureTheory.integral_const_mul,
      MeasureTheory.integral_mul_const, MeasureTheory.integral_div]
  |
    fail "integral_linearity: nothing to normalize.\n\
                  The normal form is: `+`, `-`, negation, `•`, a factor independent of the \
                  integration variable, and finite sums all outside the `∫`. Either the goal is \
                  already in that form, or the integrand's linear structure is hidden (unfold or \
                  `integral_congr_ae` first), or an integrability side condition could not be \
                  discharged by `assumption` or `fun_prop` (state it as a `have`)."
  have hpush :
    ∀ t ∈ T,
      ∫ ω, ∑ q ∈ T, g (fun j => S.Z (t j : ℕ) ω) * g (fun j => S.Z (q j : ℕ) ω) ∂μ =
        ∑ q ∈ T, F t q :=
    by
    intro t ht
    exact
      integral_finset_sum _
        (fun q hq =>
          S.integrable_orderTerm_mul hg.meas hg.sq (hinj_of_mem_T t ht) (hinj_of_mem_T q hq))
  rw [Finset.sum_congr rfl hpush]
  have hinner : ∀ t ∈ T, ∑ q ∈ T, F t q = (m.factorial : ℝ) * zetaOrder P g :=
    by
    intro t ht
    let SAME : Finset (Fin m → Fin n) :=
      T.filter (fun q => Finset.univ.image q = Finset.univ.image t)
    have hsub : SAME ⊆ T := Finset.filter_subset _ _
    have hzero : ∀ q ∈ T, q ∉ SAME → F t q = 0 :=
      by
      intro q hq hqnot
      have htinj := hinj_of_mem_T t ht
      have hqinj := hinj_of_mem_T q hq
      have hne : Finset.univ.image t ≠ Finset.univ.image q :=
        by
        intro h
        exact hqnot (Finset.mem_filter.mpr ⟨hq, h.symm⟩)
      simpa [F] using S.crossterm_eq_zero_of_image_ne hg.meas hg.sq hg.deg htinj hqinj hne
    rw [← Finset.sum_subset hsub hzero]
    have hsame : ∀ q ∈ SAME, F t q = zetaOrder P g :=
      by
      intro q hq
      have htinj := hinj_of_mem_T t ht
      have hqT : q ∈ T := (Finset.mem_filter.mp hq).1
      have hqinj := hinj_of_mem_T q hqT
      have himg : Finset.univ.image t = Finset.univ.image q := (Finset.mem_filter.mp hq).2.symm
      simpa [F] using S.crossterm_eq_zeta_of_image_eq hg.meas hg.symm htinj hqinj himg
    rw [Finset.sum_congr rfl hsame]
    rw [Finset.sum_const, nsmul_eq_mul]
    have htinj := hinj_of_mem_T t ht
    have hcard : SAME.card = m.factorial := by
      simpa [SAME, T] using card_injectiveTuples_image_eq (m := m) (n := n) htinj
    rw [hcard]
  rw [Finset.sum_congr rfl hinner]
  rw [Finset.sum_const, nsmul_eq_mul]
  simp only [T, injectiveTupleCount]
  ring


-- @@ L443-481 expanded
omit [IsProbabilityMeasure μ] in
/-- **Exact variance of the rescaled degenerate fixed-order U-statistic.** For an i.i.d.
sample `S`, if the order-`m` kernel `g` is [completely degenerate](hyp:hg) and the
sample size [is at least `m`](hyp:hmn), then [the second moment of the `√n`-rescaled
order-`m` U-statistic of `g` equals `n · m! · ζ_m / n^{(m)}`, which specializes to
`2ζ/(n−1)` when `m = 2`](goal). -/
theorem integral_rescaled_order_sq_degen (hg : OrderDegenKernel P g) {n : ℕ} (hmn : m ≤ n) :
    ∫ ω, (Real.sqrt (n : ℝ) * uStatisticOrder S g n ω) ^ 2 ∂μ =
      (n : ℝ) * (m.factorial : ℝ) * zetaOrder P g / injectiveTupleCount m n :=
  by
  letI : IsProbabilityMeasure μ :=
    ⟨by
      calc
        μ Set.univ = μ (S.Z 0 ⁻¹' Set.univ) := by simp
        _ = (μ.map (S.Z 0)) Set.univ := (Measure.map_apply (S.meas 0) MeasurableSet.univ).symm
        _ = P Set.univ := by rw [S.law]
        _ = 1 := measure_univ⟩
  letI : IsProbabilityMeasure P := by
    rw [← S.law]
    exact Measure.isProbabilityMeasure_map (S.meas 0).aemeasurable
  classical
  let K : Ω → ℝ := fun ω => ∑ t ∈ injectiveTuples m n, g (fun j => S.Z (t j : ℕ) ω)
  have hcount_ne : injectiveTupleCount m n ≠ 0 := injectiveTupleCount_ne_zero hmn
  have hnnonneg : 0 ≤ (n : ℝ) := by positivity
  have hpoint :
    (fun ω => (Real.sqrt (n : ℝ) * uStatisticOrder S g n ω) ^ 2) =
      (fun ω => ((n : ℝ) * (injectiveTupleCount m n)⁻¹ ^ 2) * (K ω) ^ 2) :=
    by
    funext ω
    simp only [uStatisticOrder, K]
    rw [mul_pow, Real.sq_sqrt hnnonneg]
    ring
  rw [hpoint]
  first
  |
    simp (disch :=
      first
      | assumption
      | fun_prop) only [MeasureTheory.integral_add,
      MeasureTheory.integral_add', MeasureTheory.integral_sub, MeasureTheory.integral_sub',
      MeasureTheory.integral_neg, MeasureTheory.integral_neg', MeasureTheory.integral_finsetSum,
      MeasureTheory.integral_smul, MeasureTheory.integral_const_mul,
      MeasureTheory.integral_mul_const, MeasureTheory.integral_div]
  |
    fail "integral_linearity: nothing to normalize.\n\
                  The normal form is: `+`, `-`, negation, `•`, a factor independent of the \
                  integration variable, and finite sums all outside the `∫`. Either the goal is \
                  already in that form, or the integrand's linear structure is hidden (unfold or \
                  `integral_congr_ae` first), or an integrability side condition could not be \
                  discharged by `assumption` or `fun_prop` (state it as a `have`)."
  rw [show
      (∫ ω, (K ω) ^ 2 ∂μ) = ∫ ω, (∑ t ∈ injectiveTuples m n, g (fun j => S.Z (t j : ℕ) ω)) ^ 2 ∂μ by
      rfl]
  rw [S.integral_injectiveTuples_sum_sq_degen hg n]
  field_simp [hcount_ne]


-- @@ L483-483 verbatim
end IIDSample


-- @@ L485-485 verbatim
end Causalean.Stat
