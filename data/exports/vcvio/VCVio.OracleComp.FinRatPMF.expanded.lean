/-
Copyright (c) 2025 Quang Dao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Quang Dao
-/

module
public import VCVio.OracleComp.EvalDist
public import VCVio.EvalDist.Instances.FinRatPMF


-- @@ L11-16 verbatim
/-!
# Executable `FinRatPMF` Semantics for `OracleComp`

This file provides a computable oracle evaluator using `FinRatPMF.Raw` and proves that its
denotational semantics agree with the existing `evalSPMF` semantics of `OracleComp`.
-/


-- @@ L18-18 verbatim
@[expose] public section


-- @@ L20-20 verbatim
open OracleSpec OracleComp


-- @@ L22-22 verbatim
universe u v


-- @@ L24-24 verbatim
namespace FinRatPMF


-- @@ L26-26 verbatim
variable {ι : Type u} {spec : OracleSpec ι}


-- @@ L28-31 verbatim
/-- Computable query implementation using the executable `FinRatPMF.Raw` monad. -/
def finRatImpl [spec.Inhabited] [∀ t : spec.Domain, FinEnum (spec.Range t)] :
    QueryImpl spec Raw :=
  fun t => Raw.uniform (α := spec.Range t)


-- @@ L33-33 verbatim
namespace finRatImpl


-- @@ L35-35 verbatim
variable [spec.Inhabited] [∀ t : spec.Domain, FinEnum (spec.Range t)]


-- @@ L37-38 verbatim
local instance instSpecFintypeOfFinEnum : spec.Fintype where
  fintypeB _ := inferInstance


-- @@ L40-41 verbatim
noncomputable local instance instIsUniformSpec : IsUniformSpec spec :=
  IsUniformSpec.ofFintypeInhabited _


-- @@ L43-52 verbatim
@[simp] lemma toPMF_apply (t : spec.Domain) :
    @Raw.toPMF _ (Classical.decEq _) (finRatImpl (spec := spec) t) =
      PMF.uniformOfFintype (spec.Range t) := by
  let : DecidableEq (spec.Range t) := Classical.decEq _
  ext x
  simp only [finRatImpl, Raw.toPMF_apply, PMF.uniformOfFintype_apply]
  rw [Raw.prob_eq_prob (Classical.decEq _) FinEnum.decEq, Raw.prob_uniform]
  have hcard : Fintype.card (spec.Range t) ≠ 0 := Fintype.card_ne_zero
  rw [NNRat.cast_inv, ENNReal.coe_inv (by exact_mod_cast hcard)]
  simp


-- @@ L54-57 expanded
@[simp]
lemma evalSPMF_apply (t : spec.Domain) :
    evalSPMF (finRatImpl (spec := spec) t) = liftM (PMF.uniformOfFintype (spec.Range t)) :=
  by
  change (liftM (@Raw.toPMF _ (Classical.decEq _) (finRatImpl (spec := spec) t)) : SPMF _) = _
  rw [toPMF_apply]


-- @@ L59-63 expanded
@[simp]
lemma evalSPMF_simulateQ {α : Type v} (oa : OracleComp spec α) :
    evalSPMF (simulateQ (finRatImpl (spec := spec)) oa) = evalSPMF oa := by
  induction oa using OracleComp.inductionOn with
  | pure x => simp
  | query_bind t mx h => simp [evalSPMF_apply, OracleComp.evalSPMF_query, h]


-- @@ L65-68 expanded
@[simp]
lemma probOutput_simulateQ {α : Type v} (oa : OracleComp spec α) (x : α) :
    probOutput (simulateQ (finRatImpl (spec := spec)) oa) x = probOutput oa x := by
  rw [probOutput_def, probOutput_def, evalSPMF_simulateQ]


-- @@ L70-73 expanded
@[simp]
lemma probEvent_simulateQ {α : Type v} (oa : OracleComp spec α) (p : α → Prop) :
    probEvent (simulateQ (finRatImpl (spec := spec)) oa) p = probEvent oa p := by
  simp only [probEvent_eq_tsum_indicator, probOutput_simulateQ]


-- @@ L75-77 verbatim
@[simp] lemma support_simulateQ {α : Type v} (oa : OracleComp spec α) :
    support (simulateQ (finRatImpl (spec := spec)) oa) = support oa :=
  Set.ext fun x => mem_support_iff_of_evalSPMF_eq (evalSPMF_simulateQ (spec := spec) oa) x


-- @@ L79-83 verbatim
lemma finSupport_simulateQ {α : Type v} [DecidableEq α]
    (oa : OracleComp spec α) :
    finSupport (simulateQ (finRatImpl (spec := spec)) oa) = finSupport oa := by
  apply Finset.coe_injective
  rw [coe_finSupport, coe_finSupport, support_simulateQ]


-- @@ L85-85 verbatim
end finRatImpl


-- @@ L87-87 verbatim
namespace Demo


-- @@ L89-100 verbatim
local instance : FinEnum Bool where
  card := 2
  equiv :=
    { toFun := fun b => if b then ⟨1, by decide⟩ else ⟨0, by decide⟩
      invFun := fun i => i.1 = 1
      left_inv := by
        intro b
        cases b <;> rfl
      right_inv := by
        intro i
        fin_cases i <;> rfl }
  decEq := inferInstance


-- @@ L102-105 verbatim
instance : (t : coinSpec.Domain) → FinEnum (coinSpec.Range t) := by
  intro t
  change FinEnum Bool
  infer_instance


-- @@ L107-110 verbatim
def xorTwoCoins : FinRatPMF.Raw Bool := do
  let b1 ← FinRatPMF.Raw.coin
  let b2 ← FinRatPMF.Raw.coin
  pure (b1 != b2)


-- @@ L112-116 verbatim
def threeCoinCount : FinRatPMF.Raw Nat := do
  let b1 ← FinRatPMF.Raw.coin
  let b2 ← FinRatPMF.Raw.coin
  let b3 ← FinRatPMF.Raw.coin
  pure (cond b1 1 0 + cond b2 1 0 + cond b3 1 0)


-- @@ L118-131 verbatim
def twoCoinQueries : OracleComp coinSpec Nat := do
  let b1 ← OracleComp.coin
  let b2 ← OracleComp.coin
  pure (cond b1 1 0 + cond b2 1 0)

/-
#eval FinRatPMF.Raw.coin
#eval xorTwoCoins
#eval xorTwoCoins.normalize
#eval threeCoinCount.normalize
#eval! simulateQ (FinRatPMF.finRatImpl (spec := coinSpec)) twoCoinQueries
#eval! (simulateQ (FinRatPMF.finRatImpl (spec := coinSpec)) twoCoinQueries).normalize
#eval! (simulateQ (FinRatPMF.finRatImpl (spec := coinSpec)) twoCoinQueries).normalize.prob 1)
-/


-- @@ L133-133 verbatim
end Demo

-- @@ L134-134 verbatim
end FinRatPMF
