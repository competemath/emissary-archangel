/-
Copyright (c) 2026 Quang Dao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma, Quang Dao
-/

module
public import Examples.ElGamal.Common
public import VCVio.CryptoFoundations.AsymmEncAlg.INDCPA
public import VCVio.CryptoFoundations.HardnessAssumptions.DiffieHellman


-- @@ L12-44 verbatim
/-!
# ElGamal Encryption: IND-CPA via the generic one-time lift

This file defines ElGamal encryption over a module `Module F G` and treats the security proof as
a one-time DDH client of the generic IND-CPA lift in `AsymmEncAlg`.

## Mathematical notation

We use additive / EC-style notation throughout:

| Textbook (multiplicative) | This file (additive)             |
|---------------------------|----------------------------------|
| `g^a`                     | `a • gen`                        |
| `g^a · g^b = g^{a+b}`     | `a • gen + b • gen`              |
| `(g^a)^b = g^{ab}`        | `b • (a • gen) = (b * a) • gen` |
| `m · g^{ab}`              | `m + (a * b) • gen`              |

Here `F` is the scalar field (for example `ZMod p`), `G` is the additive group of ciphertext
payloads (for example elliptic-curve points), and `gen : G` is a fixed public generator.

## Proof structure

1. ElGamal definition and correctness.
2. One-time DDH bridge:
   `IND_CPA_OneTime_DDHReduction`,
   `IND_CPA_OneTime_game_evalSPMF_eq_ddhExpReal`,
   `IND_CPA_OneTime_DDHReduction_rand_half`, and
   `elGamal_oneTime_signedAdvantageReal_abs_eq_two_mul_ddhGuessAdvantage`.
3. Final theorem:
   `elGamal_IND_CPA_le_q_mul_ddh` is a direct instantiation of
   `AsymmEncAlg.IND_CPA_advantage_toReal_le_q_mul_of_oneTime_signedAdvantageReal_bound`
   with one-time loss `2 * ε`.
-/


-- @@ L46-46 verbatim
@[expose] public section


-- @@ L48-48 verbatim
open OracleSpec OracleComp ENNReal


-- @@ L50-50 verbatim
variable {F : Type} [Field F] [Fintype F] [DecidableEq F] [SampleableType F]

-- @@ L51-51 verbatim
variable {G : Type} [AddCommGroup G] [Module F G] [SampleableType G]


-- @@ L53-69 expanded
/-- ElGamal encryption over a module `Module F G` with generator `gen : G`.

Key generation samples a scalar `sk ← $ᵗ F` and returns `(sk • gen, sk)`.
Encryption of `msg` under public key `pk` samples `r ← $ᵗ F` and returns
`(r • gen, msg + r • pk)`. Decryption recovers `msg` as `c₂ - sk • c₁`. -/
@[simps!]
def elGamalAsymmEnc (F G : Type) [Field F] [Fintype F] [DecidableEq F] [SampleableType F]
    [AddCommGroup G] [Module F G] [SampleableType G] (gen : G) :
    AsymmEncAlg ProbComp (M := G) (PK := G) (SK := F) (C := G × G)
    where
  keygen := do
    let sk ← uniformSample F
    return (sk • gen, sk)
  encrypt := fun pk msg => do
    let r ← uniformSample F
    return (r • gen, msg + r • pk)
  decrypt := fun sk (c₁, c₂) => return (some (c₂ - sk • c₁))


-- @@ L71-71 verbatim
namespace elGamalAsymmEnc


-- @@ L73-73 verbatim
variable {F : Type} [Field F] [Fintype F] [DecidableEq F] [SampleableType F]

-- @@ L74-74 verbatim
variable {G : Type} [AddCommGroup G] [Module F G] [SampleableType G]

-- @@ L75-75 verbatim
variable {gen : G}


-- @@ L77-87 verbatim
/-- The shared one-time DDH reduction body, parameterized by its three effectful operations.

The closed probabilistic reduction, the open adversary interface, the profiled reduction, and the
fully syntactic oracle program all instantiate this definition. -/
abbrev oneTimeDDHReductionBody {m : Type → Type} [Monad m] {State : Type}
    (chooseMessages : m (G × G × State)) (coin : m Bool)
    (distinguish : State → G × G → m Bool) (B T : G) : m Bool :=
  chooseMessages >>= fun ⟨m₁, m₂, state⟩ ↦
    coin >>= fun bit ↦ do
      let bit' ← distinguish state (B, T + if bit then m₁ else m₂)
      pure (bit == bit')


-- @@ L89-99 verbatim
/-- ElGamal decryption perfectly inverts encryption: `Dec(sk, Enc(pk, msg)) = msg`. -/
theorem correct [DecidableEq G] :
    (elGamalAsymmEnc F G gen).PerfectlyCorrect ProbCompRuntime.probComp := by
  have hcancel : ∀ (msg : G) (sk r : F),
      msg + r • (sk • gen) - sk • (r • gen) = msg := by
    intro msg sk r
    have : r • (sk • gen) = sk • (r • gen) := by
      rw [← mul_smul, ← mul_smul, mul_comm]
    rw [this, add_sub_cancel_right]
  simp [AsymmEncAlg.PerfectlyCorrect, ProbCompRuntime.probComp, ProbCompRuntime.evalSPMF,
    AsymmEncAlg.CorrectExp, elGamalAsymmEnc, hcancel, probFailure_of_liftM_PMF]


-- @@ L101-101 verbatim
section IND_CPA


-- @@ L103-103 verbatim
variable [DecidableEq G]


-- @@ L105-105 verbatim
local instance : Inhabited G := ⟨0⟩


-- @@ L107-113 expanded
/-- One-time DDH reduction for ElGamal. On input `(gen, A, B, T)`, use `A` as the ElGamal public
key, form the challenge ciphertext `(B, T + m_b)`, and return whether the one-time adversary
guessed the hidden bit `b`. -/
def IND_CPA_OneTime_DDHReduction (adv : AsymmEncAlg.IND_CPA_Adv (elGamalAsymmEnc F G gen)) :
    DiffieHellman.DDHAdversary F G := fun _ A B T =>
  oneTimeDDHReductionBody (adv.chooseMessages A) (uniformSample Bool) adv.distinguish B T


-- @@ L115-150 expanded
omit [DecidableEq G] in
/-- Real-branch identification for the one-time ElGamal reduction. After unfolding
`IND_CPA_OneTime_Game_ProbComp`, `elGamalAsymmEnc`, `DiffieHellman.ddhExpReal`, and
`IND_CPA_OneTime_DDHReduction`, both sides normalize to the same sample space. -/
private lemma IND_CPA_OneTime_game_evalSPMF_eq_ddhExpReal
    (adv : AsymmEncAlg.IND_CPA_Adv (elGamalAsymmEnc F G gen)) :
    evalSPMF (AsymmEncAlg.IND_CPA_OneTime_Game_ProbComp (encAlg := elGamalAsymmEnc F G gen) adv) =
      evalSPMF
        (DiffieHellman.ddhExpReal (F := F) gen
          (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv)) :=
  by
  simp only [AsymmEncAlg.IND_CPA_OneTime_Game_ProbComp, DiffieHellman.ddhExpReal,
    IND_CPA_OneTime_DDHReduction, elGamalAsymmEnc]
  ext z
  change probOutput _ z = probOutput _ z
  simp only [bind_pure_comp, bind_map_left]
  simp only [map_eq_pure_bind]
    -- Step 1: swap $ᵗ Bool past $ᵗ F in LHS
    
  rw [probOutput_bind_bind_swap (uniformSample Bool) (uniformSample F)]
    -- Now LHS starts with $ᵗ F. Use congr under $ᵗ F.
    
  refine
    probOutput_bind_congr' (uniformSample F) z
      (fun sk => ?_)
        -- Step 2: swap $ᵗ Bool past chooseMessages in LHS
        
  rw [probOutput_bind_bind_swap (uniformSample Bool) (adv.chooseMessages (sk • gen))]
    -- Step 3: swap chooseMessages past $ᵗ F in RHS
    
  conv_rhs =>
    rw [probOutput_bind_bind_swap (uniformSample F) (adv.chooseMessages (sk • gen))]
      -- Now both start with chooseMessages. Congr under it.
      
  refine
    probOutput_bind_congr' (adv.chooseMessages (sk • gen)) z
      (fun cm => ?_)
        -- Step 4: swap $ᵗ Bool past $ᵗ F in LHS
        
  rw [probOutput_bind_bind_swap (uniformSample Bool) (uniformSample F)]
    -- Now both: $ᵗ F >>= fun r => $ᵗ Bool >>= fun bit => ...
    
  refine probOutput_bind_congr' (uniformSample F) z (fun r => ?_)
  refine
    probOutput_bind_congr' (uniformSample Bool) z
      (fun bit => ?_)
        -- Now need to show the ciphertext expressions match
          -- LHS: (r•gen, (if bit then cm.1 else cm.2.1) + r • (sk • gen))
          -- RHS: (r•gen, (sk * r)•gen + (if bit then cm.1 else cm.2.1))
        
  congr 2
  rw [smul_smul, add_comm, mul_comm]


-- @@ L152-297 expanded
omit [DecidableEq G] in
/-- Random-branch half lemma for the one-time ElGamal reduction. Under bijectivity of `(· • gen)`,
the DDH-random branch gives a uniform additive mask independent of the challenge bit, so the
adversary can do no better than random guessing. -/
private lemma IND_CPA_OneTime_DDHReduction_rand_half (hg : Function.Bijective (· • gen : F → G))
    (adv : AsymmEncAlg.IND_CPA_Adv (elGamalAsymmEnc F G gen)) :
    probOutput
        (DiffieHellman.ddhExpRand (F := F) gen
          (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv))
        true =
      1 / 2 :=
  by
  let inner : G → ProbComp Bool := fun pk => do
    let head ← (uniformSample G)
    let mask ← (uniformSample G)
    let (m₁, m₂, st) ← adv.chooseMessages pk
    let bit ← (uniformSample Bool)
    let bit' ← adv.distinguish st (head, mask + if bit then m₁ else m₂)
    pure (decide (bit = bit'))
  let f : G → Bool → ProbComp Bool := fun pk bit => do
    let head ← (uniformSample G)
    let (m₁, m₂, st) ← adv.chooseMessages pk
    let mask ← (uniformSample G)
    adv.distinguish st (head, mask + if bit then m₁ else m₂)
  have hf : ∀ pk, evalSPMF (f pk true) = evalSPMF (f pk false) :=
    by
    intro pk
    unfold f
    rw [evalSPMF_bind, evalSPMF_bind]
    congr 1
    funext head
    rw [evalSPMF_bind, evalSPMF_bind]
    congr 1
    funext x
    rcases x with ⟨m₁, m₂, st⟩
    simpa [add_comm] using
      ElGamalExamples.uniformMaskedCipher_bind_dist_indep (head := head) (m₁ := m₁) (m₂ := m₂)
        (cont := adv.distinguish st)
  have hrepr :
    ∀ pk,
      probOutput (inner pk) true =
        probOutput
          (do
            let bit ← (uniformSample Bool)
            let bit' ← f pk bit
            pure (decide (bit = bit')))
          true :=
    by
    intro pk
    trans
      probOutput
        (do
          let head ← (uniformSample G)
          let x ← adv.chooseMessages pk
          let bit ← (uniformSample Bool)
          let mask ← (uniformSample G)
          let bit' ← adv.distinguish x.2.2 (head, mask + if bit then x.1 else x.2.1)
          pure (decide (bit = bit')))
        true
    · refine probOutput_bind_congr' (uniformSample G) true ?_
      intro head
      simpa [inner, monad_norm] using
        (probOutput_bind_bind_swap (uniformSample G)
          (do
            let x ← adv.chooseMessages pk
            let bit ← (uniformSample Bool)
            pure (x, bit))
          (fun mask ⟨x, bit⟩ => do
            let bit' ← adv.distinguish x.2.2 (head, mask + if bit then x.1 else x.2.1)
            pure (decide (bit = bit')))
          true)
    ·
      simpa [f, monad_norm] using
        (probOutput_bind_bind_swap
          (do
            let head ← (uniformSample G)
            let x ← adv.chooseMessages pk
            pure (head, x))
          (uniformSample Bool)
          (fun ⟨head, x⟩ bit => do
            let mask ← (uniformSample G)
            let bit' ← adv.distinguish x.2.2 (head, mask + if bit then x.1 else x.2.1)
            pure (decide (bit = bit')))
          true)
  have hhalf : ∀ pk, probOutput (inner pk) true = 1 / 2 :=
    by
    intro pk
    rw [hrepr pk]
    exact probOutput_decide_eq_uniformBool_half (f pk) (hf pk)
  calc
    probOutput
          (DiffieHellman.ddhExpRand (F := F) gen
            (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv))
          true =
        probOutput
          (do
            let pk ← (uniformSample G)
            inner pk)
          true :=
      by
      trans
        probOutput
          (do
            let pk ← (uniformSample G)
            let b ← (uniformSample F)
            let c ← (uniformSample F)
            let (m₁, m₂, st) ← adv.chooseMessages pk
            let bit ← (uniformSample Bool)
            let bit' ← adv.distinguish st (b • gen, c • gen + if bit then m₁ else m₂)
            pure (decide (bit = bit')))
          true
      ·
        simpa [DiffieHellman.ddhExpRand, IND_CPA_OneTime_DDHReduction, oneTimeDDHReductionBody,
          monad_norm, show ∀ a b : Bool, (a == b) = decide (a = b) from by decide] using
          (probOutput_bind_bijective_uniform_cross (α := F) (β := G) (f := (· • gen)) hg (g :=
            fun pk => do
            let b ← (uniformSample F)
            let c ← (uniformSample F)
            let (m₁, m₂, st) ← adv.chooseMessages pk
            let bit ← (uniformSample Bool)
            let bit' ← adv.distinguish st (b • gen, c • gen + if bit then m₁ else m₂)
            pure (decide (bit = bit'))) true)
      · refine probOutput_bind_congr' (uniformSample G) true ?_
        intro pk
        trans
          probOutput
            (do
              let head ← (uniformSample G)
              let c ← (uniformSample F)
              let (m₁, m₂, st) ← adv.chooseMessages pk
              let bit ← (uniformSample Bool)
              let bit' ← adv.distinguish st (head, c • gen + if bit then m₁ else m₂)
              pure (decide (bit = bit')))
            true
        ·
          simpa [monad_norm] using
            (probOutput_bind_bijective_uniform_cross (α := F) (β := G) (f := (· • gen)) hg (g :=
              fun head => do
              let c ← (uniformSample F)
              let (m₁, m₂, st) ← adv.chooseMessages pk
              let bit ← (uniformSample Bool)
              let bit' ← adv.distinguish st (head, c • gen + if bit then m₁ else m₂)
              pure (decide (bit = bit'))) true)
        · refine probOutput_bind_congr' (uniformSample G) true ?_
          intro head
          simpa [inner, monad_norm] using
            (probOutput_bind_bijective_uniform_cross (α := F) (β := G) (f := (· • gen)) hg (g :=
              fun mask => do
              let (m₁, m₂, st) ← adv.chooseMessages pk
              let bit ← (uniformSample Bool)
              let bit' ← adv.distinguish st (head, mask + if bit then m₁ else m₂)
              pure (decide (bit = bit'))) true)
    _ =
        probOutput
          (do
            let pk ← (uniformSample G)
            (uniformSample Bool))
          true :=
      (probOutput_bind_congr' (uniformSample G) true
        (fun pk => by simpa [probOutput_uniformSample] using hhalf pk))
    _ = 1 / 2 := by
      rw [probOutput_bind_eq_tsum]
      have hbool : probOutput (uniformSample Bool) true = (1 / 2 : ℝ≥0∞) := by
        simp [probOutput_uniformSample]
      simp_rw [hbool]
      have hsum : ∑' x : G, probOutput (uniformSample G) x = 1 :=
        tsum_probOutput_of_liftM_PMF (uniformSample G)
      rw [ENNReal.tsum_mul_right, hsum, one_mul]


-- @@ L299-331 expanded
omit [DecidableEq G] in
/-- The absolute one-time signed IND-CPA advantage of ElGamal is exactly twice the DDH guess
advantage of the reduction above. The factor `2` is essential because the DDH guess advantage is
defined from the mixed experiment, while the one-time IND-CPA game compares the real and random
branches directly. -/
theorem elGamal_oneTime_signedAdvantageReal_abs_eq_two_mul_ddhGuessAdvantage
    (hg : Function.Bijective (· • gen : F → G))
    (adv : AsymmEncAlg.IND_CPA_Adv (elGamalAsymmEnc F G gen)) :
    |AsymmEncAlg.IND_CPA_OneTime_signedAdvantageReal (encAlg := elGamalAsymmEnc F G gen) adv| =
      2 *
        DiffieHellman.ddhGuessAdvantage gen
          (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv) :=
  by
  have h_real :
    |AsymmEncAlg.IND_CPA_OneTime_signedAdvantageReal (encAlg := elGamalAsymmEnc F G gen) adv| =
      |(probOutput
              (DiffieHellman.ddhExpReal (F := F) gen
                (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv))
              true).toReal -
          1 / 2| :=
    by
    have hprob :
      probOutput (AsymmEncAlg.IND_CPA_OneTime_Game_ProbComp (encAlg := elGamalAsymmEnc F G gen) adv)
          true =
        probOutput
          (DiffieHellman.ddhExpReal (F := F) gen
            (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv))
          true :=
      probOutput_congr rfl (IND_CPA_OneTime_game_evalSPMF_eq_ddhExpReal adv)
    simpa [AsymmEncAlg.IND_CPA_OneTime_signedAdvantageReal] using
      congrArg (fun p : ℝ≥0∞ => |p.toReal - 1 / 2|) hprob
  have h_rand :
    (1 : ℝ) / 2 =
      (probOutput
          (DiffieHellman.ddhExpRand (F := F) gen
            (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv))
          true).toReal :=
    by
    rw [IND_CPA_OneTime_DDHReduction_rand_half hg adv]
    simp [ENNReal.toReal_ofNat]
  rw [h_real, h_rand]
  exact
    DiffieHellman.ddhDistAdvantage_eq_two_mul_ddhGuessAdvantage gen
      (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv)


-- @@ L333-356 verbatim
/-- **Main theorem.** If an adversary makes at most `q` LR queries and every extracted one-time
ElGamal DDH reduction has guess advantage at most `ε`, then ElGamal has IND-CPA advantage at most
`q * (2 * ε)`. -/
theorem elGamal_IND_CPA_le_q_mul_ddh
    (hg : Function.Bijective (· • gen : F → G))
    (adversary : (elGamalAsymmEnc F G gen).IND_CPA_adversary)
    (q : ℕ) (ε : ℝ)
    (hq : adversary.MakesAtMostQueries q)
    (hddh : ∀ adv : AsymmEncAlg.IND_CPA_Adv (elGamalAsymmEnc F G gen),
      DiffieHellman.ddhGuessAdvantage gen
        (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv) ≤ ε) :
    ((elGamalAsymmEnc F G gen).IND_CPA_advantage adversary).toReal ≤ q * (2 * ε) := by
  refine AsymmEncAlg.IND_CPA_advantage_toReal_le_q_mul_of_oneTime_signedAdvantageReal_bound
    (encAlg' := elGamalAsymmEnc F G gen) adversary q (2 * ε) hq ?_
  intro adv
  calc
    |AsymmEncAlg.IND_CPA_OneTime_signedAdvantageReal
        (encAlg := elGamalAsymmEnc F G gen) adv|
      = 2 * DiffieHellman.ddhGuessAdvantage gen
          (IND_CPA_OneTime_DDHReduction (F := F) (G := G) (gen := gen) adv) :=
            elGamal_oneTime_signedAdvantageReal_abs_eq_two_mul_ddhGuessAdvantage
              (F := F) (G := G) (gen := gen) hg adv
    _ ≤ 2 * ε := by
        linarith [hddh adv]


-- @@ L358-358 verbatim
end IND_CPA


-- @@ L360-360 verbatim
end elGamalAsymmEnc
