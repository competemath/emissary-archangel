/-
Copyright (c) 2024 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma, Quang Dao
-/

module
public import VCVio.OracleComp.QueryTracking.QueryBound
public import VCVio.OracleComp.QueryTracking.Structures
public import VCVio.OracleComp.QueryTracking.Tracing
public import VCVio.OracleComp.SimSemantics.Append
public import VCVio.OracleComp.SimSemantics.QueryImpl.Basic
public import PolyFun.PFunctor.Free.Path.Execution
public import ToMathlib.Control.WriterT


-- @@ L16-30 verbatim
/-!
# Logging Queries Made by a Computation

`QueryImpl.withLogging` records every query/response pair `⟨t, u⟩` to a
`WriterT (QueryLog spec)` writer layer. It is a response-dependent trace,
defined as a specialisation of `QueryImpl.withTraceAppend` (see
`Tracing.lean`): the log is appended *after* the underlying handler returns,
so a handler failure leaves no log entry for the failed query.

We use the `Append`-flavoured `withTraceAppend` (rather than the `Monoid`
flavoured `withTrace`) because `QueryLog spec = List _` only carries an
`[EmptyCollection, Append, LawfulAppend]` structure, not a `Monoid`. This
matches the pre-existing `Monad (WriterT (QueryLog spec) m)` instance the
rest of `WriterTBridge` / `mvcgen` infrastructure already targets.
-/


-- @@ L32-32 verbatim
@[expose] public section


-- @@ L34-34 verbatim
universe u v w


-- @@ L36-36 verbatim
open OracleSpec OracleComp


-- @@ L38-38 verbatim
open scoped OracleSpec.PrimitiveQuery


-- @@ L40-40 verbatim
variable {ι} {spec : OracleSpec ι} {α β γ : Type u}


-- @@ L42-42 verbatim
namespace QueryImpl


-- @@ L44-44 verbatim
variable {m : Type u → Type v} [Monad m]


-- @@ L46-46 verbatim
section writerTMapBase


-- @@ L48-48 verbatim
variable {ι₀ ι₁ : Type u} {spec₀ : OracleSpec ι₀} {spec₁ : OracleSpec ι₁}

-- @@ L49-49 verbatim
variable {m₁ : Type u → Type v} [Monad m₁]

-- @@ L50-50 verbatim
variable {ω : Type u} [EmptyCollection ω] [Append ω]


-- @@ L52-58 verbatim
/-- Push an outer oracle interpretation through the base monad of a
`WriterT`-valued query implementation. -/
def writerTMapBase
    (outer : QueryImpl spec₁ m₁)
    (inner : QueryImpl spec₀ (WriterT ω (OracleComp spec₁))) :
    QueryImpl spec₀ (WriterT ω m₁) := fun t =>
  WriterT.mk (simulateQ outer ((inner t).run))


-- @@ L60-66 verbatim
omit [EmptyCollection ω] [Append ω] in
@[simp]
theorem writerTMapBase_apply
    (outer : QueryImpl spec₁ m₁)
    (inner : QueryImpl spec₀ (WriterT ω (OracleComp spec₁)))
    (t : spec₀.Domain) :
    (outer.writerTMapBase inner t).run = simulateQ outer ((inner t).run) := rfl


-- @@ L68-79 verbatim
/-- Running a `WriterT` handler and then interpreting its base oracle
computations is the same as first mapping the handler's base through the
outer interpreter. -/
theorem simulateQ_writerTMapBase_run [LawfulMonad m₁] [LawfulAppend ω]
    (outer : QueryImpl spec₁ m₁)
    (inner : QueryImpl spec₀ (WriterT ω (OracleComp spec₁)))
    {α : Type u} (oa : OracleComp spec₀ α) :
    simulateQ outer ((simulateQ inner oa).run) =
      (simulateQ (outer.writerTMapBase inner) oa).run := by
  induction oa using OracleComp.inductionOn with
  | pure x => simp
  | query_bind t k ih => simp [writerTMapBase, ih]


-- @@ L81-81 verbatim
end writerTMapBase


-- @@ L83-91 verbatim
/-- Given that `so` implements the oracles in `spec` using the monad `m`,
`withLogging so` gives the same implementation in the extension `WriterT (QueryLog spec) m`,
by appending a single-entry log `[⟨t, u⟩]` *after* the handler returns response `u`.

This is the response-dependent specialisation of `QueryImpl.withTraceAppend` with the
trace function `fun t u => [⟨t, u⟩]` (a single-element list, the free-monoid
generator of `QueryLog spec = List ((t : spec.Domain) × spec.Range t)`). -/
def withLogging (so : QueryImpl spec m) : QueryImpl spec (WriterT (QueryLog spec) m) :=
  so.withTraceAppend (fun t u => [⟨t, u⟩])


-- @@ L93-94 verbatim
lemma withLogging_eq_withTraceAppend (so : QueryImpl spec m) :
    so.withLogging = so.withTraceAppend (fun t u => [⟨t, u⟩]) := rfl


-- @@ L96-101 verbatim
@[simp, grind =]
lemma withLogging_apply (so : QueryImpl spec m) (t : spec.Domain) :
    so.withLogging t = do let u ← so t; tell [⟨t, u⟩]; return u := by
  rw [withLogging_eq_withTraceAppend]
  exact withTraceAppend_apply so
    (fun (t : spec.Domain) u => ([⟨t, u⟩] : QueryLog spec)) t


-- @@ L103-106 verbatim
lemma fst_map_run_withLogging [LawfulMonad m] (so : QueryImpl spec m) (mx : OracleComp spec α) :
    Prod.fst <$> (simulateQ (so.withLogging) mx).run =
    simulateQ so mx :=
  so.fst_map_run_withTraceAppend (fun (t : spec.Domain) u => ([⟨t, u⟩] : QueryLog spec)) mx


-- @@ L108-117 expanded
/-- Logging preserves failure probability: for any base monad `m` with `MonadLiftT m SPMF`,
wrapping an oracle implementation with `withLogging` does not change the probability of failure.
When `m = OracleComp spec`, both sides are `0` (trivially true). When `m` can genuinely fail
(e.g. `OptionT (OracleComp spec)`), this is a non-trivial faithfulness property. -/
lemma probFailure_run_simulateQ_withLogging [LawfulMonad m] [MonadLiftT m SPMF]
    [LawfulMonadLiftT m SPMF] (so : QueryImpl spec m) (mx : OracleComp spec α) :
    probFailure (simulateQ (so.withLogging) mx).run = probFailure (simulateQ so mx) :=
  so.probFailure_run_simulateQ_withTraceAppend
    (fun (t : spec.Domain) u => ([⟨t, u⟩] : QueryLog spec)) mx


-- @@ L119-124 verbatim
lemma NeverFail_run_simulateQ_withLogging_iff [LawfulMonad m] [MonadLiftT m SPMF]
    [LawfulMonadLiftT m SPMF]
    (so : QueryImpl spec m) (mx : OracleComp spec α) :
    NeverFail (simulateQ (so.withLogging) mx).run ↔ NeverFail (simulateQ so mx) :=
  so.neverFail_run_simulateQ_withTraceAppend_iff
    (fun (t : spec.Domain) u => ([⟨t, u⟩] : QueryLog spec)) mx


-- @@ L126-126 verbatim
variable {κ : Type} {loggedSpec : OracleSpec κ}


-- @@ L128-128 verbatim
section inputLog


-- @@ L130-130 verbatim
variable {ι₀ : Type} {spec₀ : OracleSpec.{0, 0} ι₀}

-- @@ L131-131 verbatim
variable {κ : Type} {loggedSpec : OracleSpec.{0, 0} κ}

-- @@ L132-132 verbatim
variable {m₀ : Type → Type v} [Monad m₀]


-- @@ L134-144 verbatim
/-- Run an implementation and append each queried input to a `StateT` list.

This is the state-transformer analogue of `withLogging` when only the query
inputs are needed: responses are returned exactly as in the base
implementation, while the state records the input sequence in order.

Defined as the response-independent `preInsert` instrumentation that appends
the queried input `t` to the state list before delegating to `so`. -/
def appendInputLog (so : QueryImpl loggedSpec m₀) :
    QueryImpl loggedSpec (StateT (List loggedSpec.Domain) m₀) :=
  so.preInsert (fun t => modify (· ++ [t]))


-- @@ L146-147 verbatim
lemma appendInputLog_eq_preInsert (so : QueryImpl loggedSpec m₀) :
    appendInputLog so = so.preInsert (fun t => modify (· ++ [t])) := rfl


-- @@ L149-153 verbatim
@[simp, grind =]
lemma appendInputLog_apply [LawfulMonad m₀] (so : QueryImpl loggedSpec m₀)
    (t : loggedSpec.Domain) :
    appendInputLog so t = (do modify (· ++ [t]); liftM (so t)) := by
  exact preInsert_apply so (fun t => modify (· ++ [t])) t


-- @@ L155-160 verbatim
lemma run_withLogging_apply [LawfulMonad m₀] (so : QueryImpl loggedSpec m₀)
    (t : loggedSpec.Domain) :
    (so.withLogging t).run =
      (so t >>= fun u =>
        (pure (u, [⟨t, u⟩]) : m₀ (loggedSpec.Range t × QueryLog loggedSpec))) := by
  simp


-- @@ L162-181 verbatim
/-- Every entry emitted while simulating one primitive query records that query's input.
This response-independent provenance fact is stable even when the query was transported from
a component of a dependent sum specification. -/
lemma fst_eq_input_of_mem_support_run_simulateQ_withLogging_liftM
    [LawfulMonad m₀] [MonadLiftT m₀ SetM] [LawfulMonadLiftT m₀ SetM]
    {α' : Type} (so : QueryImpl loggedSpec m₀) (q : OracleQuery loggedSpec α')
    {z : α' × QueryLog loggedSpec}
    (hz : z ∈ support ((simulateQ so.withLogging
      (liftM q : OracleComp loggedSpec α')).run))
    {e : (t : loggedSpec.Domain) × loggedSpec.Range t} (he : e ∈ z.2) :
    e.1 = q.input := by
  rw [simulateQ_query, WriterT.run_map', support_map] at hz
  obtain ⟨y, hy, hz⟩ := hz
  subst z
  rw [run_withLogging_apply, mem_support_bind_iff] at hy
  obtain ⟨u, _, hy⟩ := hy
  simp only [support_pure, Set.mem_singleton_iff] at hy
  subst hy
  simp only [Prod.map_apply, id_eq, List.mem_singleton] at he
  exact congrArg Sigma.fst he


-- @@ L183-202 verbatim
/-- State-transformer form of
`fst_eq_input_of_mem_support_run_simulateQ_withLogging_liftM`. -/
lemma fst_eq_input_of_mem_support_run_simulateQ_withLogging_liftM_stateT
    {σ : Type} [LawfulMonad m₀] [MonadLiftT m₀ SetM] [LawfulMonadLiftT m₀ SetM]
    {α' : Type} (so : QueryImpl loggedSpec (StateT σ m₀))
    (q : OracleQuery loggedSpec α') (s : σ)
    {z : (α' × QueryLog loggedSpec) × σ}
    (hz : z ∈ support (((simulateQ so.withLogging
      (liftM q : OracleComp loggedSpec α')).run).run s))
    {e : (t : loggedSpec.Domain) × loggedSpec.Range t} (he : e ∈ z.1.2) :
    e.1 = q.input := by
  rw [simulateQ_query, WriterT.run_map', StateT.run_map, support_map] at hz
  obtain ⟨y, hy, hz⟩ := hz
  subst z
  rw [run_withLogging_apply, StateT.run_bind, mem_support_bind_iff] at hy
  obtain ⟨us, _, hy⟩ := hy
  simp only [StateT.run_pure, support_pure, Set.mem_singleton_iff] at hy
  subst hy
  simp only [Prod.map_apply, id_eq, List.mem_singleton] at he
  exact congrArg Sigma.fst he


-- @@ L204-208 verbatim
lemma run_appendInputLog_apply [LawfulMonad m₀] (so : QueryImpl loggedSpec m₀)
    (t : loggedSpec.Domain) (inputs : List loggedSpec.Domain) :
    (appendInputLog so t).run inputs =
      (so t >>= fun u => pure (u, inputs ++ [t])) := by
  simp


-- @@ L210-260 verbatim
/-- A `WriterT` query log can be replayed as a `StateT` input log.

For computations over a sum `spec + loggedSpec`, this theorem compares two
implementations:

* left queries in `spec` are forwarded unchanged;
* right queries in `loggedSpec` are either handled with `withLogging`, producing
  a `QueryLog loggedSpec`, or with `appendInputLog`, appending just the queried
  inputs to a state list.

Mapping the WriterT result to `(output, initialInputs ++ loggedInputs)` yields
exactly the same base-monad computation as running the StateT
implementation from `initialInputs`. -/
theorem map_run_withLogging_inputs_eq_run_appendInputLog
    [LawfulMonad m₀] [HasQuery spec₀ m₀]
    {α' : Type}
    (so : QueryImpl loggedSpec m₀)
    (oa : OracleComp (spec₀ + loggedSpec) α')
    (initialInputs : List loggedSpec.Domain) :
    let baseW : QueryImpl spec₀ (WriterT (QueryLog loggedSpec) m₀) :=
      (HasQuery.toQueryImpl (spec := spec₀) (m := m₀)).liftTarget _
    let implW : QueryImpl (spec₀ + loggedSpec)
        (WriterT (QueryLog loggedSpec) m₀) :=
      baseW + QueryImpl.withLogging so
    let baseS : QueryImpl spec₀ (StateT (List loggedSpec.Domain) m₀) :=
      (HasQuery.toQueryImpl (spec := spec₀) (m := m₀)).liftTarget _
    let implAppend : QueryImpl (spec₀ + loggedSpec)
        (StateT (List loggedSpec.Domain) m₀) :=
      baseS + appendInputLog so
    ((fun z : α' × QueryLog loggedSpec =>
        (z.1, initialInputs ++ z.2.map (fun e => e.1))) <$>
          ((simulateQ implW oa).run : m₀ (α' × QueryLog loggedSpec))) =
      ((simulateQ implAppend oa).run initialInputs : m₀ (α' × List loggedSpec.Domain)) := by
  induction oa using OracleComp.inductionOn generalizing initialInputs with
  | pure x => simp
  | query_bind t oa ih =>
      dsimp only
      cases t with
      | inl t' =>
          rw [simulateQ_add_query_bind_left, simulateQ_add_query_bind_left]
          simp only [QueryImpl.liftTarget_apply, WriterT.run_bind', WriterT.run_liftM,
            StateT.run_bind, StateT.run_monadLift, monadLift_self, map_bind,
            monad_norm, List.empty_eq]
          exact bind_congr fun u => by
            simpa [Function.comp_apply] using ih u initialInputs
      | inr t' =>
          rw [simulateQ_add_query_bind_right, simulateQ_add_query_bind_right]
          simp only [WriterT.run_bind', StateT.run_bind, map_bind]
          rw [run_withLogging_apply, run_appendInputLog_apply]
          simp only [bind_assoc, pure_bind, Functor.map_map]
          exact bind_congr fun u => by simpa [List.append_assoc] using ih u (initialInputs ++ [t'])


-- @@ L262-262 verbatim
end inputLog


-- @@ L264-264 verbatim
end QueryImpl


-- @@ L266-271 verbatim
/-- Simulation oracle for tracking the queries in a `QueryLog`, without modifying the actual
behavior of the oracle. Each query/response pair is appended to a single `WriterT` log via
`QueryImpl.withLogging`, leaving the underlying `OracleComp` computation unchanged. -/
def OracleSpec.loggingOracle {ι : Type u} {spec : OracleSpec.{u, u} ι} :
    QueryImpl spec (WriterT (QueryLog spec) (OracleComp spec)) :=
  (QueryImpl.ofLift spec (OracleComp spec)).withLogging


-- @@ L273-273 verbatim
namespace loggingOracle


-- @@ L275-282 expanded
/-- Specialization of `QueryImpl.probFailure_run_simulateQ_withLogging` to `loggingOracle`. -/
lemma probFailure_simulateQ {spec : OracleSpec.{0, 0} ι} {α : Type} [IsUniformSpec spec]
    (oa : OracleComp spec α) :
    probFailure
        (WriterT.run (simulateQ spec.loggingOracle oa) : OracleComp spec (α × spec.QueryLog)) =
      probFailure oa :=
  by rw [loggingOracle, QueryImpl.probFailure_run_simulateQ_withLogging, simulateQ_ofLift_eq_self]


-- @@ L284-288 verbatim
@[simp]
lemma fst_map_run_simulateQ {spec : OracleSpec.{0, 0} ι} {α : Type}
    (oa : OracleComp spec α) :
    Prod.fst <$> (simulateQ spec.loggingOracle oa).run = oa := by
  rw [loggingOracle, QueryImpl.fst_map_run_withLogging, simulateQ_ofLift_eq_self]


-- @@ L290-294 verbatim
@[simp]
lemma run_simulateQ_bind_fst {spec : OracleSpec.{0, 0} ι} {α β : Type}
    (oa : OracleComp spec α) (ob : α → OracleComp spec β) :
    ((simulateQ spec.loggingOracle oa).run >>= fun x => ob x.1) = oa >>= ob := by
  rw [← bind_map_left Prod.fst, fst_map_run_simulateQ]


-- @@ L296-302 verbatim
/-- Specialization of `QueryImpl.NeverFail_run_simulateQ_withLogging_iff` to `loggingOracle`. -/
@[simp]
lemma NeverFail_run_simulateQ_iff {spec : OracleSpec.{0, 0} ι} {α : Type}
    [IsUniformSpec spec]
    (oa : OracleComp spec α) :
    NeverFail (simulateQ spec.loggingOracle oa).run ↔ NeverFail oa := by
  rw [loggingOracle, QueryImpl.NeverFail_run_simulateQ_withLogging_iff, simulateQ_ofLift_eq_self]


-- @@ L304-310 expanded
@[simp]
lemma probEvent_fst_run_simulateQ {spec : OracleSpec.{0, 0} ι} {α : Type} [IsUniformSpec spec]
    (oa : OracleComp spec α) (p : α → Prop) :
    (probEvent (simulateQ spec.loggingOracle oa).run fun z => p z.1) = probEvent oa p := by
  rw [show (fun z : α × spec.QueryLog => p z.1) = p ∘ Prod.fst from rfl, ← probEvent_map,
    fst_map_run_simulateQ]


-- @@ L312-317 expanded
lemma probOutput_fst_map_run_simulateQ {spec : OracleSpec.{0, 0} ι} {α : Type} [IsUniformSpec spec]
    (oa : OracleComp spec α) (x : α) :
    probOutput (Prod.fst <$> (simulateQ spec.loggingOracle oa).run) x = probOutput oa x := by
  rw [fst_map_run_simulateQ]


-- @@ L319-322 expanded
lemma evalSPMF_fst_map_run_simulateQ {spec : OracleSpec.{0, 0} ι} {α : Type} [IsUniformSpec spec]
    (oa : OracleComp spec α) :
    evalSPMF (Prod.fst <$> (simulateQ spec.loggingOracle oa).run) = evalSPMF oa := by
  rw [fst_map_run_simulateQ]


-- @@ L324-327 verbatim
lemma support_fst_map_run_simulateQ {spec : OracleSpec.{0, 0} ι} {α : Type}
    [IsUniformSpec spec] (oa : OracleComp spec α) :
    support (Prod.fst <$> (simulateQ spec.loggingOracle oa).run) = support oa := by
  rw [fst_map_run_simulateQ]


-- @@ L329-329 verbatim
end loggingOracle


-- @@ L331-331 verbatim
namespace OracleComp


-- @@ L333-340 expanded
lemma run_simulateQ_loggingOracle_query_bind {ι : Type u} {spec : OracleSpec.{u, u} ι} {α : Type u}
    (t : spec.Domain) (mx : spec.Range t → OracleComp spec α) :
    (simulateQ loggingOracle (liftM (OracleSpec.query t) >>= mx)).run =
      (OracleSpec.query t : OracleComp spec _) >>= fun u =>
        (fun p : α × QueryLog spec => (p.1, (⟨t, u⟩ : (i : spec.Domain) × spec.Range i) :: p.2)) <$>
          (simulateQ loggingOracle (mx u)).run :=
  by simp [loggingOracle]


-- @@ L342-342 verbatim
section isQueryBound


-- @@ L344-349 verbatim
theorem isTotalQueryBound_run_simulateQ_loggingOracle_iff
    {ι : Type} {spec : OracleSpec.{0, 0} ι} {α : Type}
    (oa : OracleComp spec α) (n : ℕ) :
    IsTotalQueryBound ((simulateQ loggingOracle oa).run) n ↔
    IsTotalQueryBound oa n :=
  isQueryBound_iff_of_map_eq (loggingOracle.fst_map_run_simulateQ oa) _ _


-- @@ L351-356 verbatim
theorem isQueryBoundP_run_simulateQ_loggingOracle_iff
    {ι : Type} {spec : OracleSpec.{0, 0} ι} [IsUniformSpec spec] {α : Type}
    (oa : OracleComp spec α) (p : ι → Prop) [DecidablePred p] (n : ℕ) :
    IsQueryBoundP ((simulateQ loggingOracle oa).run) p n ↔
    IsQueryBoundP oa p n :=
  isQueryBoundP_iff_of_map_eq (p := p) (loggingOracle.fst_map_run_simulateQ oa)


-- @@ L358-365 verbatim
theorem isTotalQueryBound_run_simulateQ_withLogging_iff
    {ι : Type} {spec : OracleSpec.{0, 0} ι}
    {ι' : Type} {spec' : OracleSpec.{0, 0} ι'}
    (so : QueryImpl spec (OracleComp spec'))
    {α : Type} (mx : OracleComp spec α) (n : ℕ) :
    IsTotalQueryBound ((simulateQ (so.withLogging) mx).run) n ↔
    IsTotalQueryBound (simulateQ so mx) n :=
  isQueryBound_iff_of_map_eq (QueryImpl.fst_map_run_withLogging so mx) _ _


-- @@ L367-375 verbatim
theorem isQueryBoundP_run_simulateQ_withLogging_iff
    {ι : Type} {spec : OracleSpec.{0, 0} ι}
    {ι' : Type} {spec' : OracleSpec.{0, 0} ι'} [IsUniformSpec spec']
    (so : QueryImpl spec (OracleComp spec'))
    {α : Type} (mx : OracleComp spec α)
    (q : ι' → Prop) [DecidablePred q] (n : ℕ) :
    IsQueryBoundP ((simulateQ (so.withLogging) mx).run) q n ↔
    IsQueryBoundP (simulateQ so mx) q n :=
  isQueryBoundP_iff_of_map_eq (p := q) (QueryImpl.fst_map_run_withLogging so mx)


-- @@ L377-383 verbatim
theorem isPerIndexQueryBound_run_simulateQ_loggingOracle_iff
    {ι : Type} [DecidableEq ι] {spec : OracleSpec.{0, 0} ι}
    [IsUniformSpec spec] {α : Type}
    (oa : OracleComp spec α) (qb : ι → ℕ) :
    IsPerIndexQueryBound ((simulateQ loggingOracle oa).run) qb ↔
    IsPerIndexQueryBound oa qb :=
  isPerIndexQueryBound_iff_of_map_eq (loggingOracle.fst_map_run_simulateQ oa)


-- @@ L385-393 verbatim
theorem isPerIndexQueryBound_run_simulateQ_withLogging_iff
    {ι : Type} {spec : OracleSpec.{0, 0} ι}
    {ι' : Type} [DecidableEq ι'] {spec' : OracleSpec.{0, 0} ι'}
    [IsUniformSpec spec']
    (so : QueryImpl spec (OracleComp spec'))
    {α : Type} (mx : OracleComp spec α) (qb : ι' → ℕ) :
    IsPerIndexQueryBound ((simulateQ (so.withLogging) mx).run) qb ↔
    IsPerIndexQueryBound (simulateQ so mx) qb :=
  isPerIndexQueryBound_iff_of_map_eq (QueryImpl.fst_map_run_withLogging so mx)


-- @@ L395-419 verbatim
/-- A total query bound controls the length of every `loggingOracle` trace in support:
if `oa` makes at most `n` queries, then every support point of
`(simulateQ loggingOracle oa).run` has log length at most `n`. -/
theorem log_length_le_of_mem_support_run_simulateQ
    {ι : Type} {spec : OracleSpec.{0, 0} ι}
    [spec.DecidableEq] [IsUniformSpec spec] {α : Type}
    {oa : OracleComp spec α} {n : ℕ}
    (hbound : IsTotalQueryBound oa n)
    {z : α × QueryLog spec}
    (hz : z ∈ support ((simulateQ loggingOracle oa).run)) :
    z.2.length ≤ n := by
  induction oa using OracleComp.inductionOn generalizing n z with
  | pure x =>
      simp only [simulateQ_pure] at hz
      subst hz
      simp
  | query_bind t mx ih =>
      rw [isTotalQueryBound_query_bind_iff] at hbound
      obtain ⟨hpos, hrest⟩ := hbound
      rw [run_simulateQ_loggingOracle_query_bind, support_bind] at hz
      simp only [Set.mem_iUnion, support_map] at hz
      obtain ⟨u, _, z', hz', rfl⟩ := hz
      have := ih u (hrest u) hz'
      simp only [List.length_cons]
      omega


-- @@ L421-421 verbatim
end isQueryBound


-- @@ L423-426 verbatim
/-- Add a query log to a computation using a logging oracle. -/
@[reducible] def withQueryLog {α} (mx : OracleComp spec α) :
    OracleComp spec (α × QueryLog spec) :=
  WriterT.run (simulateQ (QueryImpl.ofLift spec (OracleComp spec)).withLogging mx)


-- @@ L428-434 verbatim
/-- Query logging is resource-transparent: it preserves the exact total query-bound predicate. -/
theorem isTotalQueryBound_withQueryLog_iff {α : Type}
    (mx : OracleComp spec α) (n : ℕ) :
    IsTotalQueryBound mx.withQueryLog n ↔ IsTotalQueryBound mx n := by
  simpa [withQueryLog] using
    (isTotalQueryBound_run_simulateQ_withLogging_iff
      (QueryImpl.ofLift spec (OracleComp spec)) mx n)


-- @@ L436-440 verbatim
/-- Erase an intrinsic execution path to its output and query log. -/
@[reducible] def pathLogResult {i : Type} {oSpec : OracleSpec.{0, 0} i} {β : Type}
    (mx : OracleComp oSpec β) (path : PFunctor.FreeM.Path mx) :
    β × QueryLog oSpec :=
  (PFunctor.FreeM.output mx path, PFunctor.FreeM.Path.trace mx path)


-- @@ L442-445 verbatim
@[reducible] private def loggedPath
    {i : Type} {oSpec : OracleSpec.{0, 0} i} {β : Type}
    (mx : OracleComp oSpec β) : OracleComp oSpec (PFunctor.FreeM.Path mx) :=
  PFunctor.FreeM.withPath mx


-- @@ L447-456 expanded
@[simp]
private theorem loggedPath_query_bind {i : Type} {oSpec : OracleSpec.{0, 0} i} {β : Type}
    (t : oSpec.Domain) (next : oSpec.Range t → OracleComp oSpec β) :
    loggedPath (liftM (OracleSpec.query t) >>= next) =
      OracleComp.queryBind t fun u =>
        PFunctor.FreeM.map
          (fun path : PFunctor.FreeM.Path (next u) =>
            (⟨u, path⟩ : PFunctor.FreeM.Path (OracleComp.queryBind t next)))
          (loggedPath (next u)) :=
  rfl


-- @@ L458-461 verbatim
@[reducible] private def loggedRun
    {i : Type} {oSpec : OracleSpec.{0, 0} i} {β : Type}
    (mx : OracleComp oSpec β) : OracleComp oSpec (β × QueryLog oSpec) :=
  (simulateQ oSpec.loggingOracle mx).run


-- @@ L463-488 verbatim
private theorem map_pathLogResult_loggedPath
    {i : Type} {oSpec : OracleSpec.{0, 0} i} {β : Type}
    (mx : OracleComp oSpec β) :
    PFunctor.FreeM.map (pathLogResult mx) (loggedPath mx) = loggedRun mx := by
  induction mx using OracleComp.inductionOn with
  | pure x => rfl
  | query_bind t next ih =>
      rw [loggedRun, OracleComp.run_simulateQ_loggingOracle_query_bind,
        loggedPath_query_bind]
      simp only [PFunctor.FreeM.map]
      change
        OracleComp.queryBind t (fun u =>
          PFunctor.FreeM.map
            (pathLogResult (OracleComp.queryBind t next))
            (PFunctor.FreeM.map
              (fun path : PFunctor.FreeM.Path (next u) =>
                (⟨u, path⟩ : PFunctor.FreeM.Path (OracleComp.queryBind t next)))
              (loggedPath (next u)))) =
          OracleComp.queryBind t (fun u =>
            PFunctor.FreeM.map
              (fun p : β × QueryLog oSpec => (p.1, ⟨t, u⟩ :: p.2))
              (loggedRun (next u)))
      apply congrArg (OracleComp.queryBind t)
      funext u
      rw [← ih u, ← PFunctor.FreeM.comp_map, ← PFunctor.FreeM.comp_map]
      rfl


-- @@ L490-497 verbatim
/-- Retaining a computation's intrinsic path and then erasing it to an
output/log pair is writer-style query logging. -/
theorem map_pathLogResult_withPath
    {i : Type} {oSpec : OracleSpec.{0, 0} i} {β : Type}
    (mx : OracleComp oSpec β) :
    PFunctor.FreeM.map (pathLogResult mx) (PFunctor.FreeM.withPath mx) =
      mx.withQueryLog := by
  exact map_pathLogResult_loggedPath mx


-- @@ L499-505 verbatim
/-- `withQueryLog` distributes over `bind`: the combined log is the
concatenation of the prefix's log and the continuation's log. -/
lemma withQueryLog_bind {ι : Type} {spec : OracleSpec.{0, 0} ι} {α β : Type}
    (mx : OracleComp spec α) (my : α → OracleComp spec β) :
    (mx >>= my).withQueryLog =
      mx.withQueryLog >>= fun p => Prod.map id (p.2 ++ ·) <$> (my p.1).withQueryLog := by
  simp only [withQueryLog, simulateQ_bind, WriterT.run_bind']


-- @@ L507-512 verbatim
/-- `withQueryLog` of `pure x` produces `(x, [])` — no oracle queries,
empty log. -/
@[simp, grind =]
lemma withQueryLog_pure {ι : Type} {spec : OracleSpec.{0, 0} ι} {α : Type} (x : α) :
    (pure x : OracleComp spec α).withQueryLog = pure (x, []) :=
  rfl


-- @@ L514-520 verbatim
/-- `withQueryLog` of a single `query t` produces `(u, [⟨t, u⟩])` where
`u` is the oracle response: one query, one log entry. -/
lemma withQueryLog_query
    {ι : Type} {spec : OracleSpec.{0, 0} ι} (t : spec.Domain) :
    (liftM (OracleSpec.query t) : OracleComp spec _).withQueryLog =
      liftM (OracleSpec.query t) >>= fun u => pure (u, [⟨t, u⟩]) := by
  simp [withQueryLog]


-- @@ L522-528 expanded
/-- For any computation `oa` and predicate `p`, the probability of `p` holding on the output
equals the probability of `p ∘ Prod.fst` holding on the output of `oa.withQueryLog`. -/
@[simp, grind =]
lemma probEvent_withQueryLog {ι : Type} {oSpec : OracleSpec ι} [IsUniformSpec oSpec] {α : Type}
    (oa : OracleComp oSpec α) (p : α → Prop) :
    probEvent oa.withQueryLog (p ∘ Prod.fst) = probEvent oa p :=
  loggingOracle.probEvent_fst_run_simulateQ oa p


-- @@ L530-570 verbatim
/-- **Self-log fixed point.** The two log layers produced by
`oa.withQueryLog.withQueryLog` agree on every support point: simulating the
logging oracle over `oa.withQueryLog` records exactly the queries that the
inner `withQueryLog` already recorded, since `withQueryLog` does not add new
queries to the underlying `OracleComp`. -/
theorem withQueryLog_self_log_eq
    {ι : Type} {spec : OracleSpec.{0, 0} ι} {α : Type}
    (oa : OracleComp spec α) {v : α} {l₁ l₂ : spec.QueryLog}
    (hmem : ((v, l₁), l₂) ∈ support oa.withQueryLog.withQueryLog) :
    l₁ = l₂ := by
  induction oa using OracleComp.inductionOn generalizing v l₁ l₂ with
  | pure x =>
      rw [withQueryLog_pure, withQueryLog_pure, mem_support_pure_iff] at hmem
      grind
  | query_bind t mx ih =>
      -- `grind` is slow / crashes on the fully-unfolded support membership; the
      -- staged destructuring below keeps the search space small.
      rw [withQueryLog_bind, withQueryLog_bind, mem_support_bind_iff] at hmem
      obtain ⟨⟨⟨u₁, log_q1⟩, log_q2⟩, h₁, hmem⟩ := hmem
      rw [withQueryLog_query, withQueryLog_bind, mem_support_bind_iff] at h₁
      obtain ⟨⟨u₂, log_qa⟩, h₁a, h₁b⟩ := h₁
      simp only [withQueryLog_query, mem_support_bind_iff,
        mem_support_pure_iff, Prod.mk.injEq] at h₁a
      obtain ⟨u, _, rfl, rfl⟩ := h₁a
      rw [support_map, Set.mem_image] at h₁b
      obtain ⟨⟨⟨u', l_inner⟩, l_outer⟩, h_pure, h_eq_b⟩ := h₁b
      simp only [withQueryLog_pure, mem_support_pure_iff, Prod.mk.injEq] at h_pure
      obtain ⟨⟨rfl, rfl⟩, rfl⟩ := h_pure
      simp only [Prod.map_apply, id_eq, Prod.mk.injEq, List.append_nil] at h_eq_b
      obtain ⟨⟨rfl, rfl⟩, rfl⟩ := h_eq_b
      rw [support_map, Set.mem_image] at hmem
      obtain ⟨⟨⟨v', l₁'⟩, l₂'⟩, h_inner_outer, h_eq⟩ := hmem
      simp only [Prod.map_apply, id_eq, Prod.mk.injEq] at h_eq
      obtain ⟨⟨rfl, rfl⟩, rfl⟩ := h_eq
      simp only [map_eq_pure_bind, withQueryLog_bind, mem_support_bind_iff] at h_inner_outer
      obtain ⟨⟨⟨v', l₁'⟩, l₂'⟩, h_inner, ⟨pX, lX⟩, h_pX, h_eq_X⟩ := h_inner_outer
      simp only [withQueryLog_pure, mem_support_pure_iff, Prod.map_apply, id_eq,
        Prod.mk.injEq] at h_pX h_eq_X
      obtain ⟨⟨rfl, rfl⟩, rfl⟩ := h_pX
      obtain ⟨⟨rfl, rfl⟩, rfl⟩ := h_eq_X
      simp [ih u' h_inner]


-- @@ L572-572 verbatim
end OracleComp
