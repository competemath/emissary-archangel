/-
Copyright (c) 2026 Quang Dao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Quang Dao
-/

module

public import PolyFun.Control.Monad.Algebra
public import ToMathlib.Control.OptionT
public import ToMathlib.Control.StateT
public import ToMathlib.Control.WriterT
public import VCVio.EvalDist.Expectation
public import VCVio.EvalDist.Monad.Basic
public import VCVio.OracleComp.EvalDist
public import Loom.WP.Basic
public import Loom.Triple.Basic


-- @@ L19-72 verbatim
/-!
# Quantitative `WP` carrier for `OracleComp` (Loom2 default)

This file is the **home** of the quantitative algebra structure on
`OracleComp spec` valued in `ℝ≥0∞`, plus its registration as the
default `Std.Do'.WP (OracleComp spec) ℝ≥0∞ EPost.nil` instance.

It provides three layers:

1. **Expectation algebra** — `μ : OracleComp spec ℝ≥0∞ → ℝ≥0∞` and the
   legacy `MAlgOrdered (OracleComp spec) ℝ≥0∞` instance. These remain
   the structural root for `OracleComp.ProgramLogic.wp` / `.Triple` and
   the relational eRHL stack until the irreversible Commit 8 prunes
   `MAlgOrdered`.
2. **`Lean.Order` adapters for `ℝ≥0∞`** — bridges Mathlib's `≤` and
   `sSup` to Lean core's `Lean.Order.{PartialOrder, CompleteLattice}`,
   which Loom2 builds on. `Prop` ships with its own adapters in
   `Loom/LatticeExt.lean`.
3. **`Std.Do'.WP` instance** — the canonical entry point for new
   program-logic developments. Its `wpTrans` wraps the existing
   `MAlgOrdered.wp`, so the keystone alignment lemma
   `wp_eq_mAlgOrdered_wp` holds by `rfl`.

## Layout

This is one of three `WP` carriers we register on `OracleComp`. Because
`Std.Do'.WP`'s `Pred` and `EPred` are `outParam`s, only one carrier can
be *visible* to instance synthesis at a time. We register them
asymmetrically:

* This file (`Loom/Quantitative.lean`) — the `ℝ≥0∞` carrier as a normal
  `instance`, always live once the file is imported. This is the default.
* `Loom/Qualitative.lean` — the `Prop` carrier as a `scoped instance`
  under `namespace OracleComp.Qualitative`, opt-in via
  `open OracleComp.Qualitative`.
* `Loom/Probabilistic.lean` — the `Prob` carrier as a `scoped instance`
  under `namespace OracleComp.Probabilistic`, opt-in via
  `open OracleComp.Probabilistic`.

There is no umbrella `Unary/Loom.lean` re-export. Consumers import the
specific carrier they need. This makes the carrier choice explicit at
every import site.

## Loom2 vs. Mathlib lattice plumbing

Loom2 builds on `Lean.Order.{PartialOrder, CompleteLattice, CCPO}` from
`Init.Internal.Order`, a class hierarchy distinct from Mathlib's
`Order.{PartialOrder, CompleteLattice}`. We provide the narrow `ℝ≥0∞`
adapters here.

When Volo's PR #12965 lands upstream and the `Std.Do.{WP,…}` API
stabilizes, this bridge collapses to a re-export and the lattice
adapters can move to a shared `ToMathlib/Order/LeanOrderAdapter.lean`.
-/


-- @@ L74-74 verbatim
@[expose] public section


-- @@ L76-76 verbatim
open ENNReal

-- @@ L77-77 verbatim
open Lean.Order

-- @@ L78-78 verbatim
open Std.Do'


-- @@ L80-80 verbatim
universe u v


-- @@ L82-82 verbatim
namespace OracleComp.ProgramLogic


-- @@ L84-84 verbatim
variable {ι : Type u} {spec : OracleSpec ι}

-- @@ L85-85 verbatim
variable [IsUniformSpec spec]

-- @@ L86-86 verbatim
variable {α β : Type}


-- @@ L88-88 verbatim
/-! ## Expectation algebra and the `MAlgOrdered` instance -/


-- @@ L90-93 verbatim
/-- Expectation-style algebra for oracle computations returning `ℝ≥0∞`: the expectation
(`OracleComp.EvalDist.expectedValue`) of the identity functional. -/
noncomputable def μ (oa : OracleComp spec ℝ≥0∞) : ℝ≥0∞ :=
  OracleComp.EvalDist.expectedValue oa fun x => x


-- @@ L95-99 expanded
lemma μ_bind_eq_tsum {α : Type} (oa : OracleComp spec α) (ob : α → OracleComp spec ℝ≥0∞) :
    μ (oa >>= ob) = ∑' x, probOutput oa x * μ (ob x) :=
  by
  unfold μ
  rw [OracleComp.EvalDist.expectedValue_bind, OracleComp.EvalDist.expectedValue_def]


-- @@ L101-107 verbatim
noncomputable instance instMAlgOrdered : MAlgOrdered (OracleComp spec) ℝ≥0∞ where
  μ := μ (spec := spec)
  μ_pure x := by simp [μ]
  μ_bind_mono f g hfg x := by
    simp_rw [μ_bind_eq_tsum]
    gcongr with a
    exact hfg a


-- @@ L109-109 verbatim
end OracleComp.ProgramLogic


-- @@ L111-115 verbatim
/-! ## `Lean.Order` adapters for `ℝ≥0∞`

Loom2's `Std.Do'.WP m Pred EPred` requires `Assertion Pred` (a class
abbrev for `Lean.Order.CompleteLattice`). We bridge Mathlib's order
structure on `ℝ≥0∞` to Lean core's lattice hierarchy here. -/


-- @@ L117-117 verbatim
namespace OracleComp.ProgramLogic.Loom


-- @@ L119-124 verbatim
/-- Bridge Mathlib's `≤` on `ℝ≥0∞` to `Lean.Order.PartialOrder`. -/
instance : Lean.Order.PartialOrder ℝ≥0∞ where
  rel a b := a ≤ b
  rel_refl := le_refl _
  rel_trans := le_trans
  rel_antisymm := le_antisymm


-- @@ L126-133 verbatim
/-- Bridge Mathlib's `sSup` on `ℝ≥0∞` to `Lean.Order.CompleteLattice`.

`Lean.Order.CompleteLattice.has_sup c` requires a least upper bound for
the predicate-encoded set `{x | c x}`. We discharge it with Mathlib's
`sSup` specialization. -/
instance : Lean.Order.CompleteLattice ℝ≥0∞ where
  has_sup c :=
    ⟨sSup {x | c x}, fun _ => ⟨fun hsup _ hcy => le_trans (le_sSup hcy) hsup, sSup_le⟩⟩


-- @@ L135-135 verbatim
end OracleComp.ProgramLogic.Loom


-- @@ L137-149 verbatim
/-! ## `Lean.Order.CCPO` instance for `Std.Do'.EPost.nil`

Loom2's Hoare-triple notation `⦃ pre ⦄ x ⦃ post ⦄` (defined in
`Loom.Triple.Basic`) expands to `Std.Do'.Triple pre x post ⊥` where
`⊥` is the `Lean.Order.CCPO`-bottom (`Lean.Order.bot`). For monads
with no exception layer, the `EPred` is `Std.Do'.EPost.nil`, which
Loom2 ships only with a `Lean.Order.CompleteLattice` instance, not a
`CCPO` one. (`CCPO` and `CompleteLattice` are independent sibling
classes in Lean core's `Init.Internal.Order`.)

We provide the missing `CCPO` instance here. `EPost.nil` is a
single-element type, so the chain-supremum is trivial: every chain has
the unique element `EPost.nil.mk` as its least upper bound. -/


-- @@ L151-151 verbatim
namespace Std.Do'


-- @@ L153-154 verbatim
instance : Lean.Order.CCPO EPost.nil where
  has_csup _ := ⟨EPost.nil.mk, fun _ => ⟨fun _ _ _ => trivial, fun _ => trivial⟩⟩


-- @@ L156-156 verbatim
end Std.Do'


-- @@ L158-158 verbatim
namespace OracleComp.ProgramLogic.Loom


-- @@ L160-160 verbatim
/-! ## `Std.Do'.WP` instance for `OracleComp` -/


-- @@ L162-162 verbatim
variable {ι : Type u} {spec : OracleSpec ι}

-- @@ L163-163 verbatim
variable [IsUniformSpec spec]

-- @@ L164-164 verbatim
variable {α β : Type}


-- @@ L166-181 verbatim
/-- Quantitative `Std.Do'.WP` interpretation of `OracleComp spec` valued in `ℝ≥0∞`.

The `wpTrans` is the existing `MAlgOrdered.wp` (i.e. expectation of
`post` under `evalSPMF`); the `EPost.nil` argument is ignored since
`OracleComp` has no first-class exception slot. The three `WP` axioms
reduce to the existing `MAlgOrdered.{wp_pure, wp_bind, wp_mono}`
equalities. -/
noncomputable instance instWP : Std.Do'.WP (OracleComp spec) ℝ≥0∞ Std.Do'.EPost.nil where
  wpTrans x := ⟨fun post _epost =>
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) x post⟩
  wp_trans_pure x := fun post _epost =>
    (MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) x post).ge
  wp_trans_bind x f := fun post _epost =>
    (MAlgOrdered.wp_bind (m := OracleComp spec) (l := ℝ≥0∞) x f post).ge
  wp_trans_monotone x := fun _post _post' _epost _epost' _hepost hpost =>
    MAlgOrdered.wp_mono (m := OracleComp spec) (l := ℝ≥0∞) x hpost


-- @@ L183-193 verbatim
/-! ## Definitional alignment with `MAlgOrdered.wp`

The keystone lemma confirms `Std.Do'.wp` agrees with `MAlgOrdered.wp`
on the nose, so every existing quantitative `wp_*` theorem in
`HoareTriple.lean` transports for free when the user rewrites
`Std.Do'.wp _ _ _ ↦ MAlgOrdered.wp _ _`.

The epost argument is `⊥` to match Loom2's `⦃ _ ⦄ _ ⦃ _ ⦄` notation in
`Loom.Triple.Basic`. The `WP` instance for `OracleComp` ignores the
epost argument entirely, so any element of `EPost.nil` would yield the
same value. -/


-- @@ L195-198 verbatim
theorem wp_eq_mAlgOrdered_wp
    (oa : OracleComp spec α) (post : α → ℝ≥0∞) :
    Std.Do'.wp oa post Lean.Order.bot =
      MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) oa post := rfl


-- @@ L200-203 verbatim
theorem wp_eq_mAlgOrdered_wp_epost
    (oa : OracleComp spec α) (post : α → ℝ≥0∞) (epost : Std.Do'.EPost.nil) :
    Std.Do'.wp oa post epost =
      MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) oa post := rfl


-- @@ L205-205 verbatim
/-! ## `StateT (OracleComp spec)` WP normalization -/


-- @@ L207-218 verbatim
@[simp]
theorem wp_StateT_bind {σ : Type} (x : StateT σ (OracleComp spec) α)
    (f : α → StateT σ (OracleComp spec) β) (post : β → σ → ℝ≥0∞) :
    Std.Do'.wp (StateT.bind x f) post Lean.Order.bot =
      fun s => Std.Do'.wp x (fun a s' => Std.Do'.wp (f a) post Lean.Order.bot s')
        Lean.Order.bot s := by
  funext s
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((x >>= f).run s)
    (fun p : β × σ => post p.1 p.2) = _
  rw [StateT.run_bind]
  exact MAlgOrdered.wp_bind (m := OracleComp spec) (l := ℝ≥0∞) (x.run s)
    (fun p => (f p.1).run p.2) (fun p : β × σ => post p.1 p.2)


-- @@ L220-226 verbatim
@[simp]
theorem wp_StateT_bind' {σ : Type} (x : StateT σ (OracleComp spec) α)
    (f : α → StateT σ (OracleComp spec) β) (post : β → σ → ℝ≥0∞) :
    Std.Do'.wp (x >>= f) post Lean.Order.bot =
      fun s => Std.Do'.wp x (fun a s' => Std.Do'.wp (f a) post Lean.Order.bot s')
        Lean.Order.bot s :=
  wp_StateT_bind x f post


-- @@ L228-234 verbatim
@[simp]
theorem wp_StateT_pure {σ : Type} (x : α) (post : α → σ → ℝ≥0∞) :
    Std.Do'.wp (pure x : StateT σ (OracleComp spec) α) post Lean.Order.bot =
      fun s => post x s := by
  funext s
  exact MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (x, s)
    (fun p : α × σ => post p.1 p.2)


-- @@ L236-241 verbatim
theorem wp_StateT_get {σ : Type} (post : σ → σ → ℝ≥0∞) :
    Std.Do'.wp (MonadStateOf.get : StateT σ (OracleComp spec) σ) post Lean.Order.bot =
      fun s => post s s := by
  funext s
  exact MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (s, s)
    (fun p : σ × σ => post p.1 p.2)


-- @@ L243-249 verbatim
@[simp]
theorem wp_StateT_set {σ : Type} (s' : σ) (post : PUnit → σ → ℝ≥0∞) :
    Std.Do'.wp (MonadStateOf.set s' : StateT σ (OracleComp spec) PUnit) post
      Lean.Order.bot = fun _ => post ⟨⟩ s' := by
  funext s
  exact MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (PUnit.unit, s')
    (fun p : PUnit × σ => post p.1 p.2)


-- @@ L251-256 verbatim
theorem wp_StateT_modifyGet {σ : Type} (f : σ → α × σ) (post : α → σ → ℝ≥0∞) :
    Std.Do'.wp (MonadStateOf.modifyGet f : StateT σ (OracleComp spec) α) post
      Lean.Order.bot = fun s => post (f s).1 (f s).2 := by
  funext s
  exact MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (f s)
    (fun p : α × σ => post p.1 p.2)


-- @@ L258-268 verbatim
@[simp]
theorem wp_StateT_monadLift {σ : Type} (oa : OracleComp spec α)
    (post : α → σ → ℝ≥0∞) :
    Std.Do'.wp (MonadLift.monadLift oa : StateT σ (OracleComp spec) α) post
      Lean.Order.bot = fun s => Std.Do'.wp oa (fun a => post a s) Lean.Order.bot := by
  funext s
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (oa >>= fun a => pure (a, s))
      (fun p : α × σ => post p.1 p.2) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) oa (fun a => post a s)
  simp only [MAlgOrdered.wp_bind, MAlgOrdered.wp_pure]


-- @@ L270-270 verbatim
/-! ## `OptionT (OracleComp spec)` WP normalization -/


-- @@ L272-285 verbatim
theorem wp_OptionT_bind (x : OptionT (OracleComp spec) α)
    (f : α → OptionT (OracleComp spec) β) (post : β → ℝ≥0∞)
    (epost : EPost.cons ℝ≥0∞ EPost.nil) :
    Std.Do'.wp (x >>= f) post epost =
      Std.Do'.wp x (fun a => Std.Do'.wp (f a) post epost) epost := by
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((x >>= f).run)
      (epost.pushOption post) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) x.run
      (epost.pushOption fun a =>
        MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) (f a).run
          (epost.pushOption post))
  simp only [OptionT.run_bind, Option.elimM, MAlgOrdered.wp_bind]
  congr 1 with o
  cases o <;> simp [EPost.cons.pushOption]


-- @@ L287-290 verbatim
theorem wp_OptionT_pure (x : α) (post : α → ℝ≥0∞)
    (epost : EPost.cons ℝ≥0∞ EPost.nil) :
    Std.Do'.wp (pure x : OptionT (OracleComp spec) α) post epost = post x :=
  MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (some x) (epost.pushOption post)


-- @@ L292-295 verbatim
theorem wp_OptionT_failure (post : α → ℝ≥0∞)
    (epost : EPost.cons ℝ≥0∞ EPost.nil) :
    Std.Do'.wp (failure : OptionT (OracleComp spec) α) post epost = epost.head :=
  MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) none (epost.pushOption post)


-- @@ L297-304 verbatim
theorem wp_OptionT_monadLift (oa : OracleComp spec α) (post : α → ℝ≥0∞)
    (epost : EPost.cons ℝ≥0∞ EPost.nil) :
    Std.Do'.wp (MonadLift.monadLift oa : OptionT (OracleComp spec) α) post epost =
      Std.Do'.wp oa post Lean.Order.bot := by
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (oa >>= fun a => pure (some a)) (epost.pushOption post) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) oa post
  simp only [MAlgOrdered.wp_bind, MAlgOrdered.wp_pure]


-- @@ L306-310 verbatim
theorem wp_OptionT_lift (oa : OracleComp spec α) (post : α → ℝ≥0∞)
    (epost : EPost.cons ℝ≥0∞ EPost.nil) :
    Std.Do'.wp (OptionT.lift oa : OptionT (OracleComp spec) α) post epost =
      Std.Do'.wp oa post Lean.Order.bot :=
  wp_OptionT_monadLift oa post epost


-- @@ L312-321 verbatim
theorem wp_OptionT_map (f : α → β) (x : OptionT (OracleComp spec) α)
    (post : β → ℝ≥0∞) (epost : EPost.cons ℝ≥0∞ EPost.nil) :
    Std.Do'.wp (f <$> x) post epost = Std.Do'.wp x (fun a => post (f a)) epost := by
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((f <$> x).run)
      (epost.pushOption post) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) x.run
      (epost.pushOption fun a => post (f a))
  rw [OptionT.run_map, MAlgOrdered.wp_map]
  congr 1 with o
  cases o <;> rfl


-- @@ L323-323 verbatim
/-! ## `ExceptT (OracleComp spec)` WP normalization -/


-- @@ L325-338 verbatim
theorem wp_ExceptT_bind {ε : Type} (x : ExceptT ε (OracleComp spec) α)
    (f : α → ExceptT ε (OracleComp spec) β) (post : β → ℝ≥0∞)
    (epost : EPost.cons (ε → ℝ≥0∞) EPost.nil) :
    Std.Do'.wp (x >>= f) post epost =
      Std.Do'.wp x (fun a => Std.Do'.wp (f a) post epost) epost := by
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((x >>= f).run)
      (epost.pushExcept post) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) x.run
      (epost.pushExcept fun a =>
        MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) (f a).run
          (epost.pushExcept post))
  rw [ExceptT.run_bind, MAlgOrdered.wp_bind]
  congr 1 with ea
  cases ea <;> simp [EPost.cons.pushExcept, MAlgOrdered.wp_pure]


-- @@ L340-343 verbatim
theorem wp_ExceptT_pure {ε : Type} (x : α) (post : α → ℝ≥0∞)
    (epost : EPost.cons (ε → ℝ≥0∞) EPost.nil) :
    Std.Do'.wp (pure x : ExceptT ε (OracleComp spec) α) post epost = post x :=
  MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (Except.ok x) (epost.pushExcept post)


-- @@ L345-348 verbatim
theorem wp_ExceptT_throw {ε : Type} (e : ε) (post : α → ℝ≥0∞)
    (epost : EPost.cons (ε → ℝ≥0∞) EPost.nil) :
    Std.Do'.wp (throw e : ExceptT ε (OracleComp spec) α) post epost = epost.head e :=
  MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) (Except.error e) (epost.pushExcept post)


-- @@ L350-364 verbatim
theorem wp_ExceptT_monadLift {ε : Type} (oa : OracleComp spec α) (post : α → ℝ≥0∞)
    (epost : EPost.cons (ε → ℝ≥0∞) EPost.nil) :
    Std.Do'.wp (MonadLift.monadLift oa : ExceptT ε (OracleComp spec) α) post epost =
      Std.Do'.wp oa post Lean.Order.bot := by
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (Except.ok <$> oa) (epost.pushExcept post) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) oa post
  rw [map_eq_bind_pure_comp]
  rw [MAlgOrdered.wp_bind]
  congr 1
  funext a
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (pure (Except.ok a)) (epost.pushExcept post) = post a
  exact MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞)
    (Except.ok a) (epost.pushExcept post)


-- @@ L366-366 verbatim
/-! ## `ReaderT (OracleComp spec)` WP normalization -/


-- @@ L368-380 verbatim
@[simp]
theorem wp_ReaderT_bind {ρ : Type} (x : ReaderT ρ (OracleComp spec) α)
    (f : α → ReaderT ρ (OracleComp spec) β) (post : β → ρ → ℝ≥0∞) :
    Std.Do'.wp (x >>= f) post Lean.Order.bot =
      fun r => Std.Do'.wp x (fun a r' => Std.Do'.wp (f a) post Lean.Order.bot r')
        Lean.Order.bot r := by
  funext r
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((x >>= f).run r)
      (fun b => post b r) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) (x.run r)
      (fun a => MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((f a).run r)
        (fun b => post b r))
  rw [ReaderT.run_bind, MAlgOrdered.wp_bind]


-- @@ L382-386 verbatim
@[simp]
theorem wp_ReaderT_pure {ρ : Type} (x : α) (post : α → ρ → ℝ≥0∞) :
    Std.Do'.wp (pure x : ReaderT ρ (OracleComp spec) α) post Lean.Order.bot =
      fun r => post x r :=
  funext fun r => MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) x (fun a => post a r)


-- @@ L388-392 verbatim
@[simp]
theorem wp_ReaderT_read {ρ : Type} (post : ρ → ρ → ℝ≥0∞) :
    Std.Do'.wp (MonadReaderOf.read : ReaderT ρ (OracleComp spec) ρ) post Lean.Order.bot =
      fun r => post r r :=
  funext fun r => MAlgOrdered.wp_pure (m := OracleComp spec) (l := ℝ≥0∞) r (fun a => post a r)


-- @@ L394-399 verbatim
@[simp]
theorem wp_ReaderT_monadLift {ρ : Type} (oa : OracleComp spec α)
    (post : α → ρ → ℝ≥0∞) :
    Std.Do'.wp (MonadLift.monadLift oa : ReaderT ρ (OracleComp spec) α) post
      Lean.Order.bot = fun r => Std.Do'.wp oa (fun a => post a r) Lean.Order.bot :=
  rfl


-- @@ L401-401 verbatim
/-! ## Generic `WriterT` WP normalization -/


-- @@ L403-403 verbatim
end OracleComp.ProgramLogic.Loom


-- @@ L405-405 verbatim
namespace Std.Do'


-- @@ L407-449 verbatim
instance (priority := low) WriterT.instWP {m : Type → Type v} {Pred EPred ω : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred] :
    WP (WriterT ω m) (ω → Pred) EPred where
  wpTrans {γ} x :=
    ⟨fun post epost w => wp x.run (fun p : γ × ω => post p.1 (w * p.2)) epost⟩
  wp_trans_pure {γ} x := fun post epost w => by
    rw [WriterT.run_pure]
    simpa [mul_one] using
      (WP.wp_pure (m := m) (x := (x, (1 : ω)))
        (post := fun p : γ × ω => post p.1 (w * p.2)) (epost := epost))
  wp_trans_bind {γ δ} x f := fun post epost w => by
    rw [WriterT.run_bind]
    apply Lean.Order.PartialOrder.rel_trans
    · refine WP.wp_consequence (m := m) (x := x.run)
        (post := fun p : γ × ω =>
          wp (f p.1).run (fun q : δ × ω => post q.1 ((w * p.2) * q.2)) epost)
        (post' := fun p : γ × ω =>
          wp ((fun q : δ × ω => (q.1, p.2 * q.2)) <$> (f p.1).run)
            (fun p : δ × ω => post p.1 (w * p.2)) epost)
        (epost := epost) ?_
      intro p
      apply Lean.Order.PartialOrder.rel_trans
      · refine WP.wp_consequence (m := m) (x := (f p.1).run)
          (post := fun q : δ × ω => post q.1 ((w * p.2) * q.2))
          (post' := fun q : δ × ω => post q.1 (w * (p.2 * q.2)))
          (epost := epost) ?_
        intro q
        simpa [mul_assoc] using
          (show Lean.Order.PartialOrder.rel
              (post q.1 (w * (p.2 * q.2))) (post q.1 (w * (p.2 * q.2))) from
            Lean.Order.PartialOrder.rel_refl)
      · exact WP.wp_map (m := m)
          (fun q : δ × ω => (q.1, p.2 * q.2)) (f p.1).run
          (fun p : δ × ω => post p.1 (w * p.2)) epost
    · exact WP.wp_bind (m := m) x.run
        (fun p : γ × ω =>
          (fun q : δ × ω => (q.1, p.2 * q.2)) <$> (f p.1).run)
        (fun p : δ × ω => post p.1 (w * p.2)) epost
  wp_trans_monotone {γ} x := fun post post' epost epost' hepost hpost w => by
    apply WP.wp_consequence_econs (m := m) (x := x.run)
    · intro p
      exact hpost p.1 (w * p.2)
    · exact hepost


-- @@ L451-457 verbatim
@[simp, grind =]
theorem WriterT.apply_wp {m : Type → Type v} {Pred EPred ω α : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (x : WriterT ω m α) (post : α → ω → Pred) (epost : EPred) (w : ω) :
    wp x post epost w =
      wp x.run (fun p : α × ω => post p.1 (w * p.2)) epost :=
  rfl


-- @@ L459-467 verbatim
theorem WriterT.wp_pure {m : Type → Type v} {Pred EPred ω α : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (x : α) (post : α → ω → Pred) (epost : EPred) :
    (fun w => post x w) ⊑ wp (pure x : WriterT ω m α) post epost := by
  intro w
  rw [WriterT.apply_wp, WriterT.run_pure]
  simpa [mul_one] using
    (WP.wp_pure (m := m) (x := (x, (1 : ω)))
      (post := fun p : α × ω => post p.1 (w * p.2)) (epost := epost))


-- @@ L469-475 verbatim
theorem WriterT.wp_bind {m : Type → Type v} {Pred EPred ω α β : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (x : WriterT ω m α) (f : α → WriterT ω m β) (post : β → ω → Pred)
    (epost : EPred) :
    (fun w => wp x (fun a w' => wp (f a) post epost w') epost w) ⊑
      wp (x >>= f) post epost :=
  WP.wp_bind x f post epost


-- @@ L477-485 verbatim
theorem WriterT.wp_tell {m : Type → Type v} {Pred EPred ω : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (out : ω) (post : PUnit → ω → Pred) (epost : EPred) :
    (fun w => post PUnit.unit (w * out)) ⊑
      wp (MonadWriter.tell out : WriterT ω m PUnit) post epost := by
  intro w
  rw [WriterT.apply_wp, WriterT.run_tell]
  exact WP.wp_pure (m := m) (x := (PUnit.unit, out))
    (post := fun p : PUnit × ω => post p.1 (w * p.2)) (epost := epost)


-- @@ L487-504 verbatim
theorem WriterT.wp_monadLift {m : Type → Type v} {Pred EPred ω α : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (x : m α) (post : α → ω → Pred) (epost : EPred) :
    (fun w => wp x (fun a => post a w) epost) ⊑
      wp (MonadLift.monadLift x : WriterT ω m α) post epost := by
  intro w
  rw [WriterT.apply_wp]
  apply Lean.Order.PartialOrder.rel_trans
  · refine WP.wp_consequence (m := m) (x := x)
      (post := fun a => post a w)
      (post' := fun a => post a (w * (1 : ω)))
      (epost := epost) ?_
    intro a
    simpa [mul_one] using
      (show Lean.Order.PartialOrder.rel (post a w) (post a w) from
        Lean.Order.PartialOrder.rel_refl)
  · exact WP.wp_map (m := m) (fun a : α => (a, (1 : ω))) x
      (fun p : α × ω => post p.1 (w * p.2)) epost


-- @@ L506-513 verbatim
theorem WriterT.wp_map {m : Type → Type v} {Pred EPred ω α β : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (f : α → β) (x : WriterT ω m α) (post : β → ω → Pred) (epost : EPred) :
    wp x (fun a w => post (f a) w) epost ⊑ wp (f <$> x) post epost := by
  intro w
  rw [WriterT.apply_wp, WriterT.apply_wp, WriterT.run_map]
  exact WP.wp_map (m := m) (fun p : α × ω => (f p.1, p.2)) x.run
    (fun p : β × ω => post p.1 (w * p.2)) epost


-- @@ L515-520 verbatim
theorem Spec.tell_WriterT {m : Type → Type v} {Pred EPred ω : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (out : ω) (post : PUnit → ω → Pred) (epost : EPred) :
    Triple (fun w => post PUnit.unit (w * out))
      (MonadWriter.tell out : WriterT ω m PUnit) post epost :=
  Triple.iff.mpr (WriterT.wp_tell out post epost)


-- @@ L522-527 verbatim
theorem Spec.monadLift_WriterT {m : Type → Type v} {Pred EPred ω α : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (x : m α) (post : α → ω → Pred) (epost : EPred) :
    Triple (fun w => wp x (fun a => post a w) epost)
      (MonadLift.monadLift x : WriterT ω m α) post epost :=
  Triple.iff.mpr (WriterT.wp_monadLift x post epost)


-- @@ L529-534 verbatim
theorem Spec.mk_WriterT {m : Type → Type v} {Pred EPred ω α : Type}
    [Monad m] [Monoid ω] [Assertion Pred] [Assertion EPred] [WP m Pred EPred]
    (x : m (α × ω)) (post : α → ω → Pred) (epost : EPred) :
    Triple (fun w => wp x (fun p : α × ω => post p.1 (w * p.2)) epost)
      (WriterT.mk x : WriterT ω m α) post epost :=
  Triple.iff.mpr (by intro w; rfl)


-- @@ L536-536 verbatim
end Std.Do'


-- @@ L538-538 verbatim
namespace OracleComp.ProgramLogic.Loom


-- @@ L540-540 verbatim
variable {ι : Type u} {spec : OracleSpec ι}

-- @@ L541-541 verbatim
variable [IsUniformSpec spec]

-- @@ L542-542 verbatim
variable {α β : Type}


-- @@ L544-544 verbatim
namespace WriterT


-- @@ L546-569 verbatim
@[simp]
theorem wp_bind {ω : Type} [Monoid ω] (x : _root_.WriterT ω (OracleComp spec) α)
    (f : α → _root_.WriterT ω (OracleComp spec) β) (post : β → ω → ℝ≥0∞) :
    Std.Do'.wp (x >>= f) post Lean.Order.bot =
      fun w => Std.Do'.wp x
        (fun a w' => Std.Do'.wp (f a) post Lean.Order.bot w') Lean.Order.bot w := by
  funext w
  change Std.Do'.wp ((x >>= f).run) (fun p : β × ω => post p.1 (w * p.2))
      Lean.Order.bot =
    Std.Do'.wp x.run
      (fun p : α × ω =>
        Std.Do'.wp (f p.1).run (fun q : β × ω => post q.1 ((w * p.2) * q.2))
          Lean.Order.bot)
      Lean.Order.bot
  rw [_root_.WriterT.run_bind]
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (x.run >>= fun p : α × ω =>
        (fun q : β × ω => (q.1, p.2 * q.2)) <$> (f p.1).run)
      (fun p : β × ω => post p.1 (w * p.2)) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) x.run
      (fun p : α × ω =>
        MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) (f p.1).run
          (fun q : β × ω => post q.1 ((w * p.2) * q.2)))
  simp only [MAlgOrdered.wp_bind, MAlgOrdered.wp_map, mul_assoc]


-- @@ L571-582 verbatim
@[simp]
theorem wp_pure {ω : Type} [Monoid ω] (x : α) (post : α → ω → ℝ≥0∞) :
    Std.Do'.wp (pure x : _root_.WriterT ω (OracleComp spec) α) post Lean.Order.bot =
      fun w => post x w := by
  funext w
  change Std.Do'.wp ((pure x : _root_.WriterT ω (OracleComp spec) α).run)
      (fun p : α × ω => post p.1 (w * p.2)) Lean.Order.bot = post x w
  rw [_root_.WriterT.run_pure]
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (pure (x, 1) : OracleComp spec (α × ω))
      (fun p : α × ω => post p.1 (w * p.2)) = post x w
  rw [MAlgOrdered.wp_pure, mul_one]


-- @@ L584-596 verbatim
@[simp]
theorem wp_tell {ω : Type} [Monoid ω] (out : ω) (post : PUnit → ω → ℝ≥0∞) :
    Std.Do'.wp (MonadWriter.tell out : _root_.WriterT ω (OracleComp spec) PUnit) post
      Lean.Order.bot = fun w => post ⟨⟩ (w * out) := by
  funext w
  change Std.Do'.wp
    (_root_.WriterT.run (MonadWriter.tell out : _root_.WriterT ω (OracleComp spec) PUnit))
      (fun p : PUnit × ω => post p.1 (w * p.2)) Lean.Order.bot = post ⟨⟩ (w * out)
  rw [_root_.WriterT.run_tell]
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      (pure (⟨⟩, out) : OracleComp spec (PUnit × ω))
      (fun p : PUnit × ω => post p.1 (w * p.2)) = post ⟨⟩ (w * out)
  rw [MAlgOrdered.wp_pure]


-- @@ L598-610 verbatim
@[simp]
theorem wp_monadLift {ω : Type} [Monoid ω] (oa : OracleComp spec α)
    (post : α → ω → ℝ≥0∞) :
    Std.Do'.wp (MonadLift.monadLift oa : _root_.WriterT ω (OracleComp spec) α) post
      Lean.Order.bot = fun w => Std.Do'.wp oa (fun a => post a w) Lean.Order.bot := by
  funext w
  change Std.Do'.wp ((MonadLift.monadLift oa : _root_.WriterT ω (OracleComp spec) α).run)
      (fun p : α × ω => post p.1 (w * p.2)) Lean.Order.bot =
    Std.Do'.wp oa (fun a => post a w) Lean.Order.bot
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) ((fun a => (a, 1)) <$> oa)
      (fun p : α × ω => post p.1 (w * p.2)) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) oa (fun a => post a w)
  simp only [MAlgOrdered.wp_map, mul_one]


-- @@ L612-629 verbatim
@[simp]
theorem wp_map {ω : Type} [Monoid ω] (f : α → β)
    (x : _root_.WriterT ω (OracleComp spec) α) (post : β → ω → ℝ≥0∞) :
    Std.Do'.wp (f <$> x) post Lean.Order.bot =
      Std.Do'.wp x (fun a w => post (f a) w) Lean.Order.bot := by
  funext w
  change Std.Do'.wp (_root_.WriterT.run (f <$> x))
      (fun p : β × ω => post p.1 (w * p.2))
      Lean.Order.bot =
    Std.Do'.wp (WriterT.run x) (fun p : α × ω => post (f p.1) (w * p.2))
      Lean.Order.bot
  rw [_root_.WriterT.run_map]
  change MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞)
      ((fun p : α × ω => (f p.1, p.2)) <$> x.run)
      (fun p : β × ω => post p.1 (w * p.2)) =
    MAlgOrdered.wp (m := OracleComp spec) (l := ℝ≥0∞) x.run
      (fun p : α × ω => post (f p.1) (w * p.2))
  rw [MAlgOrdered.wp_map]


-- @@ L631-631 verbatim
end WriterT


-- @@ L633-643 verbatim
/-- `Std.Do'.Triple` agrees with `MAlgOrdered.Triple` propositionally.

Use as a forward iff: `triple_iff_mAlgOrdered_triple.mp` extracts
`pre ≤ MAlgOrdered.wp …` from a `Std.Do'.Triple`, and `.mpr` packages
it back. The two are *not* definitionally equal because `Std.Do'.Triple`
is an inductive wrapper rather than a `def` over `≤`. -/
theorem triple_iff_mAlgOrdered_triple
    (pre : ℝ≥0∞) (oa : OracleComp spec α) (post : α → ℝ≥0∞) :
    Std.Do'.Triple pre oa post Lean.Order.bot ↔
      MAlgOrdered.Triple (m := OracleComp spec) (l := ℝ≥0∞) pre oa post :=
  Std.Do'.Triple.iff


-- @@ L645-645 verbatim
end OracleComp.ProgramLogic.Loom
